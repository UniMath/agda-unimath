# Extensions of maps

```agda
module orthogonal-factorization-systems.extensions-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-dependent-functions
open import foundation.action-on-identifications-functions
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.monomorphisms
open import foundation.postcomposition-functions
open import foundation.propositions
open import foundation.sets
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.torsorial-type-families
```

</details>

## Idea

An **extension** of a map `f : (x : A) â†’ P x` along a map `i : A â†’ B` is a map
`g : (y : B) â†’ Q y` such that `Q` restricts along `i` to `P` and `g` restricts
along `i` to `f`.

```text
  A
  |  \
  i    f
  |      \
  âˆ¨       âˆ¨
  B - g -> P
```

## Definition

### Extensions of dependent functions

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-extension :
    {P : B â†’ UU l3} â†’
    ((x : A) â†’ P (i x)) â†’ ((y : B) â†’ P y) â†’ UU (l1 âŠ” l3)
  is-extension f g = f ~ (g âˆ˜ i)

  extension-dependent-type :
    (P : B â†’ UU l3) â†’
    ((x : A) â†’ P (i x)) â†’ UU (l1 âŠ” l2 âŠ” l3)
  extension-dependent-type P f = Î£ ((y : B) â†’ P y) (is-extension f)

  extension :
    {X : UU l3} â†’ (A â†’ X) â†’ UU (l1 âŠ” l2 âŠ” l3)
  extension {X} = extension-dependent-type (Î» _ â†’ X)

  total-extension-dependent-type : (P : B â†’ UU l3) â†’ UU (l1 âŠ” l2 âŠ” l3)
  total-extension-dependent-type P =
    Î£ ((x : A) â†’ P (i x)) (extension-dependent-type P)

  total-extension : (X : UU l3) â†’ UU (l1 âŠ” l2 âŠ” l3)
  total-extension X = total-extension-dependent-type (Î» _ â†’ X)

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {i : A â†’ B}
  {P : B â†’ UU l3} {f : (x : A) â†’ P (i x)}
  where

  map-extension : extension-dependent-type i P f â†’ (y : B) â†’ P y
  map-extension = pr1

  is-extension-map-extension :
    (E : extension-dependent-type i P f) â†’ is-extension i f (map-extension E)
  is-extension-map-extension = pr2
```

## Operations

### Vertical composition of extensions of maps

```text
  A
  |  \
  i    f
  |      \
  âˆ¨       âˆ¨
  B - g -> P
  |       âˆ§
  j      /
  |    h
  âˆ¨  /
  C
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {P : C â†’ UU l4}
  {i : A â†’ B} {j : B â†’ C}
  {f : (x : A) â†’ P (j (i x))} {g : (x : B) â†’ P (j x)} {h : (x : C) â†’ P x}
  where

  is-extension-comp-vertical :
    is-extension j g h â†’ is-extension i f g â†’ is-extension (j âˆ˜ i) f h
  is-extension-comp-vertical H G x = G x âˆ™ H (i x)
```

### Horizontal composition of extensions of maps

```text
           A
        /  |  \
      f    g    h
    /      |      \
   âˆ¨       âˆ¨       âˆ¨
  B - i -> C - j -> P
```

#### Horizontal composition of extensions of dependent functions

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {P : C â†’ UU l4}
  {f : A â†’ B} {g : A â†’ C} {h : (x : A) â†’ P (g x)}
  {i : B â†’ C} {j : (z : C) â†’ P z}
  where

  is-extension-dependent-type-comp-horizontal :
    (I : is-extension f g i) â†’
    is-extension g h j â†’ is-extension f (Î» x â†’ tr P (I x) (h x)) (j âˆ˜ i)
  is-extension-dependent-type-comp-horizontal I J x =
    ap (tr P (I x)) (J x) âˆ™ apd j (I x)
```

#### Horizontal composition of extensions of ordinary maps

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {X : UU l4}
  {f : A â†’ B} {g : A â†’ C} {h : A â†’ X}
  {i : B â†’ C} {j : C â†’ X}
  where

  is-extension-comp-horizontal :
    (I : is-extension f g i) â†’ is-extension g h j â†’ is-extension f h (j âˆ˜ i)
  is-extension-comp-horizontal I J x = (J x) âˆ™ ap j (I x)
```

### Left whiskering of extensions of maps

```text
  A
  |  \
  i    f
  |      \
  âˆ¨       âˆ¨
  B - g -> C - h -> P
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {P : C â†’ UU l4}
  {i : A â†’ B} {f : A â†’ C} {g : B â†’ C}
  where

  is-extension-left-whisker :
    (h : (x : C) â†’ P x) (F : is-extension i f g) â†’
    (is-extension i (Î» x â†’ tr P (F x) (h (f x))) (h âˆ˜ g))
  is-extension-left-whisker h F = apd h âˆ˜ F
```

### Right whiskering of extensions of maps

```text
  X - h -> A
           |  \
           i    f
           |      \
           âˆ¨       âˆ¨
           B - g -> P
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {P : B â†’ UU l3} {X : UU l4}
  {i : A â†’ B} {f : (x : A) â†’ P (i x)} {g : (y : B) â†’ P y}
  where

  is-extension-right-whisker :
    (F : is-extension i f g) (h : X â†’ A) â†’ is-extension (i âˆ˜ h) (f âˆ˜ h) g
  is-extension-right-whisker F h = F âˆ˜ h
```

### Postcomposition of extensions

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  postcomp-extension :
    (f : A â†’ B) (i : A â†’ X) (g : X â†’ Y) â†’
    extension f i â†’ extension f (g âˆ˜ i)
  postcomp-extension f i g =
    map-Î£ (is-extension f (g âˆ˜ i)) (postcomp B g) (Î» j H â†’ g Â·l H)
```

## Properties

### Characterizing identifications of extensions of maps

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  {P : B â†’ UU l3}
  (f : (x : A) â†’ P (i x))
  where

  coherence-htpy-extension :
    (e e' : extension-dependent-type i P f) â†’
    map-extension e ~ map-extension e' â†’ UU (l1 âŠ” l3)
  coherence-htpy-extension e e' K =
    (is-extension-map-extension e âˆ™h (K Â·r i)) ~ is-extension-map-extension e'

  htpy-extension : (e e' : extension-dependent-type i P f) â†’ UU (l1 âŠ” l2 âŠ” l3)
  htpy-extension e e' =
    Î£ ( map-extension e ~ map-extension e')
      ( coherence-htpy-extension e e')

  refl-htpy-extension :
    (e : extension-dependent-type i P f) â†’ htpy-extension e e
  pr1 (refl-htpy-extension e) = refl-htpy
  pr2 (refl-htpy-extension e) = right-unit-htpy

  htpy-eq-extension :
    (e e' : extension-dependent-type i P f) â†’ e ï¼ e' â†’ htpy-extension e e'
  htpy-eq-extension e .e refl = refl-htpy-extension e

  is-torsorial-htpy-extension :
    (e : extension-dependent-type i P f) â†’ is-torsorial (htpy-extension e)
  is-torsorial-htpy-extension e =
    is-torsorial-Eq-structure
      ( is-torsorial-htpy (map-extension e))
      ( map-extension e , refl-htpy)
      ( is-torsorial-htpy (is-extension-map-extension e âˆ™h refl-htpy))

  is-equiv-htpy-eq-extension :
    (e e' : extension-dependent-type i P f) â†’ is-equiv (htpy-eq-extension e e')
  is-equiv-htpy-eq-extension e =
    fundamental-theorem-id
      ( is-torsorial-htpy-extension e)
      ( htpy-eq-extension e)

  extensionality-extension :
    (e e' : extension-dependent-type i P f) â†’ (e ï¼ e') â‰ƒ (htpy-extension e e')
  pr1 (extensionality-extension e e') = htpy-eq-extension e e'
  pr2 (extensionality-extension e e') = is-equiv-htpy-eq-extension e e'

  eq-htpy-extension :
    (e e' : extension-dependent-type i P f)
    (H : map-extension e ~ map-extension e') â†’
    coherence-htpy-extension e e' H â†’ e ï¼ e'
  eq-htpy-extension e e' H K =
    map-inv-equiv (extensionality-extension e e') (H , K)
```

### The total type of extensions is equivalent to `(y : B) â†’ P y`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  inv-compute-total-extension-dependent-type :
    {P : B â†’ UU l3} â†’ total-extension-dependent-type i P â‰ƒ ((y : B) â†’ P y)
  inv-compute-total-extension-dependent-type =
    ( right-unit-law-Î£-is-contr (Î» f â†’ is-torsorial-htpy' (f âˆ˜ i))) âˆ˜e
    ( equiv-left-swap-Î£)

  compute-total-extension-dependent-type :
    {P : B â†’ UU l3} â†’ ((y : B) â†’ P y) â‰ƒ total-extension-dependent-type i P
  compute-total-extension-dependent-type =
    inv-equiv (inv-compute-total-extension-dependent-type)

  inv-compute-total-extension :
    {X : UU l3} â†’ total-extension i X â‰ƒ (B â†’ X)
  inv-compute-total-extension = inv-compute-total-extension-dependent-type

  compute-total-extension :
    {X : UU l3} â†’ (B â†’ X) â‰ƒ total-extension i X
  compute-total-extension = compute-total-extension-dependent-type
```

### The truncation level of the type of extensions is bounded by the truncation level of the codomains

```agda
module _
  {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-trunc-is-extension-dependent-type :
    {P : B â†’ UU l3} (f : (x : A) â†’ P (i x)) â†’
    ((x : A) â†’ is-trunc (succ-ğ•‹ k) (P (i x))) â†’
    (g : (x : B) â†’ P x) â†’ is-trunc k (is-extension i f g)
  is-trunc-is-extension-dependent-type f is-trunc-P g =
    is-trunc-Î  k Î» x â†’ is-trunc-P x (f x) (g (i x))

  is-trunc-extension-dependent-type :
    {P : B â†’ UU l3} (f : (x : A) â†’ P (i x)) â†’
    ((x : B) â†’ is-trunc k (P x)) â†’ is-trunc k (extension-dependent-type i P f)
  is-trunc-extension-dependent-type f is-trunc-P =
    is-trunc-Î£
      ( is-trunc-Î  k is-trunc-P)
      ( is-trunc-is-extension-dependent-type f
        ( is-trunc-succ-is-trunc k âˆ˜ (is-trunc-P âˆ˜ i)))

  is-trunc-total-extension-dependent-type :
    {P : B â†’ UU l3} â†’
    ((x : B) â†’ is-trunc k (P x)) â†’
    is-trunc k (total-extension-dependent-type i P)
  is-trunc-total-extension-dependent-type {P} is-trunc-P =
    is-trunc-equiv' k
      ( (y : B) â†’ P y)
      ( compute-total-extension-dependent-type i)
      ( is-trunc-Î  k is-trunc-P)

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-contr-is-extension :
    {P : B â†’ UU l3} (f : (x : A) â†’ P (i x)) â†’
    ((x : A) â†’ is-prop (P (i x))) â†’
    (g : (x : B) â†’ P x) â†’ is-contr (is-extension i f g)
  is-contr-is-extension f is-prop-P g =
    is-contr-Î  Î» x â†’ is-prop-P x (f x) (g (i x))

  is-prop-is-extension :
    {P : B â†’ UU l3} (f : (x : A) â†’ P (i x)) â†’
    ((x : A) â†’ is-set (P (i x))) â†’
    (g : (x : B) â†’ P x) â†’ is-prop (is-extension i f g)
  is-prop-is-extension f is-set-P g =
    is-prop-Î  (Î» x â†’ is-set-P x (f x) (g (i x)))
```

## Examples

### Every map is an extension of itself along the identity

```agda
module _
  {l1 l2 : Level} {A : UU l1} {P : A â†’ UU l2} (f : (x : A) â†’ P x)
  where

  is-extension-self : is-extension id f f
  is-extension-self = refl-htpy

  extension-self : extension-dependent-type id P f
  extension-self = f , is-extension-self
```

### The identity is an extension of every map along themselves

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  is-extension-along-self : is-extension f f id
  is-extension-along-self = refl-htpy

  extension-along-self : extension f f
  extension-along-self = id , is-extension-along-self
```

### Postcomposition of extensions by an equivalence is an equivalence

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-equiv-postcomp-extension :
    (f : A â†’ B) (i : A â†’ X) (g : X â†’ Y) â†’ is-equiv g â†’
    is-equiv (postcomp-extension f i g)
  is-equiv-postcomp-extension f i g G =
    is-equiv-map-Î£
      ( is-extension f (g âˆ˜ i))
      ( is-equiv-postcomp-is-equiv g G B)
      ( Î» j â†’
        is-equiv-map-Î -is-fiberwise-equiv
          ( Î» x â†’ is-emb-is-equiv G (i x) (j (f x))))

  equiv-postcomp-extension :
     (f : A â†’ B) (i : A â†’ X) (g : X â‰ƒ Y) â†’
     extension f i â‰ƒ extension f (map-equiv g âˆ˜ i)
  equiv-postcomp-extension f i (g , G) =
    ( postcomp-extension f i g , is-equiv-postcomp-extension f i g G)
```

### Postcomposition of extensions by an embedding is an embedding

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-emb-postcomp-extension :
    (f : A â†’ B) (i : A â†’ X) (g : X â†’ Y) â†’ is-emb g â†’
    is-emb (postcomp-extension f i g)
  is-emb-postcomp-extension f i g H =
    is-emb-map-Î£
      ( is-extension f (g âˆ˜ i))
      ( is-mono-is-emb g H B)
      ( Î» j â†’
        is-emb-is-equiv
          ( is-equiv-map-Î -is-fiberwise-equiv
            ( Î» x â†’ H (i x) (j (f x)))))

  emb-postcomp-extension :
     (f : A â†’ B) (i : A â†’ X) (g : X â†ª Y) â†’
     extension f i â†ª extension f (map-emb g âˆ˜ i)
  emb-postcomp-extension f i (g , G) =
    postcomp-extension f i g , is-emb-postcomp-extension f i g G
```

## See also

- [`orthogonal-factorization-systems.lifts-maps`](orthogonal-factorization-systems.lifts-maps.md)
  for the dual notion.
