{{#title  Lifts of maps}}

```agda
module orthogonal-factorization-systems.lifts-of-maps where

open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.functions
open import foundation.homotopies
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universe-levels


open import orthogonal-factorization-systems.extensions-of-maps
```

## Idea

A _lift_ of a map `f : X â†’ B` along a map `i : A â†’ B`
is a map `g : X â†’ A` such that the composition `i âˆ˜ g` is `f`.

```md
           A
          ^|
        /  i
      g    |
    /      v
  X - f -> B
```

## Definition

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-lift : {X : UU l3} â†’ (X â†’ B) â†’ (X â†’ A) â†’ UU (l2 âŠ” l3)
  is-lift f g = f ~ (i âˆ˜ g)

  lift : {X : UU l3} â†’ (X â†’ B) â†’ UU (l1 âŠ” l2 âŠ” l3)
  lift {X = X} f = Î£ (X â†’ A) (is-lift f)

  total-lift : (X : UU l3) â†’ UU (l1 âŠ” l2 âŠ” l3)
  total-lift X = Î£ (X â†’ B) lift
```

## Properties

### The total type of lifts is equivalent to `X â†’ A`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B) (X : UU l3)
  where

  inv-compute-total-lift : total-lift i X â‰ƒ (X â†’ A)
  inv-compute-total-lift =
    ( right-unit-law-Î£-is-contr ((Î» f â†’ is-contr-total-htpy' (i âˆ˜ f)))) âˆ˜e
    ( equiv-left-swap-Î£)

  compute-total-lift : (X â†’ A) â‰ƒ total-lift i X
  compute-total-lift = inv-equiv inv-compute-total-lift
```

## Properties

### If `P` is `k`-truncated then the type of lifts is `k`-truncated

```agda
module _
  {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-trunc-is-lift :
    {X : UU l3} (f : X â†’ B) â†’
    is-trunc (succ-ğ•‹ k) B â†’ (g : X â†’ A) â†’ is-trunc k (is-lift i f g)
  is-trunc-is-lift f is-trunc-B g =
    is-trunc-Î  k (Î» x â†’ is-trunc-B (f x) (i (g x)))

  is-trunc-lift :
    {X : UU l3} (f : X â†’ B) â†’
    is-trunc k A â†’ is-trunc (succ-ğ•‹ k) B â†’ is-trunc k (lift i f)
  is-trunc-lift f is-trunc-A is-trunc-B =
    is-trunc-Î£
      ( is-trunc-function-type k is-trunc-A)
      ( is-trunc-is-lift f is-trunc-B)
  
  is-trunc-total-lift :
    (X : UU l3) â†’ is-trunc k A â†’ is-trunc k (total-lift i X)
  is-trunc-total-lift X is-trunc-A =
    is-trunc-equiv' k
      ( X â†’ A)
      ( compute-total-lift i X)
      ( is-trunc-function-type k is-trunc-A)
```

## See also

- [`orthogonal-factorization-systems.extensions-of-maps`](orthogonal-factorization-systems.extensions-of-maps.html) for the dual notion.
