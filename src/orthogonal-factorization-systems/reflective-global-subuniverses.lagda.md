# Reflective global subuniverses

```agda
module orthogonal-factorization-systems.reflective-global-subuniverses where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cartesian-product-types
open import foundation.cones-over-cospan-diagrams
open import foundation.contractible-types
open import foundation.cospan-diagrams
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.equivalences-arrows
open import foundation.extensions-types-global-subuniverses
open import foundation.extensions-types-subuniverses
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.global-subuniverses
open import foundation.identity-types
open import foundation.precomposition-functions
open import foundation.propositions
open import foundation.pullback-cones
open import foundation.retractions
open import foundation.subuniverses
open import foundation.unit-type
open import foundation.universal-property-pullbacks
open import foundation.universe-levels

open import orthogonal-factorization-systems.localizations-at-global-subuniverses
open import orthogonal-factorization-systems.modal-induction
open import orthogonal-factorization-systems.modal-operators
open import orthogonal-factorization-systems.modal-subuniverse-induction
open import orthogonal-factorization-systems.types-local-at-maps
open import orthogonal-factorization-systems.universal-property-localizations-at-global-subuniverses
```

</details>

## Idea

A
{{#concept "reflective global subuniverse" Disambiguation="of types" Agda=reflective-global-subuniverse}},
or **localization**, is a
[global subuniverse](foundation.global-subuniverses.md) `ð’«` together with a
reflecting operator `L` on types giving
[localizations](orthogonal-factorization-systems.localizations-at-global-subuniverses.md)
at `ð’«`. I.e., for every type `A` there is a type `LA âˆˆ ð’«` equipped with a _unit
map_

```text
  Î· : A â†’ LA
```

satisfying the
[universal property of localizations](orthogonal-factorization-systems.universal-property-localizations-at-global-subuniverses.md).
This states that every type in `ð’«` is
[local](orthogonal-factorization-systems.types-local-at-maps.md) at the unit
`Î·`. As a consequence, the local types with respect to `L` are precisely the
types in `ð’«`.

## Definitions

### The predicate on global subuniverses of being reflective

```agda
is-reflective-global-subuniverse :
    {Î± : Level â†’ Level} â†’ (Level â†’ Level) â†’ global-subuniverse Î± â†’ UUÏ‰
is-reflective-global-subuniverse Î² ð’« =
  {l : Level} (X : UU l) â†’ localization-global-subuniverse ð’« (Î² l) X

module _
  {Î± Î² : Level â†’ Level}
  (ð’« : global-subuniverse Î±)
  (H : is-reflective-global-subuniverse Î² ð’«)
  where

  reflection-is-reflective-global-subuniverse :
    {l : Level} (X : UU l) â†’ extension-type-global-subuniverse ð’« (Î² l) X
  reflection-is-reflective-global-subuniverse X =
    reflection-localization-global-subuniverse (H X)

  type-reflection-is-reflective-global-subuniverse :
    {l : Level} â†’ UU l â†’ UU (Î² l)
  type-reflection-is-reflective-global-subuniverse X =
    type-localization-global-subuniverse (H X)

  is-in-global-subuniverse-type-reflection-is-reflective-global-subuniverse :
    {l : Level} (X : UU l) â†’
    is-in-global-subuniverse ð’«
      ( type-reflection-is-reflective-global-subuniverse X)
  is-in-global-subuniverse-type-reflection-is-reflective-global-subuniverse X =
    is-in-global-subuniverse-type-localization-global-subuniverse (H X)

  unit-is-reflective-global-subuniverse :
    {l : Level} (X : UU l) â†’
    X â†’ type-reflection-is-reflective-global-subuniverse X
  unit-is-reflective-global-subuniverse X =
    unit-localization-global-subuniverse (H X)

  up-localization-is-reflective-global-subuniverse :
    {l : Level} (X : UU l) â†’
    universal-property-localization-global-subuniverse ð’« X
      ( reflection-is-reflective-global-subuniverse X)
  up-localization-is-reflective-global-subuniverse X =
    up-localization-global-subuniverse (H X)
```

### Reflective global subuniverses

```agda
record
  reflective-global-subuniverse (Î± Î² : Level â†’ Level) : UUÏ‰
  where

  field
    global-subuniverse-reflective-global-subuniverse :
      global-subuniverse Î±

    is-reflective-reflective-global-subuniverse :
      is-reflective-global-subuniverse Î²
        global-subuniverse-reflective-global-subuniverse
```

```agda
  subuniverse-reflective-global-subuniverse : (l : Level) â†’ subuniverse l (Î± l)
  subuniverse-reflective-global-subuniverse =
    subuniverse-global-subuniverse
      global-subuniverse-reflective-global-subuniverse

  is-closed-under-equiv-reflective-global-subuniverse :
    {l1 l2 : Level} â†’
    is-closed-under-equiv-subuniverses Î±
      subuniverse-reflective-global-subuniverse
      l1
      l2
  is-closed-under-equiv-reflective-global-subuniverse =
    is-closed-under-equiv-global-subuniverse
      global-subuniverse-reflective-global-subuniverse

  is-in-reflective-global-subuniverse : {l : Level} â†’ UU l â†’ UU (Î± l)
  is-in-reflective-global-subuniverse =
    is-in-global-subuniverse global-subuniverse-reflective-global-subuniverse

  is-prop-is-in-reflective-global-subuniverse :
    {l : Level} (X : UU l) â†’ is-prop (is-in-reflective-global-subuniverse X)
  is-prop-is-in-reflective-global-subuniverse =
    is-prop-is-in-global-subuniverse
      global-subuniverse-reflective-global-subuniverse

  type-reflective-global-subuniverse : (l : Level) â†’ UU (Î± l âŠ” lsuc l)
  type-reflective-global-subuniverse =
    type-global-subuniverse
      global-subuniverse-reflective-global-subuniverse

  inclusion-reflective-global-subuniverse :
    {l : Level} â†’ type-reflective-global-subuniverse l â†’ UU l
  inclusion-reflective-global-subuniverse =
    inclusion-global-subuniverse
      global-subuniverse-reflective-global-subuniverse

  type-reflection-reflective-global-subuniverse :
    {l : Level} â†’ UU l â†’ UU (Î² l)
  type-reflection-reflective-global-subuniverse =
    type-reflection-is-reflective-global-subuniverse
      global-subuniverse-reflective-global-subuniverse
      is-reflective-reflective-global-subuniverse

  is-in-global-subuniverse-type-reflection-reflective-global-subuniverse :
    {l : Level} (X : UU l) â†’
    is-in-reflective-global-subuniverse
      ( type-reflection-reflective-global-subuniverse X)
  is-in-global-subuniverse-type-reflection-reflective-global-subuniverse =
    is-in-global-subuniverse-type-reflection-is-reflective-global-subuniverse
      global-subuniverse-reflective-global-subuniverse
      is-reflective-reflective-global-subuniverse

  reflection-reflective-global-subuniverse :
      {l : Level} (X : UU l) â†’
      extension-type-global-subuniverse
        global-subuniverse-reflective-global-subuniverse
        ( Î² l)
        ( X)
  reflection-reflective-global-subuniverse =
    reflection-is-reflective-global-subuniverse
      global-subuniverse-reflective-global-subuniverse
      is-reflective-reflective-global-subuniverse

  unit-reflective-global-subuniverse :
    {l : Level} (X : UU l) â†’
    X â†’ type-reflection-reflective-global-subuniverse X
  unit-reflective-global-subuniverse =
    unit-is-reflective-global-subuniverse
      global-subuniverse-reflective-global-subuniverse
      is-reflective-reflective-global-subuniverse

  up-localization-reflective-global-subuniverse :
    {l : Level} (X : UU l) â†’
    universal-property-localization-global-subuniverse
      global-subuniverse-reflective-global-subuniverse
      ( X)
      ( reflection-reflective-global-subuniverse X)
  up-localization-reflective-global-subuniverse =
    up-localization-is-reflective-global-subuniverse
      global-subuniverse-reflective-global-subuniverse
      is-reflective-reflective-global-subuniverse

open reflective-global-subuniverse public
```

## Properties

### A type is in a reflective subuniverse if and only if it is local at reflections

```agda
module _
  {Î± Î² : Level â†’ Level}
  (ð’« : reflective-global-subuniverse Î± Î²)
  where

  is-in-reflective-global-subuniverse-is-local-domain :
    {l1 : Level} {A : UU l1} â†’
    is-local (unit-reflective-global-subuniverse ð’« A) A â†’
    is-in-reflective-global-subuniverse ð’« A
  is-in-reflective-global-subuniverse-is-local-domain {A = A} =
    is-in-global-subuniverse-is-local-type-universal-property-localization-global-subuniverse
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( reflection-reflective-global-subuniverse ð’« A)
      ( up-localization-reflective-global-subuniverse ð’« A)

  is-in-reflective-global-subuniverse-is-local :
    {l1 : Level} {A : UU l1} â†’
    ( {l : Level} (X : UU l) â†’
      is-local (unit-reflective-global-subuniverse ð’« X) A) â†’
    is-in-reflective-global-subuniverse ð’« A
  is-in-reflective-global-subuniverse-is-local {A = A} H =
    is-in-reflective-global-subuniverse-is-local-domain (H A)
```

### A type `X` is in a reflective subuniverse if and only if the unit is an equivalence at `X`

```agda
module _
  {Î± Î² : Level â†’ Level}
  (ð’« : reflective-global-subuniverse Î± Î²)
  {l : Level} {X : UU l}
  where

  is-in-reflective-global-subuniverse-is-equiv-unit :
    is-equiv (unit-reflective-global-subuniverse ð’« X) â†’
    is-in-reflective-global-subuniverse ð’« X
  is-in-reflective-global-subuniverse-is-equiv-unit =
    is-in-global-subuniverse-is-equiv-unit-universal-property-localization-global-subuniverse
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( reflection-reflective-global-subuniverse ð’« X)
      ( up-localization-reflective-global-subuniverse ð’« X)

  is-equiv-unit-is-in-reflective-global-subuniverse :
    is-in-reflective-global-subuniverse ð’« X â†’
    is-equiv (unit-reflective-global-subuniverse ð’« X)
  is-equiv-unit-is-in-reflective-global-subuniverse =
    is-equiv-unit-is-in-global-subuniverse-universal-property-localization-global-subuniverse
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( reflection-reflective-global-subuniverse ð’« X)
      ( up-localization-reflective-global-subuniverse ð’« X)
```

### Reflective global subuniverses contain contractible types

```agda
module _
  {Î± Î² : Level â†’ Level} (ð’« : reflective-global-subuniverse Î± Î²)
  where

  is-in-reflective-global-subuniverse-unit :
    is-in-reflective-global-subuniverse ð’« unit
  is-in-reflective-global-subuniverse-unit =
    is-in-global-subuniverse-has-localization-global-subuniverse-unit
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( is-reflective-reflective-global-subuniverse ð’« unit)

  is-in-reflective-global-subuniverse-is-contr :
    {l : Level} {A : UU l} â†’
    is-contr A â†’
    is-in-reflective-global-subuniverse ð’« A
  is-in-reflective-global-subuniverse-is-contr {A = A} H =
    is-in-global-subuniverse-has-localization-global-subuniverse-is-contr
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( H)
      ( is-reflective-reflective-global-subuniverse ð’« A)
```

### Reflective global subuniverses are closed under dependent products

```agda
module _
  {Î± Î² : Level â†’ Level} (ð’« : reflective-global-subuniverse Î± Î²)
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  is-in-reflective-global-subuniverse-Î  :
    ((x : A) â†’ is-in-reflective-global-subuniverse ð’« (B x)) â†’
    is-in-reflective-global-subuniverse ð’« ((x : A) â†’ B x)
  is-in-reflective-global-subuniverse-Î  H =
    is-in-global-subuniverse-Î -localization-global-subuniverse
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( H)
      ( is-reflective-reflective-global-subuniverse ð’« ((x : A) â†’ B x))
```

### Reflective global subuniverses are closed under exponentials

```agda
module _
  {Î± Î² : Level â†’ Level} (ð’« : reflective-global-subuniverse Î± Î²)
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-in-reflective-global-subuniverse-exponential :
    is-in-reflective-global-subuniverse ð’« B â†’
    is-in-reflective-global-subuniverse ð’« (A â†’ B)
  is-in-reflective-global-subuniverse-exponential H =
    is-in-global-subuniverse-exponential-localization-global-subuniverse
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( H)
      ( is-reflective-reflective-global-subuniverse ð’« (A â†’ B))
```

### Reflective global subuniverses are closed under pullbacks

Consider a pullback square

```text
          q
    C --------> B
    | âŒŸ         |
  p |           | g
    âˆ¨           âˆ¨
    A --------> X
          f
```

then if `A`, `B` and `X` are in `ð’«`, then so is `C`.

This is Proposition 5.1.19 in {{#cite Rij19}}.

**Proof.** We have a commuting square

```text
                  - âˆ˜ Î·
       (LC â†’ C) --------> (C â†’ C)
           |                 |
           |                 |
  cone-map |                 | cone-map
           |                 |
           âˆ¨                 âˆ¨
      cone f g LC Â·Â·Â·Â·> cone f g C
```

where the bottom horizontal map is an equivalence by the assumptions that `A`,
`B` and `X` are `ð’«`-local. The two vertical maps are equivalences by the
assumption that `C` is a pullback and so the top map must be an equivalence as
well.

```agda
module _
  {Î± Î² : Level â†’ Level} (ð’« : reflective-global-subuniverse Î± Î²)
  {l1 l2 l3 l4 : Level}
  {ð’® : cospan-diagram l1 l2 l3}
  (c : pullback-cone ð’® l4)
  where

  is-in-reflective-global-subuniverse-pullback :
    is-in-reflective-global-subuniverse ð’« (cospanning-type-cospan-diagram ð’®) â†’
    is-in-reflective-global-subuniverse ð’« (domain-cospan-diagram ð’®) â†’
    is-in-reflective-global-subuniverse ð’« (codomain-cospan-diagram ð’®) â†’
    is-in-reflective-global-subuniverse ð’« (domain-pullback-cone ð’® c)
  is-in-reflective-global-subuniverse-pullback =
    is-in-global-subuniverse-pullback-localization-global-subuniverse
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( c)
      ( is-reflective-reflective-global-subuniverse ð’«
        ( domain-pullback-cone ð’® c))
```

### Reflective global subuniverses are closed under cartesian product types

This is Corollary 5.1.20 in {{#cite Rij19}}.

```agda
module _
  {Î± Î² : Level â†’ Level} (ð’« : reflective-global-subuniverse Î± Î²)
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-in-reflective-global-subuniverse-cartesian-product :
    is-in-reflective-global-subuniverse ð’« A â†’
    is-in-reflective-global-subuniverse ð’« B â†’
    is-in-reflective-global-subuniverse ð’« (A Ã— B)
  is-in-reflective-global-subuniverse-cartesian-product H K =
    is-in-global-subuniverse-cartesian-product-localization-global-subuniverse
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( is-in-reflective-global-subuniverse-unit ð’«)
      ( H)
      ( K)
      ( is-reflective-reflective-global-subuniverse ð’« (A Ã— B))
```

### Reflective global subuniverses are closed under identity types

This is Corollary 5.1.21 in {{#cite Rij19}}.

```agda
module _
  {Î± Î² : Level â†’ Level} (ð’« : reflective-global-subuniverse Î± Î²)
  {l : Level} {A : UU l} (x y : A) (H : is-in-reflective-global-subuniverse ð’« A)
  where

  is-in-reflective-global-subuniverse-Id :
    is-in-reflective-global-subuniverse ð’« (x ï¼ y)
  is-in-reflective-global-subuniverse-Id =
    is-in-global-subuniverse-Id-localization-global-subuniverse
      ( global-subuniverse-reflective-global-subuniverse ð’«)
      ( is-in-reflective-global-subuniverse-unit ð’«)
      ( H)
      ( is-reflective-reflective-global-subuniverse ð’« (x ï¼ y))
```

### Reflective global subuniverses are closed under types of equivalences

If `A` and `B` are `ð’«`-types, then the type of equivalences `A â‰ƒ B` is again a
`ð’«`-type. While this would follow straightforwardly from the above result and
univalence, we give a proof that is independent of the univalence axiom.

This is Corollary 5.1.23 in {{#cite Rij19}} and Proposition 2.18 in
{{#cite CORS20}}.

```agda
module _
  {Î± Î² : Level â†’ Level} (ð’« : reflective-global-subuniverse Î± Î²)
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  (H : is-in-reflective-global-subuniverse ð’« A)
  (K : is-in-reflective-global-subuniverse ð’« B)
  where

  is-in-reflective-global-subuniverse-equiv :
    is-in-reflective-global-subuniverse ð’« (A â‰ƒ B)
  is-in-reflective-global-subuniverse-equiv =
    is-in-reflective-global-subuniverse-pullback ð’«
      pullback-cone-equiv
      (is-in-reflective-global-subuniverse-cartesian-product ð’«
        ( is-in-reflective-global-subuniverse-exponential ð’« H)
        ( is-in-reflective-global-subuniverse-exponential ð’« K))
      (is-in-reflective-global-subuniverse-cartesian-product ð’«
        ( is-in-reflective-global-subuniverse-exponential ð’« K)
        ( is-in-reflective-global-subuniverse-cartesian-product ð’«
          ( is-in-reflective-global-subuniverse-exponential ð’« H)
          ( is-in-reflective-global-subuniverse-exponential ð’« H)))
      ( is-in-reflective-global-subuniverse-unit ð’«)
```

### Reflective global subuniverses are closed under sequential limits

> This remains to be formalized.

## See also

- [Î£-closed reflective subuniverses](orthogonal-factorization-systems.sigma-closed-reflective-subuniverses.md)
- [Localizations with respect to subuniverses](orthogonal-factorization-systems.localizations-at-subuniverses.md)

## References

{{#bibliography}} {{#reference UF13}} {{#reference RSS20}} {{#reference Rij19}}
