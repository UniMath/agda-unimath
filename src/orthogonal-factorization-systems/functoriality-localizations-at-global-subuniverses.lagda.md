# Functoriality of localizations at global subuniverses

```agda
module orthogonal-factorization-systems.functoriality-localizations-at-global-subuniverses where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cartesian-product-types
open import foundation.commuting-squares-of-maps
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.extensions-types-global-subuniverses
open import foundation.extensions-types-subuniverses
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.global-subuniverses
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.precomposition-functions
open import foundation.propositions
open import foundation.retractions
open import foundation.subuniverses
open import foundation.unit-type
open import foundation.universe-levels

open import orthogonal-factorization-systems.localizations-at-global-subuniverses
open import orthogonal-factorization-systems.modal-induction
open import orthogonal-factorization-systems.modal-operators
open import orthogonal-factorization-systems.modal-subuniverse-induction
open import orthogonal-factorization-systems.types-local-at-maps
open import orthogonal-factorization-systems.universal-property-localizations-at-global-subuniverses
open import orthogonal-factorization-systems.universal-property-localizations-at-subuniverses
```

</details>

## Idea

Given a [global subuniverse](foundation.global-subuniverses.md) `ğ’«`, and two
`ğ’«`-localizations `Î·_X : X â†’ LX` and `Î·_Y : Y â†’ LY` then for every map
`f : X â†’ Y` there is a map `Lf : LX â†’ LY` such that the square

```text
        f
  X --------> Y
  |           |
  |           |
  âˆ¨    Lf     âˆ¨
  LX ------> LY
```

commutes. This construction preserves identity functions and composition of maps

```text
  L(g âˆ˜ f) ~ Lg âˆ˜ Lf    and    L(id) ~ id
```

## Definitions

### The functorial action on maps of types with localizations

```agda
module _
  {Î± : Level â†’ Level} (ğ’« : global-subuniverse Î±)
  {l1 l2 l3 l4 : Level} {X : UU l1} {Y : UU l2}
  (LX : localization-global-subuniverse ğ’« l3 X)
  (LY : extension-type-global-subuniverse ğ’« l4 Y)
  where

  map-localization-global-subuniverse' :
    (X â†’ Y) â†’
    type-localization-global-subuniverse LX â†’
    type-extension-type-global-subuniverse ğ’« LY
  map-localization-global-subuniverse' f =
    map-inv-is-equiv
      ( up-localization-global-subuniverse LX
        ( type-global-subuniverse-extension-type-global-subuniverse ğ’« LY))
      ( inclusion-extension-type-global-subuniverse ğ’« LY âˆ˜ f)

  eq-naturality-map-localization-global-subuniverse' :
    (f : X â†’ Y) â†’
    map-localization-global-subuniverse' f âˆ˜
    unit-localization-global-subuniverse LX ï¼
    inclusion-extension-type-global-subuniverse ğ’« LY âˆ˜
    f
  eq-naturality-map-localization-global-subuniverse' f =
    is-section-map-inv-is-equiv
      ( up-localization-global-subuniverse LX
        ( type-global-subuniverse-extension-type-global-subuniverse ğ’« LY))
      ( inclusion-extension-type-global-subuniverse ğ’« LY âˆ˜ f)

  naturality-map-localization-global-subuniverse' :
    (f : X â†’ Y) â†’
    coherence-square-maps
      ( f)
      ( unit-localization-global-subuniverse LX)
      ( inclusion-extension-type-global-subuniverse ğ’« LY)
      ( map-localization-global-subuniverse' f)
  naturality-map-localization-global-subuniverse' f =
    htpy-eq (eq-naturality-map-localization-global-subuniverse' f)

module _
  {Î± : Level â†’ Level} (ğ’« : global-subuniverse Î±)
  {l1 l2 l3 l4 : Level} {X : UU l1} {Y : UU l2}
  (LX : localization-global-subuniverse ğ’« l3 X)
  (LY : localization-global-subuniverse ğ’« l4 Y)
  where

  map-localization-global-subuniverse :
    (X â†’ Y) â†’
    type-localization-global-subuniverse LX â†’
    type-localization-global-subuniverse LY
  map-localization-global-subuniverse =
    map-localization-global-subuniverse' ğ’« LX
      ( reflection-localization-global-subuniverse LY)

  eq-naturality-map-localization-global-subuniverse :
    (f : X â†’ Y) â†’
    map-localization-global-subuniverse f âˆ˜
    unit-localization-global-subuniverse LX ï¼
    unit-localization-global-subuniverse LY âˆ˜
    f
  eq-naturality-map-localization-global-subuniverse =
    eq-naturality-map-localization-global-subuniverse' ğ’« LX
      ( reflection-localization-global-subuniverse LY)

  naturality-map-localization-global-subuniverse :
    (f : X â†’ Y) â†’
    coherence-square-maps
      ( f)
      ( unit-localization-global-subuniverse LX)
      ( unit-localization-global-subuniverse LY)
      ( map-localization-global-subuniverse f)
  naturality-map-localization-global-subuniverse =
    naturality-map-localization-global-subuniverse' ğ’« LX
      ( reflection-localization-global-subuniverse LY)
```

### The functorial action on maps of types with localizations preserves homotopies

```agda
module _
  {Î± : Level â†’ Level} (ğ’« : global-subuniverse Î±)
  {l1 l2 l3 l4 : Level} {X : UU l1} {Y : UU l2}
  (LX : localization-global-subuniverse ğ’« l3 X)
  (LY : extension-type-global-subuniverse ğ’« l4 Y)
  where

  abstract
    preserves-htpy-map-localization-global-subuniverse' :
      {f g : X â†’ Y} â†’ f ~ g â†’
      map-localization-global-subuniverse' ğ’« LX LY f ~
      map-localization-global-subuniverse' ğ’« LX LY g
    preserves-htpy-map-localization-global-subuniverse' {f} =
      ind-htpy f
        ( Î» g _ â†’
          map-localization-global-subuniverse' ğ’« LX LY f ~
          map-localization-global-subuniverse' ğ’« LX LY g)
        ( refl-htpy)

module _
  {Î± : Level â†’ Level} (ğ’« : global-subuniverse Î±)
  {l1 l2 l3 l4 : Level} {X : UU l1} {Y : UU l2}
  (LX : localization-global-subuniverse ğ’« l3 X)
  (LY : localization-global-subuniverse ğ’« l4 Y)
  where

  preserves-htpy-map-localization-global-subuniverse :
    {f g : X â†’ Y} â†’ f ~ g â†’
    map-localization-global-subuniverse ğ’« LX LY f ~
    map-localization-global-subuniverse ğ’« LX LY g
  preserves-htpy-map-localization-global-subuniverse =
    preserves-htpy-map-localization-global-subuniverse' ğ’« LX
      ( reflection-localization-global-subuniverse LY)
```

## Properties

### The functorial action of on maps of types with localizations preserves identity functions

```agda
module _
  {Î± : Level â†’ Level} (ğ’« : global-subuniverse Î±)
  {l1 l2 : Level} {X : UU l1}
  (LX : localization-global-subuniverse ğ’« l2 X)
  where

  eq-preserves-id-map-localization-global-subuniverse :
    map-localization-global-subuniverse ğ’« LX LX id ï¼ id
  eq-preserves-id-map-localization-global-subuniverse =
    is-retraction-map-inv-is-equiv
      ( up-localization-global-subuniverse LX
        ( type-global-subuniverse-localization-global-subuniverse LX))
      ( id)

  preserves-id-map-localization-global-subuniverse :
    map-localization-global-subuniverse ğ’« LX LX id ~ id
  preserves-id-map-localization-global-subuniverse =
    htpy-eq eq-preserves-id-map-localization-global-subuniverse
```

### The functorial action of on maps of types with localizations preserves composition

```agda
module _
  {Î± : Level â†’ Level} (ğ’« : global-subuniverse Î±)
  {l1 l2 l3 l4 l5 l6 : Level}
  {X : UU l1} {Y : UU l2} {Z : UU l3}
  (LX : localization-global-subuniverse ğ’« l4 X)
  (LY : localization-global-subuniverse ğ’« l5 Y)
  (LZ : extension-type-global-subuniverse ğ’« l6 Z)
  (g : Y â†’ Z) (f : X â†’ Y)
  where

  eq-preserves-comp-map-localization-global-subuniverse' :
    map-localization-global-subuniverse' ğ’« LX LZ (g âˆ˜ f) ï¼
    map-localization-global-subuniverse' ğ’« LY LZ g âˆ˜
    map-localization-global-subuniverse ğ’« LX LY f
  eq-preserves-comp-map-localization-global-subuniverse' =
    equational-reasoning
    map-localization-global-subuniverse' ğ’« LX LZ (g âˆ˜ f)
    ï¼ ( map-inv-is-equiv
        ( up-localization-global-subuniverse LX
          ( type-global-subuniverse-extension-type-global-subuniverse ğ’« LZ))
        ( map-localization-global-subuniverse' ğ’« LY LZ g âˆ˜
          map-localization-global-subuniverse ğ’« LX LY f âˆ˜
          unit-localization-global-subuniverse LX))
    by
      ap
        ( map-inv-is-equiv
          ( up-localization-global-subuniverse LX
            ( type-global-subuniverse-extension-type-global-subuniverse ğ’« LZ)))
        ( inv
          ( ap
            ( map-localization-global-subuniverse' ğ’« LY LZ g âˆ˜_)
            ( eq-naturality-map-localization-global-subuniverse ğ’« LX LY f) âˆ™
            ap (_âˆ˜ f) (eq-naturality-map-localization-global-subuniverse' ğ’« LY LZ g)))
    ï¼ ( map-localization-global-subuniverse' ğ’« LY LZ g âˆ˜
        map-localization-global-subuniverse ğ’« LX LY f)
    by
      ( is-retraction-map-inv-is-equiv
        ( up-localization-global-subuniverse LX
          ( type-global-subuniverse-extension-type-global-subuniverse ğ’« LZ))
        ( map-localization-global-subuniverse' ğ’« LY LZ g âˆ˜
          map-localization-global-subuniverse ğ’« LX LY f))

  preserves-comp-map-localization-global-subuniverse' :
    map-localization-global-subuniverse' ğ’« LX LZ (g âˆ˜ f) ~
    map-localization-global-subuniverse' ğ’« LY LZ g âˆ˜
    map-localization-global-subuniverse ğ’« LX LY f
  preserves-comp-map-localization-global-subuniverse' =
    htpy-eq eq-preserves-comp-map-localization-global-subuniverse'

module _
  {Î± : Level â†’ Level} (ğ’« : global-subuniverse Î±)
  {l1 l2 l3 l4 l5 l6 : Level}
  {X : UU l1} {Y : UU l2} {Z : UU l3}
  (LX : localization-global-subuniverse ğ’« l4 X)
  (LY : localization-global-subuniverse ğ’« l5 Y)
  (LZ : localization-global-subuniverse ğ’« l6 Z)
  (g : Y â†’ Z) (f : X â†’ Y)
  where

  eq-preserves-comp-map-localization-global-subuniverse :
    map-localization-global-subuniverse ğ’« LX LZ (g âˆ˜ f) ï¼
    map-localization-global-subuniverse ğ’« LY LZ g âˆ˜
    map-localization-global-subuniverse ğ’« LX LY f
  eq-preserves-comp-map-localization-global-subuniverse =
    eq-preserves-comp-map-localization-global-subuniverse' ğ’«
      LX LY (reflection-localization-global-subuniverse LZ) g f

  preserves-comp-map-localization-global-subuniverse :
    map-localization-global-subuniverse ğ’« LX LZ (g âˆ˜ f) ~
    map-localization-global-subuniverse ğ’« LY LZ g âˆ˜
    map-localization-global-subuniverse ğ’« LX LY f
  preserves-comp-map-localization-global-subuniverse =
    preserves-comp-map-localization-global-subuniverse' ğ’«
      LX LY (reflection-localization-global-subuniverse LZ) g f
```

## References

{{#bibliography}} {{#reference Rij19}} {{#reference CORS20}}
