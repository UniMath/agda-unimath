---
title: Lifts of maps
---

```agda
module orthogonal-factorization-systems.lifting-problems where

open import foundation.cartesian-product-types
open import foundation.contractible-types
open import foundation.commuting-squares
open import foundation.dependent-pair-types
open import foundation.equality-dependent-pair-types
open import foundation.empty-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.functions
open import foundation.identity-types
open import foundation.homotopies
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universe-levels
open import foundation.universal-property-empty-type
open import foundation.unit-type


open import orthogonal-factorization-systems.extensions-of-maps
open import orthogonal-factorization-systems.lifts-of-maps
```

## Idea

A _lifting problem_ is the question of whether a given
commutative square

```md
       h
  A ------> B
  |         |
 f|         |g
  |         |
  V         V
  X ------> Y
       i
```

has a diagonal map `j : X â†’ B` such that the complete
diagram

```md
       h
  A ------> B
  |       ^ |
 f|   j  /  |g
  |    /    |
  V  /      V
  X ------> Y
       i
```

commutes. This we phrase as `j` being a simultaneous
extension of `h` along `f` and lift of `i` along `g`,
satisfying a coherence with the original commutativity
proof.

## Definition

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (h : A â†’ B) (f : A â†’ X) (g : B â†’ Y) (i : X â†’ Y) (H : coherence-square h f g i)
  where

  is-lifting-square : (j : X â†’ B) â†’ UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  is-lifting-square j = Î£
    ( is-extension f h j Ã— is-lift g i j)
    ( Î» (E , L) â†’ (H âˆ™h (g Â·l E)) ~ (L Â·r f))

  lifting-square : UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  lifting-square = Î£ (X â†’ B) (is-lifting-square)

  is-extension-is-lifting-square :
    (j : X â†’ B) â†’ is-lifting-square j â†’ is-extension f h j
  is-extension-is-lifting-square j = pr1 âˆ˜ pr1

  map-extension-lifting-square : lifting-square â†’ extension f (Î» _ â†’ B) h
  pr1 (map-extension-lifting-square L) = pr1 L
  pr2 (map-extension-lifting-square L) = pr1 (pr1 (pr2 L))

  is-lift-is-lifting-square :
    (j : X â†’ B) â†’ is-lifting-square j â†’ is-lift g i j
  is-lift-is-lifting-square j = pr2 âˆ˜ pr1

  map-lift-lifting-square : lifting-square â†’ lift g i
  pr1 (map-lift-lifting-square L) = pr1 L
  pr2 (map-lift-lifting-square L) = pr2 (pr1 (pr2 L))
```

## Properties

The diagram

```md
  A         B
  |       ^ |
 f|   j  /  |g
  |    /    |
  V  /      V
  X         Y
```

gives rise to a lifting square.

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y)
  where

  is-lifting-square-spine : (j : X â†’ B) â†’ is-lifting-square (j âˆ˜ f) f g (g âˆ˜ j) refl-htpy j
  pr1 (pr1 (is-lifting-square-spine j)) = refl-htpy
  pr2 (pr1 (is-lifting-square-spine j)) = refl-htpy
  pr2 (is-lifting-square-spine j) = refl-htpy
```

### Extensions as lifting squares

Extensions can be canonically interpreted as lifting squares where the terminal vertex is the terminal type.

```md
       h
  A ------> B
  |       ^ |
 f|   j  /  |
  |    /    |
  V  /      V
  X ----> unit
```

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (h : A â†’ B) (f : A â†’ X)
  where

  map-lifting-square-extension : extension f (Î» _ â†’ B) h â†’ lifting-square h f (Î» _ â†’ star) (Î» _ â†’ star) refl-htpy
  pr1 (map-lifting-square-extension (j , H)) = j
  pr1 (pr1 (pr2 (map-lifting-square-extension (j , H)))) = H
  pr2 (pr1 (pr2 (map-lifting-square-extension (j , H)))) = refl-htpy
  pr2 (pr2 (map-lifting-square-extension (j , H))) _ = eq-is-contr (is-prop-unit star star)

  isretr-map-lifting-square-extension :
    ((map-extension-lifting-square h f (Î» _ â†’ star) (Î» _ â†’ star) refl-htpy) âˆ˜ map-lifting-square-extension) ~ id
  isretr-map-lifting-square-extension = refl-htpy

  issec-map-lifting-square-extension :
    (map-lifting-square-extension âˆ˜ (map-extension-lifting-square h f (Î» _ â†’ star) (Î» _ â†’ star) refl-htpy)) ~ id
  issec-map-lifting-square-extension el =
    eq-pair-Î£ refl
      ( eq-pair-Î£
        ( eq-pair-Î£
          refl
          ( eq-is-contr
            ( is-contr-is-lift (Î» _ â†’ star) (Î» _ â†’ star) is-prop-unit (pr1 el))))
        (eq-htpy Î» x â†’ eq-is-contr (is-trunc-is-contr zero-ð•‹ is-contr-unit _ _ _ _)))

  is-equiv-map-lifting-square-extension : is-equiv map-lifting-square-extension
  is-equiv-map-lifting-square-extension =
    is-equiv-has-inverse
      ( map-extension-lifting-square h f (Î» _ â†’ star) (Î» _ â†’ star) refl-htpy)
      ( issec-map-lifting-square-extension)
      ( isretr-map-lifting-square-extension)
  
  equiv-lifting-square-extension :
    extension f (Î» _ â†’ B) h â‰ƒ lifting-square h f (Î» _ â†’ star) (Î» _ â†’ star) refl-htpy
  pr1 equiv-lifting-square-extension = map-lifting-square-extension
  pr2 equiv-lifting-square-extension = is-equiv-map-lifting-square-extension

  is-equiv-map-extension-lifting-square :
    is-equiv (map-extension-lifting-square h f (Î» _ â†’ star) (Î» _ â†’ star) refl-htpy)
  is-equiv-map-extension-lifting-square =
    is-equiv-has-inverse
      ( map-lifting-square-extension)
      ( isretr-map-lifting-square-extension)
      ( issec-map-lifting-square-extension)

  equiv-extension-lifting-square :
    lifting-square h f (Î» _ â†’ star) (Î» _ â†’ star) refl-htpy â‰ƒ extension f (Î» _ â†’ B) h
  pr1 equiv-extension-lifting-square = map-extension-lifting-square h f (Î» _ â†’ star) (Î» _ â†’ star) refl-htpy
  pr2 equiv-extension-lifting-square = is-equiv-map-extension-lifting-square
```

### Lifts as lifting squares

Lifts can be canonically interpreted as lifting squares where the initial vertex is the initial type.

```md
 empty ---> B
  |       ^ |
  |   j  /  |g
  |    /    |
  V  /      V
  X ------> Y
       i
```

```agda
module _
  {l2 l3 l4 : Level} {B : UU l2} {X : UU l3} {Y : UU l4}
  (g : B â†’ Y) (i : X â†’ Y)
  where

  map-lifting-square-lift : lift g i â†’ lifting-square ex-falso ex-falso g i ind-empty
  pr1 (map-lifting-square-lift (j , H)) = j
  pr1 (pr1 (pr2 (map-lifting-square-lift (j , H)))) = ind-empty
  pr2 (pr1 (pr2 (map-lifting-square-lift (j , H)))) = H
  pr2 (pr2 (map-lifting-square-lift (j , H))) = ind-empty

  isretr-map-lifting-square-lift :
    ((map-lift-lifting-square ex-falso ex-falso g i ind-empty) âˆ˜ map-lifting-square-lift) ~ id
  isretr-map-lifting-square-lift = refl-htpy

  issec-map-lifting-square-lift :
    (map-lifting-square-lift âˆ˜ (map-lift-lifting-square ex-falso ex-falso g i ind-empty)) ~ id
  issec-map-lifting-square-lift el =
    eq-pair-Î£
      refl 
      ( eq-pair-Î£
        ( eq-pair-Î£
          (eq-htpy ind-empty)
          (tr-const (eq-htpy ind-empty) (pr2 (pr1 (pr2 el)))))
        ( eq-htpy ind-empty))

  is-equiv-map-lifting-square-lift : is-equiv map-lifting-square-lift
  is-equiv-map-lifting-square-lift =
    is-equiv-has-inverse
      ( map-lift-lifting-square ex-falso ex-falso g i ind-empty)
      ( issec-map-lifting-square-lift)
      ( isretr-map-lifting-square-lift)

  equiv-lifting-square-lift :
    lift g i â‰ƒ lifting-square ex-falso ex-falso g i ind-empty
  pr1 equiv-lifting-square-lift = map-lifting-square-lift
  pr2 equiv-lifting-square-lift = is-equiv-map-lifting-square-lift

  is-equiv-map-lift-lifting-square : is-equiv (map-lift-lifting-square ex-falso ex-falso g i ind-empty)
  is-equiv-map-lift-lifting-square =
    is-equiv-has-inverse
      ( map-lifting-square-lift)
      ( isretr-map-lifting-square-lift)
      ( issec-map-lifting-square-lift)

  equiv-lift-lifting-square :
    lifting-square ex-falso ex-falso g i ind-empty â‰ƒ lift g i
  pr1 equiv-lift-lifting-square = map-lift-lifting-square ex-falso ex-falso g i ind-empty
  pr2 equiv-lift-lifting-square = is-equiv-map-lift-lifting-square
```
