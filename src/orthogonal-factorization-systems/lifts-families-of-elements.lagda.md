# Lifts of families of elements

```agda
module orthogonal-factorization-systems.lifts-families-of-elements where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.precomposition-functions
open import foundation.precomposition-type-families
open import foundation.transport-along-homotopies
open import foundation.transport-along-identifications
open import foundation.universe-levels
```

</details>

## Idea

Consider a type family

```text
  B : (i : I) â†’ A i â†’ ğ’°
```

and a family of elements `a : (i : I) â†’ A i`.

A {{#concept "dependent lift" Disambiguation="family of elements"}} of the
family of elements `a` to the type family `B` is a family of elements

```text
  (i : I) â†’ B i (a i).
```

An important special case occurs when `a : I â†’ A` is a family of elements of a
fixed type `A`, and `B` is a type family over `A`. In this case, a
{{#concept "lift" Disambiguation="family of elements"}} of the family of
elements `a` is a family of elements

```text
  (i : I) â†’ B (a i).
```

To see how these families relate to
[lifts of maps](orthogonal-factorization-systems.lifts-of-maps.md), consider the
lifting diagram

```text
      Î£ (x : A) (B x)
            |
            | pr1
            |
            v
  I ------> A         .
       a
```

Then a lift of the map `a` against `pr1` is a map `b : I â†’ Î£ A B`, such that the
triangle commutes. Invoking the
[type theoretic principle of choice](foundation.type-theoretic-principle-of-choice.md),
we can show that this type is equivalent to the type of families of elements
`(i : I) â†’ B (a i)`:

```text
  Î£ (b : I â†’ Î£ A B) ((i : I) â†’ a i ï¼ pr1 (b i))
    â‰ƒ (i : I) â†’ Î£ ((x , b) : Î£ A B) (a i ï¼ x)
    â‰ƒ (i : I) â†’ Î£ (x : A) (a i ï¼ x Ã— B x)
    â‰ƒ (i : I) â†’ B (a i) .
```

The first equivalence is the principle of choice, the second is associativity of
dependent pair types, and the third is the left unit law of dependent pair
types, since `Î£ (x : A) (a i ï¼ x)` is
[contractible](foundation.contractible-types.md).

## Definitions

### Dependent lifts of families of elements

```agda
module _
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} (B : (i : I) â†’ A i â†’ UU l3)
  (a : (i : I) â†’ A i)
  where

  dependent-lift-family-of-elements : UU (l1 âŠ” l3)
  dependent-lift-family-of-elements = (i : I) â†’ B i (a i)
```

### Lifts of families of elements

```agda
module _
  {l1 l2 l3 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) (a : I â†’ A)
  where

  lift-family-of-elements : UU (l1 âŠ” l3)
  lift-family-of-elements = dependent-lift-family-of-elements (Î» _ â†’ B) a
```

### Total space of dependent lifts of families of elements

```agda
module _
  {l1 l2 l3 : Level} {I : UU l1} (A : I â†’ UU l2) (B : (i : I) â†’ A i â†’ UU l3)
  where

  total-dependent-lift-family-of-elements : UU (l1 âŠ” l2 âŠ” l3)
  total-dependent-lift-family-of-elements =
    Î£ ( (i : I) â†’ A i)
      ( dependent-lift-family-of-elements B)
```

### Total space of lifts of families of elements

```agda
module _
  {l1 l2 l3 : Level} (I : UU l1) {A : UU l2} (B : A â†’ UU l3)
  where

  total-lift-family-of-elements : UU (l1 âŠ” l2 âŠ” l3)
  total-lift-family-of-elements =
    total-dependent-lift-family-of-elements (Î» (_ : I) â†’ A) (Î» _ â†’ B)
```

### Dependent lifts of binary families of elements

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2} {B : (i : I) â†’ A i â†’ UU l3}
  (C : (i : I) (x : A i) â†’ B i x â†’ UU l4) (a : (i : I) â†’ A i)
  where

  dependent-lift-binary-family-of-elements : UU (l1 âŠ” l3 âŠ” l4)
  dependent-lift-binary-family-of-elements =
    dependent-lift-family-of-elements (Î» i x â†’ (y : B i x) â†’ C i x y) a
```

### Lifts of binary families of elements

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} {B : A â†’ UU l3}
  {C : (x : A) â†’ B x â†’ UU l4} (a : I â†’ A)
  where

  lift-binary-family-of-elements : UU (l1 âŠ” l3 âŠ” l4)
  lift-binary-family-of-elements =
    dependent-lift-binary-family-of-elements (Î» _ â†’ C) a
```

## Properties

### Transport in lifts of families of elements

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) (a : I â†’ A)
  {J : UU l4} {f : J â†’ I}
  where

  statement-tr-lift-family-of-elements : {g : J â†’ I} (H : f ~ g) â†’ UU (l3 âŠ” l4)
  statement-tr-lift-family-of-elements H =
    tr (lift-family-of-elements B) (htpy-precomp H A a) ~
    tr-htpy (Î» _ â†’ precomp-family a B) H

  tr-lift-family-of-elements-refl-htpy :
    statement-tr-lift-family-of-elements refl-htpy
  tr-lift-family-of-elements-refl-htpy b =
    ap
      ( Î» p â†’ tr (lift-family-of-elements B) p b)
      ( compute-htpy-precomp-refl-htpy f A a)

  tr-lift-family-of-elements :
    {g : J â†’ I} (H : f ~ g) â†’ statement-tr-lift-family-of-elements H
  tr-lift-family-of-elements =
    ind-htpy f
      ( Î» g â†’ statement-tr-lift-family-of-elements)
      ( tr-lift-family-of-elements-refl-htpy)

  abstract
    compute-tr-lift-family-of-elements :
      tr-lift-family-of-elements refl-htpy ï¼
      tr-lift-family-of-elements-refl-htpy
    compute-tr-lift-family-of-elements =
      compute-ind-htpy f
        ( Î» g â†’ statement-tr-lift-family-of-elements)
        ( tr-lift-family-of-elements-refl-htpy)
```

## See also

- [Double lifts of families of elements](orthogonal-factorization-systems.double-lifts-families-of-elements.md)
- [Lifts of maps](orthogonal-factorization-systems.lifts-of-maps.md)
