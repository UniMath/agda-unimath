# Postcomposition of extensions of maps

```agda
module orthogonal-factorization-systems.postcomposition-extensions-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-dependent-functions
open import foundation.action-on-identifications-functions
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.functoriality-function-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.monomorphisms
open import foundation.postcomposition-functions
open import foundation.propositions
open import foundation.sets
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.truncated-maps
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.torsorial-type-families

open import orthogonal-factorization-systems.extensions-maps
```

</details>

## Idea

Given a map `i : A ‚Üí B` and a map `f : A ‚Üí X`, then we may
{{#concept "postcompose" Disambiguation="extension of map by map" Agda=postcomp-extension}}
any [extension](orthogonal-factorization-systems.extensions-maps.md)
`Œ± : extension i f` by `g` to obtain an extension `g ‚àò Œ± : extension i (g ‚àò f)`.

## Definition

### Postcomposition of extensions

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  postcomp-extension :
    (i : A ‚Üí B) (f : A ‚Üí X) (g : X ‚Üí Y) ‚Üí
    extension i f ‚Üí extension i (g ‚àò f)
  postcomp-extension i f g =
    map-Œ£ (is-extension i (g ‚àò f)) (postcomp B g) (Œª j H ‚Üí g ¬∑l H)
```

## Properties

### Postcomposition of extensions by an equivalence is an equivalence

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-equiv-postcomp-extension :
    (f : A ‚Üí B) (i : A ‚Üí X) (g : X ‚Üí Y) ‚Üí is-equiv g ‚Üí
    is-equiv (postcomp-extension f i g)
  is-equiv-postcomp-extension f i g G =
    is-equiv-map-Œ£
      ( is-extension f (g ‚àò i))
      ( is-equiv-postcomp-is-equiv g G B)
      ( Œª j ‚Üí
        is-equiv-map-Œ†-is-fiberwise-equiv
          ( Œª x ‚Üí is-emb-is-equiv G (i x) (j (f x))))

  equiv-postcomp-extension :
    (f : A ‚Üí B) (i : A ‚Üí X) (g : X ‚âÉ Y) ‚Üí
    extension f i ‚âÉ extension f (map-equiv g ‚àò i)
  equiv-postcomp-extension f i (g , G) =
    ( postcomp-extension f i g , is-equiv-postcomp-extension f i g G)
```

### Postcomposition of extensions by an embedding is an embedding

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-emb-postcomp-extension :
    (f : A ‚Üí B) (i : A ‚Üí X) (g : X ‚Üí Y) ‚Üí is-emb g ‚Üí
    is-emb (postcomp-extension f i g)
  is-emb-postcomp-extension f i g H =
    is-emb-map-Œ£
      ( is-extension f (g ‚àò i))
      ( is-mono-is-emb g H B)
      ( Œª j ‚Üí
        is-emb-is-equiv
          ( is-equiv-map-Œ†-is-fiberwise-equiv
            ( Œª x ‚Üí H (i x) (j (f x)))))

  emb-postcomp-extension :
    (f : A ‚Üí B) (i : A ‚Üí X) (g : X ‚Ü™ Y) ‚Üí
    extension f i ‚Ü™ extension f (map-emb g ‚àò i)
  emb-postcomp-extension f i (g , G) =
    postcomp-extension f i g , is-emb-postcomp-extension f i g G
```

### Postcomposition of extensions by a `k`-truncated map is `k`-truncated

```agda
module _
  {l1 l2 l3 l4 : Level} (k : ùïã) {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-trunc-map-postcomp-extension :
    (f : A ‚Üí B) (i : A ‚Üí X) (g : X ‚Üí Y) ‚Üí is-trunc-map k g ‚Üí
    is-trunc-map k (postcomp-extension f i g)
  is-trunc-map-postcomp-extension f i g G =
    is-trunc-map-Œ£ k
      ( is-extension f (g ‚àò i))
      ( is-trunc-map-postcomp-is-trunc-map k g G B)
      ( Œª j ‚Üí
        is-trunc-map-Œ† k
          ( Œª a ‚Üí ap g)
          ( Œª a ‚Üí is-trunc-map-ap-is-trunc-map k g G (i a) (j (f a))))

  trunc-map-postcomp-extension :
    (f : A ‚Üí B) (i : A ‚Üí X) (g : trunc-map k X Y) ‚Üí
    trunc-map k (extension f i) (extension f (map-trunc-map g ‚àò i))
  trunc-map-postcomp-extension f i (g , G) =
    ( postcomp-extension f i g , is-trunc-map-postcomp-extension f i g G)
```
