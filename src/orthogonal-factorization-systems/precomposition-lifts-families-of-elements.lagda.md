# Precomposition of lifts of families of elements by maps

```agda
module orthogonal-factorization-systems.precomposition-lifts-families-of-elements where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-dependent-functions
open import foundation.commuting-squares-of-homotopies
open import foundation.commuting-squares-of-maps
open import foundation.commuting-triangles-of-maps
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.path-algebra
open import foundation.precomposition-functions
open import foundation.transport-along-identifications
open import foundation.type-theoretic-principle-of-choice
open import foundation.universe-levels
open import foundation.whiskering-homotopies

open import orthogonal-factorization-systems.lifts-families-of-elements
```

</details>

## Idea

Consider a type family `B : A ‚Üí ùì§` and a map `a : I ‚Üí A`. Then, given a map
`f : J ‚Üí I`, we may pull back a
[lift](orthogonal-factorization-systems.lifts-families-of-elements.md) of `a` to
a lift of `a ‚àò f`.

In other words, given a diagram

```text
                Œ£ (x : A) (B x)
                      |
                      | pr1
                      |
                      v
  J ------> I ------> A         ,
       f         a
```

we get a map of diagonals

```text
  ((i : I) ‚Üí B (a i)) ‚Üí ((j : J) ‚Üí B (a (f j))) .
```

This map of lifts induces a map from lifted families of elements indexed by `I`
to lifted families of elements indexed by `J`.

## Definitions

### Precomposition of lifts of families of elements by functions

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A ‚Üí UU l3) {J : UU l4}
  (f : J ‚Üí I)
  where

  precomp-lift-family-of-elements :
    (a : I ‚Üí A) ‚Üí
    lift-family-of-elements B a ‚Üí lift-family-of-elements B (a ‚àò f)
  precomp-lift-family-of-elements a b i = b (f i)
```

### Precomposition in lifted families of elements

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A ‚Üí UU l3) {J : UU l4}
  (f : J ‚Üí I)
  where

  precomp-lifted-family-of-elements :
    lifted-family-of-elements I B ‚Üí lifted-family-of-elements J B
  precomp-lifted-family-of-elements =
    map-Œ£
      ( lift-family-of-elements B)
      ( precomp f A)
      ( precomp-lift-family-of-elements B f)
```

## Properties

### Homotopies between maps induce commuting triangles of precompositions of lifts of families of elements

Consider two maps `f, g : J ‚Üí I` and a homotopy `H : f ~ g` between them. The
precomposition functions they induce on lifts of families of elements have
different codomains, namely `lift-family-of-elements B (a ‚àò f)` and
`lift-family-of-elements B (a ‚àò g)`, but they fit into a
[commuting triangle](foundation.commuting-triangles-of-maps.md) with
[transport](foundation.transport-along-identifications.md) in the type of lifts:

```text
                              precomp-lift B f a
  lift-family-of-elements B a ------------------> lift-family-of-elements B (a ‚àò f)
                      \                                /
                         \                          /
                            \                    /
           precomp-lift B g a  \              / tr (lift-family-of-elements B) (htpy-precomp H A a)
                                  \        /
                                     V  V
                       lift-family-of-elements B (a ‚àò g)
```

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A ‚Üí UU l3) (a : I ‚Üí A)
  {J : UU l4} {f : J ‚Üí I}
  where

  statement-triangle-precomp-lift-family-of-elements-htpy :
    {g : J ‚Üí I} (H : f ~ g) ‚Üí UU (l1 ‚äî l3 ‚äî l4)
  statement-triangle-precomp-lift-family-of-elements-htpy {g} H =
    coherence-triangle-maps'
      ( precomp-lift-family-of-elements B g a)
      ( tr (lift-family-of-elements B) (htpy-precomp H A a))
      ( precomp-lift-family-of-elements B f a)

  triangle-precomp-lift-family-of-elements-htpy-refl-htpy :
    statement-triangle-precomp-lift-family-of-elements-htpy refl-htpy
  triangle-precomp-lift-family-of-elements-htpy-refl-htpy b =
    tr-lift-family-of-elements-precomp B a refl-htpy (b ‚àò f)

  triangle-precomp-lift-family-of-elements-htpy :
    {g : J ‚Üí I} (H : f ~ g) ‚Üí
    statement-triangle-precomp-lift-family-of-elements-htpy H
  triangle-precomp-lift-family-of-elements-htpy =
    ind-htpy f
      ( Œª g ‚Üí statement-triangle-precomp-lift-family-of-elements-htpy)
      ( triangle-precomp-lift-family-of-elements-htpy-refl-htpy)

  abstract
    compute-triangle-precomp-lift-family-of-elements-htpy :
      triangle-precomp-lift-family-of-elements-htpy refl-htpy Ôºù
      triangle-precomp-lift-family-of-elements-htpy-refl-htpy
    compute-triangle-precomp-lift-family-of-elements-htpy =
      compute-ind-htpy f
        ( Œª g ‚Üí statement-triangle-precomp-lift-family-of-elements-htpy)
        ( triangle-precomp-lift-family-of-elements-htpy-refl-htpy)
```

### TODO

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A ‚Üí UU l3) (a : I ‚Üí A)
  {J : UU l4} {f : J ‚Üí I}
  where

  statement-coherence-triangle-precomp-lift-family-of-elements-htpy :
    {g : J ‚Üí I} (H : f ~ g) ‚Üí UU (l1 ‚äî l3 ‚äî l4)
  statement-coherence-triangle-precomp-lift-family-of-elements-htpy H =
    ( triangle-precomp-lift-family-of-elements-htpy B a H) ~
    ( ( ( tr-lift-family-of-elements-precomp B a H) ¬∑r
        ( precomp-lift-family-of-elements B f a)) ‚àôh
      ( Œª b ‚Üí eq-htpy (Œª j ‚Üí apd b (H j))))

  coherence-triangle-precomp-lift-family-of-elements-htpy-refl-htpy :
    statement-coherence-triangle-precomp-lift-family-of-elements-htpy
      ( refl-htpy)
  coherence-triangle-precomp-lift-family-of-elements-htpy-refl-htpy b =
    ( htpy-eq (compute-triangle-precomp-lift-family-of-elements-htpy B a) b) ‚àô
    ( inv right-unit) ‚àô
    ( identification-left-whisk
      ( triangle-precomp-lift-family-of-elements-htpy-refl-htpy B a b)
      ( inv (eq-htpy-refl-htpy (b ‚àò f))))

  coherence-triangle-precomp-lift-family-of-elements-htpy :
    {g : J ‚Üí I} (H : f ~ g) ‚Üí
    statement-coherence-triangle-precomp-lift-family-of-elements-htpy H
  coherence-triangle-precomp-lift-family-of-elements-htpy =
    ind-htpy f
      ( Œª g ‚Üí
        statement-coherence-triangle-precomp-lift-family-of-elements-htpy)
      ( coherence-triangle-precomp-lift-family-of-elements-htpy-refl-htpy)

  abstract
    compute-coherence-triangle-precomp-lift-family-of-elements-htpy :
      coherence-triangle-precomp-lift-family-of-elements-htpy refl-htpy Ôºù
      coherence-triangle-precomp-lift-family-of-elements-htpy-refl-htpy
    compute-coherence-triangle-precomp-lift-family-of-elements-htpy =
      compute-ind-htpy f
        ( Œª g ‚Üí
          statement-coherence-triangle-precomp-lift-family-of-elements-htpy)
        ( coherence-triangle-precomp-lift-family-of-elements-htpy-refl-htpy)
```

### TODO

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A ‚Üí UU l3) {J : UU l4}
  (f : J ‚Üí I)
  where

  coherence-square-precomp-map-inv-distributive-Œ†-Œ£ :
    coherence-square-maps
      ( precomp-lifted-family-of-elements B f)
      ( map-inv-distributive-Œ†-Œ£)
      ( map-inv-distributive-Œ†-Œ£)
      ( precomp f (Œ£ A B))
  coherence-square-precomp-map-inv-distributive-Œ†-Œ£ = refl-htpy
```

### TODO

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A ‚Üí UU l3) {J : UU l4}
  {f : J ‚Üí I}
  where

  htpy-precomp-lifted-family-of-elements-htpy :
    {g : J ‚Üí I} (H : f ~ g) ‚Üí
    ( precomp-lifted-family-of-elements B f) ~
    ( precomp-lifted-family-of-elements B g)
  htpy-precomp-lifted-family-of-elements-htpy H =
    htpy-map-Œ£
      ( lift-family-of-elements B)
      ( htpy-precomp H A)
      ( precomp-lift-family-of-elements B f)
      ( Œª a ‚Üí triangle-precomp-lift-family-of-elements-htpy B a H)

  abstract
    compute-htpy-precomp-lifted-family-of-elements-htpy :
      htpy-precomp-lifted-family-of-elements-htpy refl-htpy ~
      refl-htpy
    compute-htpy-precomp-lifted-family-of-elements-htpy =
      htpy-htpy-map-Œ£-refl-htpy
        ( lift-family-of-elements B)
        ( compute-htpy-precomp-refl-htpy f A)
        ( Œª a ‚Üí
          ( htpy-eq
            ( compute-triangle-precomp-lift-family-of-elements-htpy B a)) ‚àôh
          ( Œª b ‚Üí
            htpy-eq (compute-tr-lift-family-of-elements-precomp B a) (b ‚àò f)))
```

### TODO

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A ‚Üí UU l3) {J : UU l4}
  {f : J ‚Üí I}
  where

  statement-coherence-blabla :
    {g : J ‚Üí I} (H : f ~ g) ‚Üí UU (l1 ‚äî l2 ‚äî l3 ‚äî l4)
  statement-coherence-blabla {g} H =
    coherence-square-homotopies
      ( htpy-precomp H (Œ£ A B) ¬∑r map-inv-distributive-Œ†-Œ£)
      ( coherence-square-precomp-map-inv-distributive-Œ†-Œ£ B f)
      ( coherence-square-precomp-map-inv-distributive-Œ†-Œ£ B g)
      ( ( map-inv-distributive-Œ†-Œ£) ¬∑l
        ( htpy-precomp-lifted-family-of-elements-htpy B H))

  coherence-blabla-refl-htpy : statement-coherence-blabla refl-htpy
  coherence-blabla-refl-htpy =
    ( ap-left-whisk-htpy
      ( map-inv-distributive-Œ†-Œ£)
      ( compute-htpy-precomp-lifted-family-of-elements-htpy B)) ‚àôh
    ( inv-htpy
      ( Œª h ‚Üí
        compute-htpy-precomp-refl-htpy f
          ( Œ£ A B)
          ( map-inv-distributive-Œ†-Œ£ h))) ‚àôh
    ( inv-htpy-right-unit-htpy)

  coherence-blabla :
    {g : J ‚Üí I} (H : f ~ g) ‚Üí statement-coherence-blabla H
  coherence-blabla =
    ind-htpy f
      ( Œª g ‚Üí statement-coherence-blabla)
      ( coherence-blabla-refl-htpy)
```
