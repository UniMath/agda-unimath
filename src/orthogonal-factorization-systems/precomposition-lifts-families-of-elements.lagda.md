# Precomposition of lifts of families of elements by maps

```agda
module orthogonal-factorization-systems.precomposition-lifts-families-of-elements where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-dependent-functions
open import foundation.commuting-squares-of-homotopies
open import foundation.commuting-squares-of-maps
open import foundation.commuting-triangles-of-maps
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.precomposition-functions
open import foundation.transport-along-identifications
open import foundation.type-theoretic-principle-of-choice
open import foundation.universe-levels
open import foundation.whiskering-higher-homotopies-composition
open import foundation.whiskering-homotopies-composition
open import foundation.whiskering-identifications-concatenation

open import orthogonal-factorization-systems.lifts-families-of-elements
```

</details>

## Idea

Consider a type family `B : A â†’ ð’°` and a map `a : I â†’ A`. Then, given a map
`f : J â†’ I`, we may pull back a
[lift](orthogonal-factorization-systems.lifts-families-of-elements.md) of `a` to
a lift of `a âˆ˜ f`.

In other words, given a diagram

```text
                Î£ (x : A) (B x)
                      |
                      | pr1
                      |
                      âˆ¨
  J ------> I ------> A         ,
       f         a
```

we get a map of lifts of families of elements

```text
  ((i : I) â†’ B (a i)) â†’ ((j : J) â†’ B (a (f j))) .
```

This map of lifts induces a map from lifted families of elements indexed by `I`
to lifted families of elements indexed by `J`.

## Definitions

### Precomposition of lifts of families of elements by functions

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) {J : UU l4}
  (f : J â†’ I)
  where

  precomp-lift-family-of-elements :
    (a : I â†’ A) â†’
    lift-family-of-elements B a â†’ lift-family-of-elements B (a âˆ˜ f)
  precomp-lift-family-of-elements a b i = b (f i)
```

### Precomposition in lifted families of elements

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) {J : UU l4}
  (f : J â†’ I)
  where

  precomp-lifted-family-of-elements :
    lifted-family-of-elements I B â†’ lifted-family-of-elements J B
  precomp-lifted-family-of-elements =
    map-Î£
      ( lift-family-of-elements B)
      ( precomp f A)
      ( precomp-lift-family-of-elements B f)
```

## Properties

### Homotopies between maps induce commuting triangles of precompositions of lifts of families of elements

Consider two maps `f, g : J â†’ I` and a homotopy `H : f ~ g` between them. The
precomposition functions they induce on lifts of families of elements have
different codomains, namely `lift-family-of-elements B (a âˆ˜ f)` and
`lift-family-of-elements B (a âˆ˜ g)`, but they fit into a
[commuting triangle](foundation.commuting-triangles-of-maps.md) with
[transport](foundation.transport-along-identifications.md) in the type of lifts:

```text
                              precomp-lift B f a
  lift-family-of-elements B a ------------------> lift-family-of-elements B (a âˆ˜ f)
                      \                                /
                         \                          /
                            \                    /
           precomp-lift B g a  \              / tr (lift-family-of-elements B) (htpy-precomp H A a)
                                  \        /
                                     âˆ¨  âˆ¨
                       lift-family-of-elements B (a âˆ˜ g)
```

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) (a : I â†’ A)
  {J : UU l4} {f : J â†’ I}
  where

  statement-triangle-precomp-lift-family-of-elements-htpy :
    {g : J â†’ I} (H : f ~ g) â†’ UU (l1 âŠ” l3 âŠ” l4)
  statement-triangle-precomp-lift-family-of-elements-htpy {g} H =
    coherence-triangle-maps'
      ( precomp-lift-family-of-elements B g a)
      ( tr (lift-family-of-elements B) (htpy-precomp H A a))
      ( precomp-lift-family-of-elements B f a)

  triangle-precomp-lift-family-of-elements-htpy-refl-htpy :
    statement-triangle-precomp-lift-family-of-elements-htpy refl-htpy
  triangle-precomp-lift-family-of-elements-htpy-refl-htpy b =
    tr-lift-family-of-elements-precomp B a refl-htpy (b âˆ˜ f)

  abstract
    triangle-precomp-lift-family-of-elements-htpy :
      {g : J â†’ I} (H : f ~ g) â†’
      statement-triangle-precomp-lift-family-of-elements-htpy H
    triangle-precomp-lift-family-of-elements-htpy =
      ind-htpy f
        ( Î» g â†’ statement-triangle-precomp-lift-family-of-elements-htpy)
        ( triangle-precomp-lift-family-of-elements-htpy-refl-htpy)

    compute-triangle-precomp-lift-family-of-elements-htpy :
      triangle-precomp-lift-family-of-elements-htpy refl-htpy ï¼
      triangle-precomp-lift-family-of-elements-htpy-refl-htpy
    compute-triangle-precomp-lift-family-of-elements-htpy =
      compute-ind-htpy f
        ( Î» g â†’ statement-triangle-precomp-lift-family-of-elements-htpy)
        ( triangle-precomp-lift-family-of-elements-htpy-refl-htpy)
```

### `triangle-precomp-lift-family-of-elements-htpy` factors through transport along a homotopy in the famiy `B âˆ˜ a`

Instead of defining the homotopy `triangle-precomp-lift-family-of-elements-htpy`
by homotopy induction, we could have defined it manually using the
characterization of transport in the type of lifts of a family of elements.

We show that these two definitions are homotopic.

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) (a : I â†’ A)
  {J : UU l4} {f : J â†’ I}
  where

  statement-coherence-triangle-precomp-lift-family-of-elements :
    {g : J â†’ I} (H : f ~ g) â†’ UU (l1 âŠ” l3 âŠ” l4)
  statement-coherence-triangle-precomp-lift-family-of-elements H =
    ( triangle-precomp-lift-family-of-elements-htpy B a H) ~
    ( ( ( tr-lift-family-of-elements-precomp B a H) Â·r
        ( precomp-lift-family-of-elements B f a)) âˆ™h
      ( Î» b â†’ eq-htpy (Î» j â†’ apd b (H j))))

  coherence-triangle-precomp-lift-family-of-elements-refl-htpy :
    statement-coherence-triangle-precomp-lift-family-of-elements
      ( refl-htpy)
  coherence-triangle-precomp-lift-family-of-elements-refl-htpy b =
    ( htpy-eq (compute-triangle-precomp-lift-family-of-elements-htpy B a) b) âˆ™
    ( inv right-unit) âˆ™
    ( left-whisker-concat
      ( triangle-precomp-lift-family-of-elements-htpy-refl-htpy B a b)
      ( inv (eq-htpy-refl-htpy (b âˆ˜ f))))

  abstract
    coherence-triangle-precomp-lift-family-of-elements :
      {g : J â†’ I} (H : f ~ g) â†’
      statement-coherence-triangle-precomp-lift-family-of-elements H
    coherence-triangle-precomp-lift-family-of-elements =
      ind-htpy f
        ( Î» g â†’
          statement-coherence-triangle-precomp-lift-family-of-elements)
        ( coherence-triangle-precomp-lift-family-of-elements-refl-htpy)

    compute-coherence-triangle-precomp-lift-family-of-elements :
      coherence-triangle-precomp-lift-family-of-elements refl-htpy ï¼
      coherence-triangle-precomp-lift-family-of-elements-refl-htpy
    compute-coherence-triangle-precomp-lift-family-of-elements =
      compute-ind-htpy f
        ( Î» g â†’
          statement-coherence-triangle-precomp-lift-family-of-elements)
        ( coherence-triangle-precomp-lift-family-of-elements-refl-htpy)
```

### `precomp-lifted-family-of-elements` is homotopic to the precomposition map on functions up to equivalence

We have a [commuting square](foundation.commuting-squares-of-maps.md) like this:

```text
                                     precomp-lifted-family f
  Î£ (a : I â†’ A) ((i : I) â†’ B (a i)) ------------------------> Î£ (a : J â†’ A) ((j : J) â†’ B (a j))
                  |                                                           |
                  |                                                           |
                  | map-inv-distributive-Î -Î£    â‡—    map-inv-distributive-Î -Î£ |
                  |                                                           |
                  âˆ¨                                                           âˆ¨
              I â†’ Î£ A B ------------------------------------------------> J â†’ Î£ A B ,
                                               - âˆ˜ f
```

which shows that `precomp-lifted-family-of-elements f` is a good choice for a
precomposition map in the type of lifted families of elements, since it's
homotopic to the regular precomposition map up to equivalence.

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) {J : UU l4}
  (f : J â†’ I)
  where

  coherence-square-precomp-map-inv-distributive-Î -Î£ :
    coherence-square-maps
      ( precomp-lifted-family-of-elements B f)
      ( map-inv-distributive-Î -Î£)
      ( map-inv-distributive-Î -Î£)
      ( precomp f (Î£ A B))
  coherence-square-precomp-map-inv-distributive-Î -Î£ = refl-htpy
```

### Precomposition of lifted families of elements preserves homotopies

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) {J : UU l4}
  {f : J â†’ I}
  where

  htpy-precomp-lifted-family-of-elements :
    {g : J â†’ I} (H : f ~ g) â†’
    ( precomp-lifted-family-of-elements B f) ~
    ( precomp-lifted-family-of-elements B g)
  htpy-precomp-lifted-family-of-elements H =
    htpy-map-Î£
      ( lift-family-of-elements B)
      ( htpy-precomp H A)
      ( precomp-lift-family-of-elements B f)
      ( Î» a â†’ triangle-precomp-lift-family-of-elements-htpy B a H)

  abstract
    compute-htpy-precomp-lifted-family-of-elements :
      htpy-precomp-lifted-family-of-elements refl-htpy ~
      refl-htpy
    compute-htpy-precomp-lifted-family-of-elements =
      htpy-htpy-map-Î£-refl-htpy
        ( lift-family-of-elements B)
        ( compute-htpy-precomp-refl-htpy f A)
        ( Î» a â†’
          ( htpy-eq
            ( compute-triangle-precomp-lift-family-of-elements-htpy B a)) âˆ™h
          ( Î» b â†’
            htpy-eq (compute-tr-lift-family-of-elements-precomp B a) (b âˆ˜ f)))
```

### `coherence-square-precomp-map-inv-distributive-Î -Î£` commutes with induced homotopies between precompositions maps

Diagrammatically, we have two ways of composing homotopies to connect `- âˆ˜ f`
and `precomp-lifted-family-of-elements g`. One factors through
`precomp-lifted-family-of-elements f`:

```text
                                     precomp-lifted-family g
                               -----------------------------------
                             /                                     \
                           /     â‡— htpy-precomp-lifted-family H      \
                         /                                             âˆ¨
  Î£ (a : I â†’ A) ((i : I) â†’ B (a i)) ------------------------> Î£ (a : J â†’ A) ((j : J) â†’ B (a j))
                  |                  precomp-lifted-family f                  |
                  |                                                           |
                  |                             â‡—                             |
                  | map-inv-distributive-Î -Î£         map-inv-distributive-Î -Î£ |
                  âˆ¨                                                           âˆ¨
              I â†’ Î£ A B ------------------------------------------------> J â†’ Î£ A B ,
                                              - âˆ˜ f
```

while the other factors through `- âˆ˜ g`:

```text
                                     precomp-lifted-family g
  Î£ (a : I â†’ A) ((i : I) â†’ B (a i)) ------------------------> Î£ (a : J â†’ A) ((j : J) â†’ B (a j))
                  |                                                           |
                  |                                                           |
                  | map-inv-distributive-Î -Î£    â‡—    map-inv-distributive-Î -Î£ |
                  |                                                           |
                  âˆ¨                           - âˆ˜ g                           âˆ¨
              I â†’ Î£ A B ------------------------------------------------> J â†’ Î£ A B .
                        \                                               >
                          \             â‡—  htpy-precomp H             /
                            \                                       /
                              -------------------------------------
                                              - âˆ˜ f
```

We show that these homotopies are themselves homotopic, filling the cylinder.

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : UU l2} (B : A â†’ UU l3) {J : UU l4}
  {f : J â†’ I}
  where

  statement-coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£ :
    {g : J â†’ I} (H : f ~ g) â†’ UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  statement-coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£
    {g} H =
    coherence-square-homotopies
      ( htpy-precomp H (Î£ A B) Â·r map-inv-distributive-Î -Î£)
      ( coherence-square-precomp-map-inv-distributive-Î -Î£ B f)
      ( coherence-square-precomp-map-inv-distributive-Î -Î£ B g)
      ( ( map-inv-distributive-Î -Î£) Â·l
        ( htpy-precomp-lifted-family-of-elements B H))

  coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£-refl-htpy :
    statement-coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£
      ( refl-htpy)
  coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£-refl-htpy =
    ( left-whisker-compÂ²
      ( map-inv-distributive-Î -Î£)
      ( compute-htpy-precomp-lifted-family-of-elements B)) âˆ™h
    ( inv-htpy
      ( Î» h â†’
        compute-htpy-precomp-refl-htpy f
          ( Î£ A B)
          ( map-inv-distributive-Î -Î£ h))) âˆ™h
    ( inv-htpy-right-unit-htpy)

  coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£ :
    {g : J â†’ I} (H : f ~ g) â†’
    statement-coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£
      ( H)
  coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£ =
    ind-htpy f
      ( Î» g â†’
        statement-coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£)
      ( coherence-htpy-precomp-coherence-square-precomp-map-inv-distributive-Î -Î£-refl-htpy)
```
