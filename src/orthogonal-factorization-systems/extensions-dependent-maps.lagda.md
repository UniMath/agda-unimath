# Extensions of dependent maps

```agda
module orthogonal-factorization-systems.extensions-dependent-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-dependent-functions
open import foundation.action-on-identifications-functions
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.propositions
open import foundation.sets
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.torsorial-type-families
```

</details>

## Idea

An
{{#concept "extension" Disambiguation="of a dependent map along a map, types" Agda=extension-dependent-type}}
of a dependent map `f : (x : A) â†’ P (i x)` along a map `i : A â†’ B` is a map
`g : (y : B) â†’ P y` such that `g` restricts along `i` to `f`.

```text
      A
      |  \
    i |    \ f
      |      \
      âˆ¨   g   âˆ¨
  b âˆˆ B -----> P b
```

## Definition

### Extensions of dependent maps

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-extension-dependent-type :
    {P : B â†’ UU l3} â†’
    ((x : A) â†’ P (i x)) â†’ ((y : B) â†’ P y) â†’ UU (l1 âŠ” l3)
  is-extension-dependent-type f g = (f ~ g âˆ˜ i)

  extension-dependent-type :
    (P : B â†’ UU l3) â†’
    ((x : A) â†’ P (i x)) â†’ UU (l1 âŠ” l2 âŠ” l3)
  extension-dependent-type P f =
    Î£ ((y : B) â†’ P y) (is-extension-dependent-type f)

  total-extension-dependent-type : (P : B â†’ UU l3) â†’ UU (l1 âŠ” l2 âŠ” l3)
  total-extension-dependent-type P =
    Î£ ((x : A) â†’ P (i x)) (extension-dependent-type P)

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {i : A â†’ B}
  {P : B â†’ UU l3} {f : (x : A) â†’ P (i x)}
  where

  map-extension-dependent-type : extension-dependent-type i P f â†’ (y : B) â†’ P y
  map-extension-dependent-type = pr1

  is-extension-map-extension-dependent-type :
    (E : extension-dependent-type i P f) â†’
    is-extension-dependent-type i f (map-extension-dependent-type E)
  is-extension-map-extension-dependent-type = pr2
```

### Extensions of dependent maps with homotopies going the other way

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-extension-dependent-type' :
    {P : B â†’ UU l3} â†’
    ((x : A) â†’ P (i x)) â†’ ((y : B) â†’ P y) â†’ UU (l1 âŠ” l3)
  is-extension-dependent-type' f g = (g âˆ˜ i ~ f)

  extension-dependent-type' :
    (P : B â†’ UU l3) â†’
    ((x : A) â†’ P (i x)) â†’ UU (l1 âŠ” l2 âŠ” l3)
  extension-dependent-type' P f =
    Î£ ((y : B) â†’ P y) (is-extension-dependent-type' f)

  total-extension-dependent-type' : (P : B â†’ UU l3) â†’ UU (l1 âŠ” l2 âŠ” l3)
  total-extension-dependent-type' P =
    Î£ ((x : A) â†’ P (i x)) (extension-dependent-type' P)

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {i : A â†’ B}
  {P : B â†’ UU l3} {f : (x : A) â†’ P (i x)}
  where

  map-extension-dependent-type' :
    extension-dependent-type' i P f â†’ (y : B) â†’ P y
  map-extension-dependent-type' = pr1

  is-extension-map-extension-dependent-type' :
    (E : extension-dependent-type' i P f) â†’
    is-extension-dependent-type' i f (map-extension-dependent-type' E)
  is-extension-map-extension-dependent-type' = pr2
```

## Operations

### Vertical composition of extensions of dependent maps

```text
  A
  |  \
  i    f
  |      \
  âˆ¨       âˆ¨
  B - g -> P
  |       âˆ§
  j      /
  |    h
  âˆ¨  /
  C
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {P : C â†’ UU l4}
  {i : A â†’ B} {j : B â†’ C}
  {f : (x : A) â†’ P (j (i x))} {g : (x : B) â†’ P (j x)} {h : (x : C) â†’ P x}
  where

  is-extension-dependent-type-comp-vertical :
    is-extension-dependent-type j g h â†’
    is-extension-dependent-type i f g â†’
    is-extension-dependent-type (j âˆ˜ i) f h
  is-extension-dependent-type-comp-vertical H G x = G x âˆ™ H (i x)
```

### Horizontal composition of extensions of dependent maps

```text
           A
        /  |  \
      f    g    h
    /      |      \
   âˆ¨       âˆ¨       âˆ¨
  B - i -> C - j -> P
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {P : C â†’ UU l4}
  {f : A â†’ B} {g : A â†’ C} {h : (x : A) â†’ P (g x)}
  {i : B â†’ C} {j : (z : C) â†’ P z}
  where

  is-extension-dependent-type-comp-horizontal :
    (I : is-extension-dependent-type f g i) â†’
    is-extension-dependent-type g h j â†’
    is-extension-dependent-type f (Î» x â†’ tr P (I x) (h x)) (j âˆ˜ i)
  is-extension-dependent-type-comp-horizontal I J x =
    ap (tr P (I x)) (J x) âˆ™ apd j (I x)
```

### Left whiskering of extensions of dependent maps

```text
  A
  |  \
  i    f
  |      \
  âˆ¨       âˆ¨
  B - g -> C - h -> P
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {P : C â†’ UU l4}
  {i : A â†’ B} {f : A â†’ C} {g : B â†’ C}
  where

  is-extension-dependent-type-left-whisker :
    (h : (x : C) â†’ P x) (F : is-extension-dependent-type i f g) â†’
    is-extension-dependent-type i (Î» x â†’ tr P (F x) (h (f x))) (h âˆ˜ g)
  is-extension-dependent-type-left-whisker h F = apd h âˆ˜ F
```

### Right whiskering of extensions of dependent maps

```text
  X - h -> A
           |  \
           i    f
           |      \
           âˆ¨       âˆ¨
           B - g -> P
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {P : B â†’ UU l3} {X : UU l4}
  {i : A â†’ B} {f : (x : A) â†’ P (i x)} {g : (y : B) â†’ P y}
  where

  is-extension-dependent-type-right-whisker :
    is-extension-dependent-type i f g â†’
    (h : X â†’ A) â†’
    is-extension-dependent-type (i âˆ˜ h) (f âˆ˜ h) g
  is-extension-dependent-type-right-whisker F h = F âˆ˜ h
```

## Properties

### The total type of extensions is equivalent to `(y : B) â†’ P y`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  inv-compute-total-extension-dependent-type :
    {P : B â†’ UU l3} â†’ total-extension-dependent-type i P â‰ƒ ((y : B) â†’ P y)
  inv-compute-total-extension-dependent-type =
    ( right-unit-law-Î£-is-contr (Î» f â†’ is-torsorial-htpy' (f âˆ˜ i))) âˆ˜e
    ( equiv-left-swap-Î£)

  compute-total-extension-dependent-type :
    {P : B â†’ UU l3} â†’ ((y : B) â†’ P y) â‰ƒ total-extension-dependent-type i P
  compute-total-extension-dependent-type =
    inv-equiv (inv-compute-total-extension-dependent-type)
```

### The truncation level of the type of extensions is bounded by the truncation level of the codomain

```agda
module _
  {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-trunc-is-extension-dependent-type :
    {P : B â†’ UU l3} (f : (x : A) â†’ P (i x)) â†’
    ((x : A) â†’ is-trunc (succ-ğ•‹ k) (P (i x))) â†’
    (g : (x : B) â†’ P x) â†’ is-trunc k (is-extension-dependent-type i f g)
  is-trunc-is-extension-dependent-type f is-trunc-P g =
    is-trunc-Î  k (Î» x â†’ is-trunc-P x (f x) (g (i x)))

  is-trunc-extension-dependent-type :
    {P : B â†’ UU l3} (f : (x : A) â†’ P (i x)) â†’
    ((x : B) â†’ is-trunc k (P x)) â†’ is-trunc k (extension-dependent-type i P f)
  is-trunc-extension-dependent-type f is-trunc-P =
    is-trunc-Î£
      ( is-trunc-Î  k is-trunc-P)
      ( is-trunc-is-extension-dependent-type f
        ( is-trunc-succ-is-trunc k âˆ˜ (is-trunc-P âˆ˜ i)))

  is-trunc-total-extension-dependent-type :
    {P : B â†’ UU l3} â†’
    ((x : B) â†’ is-trunc k (P x)) â†’
    is-trunc k (total-extension-dependent-type i P)
  is-trunc-total-extension-dependent-type {P} is-trunc-P =
    is-trunc-equiv' k
      ( (y : B) â†’ P y)
      ( compute-total-extension-dependent-type i)
      ( is-trunc-Î  k is-trunc-P)

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-contr-is-extension-dependent-type :
    {P : B â†’ UU l3} (f : (x : A) â†’ P (i x)) â†’
    ((x : A) â†’ is-prop (P (i x))) â†’
    (g : (x : B) â†’ P x) â†’ is-contr (is-extension-dependent-type i f g)
  is-contr-is-extension-dependent-type f is-prop-P g =
    is-contr-Î  (Î» x â†’ is-prop-P x (f x) (g (i x)))

  is-prop-is-extension-dependent-type :
    {P : B â†’ UU l3} (f : (x : A) â†’ P (i x)) â†’
    ((x : A) â†’ is-set (P (i x))) â†’
    (g : (x : B) â†’ P x) â†’ is-prop (is-extension-dependent-type i f g)
  is-prop-is-extension-dependent-type f is-set-P g =
    is-prop-Î  (Î» x â†’ is-set-P x (f x) (g (i x)))
```

## Examples

### Every dependent map is an extension of itself along the identity

```agda
module _
  {l1 l2 : Level} {A : UU l1} {P : A â†’ UU l2} (f : (x : A) â†’ P x)
  where

  is-extension-dependent-type-self : is-extension-dependent-type id f f
  is-extension-dependent-type-self = refl-htpy

  extension-self : extension-dependent-type id P f
  extension-self = (f , is-extension-dependent-type-self)
```

## See also

- [`orthogonal-factorization-systems.lifts-maps`](orthogonal-factorization-systems.lifts-maps.md)
  for the dual notion.
