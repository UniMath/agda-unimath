# Lifts of maps

```agda
module orthogonal-factorization-systems.lifts-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.propositions
open import foundation.sets
open import foundation.small-types
open import foundation.structure-identity-principle
open import foundation.torsorial-type-families
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition
```

</details>

## Idea

A **lift** of a map `f : X â†’ B` along a map `i : A â†’ B` is a map `g : X â†’ A`
such that the composition `i âˆ˜ g` is `f`.

```text
           A
          âˆ§|
        /  i
      g    |
    /      âˆ¨
  X - f -> B
```

## Definition

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-lift : {X : UU l3} â†’ (X â†’ B) â†’ (X â†’ A) â†’ UU (l2 âŠ” l3)
  is-lift f g = f ~ (i âˆ˜ g)

  lift : {X : UU l3} â†’ (X â†’ B) â†’ UU (l1 âŠ” l2 âŠ” l3)
  lift {X} f = Î£ (X â†’ A) (is-lift f)

  total-lift : (X : UU l3) â†’ UU (l1 âŠ” l2 âŠ” l3)
  total-lift X = Î£ (X â†’ B) lift

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  {X : UU l3} {f : X â†’ B}
  where

  map-lift : lift i f â†’ X â†’ A
  map-lift = pr1

  is-lift-map-lift : (l : lift i f) â†’ is-lift i f (map-lift l)
  is-lift-map-lift = pr2
```

## Operations

### Vertical composition of lifts of maps

```text
           A
          âˆ§|
        /  i
      g    |
    /      âˆ¨
  X - f -> B
    \      |
      h    j
       \   |
         âˆ¨ âˆ¨
           C
```

```agda
module _
  {l1 l2 l3 l4 : Level} {X : UU l1} {A : UU l2} {B : UU l3} {C : UU l4}
  {i : A â†’ B} {j : B â†’ C} {f : X â†’ B} {h : X â†’ C} {g : X â†’ A}
  where

  is-lift-comp-vertical : is-lift i f g â†’ is-lift j h f â†’ is-lift (j âˆ˜ i) h g
  is-lift-comp-vertical F H x = H x âˆ™ ap j (F x)
```

### Horizontal composition of lifts of maps

```text
  A - f -> B - g -> C
    \      |      /
      h    i    j
        \  |  /
         âˆ¨ âˆ¨ âˆ¨
           X
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {X : UU l4}
  {f : A â†’ B} {g : B â†’ C} {h : A â†’ X} {i : B â†’ X} {j : C â†’ X}
  where

  is-lift-comp-horizontal :
    is-lift j i g â†’ is-lift i h f â†’ is-lift j h (g âˆ˜ f)
  is-lift-comp-horizontal J I x = I x âˆ™ J (f x)
```

## Left whiskering of lifts of maps

```text
           A
          âˆ§|
        /  i
      g    |
    /      âˆ¨
  X - f -> B - h -> S
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {S : UU l4}
  {i : A â†’ B} {f : X â†’ B} {g : X â†’ A}
  where

  is-lift-left-whisker : (h : B â†’ S) â†’ is-lift i f g â†’ is-lift (h âˆ˜ i) (h âˆ˜ f) g
  is-lift-left-whisker h H x = ap h (H x)
```

## Right whiskering of lifts of maps

```text
                    A
                   âˆ§|
                 /  i
               g    |
             /      âˆ¨
  S - h -> X - f -> B
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {S : UU l4}
  {i : A â†’ B} {f : X â†’ B} {g : X â†’ A}
  where

  is-lift-right-whisker :
    is-lift i f g â†’ (h : S â†’ X) â†’ is-lift i (f âˆ˜ h) (g âˆ˜ h)
  is-lift-right-whisker H h s = H (h s)
```

## Properties

### Characterizing identifications of lifts of maps

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  {X : UU l3} (f : X â†’ B)
  where

  coherence-htpy-lift :
    (l l' : lift i f) â†’ map-lift i l ~ map-lift i l' â†’ UU (l2 âŠ” l3)
  coherence-htpy-lift l l' K =
    (is-lift-map-lift i l âˆ™h (i Â·l K)) ~ is-lift-map-lift i l'

  htpy-lift : (l l' : lift i f) â†’ UU (l1 âŠ” l2 âŠ” l3)
  htpy-lift l l' =
    Î£ ( map-lift i l ~ map-lift i l')
      ( coherence-htpy-lift l l')

  refl-htpy-lift : (l : lift i f) â†’ htpy-lift l l
  pr1 (refl-htpy-lift l) = refl-htpy
  pr2 (refl-htpy-lift l) = right-unit-htpy

  htpy-eq-lift : (l l' : lift i f) â†’ l ï¼ l' â†’ htpy-lift l l'
  htpy-eq-lift l .l refl = refl-htpy-lift l

  is-torsorial-htpy-lift :
    (l : lift i f) â†’ is-torsorial (htpy-lift l)
  is-torsorial-htpy-lift l =
    is-torsorial-Eq-structure
      (is-torsorial-htpy (map-lift i l))
      (map-lift i l , refl-htpy)
      (is-torsorial-htpy (is-lift-map-lift i l âˆ™h refl-htpy))

  is-equiv-htpy-eq-lift :
    (l l' : lift i f) â†’ is-equiv (htpy-eq-lift l l')
  is-equiv-htpy-eq-lift l =
    fundamental-theorem-id (is-torsorial-htpy-lift l) (htpy-eq-lift l)

  extensionality-lift :
    (l l' : lift i f) â†’ (l ï¼ l') â‰ƒ (htpy-lift l l')
  pr1 (extensionality-lift l l') = htpy-eq-lift l l'
  pr2 (extensionality-lift l l') = is-equiv-htpy-eq-lift l l'

  eq-htpy-lift : (l l' : lift i f) â†’ htpy-lift l l' â†’ l ï¼ l'
  eq-htpy-lift l l' = map-inv-equiv (extensionality-lift l l')
```

### The total type of lifts of maps is equivalent to `X â†’ A`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B) (X : UU l3)
  where

  inv-compute-total-lift : total-lift i X â‰ƒ (X â†’ A)
  inv-compute-total-lift =
    ( right-unit-law-Î£-is-contr ( Î» f â†’ is-torsorial-htpy' (i âˆ˜ f))) âˆ˜e
    ( equiv-left-swap-Î£)

  compute-total-lift : (X â†’ A) â‰ƒ total-lift i X
  compute-total-lift = inv-equiv inv-compute-total-lift

  is-small-total-lift : is-small (l1 âŠ” l3) (total-lift i X)
  pr1 (is-small-total-lift) = X â†’ A
  pr2 (is-small-total-lift) = inv-compute-total-lift
```

### The truncation level of the type of lifts is bounded by the truncation level of the codomains

```agda
module _
  {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-trunc-is-lift :
    {X : UU l3} (f : X â†’ B) â†’
    is-trunc (succ-ğ•‹ k) B â†’ (g : X â†’ A) â†’ is-trunc k (is-lift i f g)
  is-trunc-is-lift f is-trunc-B g =
    is-trunc-Î  k (Î» x â†’ is-trunc-B (f x) (i (g x)))

  is-trunc-lift :
    {X : UU l3} (f : X â†’ B) â†’
    is-trunc k A â†’ is-trunc (succ-ğ•‹ k) B â†’ is-trunc k (lift i f)
  is-trunc-lift f is-trunc-A is-trunc-B =
    is-trunc-Î£
      ( is-trunc-function-type k is-trunc-A)
      ( is-trunc-is-lift f is-trunc-B)

  is-trunc-total-lift :
    (X : UU l3) â†’ is-trunc k A â†’ is-trunc k (total-lift i X)
  is-trunc-total-lift X is-trunc-A =
    is-trunc-equiv' k
      ( X â†’ A)
      ( compute-total-lift i X)
      ( is-trunc-function-type k is-trunc-A)

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (i : A â†’ B)
  where

  is-contr-is-lift :
    {X : UU l3} (f : X â†’ B) â†’
    is-prop B â†’ (g : X â†’ A) â†’ is-contr (is-lift i f g)
  is-contr-is-lift f is-prop-B g = is-contr-Î  Î» x â†’ is-prop-B (f x) (i (g x))

  is-prop-is-lift :
    {X : UU l3} (f : X â†’ B) â†’
    is-set B â†’ (g : X â†’ A) â†’ is-prop (is-lift i f g)
  is-prop-is-lift f is-set-B g = is-prop-Î  Î» x â†’ is-set-B (f x) (i (g x))
```

## See also

- [`orthogonal-factorization-systems.extensions-maps`](orthogonal-factorization-systems.extensions-maps.md)
  for the dual notion.
