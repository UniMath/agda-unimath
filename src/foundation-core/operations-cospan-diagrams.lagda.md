# Operations on cospan diagrams

```agda
module foundation-core.operations-cospan-diagrams where
```

<details><summary>Imports</summary>

```agda
open import foundation.cospan-diagrams
open import foundation.cospans
open import foundation.dependent-pair-types
open import foundation.morphisms-arrows
open import foundation.operations-cospans
open import foundation.universe-levels

open import foundation-core.function-types
```

</details>

## Idea

This file contains some operations on
[cospan diagrams](foundation.cospan-diagrams.md) that produce new cospan
diagrams from given cospan diagrams and possibly other data.

## Definitions

### Concatenating cospan diagrams and maps on both sides

Consider a [cospan diagram](foundation.cospan-diagrams.md) `ð’®` given by

```text
       f         g
  A ------> S <------ B
```

and maps `i : A' â†’ A` and `j : B' â†’ B`. The
{{#concept "concatenation-cospan-diagram" Disambiguation="cospan diagram" Agda=concat-cospan-diagram}}
of `ð’®`, `i`, and `j` is the cospan diagram

```text
      f âˆ˜ i     g âˆ˜ j
  A' ------> S <------ B'.
```

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  where

  concat-cospan-diagram :
    (ð’® : cospan-diagram l1 l2 l3)
    {A' : UU l4} (f : A' â†’ domain-cospan-diagram ð’®)
    {B' : UU l5} (g : B' â†’ codomain-cospan-diagram ð’®) â†’
    cospan-diagram l4 l5 l3
  pr1 (concat-cospan-diagram ð’® {A'} f {B'} g) =
    A'
  pr1 (pr2 (concat-cospan-diagram ð’® {A'} f {B'} g)) =
    B'
  pr2 (pr2 (concat-cospan-diagram ð’® {A'} f {B'} g)) =
    concat-cospan (cospan-cospan-diagram ð’®) f g
```

### Concatenating cospan diagrams and maps on the left

Consider a [cospan diagram](foundation.cospan-diagrams.md) `ð’®` given by

```text
       f         g
  A ------> S <------ B
```

and a map `i : A' â†’ A`. The
{{#concept "left concatenation" Disambiguation="cospan diagram" Agda=left-concat-cospan-diagram}}
of `ð’®` and `i` is the cospan diagram

```text
      f âˆ˜ i       g
  A' ------> S <------ B.
```

```agda
module _
  {l1 l2 l3 l4 : Level}
  where

  left-concat-cospan-diagram :
    (ð’® : cospan-diagram l1 l2 l3) {A' : UU l4} â†’
    (A' â†’ domain-cospan-diagram ð’®) â†’ cospan-diagram l4 l2 l3
  left-concat-cospan-diagram ð’® f = concat-cospan-diagram ð’® f id
```

### Concatenating cospan diagrams and maps on the right

Consider a [cospan diagram](foundation.cospan-diagrams.md) `ð’®` given by

```text
       f         g
  A ------> S <------ B
```

and a map `j : B' â†’ B`. The
{{#concept "right concatenation" Disambiguation="cospan diagram" Agda=right-concat-cospan-diagram}}
of `ð’®` by `j` is the cospan diagram

```text
        f       g âˆ˜ j
  A' ------> S <------ B'.
```

```agda
module _
  {l1 l2 l3 l4 : Level}
  where

  right-concat-cospan-diagram :
    (ð’® : cospan-diagram l1 l2 l3) {B' : UU l4} â†’
    (B' â†’ codomain-cospan-diagram ð’®) â†’ cospan-diagram l1 l4 l3
  right-concat-cospan-diagram ð’® g = concat-cospan-diagram ð’® id g
```

### Concatenation of cospan diagrams and morphisms of arrows on the left

Consider a cospan diagram `ð’®` given by

```text
       f         g
  A ------> S <------ B
```

and a [morphism of arrows](foundation.morphisms-arrows.md) `h : hom-arrow f' f`
as indicated in the diagram

```text
          f         g
     A ------> S <------ B.
     |         |
  hâ‚€ |         | hâ‚
     âˆ¨         âˆ¨
     A' -----> S'
          f'
```

Then we obtain a cospan diagram `A' --> S' <-- B`.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} (ð’® : cospan-diagram l1 l2 l3)
  {S' : UU l4} {A' : UU l5} (f' : A' â†’ S')
  (h : hom-arrow (left-map-cospan-diagram ð’®) f')
  where

  domain-left-concat-hom-arrow-cospan-diagram : UU l5
  domain-left-concat-hom-arrow-cospan-diagram = A'

  codomain-left-concat-hom-arrow-cospan-diagram : UU l2
  codomain-left-concat-hom-arrow-cospan-diagram = codomain-cospan-diagram ð’®

  cospan-left-concat-hom-arrow-cospan-diagram :
    cospan l4
      ( domain-left-concat-hom-arrow-cospan-diagram)
      ( codomain-left-concat-hom-arrow-cospan-diagram)
  cospan-left-concat-hom-arrow-cospan-diagram =
    left-concat-hom-arrow-cospan
      ( cospan-cospan-diagram ð’®)
      ( f')
      ( h)

  left-concat-hom-arrow-cospan-diagram : cospan-diagram l5 l2 l4
  pr1 left-concat-hom-arrow-cospan-diagram =
    domain-left-concat-hom-arrow-cospan-diagram
  pr1 (pr2 left-concat-hom-arrow-cospan-diagram) =
    codomain-left-concat-hom-arrow-cospan-diagram
  pr2 (pr2 left-concat-hom-arrow-cospan-diagram) =
    cospan-left-concat-hom-arrow-cospan-diagram

  cospanning-type-left-concat-hom-arrow-cospan-diagram : UU l4
  cospanning-type-left-concat-hom-arrow-cospan-diagram =
    cospanning-type-cospan-diagram left-concat-hom-arrow-cospan-diagram

  left-map-left-concat-hom-arrow-cospan-diagram :
    domain-left-concat-hom-arrow-cospan-diagram â†’
    cospanning-type-left-concat-hom-arrow-cospan-diagram
  left-map-left-concat-hom-arrow-cospan-diagram =
    left-map-cospan-diagram left-concat-hom-arrow-cospan-diagram

  right-map-left-concat-hom-arrow-cospan-diagram :
    codomain-left-concat-hom-arrow-cospan-diagram â†’
    cospanning-type-left-concat-hom-arrow-cospan-diagram
  right-map-left-concat-hom-arrow-cospan-diagram =
    right-map-cospan-diagram left-concat-hom-arrow-cospan-diagram
```

### Concatenation of cospan diagrams and morphisms of arrows on the right

Consider a cospan diagram `ð’®` given by

```text
       f         g
  A ------> S <------ B
```

and a [morphism of arrows](foundation.morphisms-arrows.md) `h : hom-arrow g' g`
as indicated in the diagram

```text
       f         g
  A ------> S <------ B.
            |         |
         hâ‚€ |         | hâ‚
            âˆ¨         âˆ¨
            S' <----- B'
                 g'
```

Then we obtain a cospan diagram `A --> S' <-- B'`.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} (ð’® : cospan-diagram l1 l2 l3)
  {S' : UU l4} {B' : UU l5} (g' : B' â†’ S')
  (h : hom-arrow (right-map-cospan-diagram ð’®) g')
  where

  domain-right-concat-hom-arrow-cospan-diagram : UU l1
  domain-right-concat-hom-arrow-cospan-diagram = domain-cospan-diagram ð’®

  codomain-right-concat-hom-arrow-cospan-diagram : UU l5
  codomain-right-concat-hom-arrow-cospan-diagram = B'

  cospan-right-concat-hom-arrow-cospan-diagram :
    cospan l4
      ( domain-right-concat-hom-arrow-cospan-diagram)
      ( codomain-right-concat-hom-arrow-cospan-diagram)
  cospan-right-concat-hom-arrow-cospan-diagram =
    right-concat-hom-arrow-cospan
      ( cospan-cospan-diagram ð’®)
      ( g')
      ( h)

  right-concat-hom-arrow-cospan-diagram : cospan-diagram l1 l5 l4
  pr1 right-concat-hom-arrow-cospan-diagram =
    domain-right-concat-hom-arrow-cospan-diagram
  pr1 (pr2 right-concat-hom-arrow-cospan-diagram) =
    codomain-right-concat-hom-arrow-cospan-diagram
  pr2 (pr2 right-concat-hom-arrow-cospan-diagram) =
    cospan-right-concat-hom-arrow-cospan-diagram

  cospanning-type-right-concat-hom-arrow-cospan-diagram : UU l4
  cospanning-type-right-concat-hom-arrow-cospan-diagram =
    cospanning-type-cospan-diagram right-concat-hom-arrow-cospan-diagram

  left-map-right-concat-hom-arrow-cospan-diagram :
    domain-right-concat-hom-arrow-cospan-diagram â†’
    cospanning-type-right-concat-hom-arrow-cospan-diagram
  left-map-right-concat-hom-arrow-cospan-diagram =
    left-map-cospan-diagram right-concat-hom-arrow-cospan-diagram

  right-map-right-concat-hom-arrow-cospan-diagram :
    codomain-right-concat-hom-arrow-cospan-diagram â†’
    cospanning-type-right-concat-hom-arrow-cospan-diagram
  right-map-right-concat-hom-arrow-cospan-diagram =
    right-map-cospan-diagram right-concat-hom-arrow-cospan-diagram
```
