---
title: 0-Maps
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation-core.0-maps where

open import foundation-core.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation-core.fibers-of-maps using (fib; equiv-fib-pr1)
open import foundation-core.functions using (_âˆ˜_)
open import foundation-core.functoriality-dependent-pair-types using
  (tot; map-Î£-map-base; map-Î£)
open import foundation-core.homotopies using (_~_)
open import foundation-core.sets using
  ( is-set; is-set-equiv; Set; type-Set; is-set-type-Set)
open import foundation-core.truncated-maps using
  ( is-trunc-map-htpy; is-trunc-map-comp; is-trunc-map-right-factor;
    is-trunc-map-tot; is-trunc-map-map-Î£-map-base; is-trunc-map-map-Î£)
open import foundation-core.truncation-levels using (zero-ğ•‹)
open import foundation-core.universe-levels using (Level; UU; _âŠ”_)
```

## Definition

Maps `f : A â†’ B` of which the fibers are sets, i.e., 0-truncated types, are called 0-maps. We will show in `foundation-core.faithful-maps` that a map f is a 0-map if and only if f is faithful, i.e., f induces embeddings on identity types.

```agda
module _
  {l1 l2 : Level}
  where

  is-0-map : {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU (l1 âŠ” l2)
  is-0-map {A} {B} f = (y : B) â†’ is-set (fib f y)

  0-map : (A : UU l1) (B : UU l2) â†’ UU (l1 âŠ” l2)
  0-map A B = Î£ (A â†’ B) is-0-map

  map-0-map : {A : UU l1} {B : UU l2} â†’ 0-map A B â†’ A â†’ B
  map-0-map = pr1

  is-0-map-map-0-map :
    {A : UU l1} {B : UU l2} (f : 0-map A B) â†’ is-0-map (map-0-map f)
  is-0-map-map-0-map = pr2
```

## Properties

### Projections of families of sets are 0-maps

```agda
module _
  {l1 l2 : Level} {A : UU l1}
  where
  
  abstract
    is-0-map-pr1 :
      {B : A â†’ UU l2} â†’ ((x : A) â†’ is-set (B x)) â†’ is-0-map (pr1 {B = B})
    is-0-map-pr1 {B} H x =
      is-set-equiv (B x) (equiv-fib-pr1 B x) (H x)
                                                  
  pr1-0-map :
    (B : A â†’ Set l2) â†’ 0-map (Î£ A (Î» x â†’ type-Set (B x))) A
  pr1 (pr1-0-map B) = pr1
  pr2 (pr1-0-map B) = is-0-map-pr1 (Î» x â†’ is-set-type-Set (B x))
```

### 0-maps are closed under homotopies

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f g : A â†’ B} (H : f ~ g)
  where
  
  abstract
    is-0-map-htpy : is-0-map g â†’ is-0-map f
    is-0-map-htpy = is-trunc-map-htpy zero-ğ•‹ H
```

### 0-maps are closed under composition

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h))
  where
  
  abstract
    is-0-map-comp : is-0-map g â†’ is-0-map h â†’ is-0-map f
    is-0-map-comp = is-trunc-map-comp zero-ğ•‹ f g h H
```

### If a composite is a 0-map, then so is its right factor

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h))
  where
  
  is-0-map-right-factor : is-0-map g â†’ is-0-map f â†’ is-0-map h
  is-0-map-right-factor = is-trunc-map-right-factor zero-ğ•‹ f g h H
```

### A family of 0-maps induces a 0-map on total spaces

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  {f : (x : A) â†’ B x â†’ C x}
  where
  
  abstract
    is-0-map-tot : ((x : A) â†’ is-0-map (f x)) â†’ is-0-map (tot f)
    is-0-map-tot = is-trunc-map-tot zero-ğ•‹
```

### For any type family over the codomain, a 0-map induces a 0-map on total spaces.

In other words, 0-maps are stable under pullbacks. We will come to this point when we introduce homotopy pullbacks.

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} (C : B â†’ UU l3)
  where
    
  abstract
    is-0-map-map-Î£-map-base : is-0-map f â†’ is-0-map (map-Î£-map-base f C)
    is-0-map-map-Î£-map-base = is-trunc-map-map-Î£-map-base zero-ğ•‹ C
```

### The functorial action of `Î£` preserves 0-maps.

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : A â†’ UU l3}
  (D : B â†’ UU l4) {f : A â†’ B} {g : (x : A) â†’ C x â†’ D (f x)}
  where
    
  is-0-map-map-Î£ :
    is-0-map f â†’ ((x : A) â†’ is-0-map (g x)) â†’ is-0-map (map-Î£ D f g)
  is-0-map-map-Î£ = is-trunc-map-map-Î£ zero-ğ•‹ D
```
