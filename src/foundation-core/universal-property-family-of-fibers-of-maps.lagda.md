# The universal property of family of fibers of maps

```agda
module foundation-core.universal-property-family-of-fibers-of-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.universe-levels

open import foundation-core.constant-maps
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-function-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.precomposition-dependent-functions
```

</details>

## Idea

Any map `f : A â†’ B` induces a type family `fiber f : B â†’ ğ’°` of
[fibers](foundation-core.fibers-of-maps.md) of `f`. By
[precomposing](foundation.precomposition-type-families.md) with `f`, we obtain
the type family `(fiber f) âˆ˜ f : A â†’ ğ’°`, which always has a section given by

```text
  Î» a â†’ (a , refl) : (a : A) â†’ fiber f (f a).
```

We can uniquely characterize the family of fibers `fiber f : B â†’ ğ’°` as the
initial type family equipped with such a section. Explicitly, the
{{#concept "universal property of the family of fibers" Disambiguation="of a map"}}
`fiber f : B â†’ ğ’°` of a map `f` is that the precomposition operation

```text
  ((b : B) â†’ fiber f b â†’ P b) â†’ ((a : A) â†’ P (f a))
```

is an [equivalence](foundation-core.equivalences.md) for any type family
`P : B â†’ ğ’°`. Note that for any type family `P` over `B` and any map `f : A â†’ B`,
the type of _lifts_ of `f` to `P` is precisely the type of sections

```text
  (a : A) â†’ P (f a).
```

The family of fibers of `f` is therefore the initial type family over `B`
equipped with a lift of `f`.

This universal property is especially useful when `A` or `B` enjoy mapping out
universal properties. This lets us characterize the sections `(a : A) â†’ P (f a)`
in terms of the mapping out properties of `A` and the descent data of `B`.

**Note:** We disambiguate between the _universal property of the family of
fibers of a map_ and the _universal property of the fiber of a map_ at a point
in the codomain. The universal property of the family of fibers of a map is as
described above, while the universal property of the fiber `fiber f b` of a map
`f` at `b` is a special case of the universal property of pullbacks.

## Definitions

### The canonical map used in the universal property and dependent universal property of the family of fibers of a map

The underlying map of the universal property of the family of fibers of a map is
defined to be the evaluation map

```text
  ((b : B) (z : F b) â†’ P b z) â†’ ((a : A) â†’ P (f a) (Î´ a))
```

for any type family `F : B â†’ ğ’°` equipped with a lift `Î´ : (a : A) â†’ F (f a)`.
This map takes a dependent function `h` and evaluates it at the values of the
lift `Î´`. Hence we call it `ev-lift`.

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (F : B â†’ UU l3) (Î´ : (a : A) â†’ F (f a))
  where

  ev-lift :
    {l4 : Level} {P : (b : B) â†’ F b â†’ UU l4} â†’
    ((b : B) (z : F b) â†’ P b z) â†’ (a : A) â†’ P (f a) (Î´ a)
  ev-lift h a = h (f a) (Î´ a)
```

### The universal property of the fibers of a map

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  where

  universal-property-family-of-fibers :
    (f : A â†’ B) (F : B â†’ UU l3) (Î´ : (a : A) â†’ F (f a)) â†’ UUÏ‰
  universal-property-family-of-fibers f F Î´ =
    {l : Level} (P : B â†’ UU l) â†’ is-equiv (ev-lift f F Î´ {l} {Î» b _ â†’ P b})
```

### The dependent universal property of the fibers of a map

```agda
  dependent-universal-property-family-of-fibers :
    (f : A â†’ B) (F : B â†’ UU l3) (Î´ : (a : A) â†’ F (f a)) â†’ UUÏ‰
  dependent-universal-property-family-of-fibers f F Î´ =
    {l : Level} (P : (b : B) â†’ F b â†’ UU l) â†’ is-equiv (ev-lift f F Î´ {l} {P})
```

### The lift of any map to its family of fibers

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  lift-family-of-fibers : (a : A) â†’ fiber f (f a)
  pr1 (lift-family-of-fibers a) = a
  pr2 (lift-family-of-fibers a) = refl
```

## Properties

### The family of fibers of a map satisfies the dependent universal property of the family of fibers of a map

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  module _
    {l3 : Level} (C : (y : B) (z : fiber f y) â†’ UU l3)
    where

    ev-lift-family-of-fibers :
      ((y : B) (z : fiber f y) â†’ C y z) â†’ ((x : A) â†’ C (f x) (x , refl))
    ev-lift-family-of-fibers = ev-lift f (fiber f) (lift-family-of-fibers f)

    inv-ev-lift-family-of-fibers :
      ((x : A) â†’ C (f x) (x , refl)) â†’ ((y : B) (z : fiber f y) â†’ C y z)
    inv-ev-lift-family-of-fibers h .(f x) (x , refl) = h x

    is-section-inv-ev-lift-family-of-fibers :
      (ev-lift-family-of-fibers âˆ˜ inv-ev-lift-family-of-fibers) ~ id
    is-section-inv-ev-lift-family-of-fibers h = refl

    is-retraction-inv-ev-lift-family-of-fibers' :
      (h : (y : B) (z : fiber f y) â†’ C y z) (y : B) â†’
      (inv-ev-lift-family-of-fibers (ev-lift-family-of-fibers h) y) ~ (h y)
    is-retraction-inv-ev-lift-family-of-fibers' h .(f z) (z , refl) = refl

    is-retraction-inv-ev-lift-family-of-fibers :
      (inv-ev-lift-family-of-fibers âˆ˜ ev-lift-family-of-fibers) ~ id
    is-retraction-inv-ev-lift-family-of-fibers h =
      eq-htpy (eq-htpy âˆ˜ is-retraction-inv-ev-lift-family-of-fibers' h)

    is-equiv-inv-ev-lift-family-of-fibers :
      is-equiv inv-ev-lift-family-of-fibers
    is-equiv-inv-ev-lift-family-of-fibers =
      is-equiv-is-invertible
        ( ev-lift-family-of-fibers)
        ( is-retraction-inv-ev-lift-family-of-fibers)
        ( is-section-inv-ev-lift-family-of-fibers)

    inv-equiv-dependent-universal-property-family-of-fibers :
      ((x : A) â†’ C (f x) (x , refl)) â‰ƒ ((y : B) (z : fiber f y) â†’ C y z)
    pr1 inv-equiv-dependent-universal-property-family-of-fibers =
      inv-ev-lift-family-of-fibers
    pr2 inv-equiv-dependent-universal-property-family-of-fibers =
      is-equiv-inv-ev-lift-family-of-fibers

  dependent-universal-property-family-of-fibers-fiber :
    dependent-universal-property-family-of-fibers f
      ( fiber f)
      ( lift-family-of-fibers f)
  dependent-universal-property-family-of-fibers-fiber C =
    is-equiv-is-invertible
      ( inv-ev-lift-family-of-fibers C)
      ( is-section-inv-ev-lift-family-of-fibers C)
      ( is-retraction-inv-ev-lift-family-of-fibers C)

  equiv-dependent-universal-property-family-of-fibers :
    {l3 : Level} (C : (y : B) (z : fiber f y) â†’ UU l3) â†’
    ((y : B) (z : fiber f y) â†’ C y z) â‰ƒ ((x : A) â†’ C (f x) (x , refl))
  pr1 (equiv-dependent-universal-property-family-of-fibers C) =
    ev-lift-family-of-fibers C
  pr2 (equiv-dependent-universal-property-family-of-fibers C) =
    dependent-universal-property-family-of-fibers-fiber C
```

### The family of fibers of a map satisfies the dependent universal property of the family of fibers of a map

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  universal-property-family-of-fibers-fiber :
    universal-property-family-of-fibers f (fiber f) (lift-family-of-fibers f)
  universal-property-family-of-fibers-fiber C =
    dependent-universal-property-family-of-fibers-fiber f (Î» y _ â†’ C y)

  equiv-universal-property-family-of-fibers :
    {l3 : Level} (C : B â†’ UU l3) â†’
    ((y : B) â†’ fiber f y â†’ C y) â‰ƒ ((x : A) â†’ C (f x))
  equiv-universal-property-family-of-fibers C =
    equiv-dependent-universal-property-family-of-fibers f (Î» y _ â†’ C y)
```

### The inverse equivalence of the universal property of the family of fibers of a map

The inverse of the equivalence `equiv-universal-property-family-of-fibers` has a reasonably nice definition, so we also record it here.

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (C : B â†’ UU l3)
  where

  inv-equiv-universal-property-family-of-fibers :
    ((x : A) â†’ C (f x)) â‰ƒ ((y : B) â†’ fiber f y â†’ C y)
  inv-equiv-universal-property-family-of-fibers =
    inv-equiv-dependent-universal-property-family-of-fibers f (Î» y _ â†’ C y)
```

### A type family `C` over `B` satisfies the universal property of the family of fibers of a map `f : A â†’ B` if and only if the diagonal map `C b â†’ (fiber f b â†’ C b)` is an equivalence for every `b : B`

This condition simplifies, for example, the proof that connected maps satisfy a
dependent universal property.

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B}
  where

  is-equiv-precomp-Î -fiber-condition :
    {l3 : Level} {C : B â†’ UU l3} â†’
    ((b : B) â†’ is-equiv (Î» (c : C b) â†’ const (fiber f b) (C b) c)) â†’
    is-equiv (precomp-Î  f C)
  is-equiv-precomp-Î -fiber-condition {l3} {C} H =
    is-equiv-comp
      ( ev-lift-family-of-fibers f (Î» b _ â†’ C b))
      ( map-Î  (Î» b u _ â†’ u))
      ( is-equiv-map-Î -is-fiberwise-equiv H)
      ( universal-property-family-of-fibers-fiber f C)
```
