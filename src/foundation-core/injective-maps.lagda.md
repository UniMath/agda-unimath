---
title: Injective maps
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation-core.injective-maps where

open import foundation-core.contractible-types
open import foundation-core.dependent-pair-types
open import foundation-core.embeddings
open import foundation-core.equivalences
open import foundation-core.functions
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.propositional-maps
open import foundation-core.propositions
open import foundation-core.sections
open import foundation-core.sets
open import foundation-core.universe-levels
```

## Idea

A map `f : A â†’ B` is injective if `Id (f x) (f y)` implies `Id x y`.

## Warning

The notion of injective map is, however, not homotopically coherent. It is fine to use injectivity for maps between sets, but for maps between general types it is recommended to use the notion of embedding.

## Definition

```agda
is-injective : {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU (l1 âŠ” l2)
is-injective {l1} {l2} {A} {B} f = {x y : A} â†’ f x ï¼ f y â†’ x ï¼ y
```

## Examples

### The identity function is injective

```agda
is-injective-id : {l1 : Level} {A : UU l1} â†’ is-injective (id {A = A})
is-injective-id p = p
```

## Properties

### If a composite is injective, then so is its right factor

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  where

  is-injective-right-factor :
    (g : B â†’ C) (h : A â†’ B) â†’
    is-injective (g âˆ˜ h) â†’ is-injective h
  is-injective-right-factor g h is-inj-gh p = is-inj-gh (ap g p)

  is-injective-right-factor-htpy :
    (f : A â†’ C) (g : B â†’ C) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
    is-injective f â†’ is-injective h
  is-injective-right-factor-htpy f g h H is-inj-f {x} {x'} p =
    is-inj-f {x} {x'} ((H x) âˆ™ ((ap g p) âˆ™ (inv (H x'))))
```

### Injective maps are closed under composition

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  where

  is-injective-comp :
    {g : B â†’ C} {h : A â†’ B} â†’
    is-injective h â†’ is-injective g â†’ is-injective (g âˆ˜ h)
  is-injective-comp is-inj-h is-inj-g = is-inj-h âˆ˜ is-inj-g

  is-injective-comp-htpy :
    (f : A â†’ C) (g : B â†’ C) (h : A â†’ B) â†’ f ~ (g âˆ˜ h) â†’
    is-injective h â†’ is-injective g â†’ is-injective f
  is-injective-comp-htpy f g h H is-inj-h is-inj-g {x} {x'} p =
    is-inj-h (is-inj-g ((inv (H x)) âˆ™ (p âˆ™ (H x'))))
```

### Equivalences are injective

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-injective-is-equiv : {f : A â†’ B} â†’ is-equiv f â†’ is-injective f
    is-injective-is-equiv H {x} {y} p =
      ( inv (isretr-map-inv-is-equiv H x)) âˆ™
      ( ( ap (map-inv-is-equiv H) p) âˆ™
        ( isretr-map-inv-is-equiv H y))

  abstract
    is-injective-map-equiv : (e : A â‰ƒ B) â†’ is-injective (map-equiv e)
    is-injective-map-equiv (pair f H) = is-injective-is-equiv H

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-injective-map-inv-equiv : (e : A â‰ƒ B) â†’ is-injective (map-inv-equiv e)
    is-injective-map-inv-equiv e =
      is-injective-is-equiv (is-equiv-map-inv-equiv e)

  is-equiv-is-injective : {f : A â†’ B} â†’ sec f â†’ is-injective f â†’ is-equiv f
  is-equiv-is-injective {f} (pair g G) H =
    is-equiv-has-inverse g G (Î» x â†’ H (G (f x)))
```

### Any embedding is injective

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-injective-is-emb : {f : A â†’ B} â†’ is-emb f â†’ is-injective f
  is-injective-is-emb is-emb-f {x} {y} = map-inv-is-equiv (is-emb-f x y)

  is-injective-emb : (e : A â†ª B) â†’ is-injective (map-emb e)
  is-injective-emb e {x} {y} = map-inv-is-equiv (is-emb-map-emb e x y)
```

### Any injective map between sets is an embedding

```agda
abstract
  is-emb-is-injective' : {l1 l2 : Level} {A : UU l1} (is-set-A : is-set A)
    {B : UU l2} (is-set-B : is-set B) (f : A â†’ B) â†’
    is-injective f â†’ is-emb f
  is-emb-is-injective' is-set-A is-set-B f is-injective-f x y =
    is-equiv-is-prop
      ( is-set-A x y)
      ( is-set-B (f x) (f y))
      ( is-injective-f)

  is-set-is-injective :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-set B â†’ is-injective f â†’ is-set A
  is-set-is-injective {f = f} H I =
    is-set-prop-in-id
      ( Î» x y â†’ f x ï¼ f y)
      ( Î» x y â†’ H (f x) (f y))
      ( Î» x â†’ refl)
      ( Î» x y â†’ I)

  is-emb-is-injective :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-set B â†’ is-injective f â†’ is-emb f
  is-emb-is-injective {f = f} H I =
    is-emb-is-injective' (is-set-is-injective H I) H f I

  is-prop-map-is-injective :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-set B â†’ is-injective f â†’ is-prop-map f
  is-prop-map-is-injective {f = f} H I =
    is-prop-map-is-emb (is-emb-is-injective H I)
```

### For a map between sets, being injective is a property

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-prop-is-injective :
    is-set A â†’ (f : A â†’ B) â†’ is-prop (is-injective f)
  is-prop-is-injective H f =
    is-prop-Î '
      ( Î» x â†’
        is-prop-Î '
          ( Î» y â†’ is-prop-function-type (H x y)))

  is-injective-Prop : is-set A â†’ (A â†’ B) â†’ Prop (l1 âŠ” l2)
  pr1 (is-injective-Prop H f) = is-injective f
  pr2 (is-injective-Prop H f) = is-prop-is-injective H f
```

### Any map out of a contractible type is injective

```agda
is-injective-is-contr :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-contr A â†’ is-injective f
is-injective-is-contr f H p = eq-is-contr H
```
