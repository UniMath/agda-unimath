# The universal property of truncations

```agda
module foundation-core.universal-property-truncation where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.universal-property-equivalences
open import foundation.universe-levels

open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.precomposition-functions
open import foundation-core.sections
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
open import foundation-core.type-theoretic-principle-of-choice
```

</details>

## Idea

We say that a map `f : A â†’ B` into a `k`-truncated type `B` is a
**`k`-truncation** of `A` -- or that it **satisfies the universal property of
the `k`-truncation** of `A` -- if any map `g : A â†’ C` into a `k`-truncated type
`C` extends uniquely along `f` to a map `B â†’ C`.

## Definition

### The condition on a map to be a truncation

```agda
precomp-Trunc :
  {l1 l2 l3 : Level} {k : ğ•‹} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (C : Truncated-Type l3 k) â†’
  (B â†’ type-Truncated-Type C) â†’ (A â†’ type-Truncated-Type C)
precomp-Trunc f C = precomp f (type-Truncated-Type C)

is-truncation :
  (l : Level) {l1 l2 : Level} {k : ğ•‹} {A : UU l1}
  (B : Truncated-Type l2 k) â†’ (A â†’ type-Truncated-Type B) â†’
  UU (l1 âŠ” l2 âŠ” lsuc l)
is-truncation l {k = k} B f =
  (C : Truncated-Type l k) â†’ is-equiv (precomp-Trunc f C)

equiv-is-truncation :
  {l1 l2 l3 : Level} {k : ğ•‹} {A : UU l1} (B : Truncated-Type l2 k)
  (f : A â†’ type-Truncated-Type B)
  (H : {l : Level} â†’ is-truncation l B f) â†’
  (C : Truncated-Type l3 k) â†’
  (type-Truncated-Type B â†’ type-Truncated-Type C) â‰ƒ (A â†’ type-Truncated-Type C)
pr1 (equiv-is-truncation B f H C) = precomp-Trunc f C
pr2 (equiv-is-truncation B f H C) = H C
```

### The universal property of truncations

```agda
universal-property-truncation :
  (l : Level) {l1 l2 : Level} {k : ğ•‹} {A : UU l1}
  (B : Truncated-Type l2 k) (f : A â†’ type-Truncated-Type B) â†’
  UU (lsuc l âŠ” l1 âŠ” l2)
universal-property-truncation l {k = k} {A} B f =
  (C : Truncated-Type l k) (g : A â†’ type-Truncated-Type C) â†’
  is-contr (Î£ (type-hom-Truncated-Type k B C) (Î» h â†’ (h âˆ˜ f) ~ g))
```

### The dependent universal property of truncations

```agda
precomp-Î -Truncated-Type :
  {l1 l2 l3 : Level} {k : ğ•‹} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (C : B â†’ Truncated-Type l3 k) â†’
  ((b : B) â†’ type-Truncated-Type (C b)) â†’
  ((a : A) â†’ type-Truncated-Type (C (f a)))
precomp-Î -Truncated-Type f C h a = h (f a)

dependent-universal-property-truncation :
  {l1 l2 : Level} (l : Level) {k : ğ•‹} {A : UU l1} (B : Truncated-Type l2 k)
  (f : A â†’ type-Truncated-Type B) â†’ UU (l1 âŠ” l2 âŠ” lsuc l)
dependent-universal-property-truncation l {k} B f =
  (X : type-Truncated-Type B â†’ Truncated-Type l k) â†’
  is-equiv (precomp-Î -Truncated-Type f X)
```

## Properties

### Equivalences into `k`-truncated types are truncations

```agda
abstract
  is-truncation-id :
    {l1 : Level} {k : ğ•‹} {A : UU l1} (H : is-trunc k A) â†’
    {l : Level} â†’ is-truncation l (A , H) id
  is-truncation-id H B =
    is-equiv-precomp-is-equiv id is-equiv-id (type-Truncated-Type B)

abstract
  is-truncation-equiv :
    {l1 l2 : Level} {k : ğ•‹} {A : UU l1} (B : Truncated-Type l2 k)
    (e : A â‰ƒ type-Truncated-Type B) â†’
    {l : Level} â†’ is-truncation l B (map-equiv e)
  is-truncation-equiv B e C =
    is-equiv-precomp-is-equiv
      ( map-equiv e)
      ( is-equiv-map-equiv e)
      ( type-Truncated-Type C)
```

### A map into a truncated type is a truncation if and only if it satisfies the universal property of the truncation

```agda
module _
  {l1 l2 : Level} {k : ğ•‹} {A : UU l1} (B : Truncated-Type l2 k)
  (f : A â†’ type-Truncated-Type B)
  where

  abstract
    is-truncation-universal-property-truncation :
      ({l : Level} â†’ universal-property-truncation l B f) â†’
      ({l : Level} â†’ is-truncation l B f)
    is-truncation-universal-property-truncation H C =
      is-equiv-is-contr-map
        ( Î» g â†’
          is-contr-equiv
            ( Î£ (type-hom-Truncated-Type k B C) (Î» h â†’ (h âˆ˜ f) ~ g))
            ( equiv-tot (Î» h â†’ equiv-funext))
            ( H C g))

  abstract
    universal-property-truncation-is-truncation :
      ({l : Level} â†’ is-truncation l B f) â†’
      ({l : Level} â†’ universal-property-truncation l B f)
    universal-property-truncation-is-truncation H C g =
      is-contr-equiv'
        ( Î£ (type-hom-Truncated-Type k B C) (Î» h â†’ (h âˆ˜ f) ï¼ g))
        ( equiv-tot (Î» h â†’ equiv-funext))
        ( is-contr-map-is-equiv (H C) g)

  map-is-truncation :
    ({l : Level} â†’ is-truncation l B f) â†’
    ({l : Level} (C : Truncated-Type l k) (g : A â†’ type-Truncated-Type C) â†’
    type-hom-Truncated-Type k B C)
  map-is-truncation H C g =
    pr1 (center (universal-property-truncation-is-truncation H C g))

  triangle-is-truncation :
    (H : {l : Level} â†’ is-truncation l B f) â†’
    {l : Level} (C : Truncated-Type l k) (g : A â†’ type-Truncated-Type C) â†’
    (map-is-truncation H C g âˆ˜ f) ~ g
  triangle-is-truncation H C g =
    pr2 (center (universal-property-truncation-is-truncation H C g))
```

### A map into a truncated type is a truncation if and only if it satisfies the dependent universal property of the truncation

```agda
module _
  {l1 l2 : Level} {k : ğ•‹} {A : UU l1} (B : Truncated-Type l2 k)
  (f : A â†’ type-Truncated-Type B)
  where

  abstract
    dependent-universal-property-truncation-is-truncation :
      ({l : Level} â†’ is-truncation l B f) â†’
      {l : Level} â†’ dependent-universal-property-truncation l B f
    dependent-universal-property-truncation-is-truncation H X =
      is-fiberwise-equiv-is-equiv-map-Î£
        ( Î» (h : A â†’ type-Truncated-Type B) â†’
          (a : A) â†’ type-Truncated-Type (X (h a)))
        ( Î» (g : type-Truncated-Type B â†’ type-Truncated-Type B) â†’ g âˆ˜ f)
        ( Î» g (s : (b : type-Truncated-Type B) â†’
          type-Truncated-Type (X (g b))) (a : A) â†’ s (f a))
        ( H B)
        ( is-equiv-equiv
          ( inv-distributive-Î -Î£)
          ( inv-distributive-Î -Î£)
          ( ind-Î£ (Î» g s â†’ refl))
          ( H (Î£-Truncated-Type B X)))
        ( id)

  abstract
    is-truncation-dependent-universal-property-truncation :
      ({l : Level} â†’ dependent-universal-property-truncation l B f) â†’
      {l : Level} â†’ is-truncation l B f
    is-truncation-dependent-universal-property-truncation H X =
      H (Î» b â†’ X)

  section-is-truncation :
    ({l : Level} â†’ is-truncation l B f) â†’
    {l3 : Level} (C : Truncated-Type l3 k)
    (h : A â†’ type-Truncated-Type C) (g : type-hom-Truncated-Type k C B) â†’
    f ~ (g âˆ˜ h) â†’ section g
  section-is-truncation H C h g K =
    map-distributive-Î -Î£
      ( map-inv-is-equiv
        ( dependent-universal-property-truncation-is-truncation H
          ( fiber-Truncated-Type C B g))
        ( Î» a â†’ h a , inv (K a)))
```
