# Empty types

```agda
module foundation-core.empty-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.universe-levels

open import foundation-core.embeddings
open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.propositions
open import foundation-core.sets
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Idea

An **empty type** is a type with no elements. The (standard) empty type is
introduced as an inductive type with no constructors. With the standard empty
type available, we will say that a type is empty if it maps into the standard
empty type.

## Definition

### Empty types

```agda
data empty : UU lzero where

ind-empty : {l : Level} {P : empty â†’ UU l} â†’ ((x : empty) â†’ P x)
ind-empty ()

ex-falso : {l : Level} {A : UU l} â†’ empty â†’ A
ex-falso = ind-empty

is-empty : {l : Level} â†’ UU l â†’ UU l
is-empty A = A â†’ empty

is-nonempty : {l : Level} â†’ UU l â†’ UU l
is-nonempty A = is-empty (is-empty A)
```

## Properties

### The map `ex-falso` is an embedding

```agda
module _
  {l : Level} {A : UU l}
  where

  abstract
    is-emb-ex-falso : is-emb (ex-falso {A = A})
    is-emb-ex-falso ()

  ex-falso-emb : empty â†ª A
  pr1 ex-falso-emb = ex-falso
  pr2 ex-falso-emb = is-emb-ex-falso
```

### Any map into an empty type is an equivalence

```agda
abstract
  is-equiv-is-empty :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    is-empty B â†’ is-equiv f
  is-equiv-is-empty f H =
    is-equiv-is-invertible
      ( ex-falso âˆ˜ H)
      ( Î» y â†’ ex-falso (H y))
      ( Î» x â†’ ex-falso (H (f x)))

abstract
  is-equiv-is-empty' :
    {l : Level} {A : UU l} (f : is-empty A) â†’ is-equiv f
  is-equiv-is-empty' f = is-equiv-is-empty f id

equiv-is-empty' : {l : Level} {A : UU l} â†’ is-empty A â†’ A â‰ƒ empty
equiv-is-empty' f = (f , is-equiv-is-empty' f)

equiv-is-empty :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ is-empty A â†’ is-empty B â†’ A â‰ƒ B
equiv-is-empty f g =
  ( inv-equiv (pair g (is-equiv-is-empty g id))) âˆ˜e
  ( pair f (is-equiv-is-empty f id))
```

### Any map into an empty type is an embedding

```agda
abstract
  is-emb-is-empty :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    is-empty B â†’ is-emb f
  is-emb-is-empty f H = is-emb-is-equiv (is-equiv-is-empty f H)
```

### The empty type is a proposition

```agda
abstract
  is-prop-empty : is-prop empty
  is-prop-empty ()

empty-Prop : Prop lzero
pr1 empty-Prop = empty
pr2 empty-Prop = is-prop-empty

is-prop-is-empty : {l : Level} {A : UU l} â†’ is-empty A â†’ is-prop A
is-prop-is-empty is-empty-A = ex-falso âˆ˜ is-empty-A
```

### The empty type is a set

```agda
is-set-empty : is-set empty
is-set-empty ()

empty-Set : Set lzero
pr1 empty-Set = empty
pr2 empty-Set = is-set-empty
```

### The empty type is `k`-truncated for any `k â‰¥ 1`

```agda
abstract
  is-trunc-empty : (k : ğ•‹) â†’ is-trunc (succ-ğ•‹ k) empty
  is-trunc-empty k ()

empty-Truncated-Type : (k : ğ•‹) â†’ Truncated-Type lzero (succ-ğ•‹ k)
pr1 (empty-Truncated-Type k) = empty
pr2 (empty-Truncated-Type k) = is-trunc-empty k

abstract
  is-trunc-is-empty :
    {l : Level} (k : ğ•‹) {A : UU l} â†’ is-empty A â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-is-empty k f x = ex-falso (f x)
```
