# Involutions

```agda
module foundation-core.involutions where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.automorphisms
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.transport
open import foundation.universe-levels

open import foundation-core.equivalences
open import foundation-core.homotopies
open import foundation-core.truncated-types
open import foundation-core.truncation-levels

open import structured-types.pointed-types
```

</details>

## Idea

An **involution** on a type `A` is a map `f : A â†’ A` such that `(f âˆ˜ f) ~ id`.

## Definition

```agda
module _
  {l : Level} {A : UU l}
  where

  is-involution : (A â†’ A) â†’ UU l
  is-involution f = (f âˆ˜ f) ~ id

  is-involution-aut : Aut A â†’ UU l
  is-involution-aut e = is-involution (map-equiv e)
```

### The type of involutions on `A`

```agda
involution : {l : Level} â†’ UU l â†’ UU l
involution A = Î£ (A â†’ A) is-involution

module _
  {l : Level} {A : UU l} (f : involution A)
  where

  map-involution : A â†’ A
  map-involution = pr1 f

  is-involution-map-involution : is-involution map-involution
  is-involution-map-involution = pr2 f
```

## Properties

### Involutions are equivalences

```agda
is-equiv-is-involution :
  {l : Level} {A : UU l} {f : A â†’ A} â†’ is-involution f â†’ is-equiv f
is-equiv-is-involution {f = f} is-involution-f =
  is-equiv-has-inverse f is-involution-f is-involution-f

is-equiv-map-involution :
  {l : Level} {A : UU l} (f : involution A) â†’ is-equiv (map-involution f)
is-equiv-map-involution = is-equiv-is-involution âˆ˜ is-involution-map-involution

equiv-is-involution :
  {l : Level} {A : UU l} {f : A â†’ A} â†’ is-involution f â†’ A â‰ƒ A
pr1 (equiv-is-involution {f = f} is-involution-f) = f
pr2 (equiv-is-involution is-involution-f) =
  is-equiv-is-involution is-involution-f

equiv-involution :
  {l : Level} {A : UU l} â†’ involution A â†’ A â‰ƒ A
equiv-involution f =
  equiv-is-involution {f = map-involution f} (is-involution-map-involution f)
```

### If `A` is `k`-truncated then the type of involutions is `k`-truncated

```agda
is-trunc-is-involution :
  {l : Level} (k : ğ•‹) {A : UU l} â†’
  is-trunc (succ-ğ•‹ k) A â†’ (f : A â†’ A) â†’ is-trunc k (is-involution f)
is-trunc-is-involution k is-trunc-A f =
  is-trunc-Î  k Î» x â†’ is-trunc-A (f (f x)) x

is-involution-Truncated-Type :
  {l : Level} (k : ğ•‹) {A : UU l} â†’
  is-trunc (succ-ğ•‹ k) A â†’ (A â†’ A) â†’ Truncated-Type l k
pr1 (is-involution-Truncated-Type k is-trunc-A f) = is-involution f
pr2 (is-involution-Truncated-Type k is-trunc-A f) =
  is-trunc-is-involution k is-trunc-A f

is-trunc-involution :
  {l : Level} (k : ğ•‹) {A : UU l} â†’
  is-trunc k A â†’ is-trunc k (involution A)
is-trunc-involution k is-trunc-A =
  is-trunc-Î£
    ( is-trunc-function-type k is-trunc-A)
    ( is-trunc-is-involution k (is-trunc-succ-is-trunc k is-trunc-A))

involution-Truncated-Type :
  {l : Level} (k : ğ•‹) â†’ Truncated-Type l k â†’ Truncated-Type l k
pr1 (involution-Truncated-Type k (A , is-trunc-A)) = involution A
pr2 (involution-Truncated-Type k (A , is-trunc-A)) =
  is-trunc-involution k is-trunc-A
```

### The identity function is an involution

```agda
is-involution-id :
  {l : Level} {A : UU l} â†’ is-involution (id {A = A})
is-involution-id = refl-htpy

id-involution :
  {l : Level} {A : UU l} â†’ involution A
pr1 id-involution = id
pr2 id-involution = is-involution-id

involution-Pointed-Type :
  {l : Level} (A : UU l) â†’ Pointed-Type l
pr1 (involution-Pointed-Type A) = involution A
pr2 (involution-Pointed-Type A) = id-involution
```

### Involutions on dependent function types

```agda
involution-Î -involution-fam :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  ((x : A) â†’ involution (B x)) â†’ involution ((x : A) â†’ B x)
pr1 (involution-Î -involution-fam i) f x =
  map-involution (i x) (f x)
pr2 (involution-Î -involution-fam i) f =
  eq-htpy (Î» x â†’ is-involution-map-involution (i x) (f x))
```
