# Truncated types

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation-core.truncated-types where

open import foundation-core.contractible-types using
  ( is-contr; eq-is-contr; is-contr-is-equiv; is-contr-retract-of)
open import foundation-core.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation-core.embeddings using
  ( is-emb; _â†ª_; map-emb; is-emb-map-emb)
open import foundation-core.equivalences using
  ( is-equiv; _â‰ƒ_; map-inv-is-equiv; is-equiv-map-inv-is-equiv)
open import foundation-core.identity-types using (Id; refl; left-inv; ap)
open import foundation-core.retractions using (_retract-of_; retract-eq)
open import foundation-core.truncation-levels using (ğ•‹; neg-two-ğ•‹; succ-ğ•‹)
open import foundation-core.universe-levels using (Level; UU; lsuc)
```

## Idea

The truncatedness of a type is a measure of the complexity of its identity types. The simplest case is a contractible type. This is the base case of the inductive definition of truncatedness for types. A type is (k+1)-truncated if its identity types are k-truncated.

## Definition

### The condition of truncatedness

```agda
is-trunc : {i : Level} (k : ğ•‹) â†’ UU i â†’ UU i
is-trunc neg-two-ğ•‹ A = is-contr A
is-trunc (succ-ğ•‹ k) A = (x y : A) â†’ is-trunc k (Id x y)
```

### The universe of truncated types

```agda
UU-Truncated-Type : (l : Level) â†’ ğ•‹ â†’ UU (lsuc l)
UU-Truncated-Type l k = Î£ (UU l) (is-trunc k)

module _
  {k : ğ•‹} {l : Level}
  where
  
  type-Truncated-Type : UU-Truncated-Type l k â†’ UU l
  type-Truncated-Type = pr1

  abstract
    is-trunc-type-Truncated-Type :
      (A : UU-Truncated-Type l k) â†’ is-trunc k (type-Truncated-Type A)
    is-trunc-type-Truncated-Type = pr2
```

## Properties

### If a type is k-truncated, then it is (k+1)-truncated.

```agda
abstract
  is-trunc-succ-is-trunc :
    (k : ğ•‹) {i : Level} {A : UU i} â†’ is-trunc k A â†’ is-trunc (succ-ğ•‹ k) A
  pr1 (is-trunc-succ-is-trunc neg-two-ğ•‹ H x y) = eq-is-contr H
  pr2 (is-trunc-succ-is-trunc neg-two-ğ•‹ H x .x) refl = left-inv (pr2 H x)
  is-trunc-succ-is-trunc (succ-ğ•‹ k) H x y = is-trunc-succ-is-trunc k (H x y)

truncated-type-succ-Truncated-Type :
  (k : ğ•‹) {l : Level} â†’ UU-Truncated-Type l k â†’ UU-Truncated-Type l (succ-ğ•‹ k)
pr1 (truncated-type-succ-Truncated-Type k A) = type-Truncated-Type A
pr2 (truncated-type-succ-Truncated-Type k A) =
  is-trunc-succ-is-trunc k (is-trunc-type-Truncated-Type A)
```

### The identity type of a k-truncated type is k-truncated

```agda
abstract
  is-trunc-Id :
    {l : Level} {k : ğ•‹} {A : UU l} â†’
    is-trunc k A â†’ (x y : A) â†’ is-trunc k (Id x y)
  is-trunc-Id {l} {k}= is-trunc-succ-is-trunc k

Id-Truncated-Type :
  {l : Level} {k : ğ•‹} (A : UU-Truncated-Type l (succ-ğ•‹ k)) â†’
  (x y : type-Truncated-Type A) â†’ UU-Truncated-Type l k
pr1 (Id-Truncated-Type A x y) = Id x y
pr2 (Id-Truncated-Type A x y) = is-trunc-type-Truncated-Type A x y

Id-Truncated-Type' :
  {l : Level} {k : ğ•‹} (A : UU-Truncated-Type l k) â†’
  (x y : type-Truncated-Type A) â†’ UU-Truncated-Type l k
pr1 (Id-Truncated-Type' A x y) = Id x y
pr2 (Id-Truncated-Type' A x y) =
  is-trunc-Id (is-trunc-type-Truncated-Type A) x y
```

### k-truncated types are closed under retracts

```agda
module _
  {l1 l2 : Level}
  where

  is-trunc-retract-of :
    {k : ğ•‹} {A : UU l1} {B : UU l2} â†’
    A retract-of B â†’ is-trunc k B â†’ is-trunc k A
  is-trunc-retract-of {neg-two-ğ•‹} = is-contr-retract-of _
  is-trunc-retract-of {succ-ğ•‹ k} R H x y =
    is-trunc-retract-of (retract-eq R x y) (H (pr1 R x) (pr1 R y))
```

### k-truncated types are closed under equivalences

```agda
abstract
  is-trunc-is-equiv :
    {i j : Level} (k : ğ•‹) {A : UU i} (B : UU j) (f : A â†’ B) â†’ is-equiv f â†’
    is-trunc k B â†’ is-trunc k A
  is-trunc-is-equiv k B f is-equiv-f =
    is-trunc-retract-of (pair f (pr2 is-equiv-f))

abstract
  is-trunc-equiv :
    {i j : Level} (k : ğ•‹) {A : UU i} (B : UU  j) (e : A â‰ƒ B) â†’
    is-trunc k B â†’ is-trunc k A
  is-trunc-equiv k B (pair f is-equiv-f) =
    is-trunc-is-equiv k B f is-equiv-f

abstract
  is-trunc-is-equiv' :
    {i j : Level} (k : ğ•‹) (A : UU i) {B : UU j} (f : A â†’ B) â†’
    is-equiv f â†’ is-trunc k A â†’ is-trunc k B
  is-trunc-is-equiv' k A  f is-equiv-f is-trunc-A =
    is-trunc-is-equiv k A
      ( map-inv-is-equiv is-equiv-f)
      ( is-equiv-map-inv-is-equiv is-equiv-f)
      ( is-trunc-A)

abstract
  is-trunc-equiv' :
    {i j : Level} (k : ğ•‹) (A : UU i) {B : UU j} (e : A â‰ƒ B) â†’
    is-trunc k A â†’ is-trunc k B
  is-trunc-equiv' k A (pair f is-equiv-f) =
    is-trunc-is-equiv' k A f is-equiv-f

```

### If a type embeds into a (k+1)-truncated type, then it is (k+1)-truncated

```agda
abstract
  is-trunc-is-emb :
    {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} (f : A â†’ B) â†’
    is-emb f â†’ is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-is-emb k f Ef H x y =
    is-trunc-is-equiv k (Id (f x) (f y)) (ap f {x} {y}) (Ef x y) (H (f x) (f y))

abstract
  is-trunc-emb :
    {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} (f : A â†ª B) â†’
    is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-emb k f = is-trunc-is-emb k (map-emb f) (is-emb-map-emb f)
```
