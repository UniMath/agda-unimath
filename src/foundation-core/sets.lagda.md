# Sets

```agda
module foundation-core.sets where
```

<details><summary>Imports</summary>

```agda
open import foundation-core.contractible-types
open import foundation-core.dependent-pair-types
open import foundation-core.equivalences
open import foundation-core.fundamental-theorem-of-identity-types
open import foundation-core.identity-types
open import foundation-core.propositions
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
open import foundation-core.universe-levels
```

</details>

## Idea

A type is a set if its identity types are propositions.

## Definition

```agda
is-set :
  {i : Level} â†’ UU i â†’ UU i
is-set A = (x y : A) â†’ is-prop (x ï¼ y)

Set :
  (i : Level) â†’ UU (lsuc i)
Set i = Î£ (UU i) is-set

module _
  {l : Level} (X : Set l)
  where

  type-Set : UU l
  type-Set = pr1 X

  abstract
    is-set-type-Set : is-set type-Set
    is-set-type-Set = pr2 X

  Id-Prop : (x y : type-Set) â†’ Prop l
  pr1 (Id-Prop x y) = (x ï¼ y)
  pr2 (Id-Prop x y) = is-set-type-Set x y
```

## Properties

### A type is a set if and only if it satisfies Streicher's axiom K

```agda
axiom-K :
  {i : Level} â†’ UU i â†’ UU i
axiom-K A = (x : A) (p : x ï¼ x) â†’ refl ï¼ p

module _
  {l : Level} {A : UU l}
  where

  abstract
    is-set-axiom-K' : axiom-K A â†’ (x y : A) â†’ all-elements-equal (x ï¼ y)
    is-set-axiom-K' K x .x refl q with K x q
    ... | refl = refl

  abstract
    is-set-axiom-K : axiom-K A â†’ is-set A
    is-set-axiom-K H x y = is-prop-all-elements-equal (is-set-axiom-K' H x y)

  abstract
    axiom-K-is-set : is-set A â†’ axiom-K A
    axiom-K-is-set H x p =
      ( inv (contraction (is-proof-irrelevant-is-prop (H x x) refl) refl)) âˆ™
      ( contraction (is-proof-irrelevant-is-prop (H x x) refl) p)
```

### If a reflexive binary relation maps into the identity type of A, then A is a set

```
module _
  {l1 l2 : Level} {A : UU l1} (R : A â†’ A â†’ UU l2)
  (p : (x y : A) â†’ is-prop (R x y)) (Ï : (x : A) â†’ R x x)
  (i : (x y : A) â†’ R x y â†’ x ï¼ y)
  where

  abstract
    is-equiv-prop-in-id : (x y : A) â†’ is-equiv (i x y)
    is-equiv-prop-in-id x =
      fundamental-theorem-id-retr x (i x)
        ( Î» y â†’
          pair
            ( ind-Id x (Î» z p â†’ R x z) (Ï x) y)
            ( Î» r â†’ eq-is-prop (p x y)))

  abstract
    is-set-prop-in-id : is-set A
    is-set-prop-in-id x y = is-prop-is-equiv' (is-equiv-prop-in-id x y) (p x y)
```

### Any proposition is a set

```agda
abstract
  is-set-is-prop :
    {l : Level} {P : UU l} â†’ is-prop P â†’ is-set P
  is-set-is-prop = is-trunc-succ-is-trunc neg-one-ğ•‹

set-Prop :
  {l : Level} â†’ Prop l â†’ Set l
set-Prop P = truncated-type-succ-Truncated-Type neg-one-ğ•‹ P
```

### Sets are closed under equivalences

```agda
abstract
  is-set-is-equiv :
    {i j : Level} {A : UU i} (B : UU j) (f : A â†’ B) â†’ is-equiv f â†’
    is-set B â†’ is-set A
  is-set-is-equiv = is-trunc-is-equiv zero-ğ•‹

abstract
  is-set-equiv :
    {i j : Level} {A : UU i} (B : UU j) (e : A â‰ƒ B) â†’
    is-set B â†’ is-set A
  is-set-equiv = is-trunc-equiv zero-ğ•‹

abstract
  is-set-is-equiv' :
    {i j : Level} (A : UU i) {B : UU j} (f : A â†’ B) â†’ is-equiv f â†’
    is-set A â†’ is-set B
  is-set-is-equiv' = is-trunc-is-equiv' zero-ğ•‹

abstract
  is-set-equiv' :
    {i j : Level} (A : UU i) {B : UU j} (e : A â‰ƒ B) â†’
    is-set A â†’ is-set B
  is-set-equiv' = is-trunc-equiv' zero-ğ•‹
```
