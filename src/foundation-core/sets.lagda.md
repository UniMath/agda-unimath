# Sets

```agda
module foundation-core.sets where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.universe-levels

open import foundation-core.contractible-types
open import foundation-core.embeddings
open import foundation-core.equivalences
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.propositions
open import foundation-core.torsorial-type-families
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Idea

A type is a {{#concept "set" Agda=is-set}} if its
[identity types](foundation-core.identity-types.md) are
[propositions](foundation-core.propositions.md).

## Definition

```agda
is-set : {l : Level} â†’ UU l â†’ UU l
is-set A = (x y : A) â†’ is-prop (x ï¼ y)

Set : (l : Level) â†’ UU (lsuc l)
Set l = Î£ (UU l) is-set

module _
  {l : Level} (X : Set l)
  where

  type-Set : UU l
  type-Set = pr1 X

  abstract
    is-set-type-Set : is-set type-Set
    is-set-type-Set = pr2 X

  Id-Prop : (x y : type-Set) â†’ Prop l
  Id-Prop x y = (x ï¼ y , is-set-type-Set x y)
```

## Properties

### A type is a set if and only if it satisfies Streicher's axiom K

```agda
instance-axiom-K : {l : Level} â†’ UU l â†’ UU l
instance-axiom-K A = (x : A) (p : x ï¼ x) â†’ refl ï¼ p

axiom-K-Level : (l : Level) â†’ UU (lsuc l)
axiom-K-Level l = (A : UU l) â†’ instance-axiom-K A

axiom-K : UUÏ‰
axiom-K = {l : Level} â†’ axiom-K-Level l

module _
  {l : Level} {A : UU l}
  where

  abstract
    is-set-axiom-K' :
      instance-axiom-K A â†’ (x y : A) â†’ all-elements-equal (x ï¼ y)
    is-set-axiom-K' K x .x refl q with K x q
    ... | refl = refl

  abstract
    is-set-axiom-K : instance-axiom-K A â†’ is-set A
    is-set-axiom-K H x y = is-prop-all-elements-equal (is-set-axiom-K' H x y)

  abstract
    axiom-K-is-set : is-set A â†’ instance-axiom-K A
    axiom-K-is-set H x p =
      ( inv (contraction (is-proof-irrelevant-is-prop (H x x) refl) refl)) âˆ™
      ( contraction (is-proof-irrelevant-is-prop (H x x) refl) p)
```

### A type is a set if and only if it satisfies uniqueness of identity proofs

A type `A` is said to satisfy
{{#concept "uniqueness of identity proofs" Agda=has-uip}} if for all elements
`x y : A` all equality proofs `x ï¼ y` are equal.

```agda
has-uip : {l : Level} â†’ UU l â†’ UU l
has-uip A = (x y : A) â†’ all-elements-equal (x ï¼ y)

module _
  {l : Level} {A : UU l}
  where

  is-set-has-uip : is-set A â†’ has-uip A
  is-set-has-uip is-set-A x y = eq-is-prop' (is-set-A x y)

  has-uip-is-set : has-uip A â†’ is-set A
  has-uip-is-set uip-A x y = is-prop-all-elements-equal (uip-A x y)
```

### If a reflexive binary relation maps into the identity type of `A`, then `A` is a set

```agda
module _
  {l1 l2 : Level} {A : UU l1} (x : A) (R : A â†’ UU l2)
  (p : (y : A) â†’ is-prop (R y)) (Ï : R x)
  (i : (y : A) â†’ R y â†’ x ï¼ y)
  where

  abstract
    is-equiv-prop-in-based-id : (y : A) â†’ is-equiv (i y)
    is-equiv-prop-in-based-id =
      fundamental-theorem-id-retraction x i
        ( Î» y â†’ (ind-Id x (Î» z p â†’ R z) Ï y) , (Î» r â†’ eq-is-prop (p y)))

  abstract
    is-torsorial-prop-in-based-id : is-torsorial R
    is-torsorial-prop-in-based-id =
      fundamental-theorem-id'
        ( Î» y â†’ map-inv-is-equiv (is-equiv-prop-in-based-id y))
        ( Î» y â†’ is-equiv-map-inv-is-equiv (is-equiv-prop-in-based-id y))

  abstract
    is-prop-based-Id-prop-in-based-id : (y : A) â†’ is-prop (x ï¼ y)
    is-prop-based-Id-prop-in-based-id y =
      is-prop-is-equiv' (is-equiv-prop-in-based-id y) (p y)
```

```agda
module _
  {l1 l2 : Level} {A : UU l1} (R : A â†’ A â†’ UU l2)
  (p : (x y : A) â†’ is-prop (R x y)) (Ï : (x : A) â†’ R x x)
  (i : (x y : A) â†’ R x y â†’ x ï¼ y)
  where

  abstract
    is-equiv-prop-in-id : (x y : A) â†’ is-equiv (i x y)
    is-equiv-prop-in-id x = is-equiv-prop-in-based-id x (R x) (p x) (Ï x) (i x)

  abstract
    is-set-prop-in-id : is-set A
    is-set-prop-in-id x =
      is-prop-based-Id-prop-in-based-id x (R x) (p x) (Ï x) (i x)
```

### Any proposition is a set

```agda
abstract
  is-set-is-prop :
    {l : Level} {P : UU l} â†’ is-prop P â†’ is-set P
  is-set-is-prop = is-trunc-succ-is-trunc neg-one-ğ•‹

set-Prop :
  {l : Level} â†’ Prop l â†’ Set l
set-Prop P = truncated-type-succ-Truncated-Type neg-one-ğ•‹ P
```

### Sets are closed under equivalences

```agda
abstract
  is-set-is-equiv :
    {l1 l2 : Level} {A : UU l1} (B : UU l2) (f : A â†’ B) â†’ is-equiv f â†’
    is-set B â†’ is-set A
  is-set-is-equiv = is-trunc-is-equiv zero-ğ•‹

abstract
  is-set-equiv :
    {l1 l2 : Level} {A : UU l1} (B : UU l2) (e : A â‰ƒ B) â†’
    is-set B â†’ is-set A
  is-set-equiv = is-trunc-equiv zero-ğ•‹

abstract
  is-set-is-equiv' :
    {l1 l2 : Level} (A : UU l1) {B : UU l2} (f : A â†’ B) â†’ is-equiv f â†’
    is-set A â†’ is-set B
  is-set-is-equiv' = is-trunc-is-equiv' zero-ğ•‹

abstract
  is-set-equiv' :
    {l1 l2 : Level} (A : UU l1) {B : UU l2} (e : A â‰ƒ B) â†’
    is-set A â†’ is-set B
  is-set-equiv' = is-trunc-equiv' zero-ğ•‹

abstract
  is-set-equiv-is-set :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-set A â†’ is-set B â†’ is-set (A â‰ƒ B)
  is-set-equiv-is-set = is-trunc-equiv-is-trunc zero-ğ•‹

module _
  {l1 l2 : Level} (A : Set l1) (B : Set l2)
  where

  equiv-Set : UU (l1 âŠ” l2)
  equiv-Set = type-Set A â‰ƒ type-Set B

  equiv-set-Set : Set (l1 âŠ” l2)
  pr1 equiv-set-Set = equiv-Set
  pr2 equiv-set-Set =
    is-set-equiv-is-set (is-set-type-Set A) (is-set-type-Set B)
```

### If a type injects into a set, then it is a set

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-set-is-injective :
      {f : A â†’ B} â†’ is-set B â†’ is-injective f â†’ is-set A
    is-set-is-injective {f} H I =
      is-set-prop-in-id
        ( Î» x y â†’ f x ï¼ f y)
        ( Î» x y â†’ H (f x) (f y))
        ( Î» x â†’ refl)
        ( Î» x y â†’ I)
```
