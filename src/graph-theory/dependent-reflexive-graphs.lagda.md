# Dependent reflexive graphs

```agda
module graph-theory.dependent-reflexive-graphs where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.universe-levels

open import graph-theory.dependent-directed-graphs
open import graph-theory.reflexive-graphs
```

</details>

## Idea

Consider a [reflexive graph](graph-theory.reflexive-graphs.md) `A`. A
{{#concept "dependent reflexive graph" Agda=Dependent-Reflexive-Graph}} `B` over
`A` consists of:

- A family `Bâ‚€ : Aâ‚€ â†’ ğ’°` of types as the type family of vertices
- A family `Bâ‚ : {x y : Aâ‚€} â†’ Aâ‚ x y â†’ Bâ‚€ x â†’ Bâ‚€ y â†’ ğ’°` of
  [binary relations](foundation.binary-relations.md) between the types of
  vertices `Bâ‚€`, indexed by the type of edges `Aâ‚` in `A`.
- A family of elements `refl B : (x : Aâ‚€) (y : Bâ‚€ x) â†’ Bâ‚ (refl A x) y y`
  witnessing the reflexivity of `Bâ‚` over the reflexivity `refl A` of `Aâ‚`.

To see that this is a sensible definition of dependent reflexive graphs, observe
that the type of reflexive graphs itself is
[equivalent](foundation-core.equivalences.md) to the type of dependent reflexive
graphs over the
[terminal reflexive graph](graph-theory.terminal-reflexive-graphs.md).
Furthermore, [graph homomorphisms](graph-theory.morphisms-reflexive-graphs.md)
into the [universal reflexive graph](graph-theory.universal-reflexive-graph.md)
are equivalent to dependent reflexive graphs.

Alternatively, a dependent reflexive graph `B` over `A` can be defined by

- A family `Bâ‚€ : Aâ‚€ â†’ Reflexive-Graph` of reflexive graphs as the type family of
  vertices
- A family `Bâ‚ : {x y : Aâ‚€} â†’ Aâ‚ x y â†’ (Bâ‚€ x)â‚€ â†’ (Bâ‚€ y)â‚€ â†’ ğ’°`.
- A [family of equivalences](foundation.families-of-equivalences.md)
  `refl B : (x : Aâ‚€) (y y' : Bâ‚€ x) â†’ Bâ‚ (refl A x) y y' â‰ƒ (Bâ‚€ x)â‚ y y'`.

This definition is more closely related to the concept of morphism into the
universal reflexive graph.

## Definitions

### Dependent reflexive graphs

```agda
Dependent-Reflexive-Graph :
  {l1 l2 : Level} (l3 l4 : Level) â†’ Reflexive-Graph l1 l2 â†’
  UU (l1 âŠ” l2 âŠ” lsuc l3 âŠ” lsuc l4)
Dependent-Reflexive-Graph l3 l4 A =
  Î£ ( Dependent-Directed-Graph l3 l4 (directed-graph-Reflexive-Graph A))
    ( Î» B â†’
      (x : vertex-Reflexive-Graph A) (y : vertex-Dependent-Directed-Graph B x) â†’
      edge-Dependent-Directed-Graph B (refl-Reflexive-Graph A x) y y)

module _
  {l1 l2 l3 l4 : Level} {A : Reflexive-Graph l1 l2}
  (B : Dependent-Reflexive-Graph l3 l4 A)
  where

  dependent-directed-graph-Dependent-Reflexive-Graph :
    Dependent-Directed-Graph l3 l4 (directed-graph-Reflexive-Graph A)
  dependent-directed-graph-Dependent-Reflexive-Graph = pr1 B

  vertex-Dependent-Reflexive-Graph :
    vertex-Reflexive-Graph A â†’ UU l3
  vertex-Dependent-Reflexive-Graph =
    vertex-Dependent-Directed-Graph
      dependent-directed-graph-Dependent-Reflexive-Graph

  edge-Dependent-Reflexive-Graph :
    {x y : vertex-Reflexive-Graph A} â†’
    edge-Reflexive-Graph A x y â†’
    vertex-Dependent-Reflexive-Graph x â†’
    vertex-Dependent-Reflexive-Graph y â†’ UU l4
  edge-Dependent-Reflexive-Graph =
    edge-Dependent-Directed-Graph
      dependent-directed-graph-Dependent-Reflexive-Graph

  refl-Dependent-Reflexive-Graph :
    {x : vertex-Reflexive-Graph A} (y : vertex-Dependent-Reflexive-Graph x) â†’
    edge-Dependent-Reflexive-Graph (refl-Reflexive-Graph A x) y y
  refl-Dependent-Reflexive-Graph = pr2 B _
```

### An equivalent definition of dependent reflexive graphs

The second definition of dependent reflexive graphs is more closely equivalent
to the concept of morphisms into the
[universal reflexive graph](graph-theory.universal-reflexive-graph.md).

```agda
Dependent-Reflexive-Graph' :
  {l1 l2 : Level} (l3 l4 : Level) â†’ Reflexive-Graph l1 l2 â†’
  UU (l1 âŠ” l2 âŠ” lsuc l3 âŠ” lsuc l4)
Dependent-Reflexive-Graph' l3 l4 G =
  Î£ ( Î£ ( vertex-Reflexive-Graph G â†’ Reflexive-Graph l3 l4)
        ( Î» H â†’
          (x y : vertex-Reflexive-Graph G)
          (e : edge-Reflexive-Graph G x y) â†’
          vertex-Reflexive-Graph (H x) â†’ vertex-Reflexive-Graph (H y) â†’ UU l4))
    ( Î» (Hâ‚€ , Hâ‚) â†’
      (x : vertex-Reflexive-Graph G)
      (u v : vertex-Reflexive-Graph (Hâ‚€ x)) â†’
      Hâ‚ x x (refl-Reflexive-Graph G x) u v â‰ƒ
      edge-Reflexive-Graph (Hâ‚€ x) u v)
```

### Constant dependent reflexive graphs

```agda
module _
  {l1 l2 l3 l4 : Level} (A : Reflexive-Graph l1 l2) (B : Reflexive-Graph l3 l4)
  where

  vertex-constant-Dependent-Reflexive-Graph :
    vertex-Reflexive-Graph A â†’ UU l3
  vertex-constant-Dependent-Reflexive-Graph x = vertex-Reflexive-Graph B

  edge-constant-Dependent-Reflexive-Graph :
    {x y : vertex-Reflexive-Graph A} â†’
    edge-Reflexive-Graph A x y â†’
    vertex-constant-Dependent-Reflexive-Graph x â†’
    vertex-constant-Dependent-Reflexive-Graph y â†’ UU l4
  edge-constant-Dependent-Reflexive-Graph _ =
    edge-Reflexive-Graph B

  refl-constant-Dependent-Reflexive-Graph :
    (x : vertex-Reflexive-Graph A)
    (y : vertex-constant-Dependent-Reflexive-Graph x) â†’
    edge-constant-Dependent-Reflexive-Graph (refl-Reflexive-Graph A x) y y
  refl-constant-Dependent-Reflexive-Graph _ =
    refl-Reflexive-Graph B

  constant-Dependent-Reflexive-Graph : Dependent-Reflexive-Graph l3 l4 A
  pr1 (pr1 constant-Dependent-Reflexive-Graph) =
    vertex-constant-Dependent-Reflexive-Graph
  pr2 (pr1 constant-Dependent-Reflexive-Graph) _ _ =
    edge-constant-Dependent-Reflexive-Graph
  pr2 constant-Dependent-Reflexive-Graph =
    refl-constant-Dependent-Reflexive-Graph
```

### Evaluating dependent reflexive graphs at a point

```agda
module _
  {l1 l2 l3 l4 : Level} {G : Reflexive-Graph l1 l2}
  (H : Dependent-Reflexive-Graph l3 l4 G) (x : vertex-Reflexive-Graph G)
  where

  ev-point-Dependent-Reflexive-Graph : Reflexive-Graph l3 l4
  pr1 (pr1 ev-point-Dependent-Reflexive-Graph) =
    vertex-Dependent-Reflexive-Graph H x
  pr2 (pr1 ev-point-Dependent-Reflexive-Graph) =
    edge-Dependent-Reflexive-Graph H (refl-Reflexive-Graph G x)
  pr2 ev-point-Dependent-Reflexive-Graph =
    refl-Dependent-Reflexive-Graph H
```

## See also

- The [universal reflexive graph](graph-theory.universal-reflexive-graph.md)
- [base change of dependent reflexive graphs](graph-theory.base-change-dependent-reflexive-graphs.md)
