# The Ï‰-iteration fixed point construction for posets

```agda
module domain-theory.omega-iteration-fixed-point-construction-posets where
```

<details><summary>Imports</summary>

```agda
open import domain-theory.omega-continuous-maps-posets

open import elementary-number-theory.decidable-total-order-natural-numbers
open import elementary-number-theory.inequality-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.dependent-pair-types
open import foundation.fixed-points-endofunctions
open import foundation.function-types
open import foundation.identity-types
open import foundation.iterating-functions
open import foundation.universe-levels

open import order-theory.inflationary-maps-posets
open import order-theory.increasing-sequences-posets
open import order-theory.least-upper-bounds-posets
open import order-theory.order-preserving-maps-posets
open import order-theory.posets
open import order-theory.upper-bounds-posets
```

</details>

## Idea

Given a map `f : ğ’œ â†’ ğ’œ` on a [poset](order-theory.posets.md) and `x : ğ’œ` such
that the [sequence](lists.sequences.md) of
[iterates](foundation.iterating-functions.md) `x â‰¤ f x â‰¤ fÂ² x â‰¤ â‹¯` is
increasing, has a
[least upper bound](order-theory.least-upper-bounds-posets.md), and `f`
preserves that least upper bound. Then that least upper bound is a
[fixed point](foundation.fixed-points-endofunctions.md) of `f`.

## Construction

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (x : type-Poset ğ’œ)
  (p : (n : â„•) â†’ leq-Poset ğ’œ (iterate n f x) (iterate (succ-â„• n) f x))
  where

  family-of-elements-construction-Ï‰-iteration-Poset :
    â„• â†’ type-Poset ğ’œ
  family-of-elements-construction-Ï‰-iteration-Poset n =
    iterate n f x

  leq-succ-family-of-elements-construction-Ï‰-iteration-Poset :
    (n : â„•) â†’
    leq-Poset ğ’œ
      ( family-of-elements-construction-Ï‰-iteration-Poset n)
      ( family-of-elements-construction-Ï‰-iteration-Poset (succ-â„• n))
  leq-succ-family-of-elements-construction-Ï‰-iteration-Poset = p

  hom-construction-Ï‰-iteration-Poset :
    hom-Poset â„•-Poset ğ’œ
  hom-construction-Ï‰-iteration-Poset =
    hom-ind-â„•-Poset ğ’œ
      ( family-of-elements-construction-Ï‰-iteration-Poset)
      ( leq-succ-family-of-elements-construction-Ï‰-iteration-Poset)

module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (x : type-Poset ğ’œ)
  (p : (n : â„•) â†’ leq-Poset ğ’œ (iterate n f x) (iterate (succ-â„• n) f x))
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-Ï‰-iteration-Poset ğ’œ x p))
  (F :
    preserves-Ï‰-supremum-Poset ğ’œ ğ’œ f
      ( hom-construction-Ï‰-iteration-Poset ğ’œ x p))
  where

  point-construction-Ï‰-iteration-Poset : type-Poset ğ’œ
  point-construction-Ï‰-iteration-Poset = pr1 s

  is-upper-bound-shifted-family-of-elements-construction-Ï‰-iteration-Poset :
    is-upper-bound-family-of-elements-Poset ğ’œ
      ( f âˆ˜ family-of-elements-construction-Ï‰-iteration-Poset ğ’œ x p)
      ( f point-construction-Ï‰-iteration-Poset)
  is-upper-bound-shifted-family-of-elements-construction-Ï‰-iteration-Poset =
    is-upper-bound-is-least-upper-bound-family-of-elements-Poset ğ’œ (F s)

  is-upper-bound-family-of-elements-map-point-construction-Ï‰-iteration-Poset :
    is-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-Ï‰-iteration-Poset ğ’œ x p)
      ( f point-construction-Ï‰-iteration-Poset)
  is-upper-bound-family-of-elements-map-point-construction-Ï‰-iteration-Poset
    zero-â„• =
    transitive-leq-Poset ğ’œ x
      ( f x)
      ( f point-construction-Ï‰-iteration-Poset)
      ( is-upper-bound-shifted-family-of-elements-construction-Ï‰-iteration-Poset
        ( zero-â„•))
      ( p zero-â„•)
  is-upper-bound-family-of-elements-map-point-construction-Ï‰-iteration-Poset
    (succ-â„• n) =
    is-upper-bound-shifted-family-of-elements-construction-Ï‰-iteration-Poset n

  leq-point-construction-Ï‰-iteration-Poset :
    leq-Poset ğ’œ
      ( point-construction-Ï‰-iteration-Poset)
      ( f point-construction-Ï‰-iteration-Poset)
  leq-point-construction-Ï‰-iteration-Poset =
    pr1
      ( pr2 s (f point-construction-Ï‰-iteration-Poset))
      ( is-upper-bound-family-of-elements-map-point-construction-Ï‰-iteration-Poset)

  geq-point-construction-Ï‰-iteration-Poset :
    leq-Poset ğ’œ
      ( f point-construction-Ï‰-iteration-Poset)
      ( point-construction-Ï‰-iteration-Poset)
  geq-point-construction-Ï‰-iteration-Poset =
    pr1
      ( F s point-construction-Ï‰-iteration-Poset)
      ( is-upper-bound-is-least-upper-bound-family-of-elements-Poset ğ’œ (pr2 s) âˆ˜
        succ-â„•)

  is-fixed-point-construction-Ï‰-iteration-Poset :
    f point-construction-Ï‰-iteration-Poset ï¼
    point-construction-Ï‰-iteration-Poset
  is-fixed-point-construction-Ï‰-iteration-Poset =
    antisymmetric-leq-Poset ğ’œ
      ( f point-construction-Ï‰-iteration-Poset)
      ( point-construction-Ï‰-iteration-Poset)
      ( geq-point-construction-Ï‰-iteration-Poset)
      ( leq-point-construction-Ï‰-iteration-Poset)

  fixed-point-construction-Ï‰-iteration-Poset :
    fixed-point f
  fixed-point-construction-Ï‰-iteration-Poset =
    ( point-construction-Ï‰-iteration-Poset ,
      is-fixed-point-construction-Ï‰-iteration-Poset)
```

## See also

- [Kleene's fixed point theorem for posets](domain-theory.keenes-fixed-point-theorem-posets.md)
- [Kleene's fixed point theorem for Ï‰-complete posets](domain-theory.keenes-fixed-point-theorem-omega-complete-posets.md)
