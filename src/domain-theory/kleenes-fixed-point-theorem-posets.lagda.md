# Kleene's fixed point theorem for posets

```agda
module domain-theory.kleenes-fixed-point-theorem-posets where
```

<details><summary>Imports</summary>

```agda
open import domain-theory.directed-families-posets
open import domain-theory.omega-continuous-maps-posets

open import elementary-number-theory.decidable-total-order-natural-numbers
open import elementary-number-theory.inequality-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.dependent-pair-types
open import foundation.fixed-points-endofunctions
open import foundation.function-types
open import foundation.identity-types
open import foundation.inhabited-types
open import foundation.iterating-functions
open import foundation.logical-equivalences
open import foundation.propositional-truncations
open import foundation.universe-levels

open import order-theory.bottom-elements-posets
open import order-theory.chains-posets
open import order-theory.increasing-sequences-posets
open import order-theory.inflattices
open import order-theory.inhabited-chains-posets
open import order-theory.least-upper-bounds-posets
open import order-theory.order-preserving-maps-posets
open import order-theory.posets
open import order-theory.suplattices
open import order-theory.upper-bounds-posets
```

</details>

## Idea

{{#concept "Kleene's fixed point theorem" Disambiguation="posets" WD="Kleene fixed-point theorem" WDID=Q3527263}}
states that given an
[Ï‰-continuous](domain-theory.omega-continuous-maps-posets.md) endomap
`f : ğ’œ â†’ ğ’œ` on a [poset](order-theory.posets.md) `ğ’œ`, then for every `x âˆˆ ğ’œ`
such that `x â‰¤ f x`, the Ï‰-transfinite application of `f` to `x`,`f^Ï‰(x)`, if it
exists, is a [fixed point](foundation.fixed-points-endofunctions.md) of `f`:

```text
  x â‰¤ f(x) â‰¤ fÂ²(x) â‰¤ â€¦ â‰¤ fâ¿(x) â‰¤ â€¦ â‰¤ f^Ï‰(x) = f(^fÏ‰(x)) = â€¦.
```

If `ğ’œ` has a [bottom element](order-theory.bottom-elements-posets.md) `âŠ¥`, then
this construction applied to `âŠ¥` gives a least fixed point of `f`.

**Duality.** Since the structure of posets is self-dual, there is a dual
Kleene's fixed point theorem that, for every Ï‰-cocontinuous endomap `f` and
point `y âˆˆ ğ’œ`, if `f(y) â‰¤ y`, then the Ï‰-transfinite application of `f` to `y`,
`f^Ï‰(y)`, given that it exists, gives a fixed point of `f`:

```text
  â€¦ = f(f^Ï‰(y)) = f^Ï‰(y) â‰¤ â€¦ â‰¤ fâ¿(y) â‰¤ â€¦ â‰¤ fÂ²(y) â‰¤ f(y) â‰¤ y.
```

If `ğ’œ` has a [top element](order-theory.top-elements-posets.md) `âŠ¤`, then this
construction applied to `âŠ¤` gives a greatest fixed point of `f`.

## Construction

### Kleene's fixed point construction for order preserving endomaps on posets

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (H : preserves-order-Poset ğ’œ ğ’œ f)
  (x : type-Poset ğ’œ)
  (p : leq-Poset ğ’œ x (f x))
  where

  family-of-elements-construction-kleene-hom-Poset : â„• â†’ type-Poset ğ’œ
  family-of-elements-construction-kleene-hom-Poset n = iterate n f x

  leq-succ-family-of-elements-construction-kleene-hom-Poset :
    (n : â„•) â†’
    leq-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset n)
      ( family-of-elements-construction-kleene-hom-Poset (succ-â„• n))
  leq-succ-family-of-elements-construction-kleene-hom-Poset zero-â„• = p
  leq-succ-family-of-elements-construction-kleene-hom-Poset (succ-â„• n) =
    H ( family-of-elements-construction-kleene-hom-Poset n)
      ( family-of-elements-construction-kleene-hom-Poset (succ-â„• n))
      ( leq-succ-family-of-elements-construction-kleene-hom-Poset n)

  hom-construction-kleene-hom-Poset : hom-Poset â„•-Poset ğ’œ
  hom-construction-kleene-hom-Poset =
    hom-ind-â„•-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset)
      ( leq-succ-family-of-elements-construction-kleene-hom-Poset)

module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (H : preserves-order-Poset ğ’œ ğ’œ f)
  (x : type-Poset ğ’œ)
  (p : leq-Poset ğ’œ x (f x))
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset ğ’œ H x p))
  where

  point-construction-kleene-hom-Poset : type-Poset ğ’œ
  point-construction-kleene-hom-Poset = pr1 s

  is-upper-bound-map-point-construction-kleene-hom-Poset :
    is-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset ğ’œ H x p)
      ( f point-construction-kleene-hom-Poset)
  is-upper-bound-map-point-construction-kleene-hom-Poset zero-â„• =
    transitive-leq-Poset ğ’œ x (f x)
      ( f point-construction-kleene-hom-Poset)
      ( H x
        ( point-construction-kleene-hom-Poset)
        ( is-upper-bound-is-least-upper-bound-family-of-elements-Poset ğ’œ
          ( pr2 s)
          ( 0)))
      ( p)
  is-upper-bound-map-point-construction-kleene-hom-Poset (succ-â„• n) =
    H ( family-of-elements-construction-kleene-hom-Poset ğ’œ H x p n)
      ( point-construction-kleene-hom-Poset)
      ( is-upper-bound-is-least-upper-bound-family-of-elements-Poset ğ’œ
        ( pr2 s)
        ( n))

  leq-point-construction-kleene-hom-Poset :
    leq-Poset ğ’œ
      ( point-construction-kleene-hom-Poset)
      ( f point-construction-kleene-hom-Poset)
  leq-point-construction-kleene-hom-Poset =
    pr1
      ( pr2 s (f point-construction-kleene-hom-Poset))
      ( is-upper-bound-map-point-construction-kleene-hom-Poset)

  geq-point-construction-kleene-hom-Poset :
    (F :
      preserves-Ï‰-supremum-Poset ğ’œ ğ’œ f
        ( hom-construction-kleene-hom-Poset ğ’œ H x p)) â†’
    leq-Poset ğ’œ
      ( f point-construction-kleene-hom-Poset)
      ( point-construction-kleene-hom-Poset)
  geq-point-construction-kleene-hom-Poset F =
    pr1
      ( F s point-construction-kleene-hom-Poset)
      ( is-upper-bound-is-least-upper-bound-family-of-elements-Poset ğ’œ (pr2 s) âˆ˜
        succ-â„•)

  is-fixed-point-construction-kleene-hom-Poset :
    (F :
      preserves-Ï‰-supremum-Poset ğ’œ ğ’œ f
        ( hom-construction-kleene-hom-Poset ğ’œ H x p)) â†’
    f (point-construction-kleene-hom-Poset) ï¼
    point-construction-kleene-hom-Poset
  is-fixed-point-construction-kleene-hom-Poset F =
    antisymmetric-leq-Poset ğ’œ
      ( f point-construction-kleene-hom-Poset)
      ( point-construction-kleene-hom-Poset)
      ( geq-point-construction-kleene-hom-Poset F)
      ( leq-point-construction-kleene-hom-Poset)

  fixed-point-construction-kleene-hom-Poset :
    (F :
      preserves-Ï‰-supremum-Poset ğ’œ ğ’œ f
        ( hom-construction-kleene-hom-Poset ğ’œ H x p)) â†’
    fixed-point f
  fixed-point-construction-kleene-hom-Poset F =
    point-construction-kleene-hom-Poset ,
    is-fixed-point-construction-kleene-hom-Poset F
```

### Kleene's fixed point construction for Ï‰-continuous endomaps on posets

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (F : is-Ï‰-continuous-Poset ğ’œ ğ’œ f)
  (x : type-Poset ğ’œ)
  (p : leq-Poset ğ’œ x (f x))
  where

  family-of-elements-construction-kleene-Poset : â„• â†’ type-Poset ğ’œ
  family-of-elements-construction-kleene-Poset =
    family-of-elements-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)

  hom-construction-kleene-Poset : hom-Poset â„•-Poset ğ’œ
  hom-construction-kleene-Poset =
    hom-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)

module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (F : is-Ï‰-continuous-Poset ğ’œ ğ’œ f)
  (x : type-Poset ğ’œ)
  (p : leq-Poset ğ’œ x (f x))
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-Poset ğ’œ F x p))
  where

  point-construction-kleene-Poset : type-Poset ğ’œ
  point-construction-kleene-Poset = pr1 s

  is-upper-bound-map-point-construction-kleene-Poset :
    is-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-Poset ğ’œ F x p)
      ( f point-construction-kleene-Poset)
  is-upper-bound-map-point-construction-kleene-Poset =
    is-upper-bound-map-point-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)
      ( s)

  leq-point-construction-kleene-Poset :
    leq-Poset ğ’œ
      ( point-construction-kleene-Poset)
      ( f point-construction-kleene-Poset)
  leq-point-construction-kleene-Poset =
    leq-point-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)
      ( s)

  geq-point-construction-kleene-Poset :
    leq-Poset ğ’œ
      ( f point-construction-kleene-Poset)
      ( point-construction-kleene-Poset)
  geq-point-construction-kleene-Poset =
    geq-point-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)
      ( s)
      ( F (hom-construction-kleene-Poset ğ’œ F x p))

  is-fixed-point-construction-kleene-Poset :
    f (point-construction-kleene-Poset) ï¼ point-construction-kleene-Poset
  is-fixed-point-construction-kleene-Poset =
    is-fixed-point-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)
      ( s)
      ( F (hom-construction-kleene-Poset ğ’œ F x p))

  fixed-point-construction-kleene-Poset : fixed-point f
  fixed-point-construction-kleene-Poset =
    point-construction-kleene-Poset , is-fixed-point-construction-kleene-Poset
```

## Theorem

### Kleene's least fixed point theorem for order preserving endomaps on posets with a bottom element

If `ğ’œ` has a bottom element, then Kleene's fixed point construction applied to
this element gives a least fixed point of `f`.

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (H : preserves-order-Poset ğ’œ ğ’œ f)
  (b@(âŠ¥ , b') : has-bottom-element-Poset ğ’œ)
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥))))
  (F :
    preserves-Ï‰-supremum-Poset ğ’œ ğ’œ f
      ( hom-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥))))
  where

  point-theorem-kleene-hom-Poset : type-Poset ğ’œ
  point-theorem-kleene-hom-Poset =
    point-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥)) s

  fixed-point-theorem-kleene-hom-Poset : fixed-point f
  fixed-point-theorem-kleene-hom-Poset =
    fixed-point-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥)) s F

  is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset :
    {z : type-Poset ğ’œ} â†’ f z ï¼ z â†’
    is-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥)))
      ( z)
  is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset
    { z} q zero-â„• =
    b' z
  is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset
    {z} q (succ-â„• n) =
    concatenate-leq-eq-Poset ğ’œ
      ( H ( iterate n f âŠ¥)
          ( z)
          ( is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset
            ( q)
            ( n)))
      ( q)

  is-least-fixed-point-theorem-kleene-hom-Poset :
    (q : fixed-point f) â†’ leq-Poset ğ’œ point-theorem-kleene-hom-Poset (pr1 q)
  is-least-fixed-point-theorem-kleene-hom-Poset (z , q) =
    pr1
      ( pr2 s z)
      ( is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset
        ( q))
```

### Kleene's least fixed point theorem for order preserving endomaps on posets with a bottom element

If `ğ’œ` has a bottom element, then Kleene's fixed point construction applied to
this element gives a least fixed point of `f`.

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (F : is-Ï‰-continuous-Poset ğ’œ ğ’œ f)
  (b@(âŠ¥ , b') : has-bottom-element-Poset ğ’œ)
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-Poset ğ’œ F âŠ¥ (b' (f âŠ¥))))
  where

  point-theorem-kleene-Poset : type-Poset ğ’œ
  point-theorem-kleene-Poset =
    point-construction-kleene-Poset ğ’œ F âŠ¥ (b' (f âŠ¥)) s

  fixed-point-theorem-kleene-Poset : fixed-point f
  fixed-point-theorem-kleene-Poset =
    fixed-point-construction-kleene-Poset ğ’œ F âŠ¥ (b' (f âŠ¥)) s

  is-least-fixed-point-theorem-kleene-Poset :
    (q : fixed-point f) â†’ leq-Poset ğ’œ point-theorem-kleene-Poset (pr1 q)
  is-least-fixed-point-theorem-kleene-Poset =
    is-least-fixed-point-theorem-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( b)
      ( s)
      ( F (hom-construction-kleene-Poset ğ’œ F âŠ¥ (b' (f âŠ¥))))
```

## External links

- [Kleene fixed-point theorem](https://en.wikipedia.org/wiki/Kleene_fixed-point_theorem)
  at Wikipedia
- [Kleene's fixed point theorem](https://ncatlab.org/nlab/show/Kleene%27s+fixed+point+theorem)
  at $n$Lab
