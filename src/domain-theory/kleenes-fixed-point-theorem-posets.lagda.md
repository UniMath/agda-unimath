# Kleene's fixed point theorem for posets

```agda
module domain-theory.kleenes-fixed-point-theorem-posets where
```

<details><summary>Imports</summary>

```agda
open import domain-theory.omega-continuous-maps-posets
open import domain-theory.omega-iteration-fixed-point-construction-posets

open import elementary-number-theory.inequality-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.dependent-pair-types
open import foundation.fixed-points-endofunctions
open import foundation.identity-types
open import foundation.iterating-functions
open import foundation.universe-levels

open import order-theory.bottom-elements-posets
open import order-theory.least-upper-bounds-posets
open import order-theory.order-preserving-maps-posets
open import order-theory.posets
open import order-theory.upper-bounds-posets
```

</details>

## Idea

{{#concept "Kleene's fixed point theorem" Disambiguation="posets" WD="Kleene fixed-point theorem" WDID=Q3527263}}
states that given an
[Ï‰-continuous](domain-theory.omega-continuous-maps-posets.md) endomap
`f : ğ’œ â†’ ğ’œ` on a [poset](order-theory.posets.md) `ğ’œ`, then for every `x âˆˆ ğ’œ`
such that `x â‰¤ f x`, the Ï‰-transfinite application of `f` to `x`,`f^Ï‰(x)`, if it
exists, is a [fixed point](foundation.fixed-points-endofunctions.md) of `f`:

```text
  x â‰¤ f(x) â‰¤ fÂ²(x) â‰¤ â€¦ â‰¤ fâ¿(x) â‰¤ â€¦ â‰¤ f^Ï‰(x) = f(^fÏ‰(x)) = â€¦.
```

If `ğ’œ` has a [bottom element](order-theory.bottom-elements-posets.md) `âŠ¥`, then
this construction applied to `âŠ¥` gives a least fixed point of `f`.

We prove this theorem as a corollary of
[the Ï‰-iteration fixed point construction for posets](domain-theory.omega-iteration-fixed-point-construction-posets.md).

## Construction

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (H : preserves-order-Poset ğ’œ ğ’œ f)
  (x : type-Poset ğ’œ)
  (p : leq-Poset ğ’œ x (f x))
  where

  leq-succ-family-of-elements-construction-kleene-hom-Poset :
    (n : â„•) â†’
    leq-Poset ğ’œ
      ( iterate n f x)
      ( iterate (succ-â„• n) f x)
  leq-succ-family-of-elements-construction-kleene-hom-Poset zero-â„• =
    p
  leq-succ-family-of-elements-construction-kleene-hom-Poset (succ-â„• n) =
    H ( iterate n f x)
      ( iterate (succ-â„• n) f x)
      ( leq-succ-family-of-elements-construction-kleene-hom-Poset n)

  family-of-elements-construction-kleene-hom-Poset : â„• â†’ type-Poset ğ’œ
  family-of-elements-construction-kleene-hom-Poset =
    family-of-elements-construction-Ï‰-iteration-Poset ğ’œ x
      leq-succ-family-of-elements-construction-kleene-hom-Poset

  hom-construction-kleene-hom-Poset :
    hom-Poset â„•-Poset ğ’œ
  hom-construction-kleene-hom-Poset =
    hom-construction-Ï‰-iteration-Poset ğ’œ x
      leq-succ-family-of-elements-construction-kleene-hom-Poset
```

## Theorems

### Fixed point theorem for order preserving maps

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (H : preserves-order-Poset ğ’œ ğ’œ f)
  (x : type-Poset ğ’œ)
  (p : leq-Poset ğ’œ x (f x))
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset ğ’œ H x p))
  (F :
    preserves-Ï‰-supremum-Poset ğ’œ ğ’œ f
      ( hom-construction-kleene-hom-Poset ğ’œ H x p))
  where

  point-construction-kleene-hom-Poset : type-Poset ğ’œ
  point-construction-kleene-hom-Poset =
    point-construction-Ï‰-iteration-Poset ğ’œ x
      ( leq-succ-family-of-elements-construction-kleene-hom-Poset ğ’œ H x p)
      ( s)
      ( F)

  is-fixed-point-construction-kleene-hom-Poset :
    f (point-construction-kleene-hom-Poset) ï¼
    point-construction-kleene-hom-Poset
  is-fixed-point-construction-kleene-hom-Poset =
    is-fixed-point-construction-Ï‰-iteration-Poset ğ’œ x
      ( leq-succ-family-of-elements-construction-kleene-hom-Poset ğ’œ H x p)
      ( s)
      ( F)

  fixed-point-construction-kleene-hom-Poset :
    fixed-point f
  fixed-point-construction-kleene-hom-Poset =
    fixed-point-construction-Ï‰-iteration-Poset ğ’œ x
      ( leq-succ-family-of-elements-construction-kleene-hom-Poset ğ’œ H x p)
      ( s)
      ( F)

module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (F : is-Ï‰-continuous-Poset ğ’œ ğ’œ f)
  (x : type-Poset ğ’œ)
  (p : leq-Poset ğ’œ x (f x))
  where

  family-of-elements-construction-kleene-Poset : â„• â†’ type-Poset ğ’œ
  family-of-elements-construction-kleene-Poset =
    family-of-elements-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)

  hom-construction-kleene-Poset : hom-Poset â„•-Poset ğ’œ
  hom-construction-kleene-Poset =
    hom-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)
```

### Fixed point theorem for Ï‰-continuous maps

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (F : is-Ï‰-continuous-Poset ğ’œ ğ’œ f)
  (x : type-Poset ğ’œ)
  (p : leq-Poset ğ’œ x (f x))
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-Poset ğ’œ F x p))
  where

  point-construction-kleene-Poset : type-Poset ğ’œ
  point-construction-kleene-Poset =
    point-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)
      ( s)
      ( F (hom-construction-kleene-Poset ğ’œ F x p))

  is-fixed-point-construction-kleene-Poset :
    f (point-construction-kleene-Poset) ï¼ point-construction-kleene-Poset
  is-fixed-point-construction-kleene-Poset =
    is-fixed-point-construction-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( x)
      ( p)
      ( s)
      ( F (hom-construction-kleene-Poset ğ’œ F x p))

  fixed-point-construction-kleene-Poset : fixed-point f
  fixed-point-construction-kleene-Poset =
    point-construction-kleene-Poset , is-fixed-point-construction-kleene-Poset
```

### Least fixed point theorem for order preserving maps

If `ğ’œ` has a bottom element, then Kleene's fixed point construction applied to
this element gives a least fixed point of `f`.

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (H : preserves-order-Poset ğ’œ ğ’œ f)
  (b@(âŠ¥ , b') : has-bottom-element-Poset ğ’œ)
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥))))
  (F :
    preserves-Ï‰-supremum-Poset ğ’œ ğ’œ f
      ( hom-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥))))
  where

  point-theorem-kleene-hom-Poset : type-Poset ğ’œ
  point-theorem-kleene-hom-Poset =
    point-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥)) s F

  fixed-point-theorem-kleene-hom-Poset : fixed-point f
  fixed-point-theorem-kleene-hom-Poset =
    fixed-point-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥)) s F

  is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset :
    {z : type-Poset ğ’œ} â†’ f z ï¼ z â†’
    is-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-hom-Poset ğ’œ H âŠ¥ (b' (f âŠ¥)))
      ( z)
  is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset
    { z} q zero-â„• =
    b' z
  is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset
    {z} q (succ-â„• n) =
    concatenate-leq-eq-Poset ğ’œ
      ( H ( iterate n f âŠ¥)
          ( z)
          ( is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset
            ( q)
            ( n)))
      ( q)

  is-least-fixed-point-theorem-kleene-hom-Poset :
    (q : fixed-point f) â†’ leq-Poset ğ’œ point-theorem-kleene-hom-Poset (pr1 q)
  is-least-fixed-point-theorem-kleene-hom-Poset (z , q) =
    pr1
      ( pr2 s z)
      ( is-upper-bound-family-of-elements-is-fixed-point-theorem-kleene-hom-Poset
        ( q))
```

### Least fixed point theorem for Ï‰-continuous maps

If `ğ’œ` has a bottom element, then Kleene's fixed point construction applied to
this element gives a least fixed point of `f`.

```agda
module _
  {l1 l2 : Level}
  (ğ’œ : Poset l1 l2)
  {f : type-Poset ğ’œ â†’ type-Poset ğ’œ}
  (F : is-Ï‰-continuous-Poset ğ’œ ğ’œ f)
  (b@(âŠ¥ , b') : has-bottom-element-Poset ğ’œ)
  (s :
    has-least-upper-bound-family-of-elements-Poset ğ’œ
      ( family-of-elements-construction-kleene-Poset ğ’œ F âŠ¥ (b' (f âŠ¥))))
  where

  point-theorem-kleene-Poset : type-Poset ğ’œ
  point-theorem-kleene-Poset =
    point-construction-kleene-Poset ğ’œ F âŠ¥ (b' (f âŠ¥)) s

  fixed-point-theorem-kleene-Poset : fixed-point f
  fixed-point-theorem-kleene-Poset =
    fixed-point-construction-kleene-Poset ğ’œ F âŠ¥ (b' (f âŠ¥)) s

  is-least-fixed-point-theorem-kleene-Poset :
    (q : fixed-point f) â†’ leq-Poset ğ’œ point-theorem-kleene-Poset (pr1 q)
  is-least-fixed-point-theorem-kleene-Poset =
    is-least-fixed-point-theorem-kleene-hom-Poset ğ’œ
      ( preserves-order-is-Ï‰-continuous-Poset ğ’œ ğ’œ F)
      ( b)
      ( s)
      ( F (hom-construction-kleene-Poset ğ’œ F âŠ¥ (b' (f âŠ¥))))
```

## External links

- [Kleene fixed-point theorem](https://en.wikipedia.org/wiki/Kleene_fixed-point_theorem)
  at Wikipedia
- [Kleene's fixed point theorem](https://ncatlab.org/nlab/show/Kleene%27s+fixed+point+theorem)
  at $n$Lab
