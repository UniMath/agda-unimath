# The type of natural numbers

```agda
{-# OPTIONS --without-K --exact-split #-}

module elementary-number-theory.natural-numbers where

open import foundation-core.empty-types using (ex-falso)

open import foundation.booleans using (bool; Eq-bool; true; false)
open import foundation.contractible-types using (eq-is-contr)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.equivalences using
  ( is-equiv; is-equiv-has-inverse; _â‰ƒ_)
open import foundation.function-extensionality using (eq-htpy)
open import foundation.functions using (id; _âˆ˜_)
open import foundation.homotopies using (_~_)
open import foundation.identity-types using (Id; refl; ap)
open import foundation.injective-maps using (is-injective)
open import foundation.logical-equivalences using (_â†”_)
open import foundation.negation using (Â¬)
open import foundation.unit-type using (star; unit)
open import foundation.universal-property-empty-type using
  ( universal-property-empty')
open import foundation.universe-levels using (Level; lzero; UU)
open import foundation.W-types using (ğ•; constant-ğ•; tree-ğ•)
```

## Idea

The type of natural numbers is inductively generated by the zero element and the successor function. The induction principle for the natural numbers works to construct sections of type families over the natural numbers.

## Definitions

### The inductive definition of the type of natural numbers

```agda
data â„• : UU lzero where
  zero-â„• : â„•
  succ-â„• : â„• â†’ â„•

{-# BUILTIN NATURAL â„• #-}
```

### Useful predicates on the natural numbers

These predicates can of course be asserted directly without much trouble. However, using the defined predicates ensures uniformity, and helps naming definitions.

```agda
is-zero-â„• : â„• â†’ UU lzero
is-zero-â„• n = Id n zero-â„•

is-zero-â„•' : â„• â†’ UU lzero
is-zero-â„•' n = Id zero-â„• n

is-successor-â„• : â„• â†’ UU lzero
is-successor-â„• n = Î£ â„• (Î» y â†’ Id n (succ-â„• y))

is-nonzero-â„• : â„• â†’ UU lzero
is-nonzero-â„• n = Â¬ (is-zero-â„• n)

is-one-â„• : â„• â†’ UU lzero
is-one-â„• n = Id n 1

is-one-â„•' : â„• â†’ UU lzero
is-one-â„•' n = Id 1 n

is-not-one-â„• : â„• â†’ UU lzero
is-not-one-â„• n = Â¬ (is-one-â„• n)

is-not-one-â„•' : â„• â†’ UU lzero
is-not-one-â„•' n = Â¬ (is-one-â„•' n)
```

### The type of natural numbers defined as a W-type

```agda
Nat-ğ• : UU lzero
Nat-ğ• = ğ• bool (Eq-bool true)

zero-Nat-ğ• : Nat-ğ•
zero-Nat-ğ• = constant-ğ• false id

succ-Nat-ğ• : Nat-ğ• â†’ Nat-ğ•
succ-Nat-ğ• x = tree-ğ• true (Î» y â†’ x)
```

## Properties

### The induction principle of â„•

```agda
ind-â„• :
  {i : Level} {P : â„• â†’ UU i} â†’
  P 0 â†’ ((n : â„•) â†’ P n â†’ P(succ-â„• n)) â†’ ((n : â„•) â†’ P n)
ind-â„• p0 pS 0 = p0
ind-â„• p0 pS (succ-â„• n) = pS n (ind-â„• p0 pS n)
```

### Peano's 7th axiom

```agda
is-injective-succ-â„• : is-injective succ-â„•
is-injective-succ-â„• refl = refl

private
  Peano-7 :
    (x y : â„•) â†’ (Id x y) â†” (Id (succ-â„• x) (succ-â„• y))
  pr1 (Peano-7 x y) refl = refl
  pr2 (Peano-7 x y) = is-injective-succ-â„•
```

### Peano's 8th axiom

```agda
private   
  Peano-8 : (x : â„•) â†’ is-nonzero-â„• (succ-â„• x)
  Peano-8 x ()

is-nonzero-succ-â„• : (x : â„•) â†’ is-nonzero-â„• (succ-â„• x)
is-nonzero-succ-â„• x ()

is-nonzero-is-successor-â„• : {x : â„•} â†’ is-successor-â„• x â†’ is-nonzero-â„• x
is-nonzero-is-successor-â„• (pair x refl) ()

is-successor-is-nonzero-â„• : {x : â„•} â†’ is-nonzero-â„• x â†’ is-successor-â„• x
is-successor-is-nonzero-â„• {zero-â„•} H = ex-falso (H refl)
pr1 (is-successor-is-nonzero-â„• {succ-â„• x} H) = x
pr2 (is-successor-is-nonzero-â„• {succ-â„• x} H) = refl

has-no-fixed-points-succ-â„• : (x : â„•) â†’ Â¬ (Id (succ-â„• x) x)
has-no-fixed-points-succ-â„• x ()
```

### Basic inequalities

```agda
is-nonzero-one-â„• : is-nonzero-â„• 1
is-nonzero-one-â„• ()

is-not-one-zero-â„• : is-not-one-â„• zero-â„•
is-not-one-zero-â„• ()

is-not-one-two-â„• : is-not-one-â„• 2
is-not-one-two-â„• ()
```

### The type of natural numbers is equivalent to the W-type Nat-ğ•

```agda
Nat-ğ•-â„• : â„• â†’ Nat-ğ•
Nat-ğ•-â„• zero-â„• = zero-Nat-ğ•
Nat-ğ•-â„• (succ-â„• x) = succ-Nat-ğ• (Nat-ğ•-â„• x)

â„•-Nat-ğ• : Nat-ğ• â†’ â„•
â„•-Nat-ğ• (tree-ğ• true Î±) = succ-â„• (â„•-Nat-ğ• (Î± star))
â„•-Nat-ğ• (tree-ğ• false Î±) = zero-â„•

issec-â„•-Nat-ğ• : (Nat-ğ•-â„• âˆ˜ â„•-Nat-ğ•) ~ id
issec-â„•-Nat-ğ• (tree-ğ• true Î±) =
  ap ( tree-ğ• true)
     ( eq-htpy H)
  where
  H : (z : unit) â†’ Id (Nat-ğ•-â„• (â„•-Nat-ğ• (Î± star))) (Î± z)
  H star = issec-â„•-Nat-ğ• (Î± star)
issec-â„•-Nat-ğ• (tree-ğ• false Î±) =
  ap (tree-ğ• false) (eq-is-contr (universal-property-empty' Nat-ğ•))

isretr-â„•-Nat-ğ• : (â„•-Nat-ğ• âˆ˜ Nat-ğ•-â„•) ~ id
isretr-â„•-Nat-ğ• zero-â„• = refl
isretr-â„•-Nat-ğ• (succ-â„• x) = ap succ-â„• (isretr-â„•-Nat-ğ• x)

is-equiv-Nat-ğ•-â„• : is-equiv Nat-ğ•-â„•
is-equiv-Nat-ğ•-â„• =
  is-equiv-has-inverse
    â„•-Nat-ğ•
    issec-â„•-Nat-ğ•
    isretr-â„•-Nat-ğ•

equiv-Nat-ğ•-â„• : â„• â‰ƒ Nat-ğ•
equiv-Nat-ğ•-â„• = pair Nat-ğ•-â„• is-equiv-Nat-ğ•-â„•

is-equiv-â„•-Nat-ğ• : is-equiv â„•-Nat-ğ•
is-equiv-â„•-Nat-ğ• =
  is-equiv-has-inverse
    Nat-ğ•-â„•
    isretr-â„•-Nat-ğ•
    issec-â„•-Nat-ğ•

equiv-â„•-Nat-ğ• : Nat-ğ• â‰ƒ â„•
equiv-â„•-Nat-ğ• = pair â„•-Nat-ğ• is-equiv-â„•-Nat-ğ•
```
