# Farey fractions

```agda
module elementary-number-theory.farey-fractions where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.addition-natural-numbers
open import elementary-number-theory.multiplication-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.action-on-identifications-functions
open import foundation.binary-relations
open import foundation.empty-types
open import foundation.identity-types
open import foundation.negation
open import foundation.universe-levels
```

</details>

## Idea

A {{#concept "Farey fraction" Agda=farey-fraction}} is a
[reduced](elementary-number-theory.reduced-integer-fractions.md)
[integer fraction](elementary-number-theory.integer-fractions.md) between $0$
and $1$ inclusive. More specifically, a Farey fraction of order $n$ is a reduced
integer fraction between $0$ and $1$ whose denominator does not exceed $n$.

The Farey fractions ‚Ñ± can be inductively generated mutually with a binary
relation $R$ with the following constructors:

```text
  0 : ‚Ñ±
  1 : ‚Ñ±
  ùìÇ : (x y : ‚Ñ±) ‚Üí ‚Ñõ x y ‚Üí ‚Ñ±

  ùìà : ‚Ñõ 0 1
  ùìá : (x y : ‚Ñ±) (r : ‚Ñõ x y) ‚Üí ‚Ñõ x (ùìÇ x y r)
  ùìÅ : (x y : ‚Ñ±) (r : ‚Ñõ x y) ‚Üí ‚Ñõ (ùìÇ x y r) y
```

The operation $m$ returns the
{{#concept "mediant" Disambiguation="Farey fractions" Agda=mediant-farey-fraction}}
of two adjacent Farey fractions. The elements $0$ and $1$ in the type of Farey
fractions represent the Farey fractions $0/1$ and $1/1$. Given two adjacent
Farey fractions representing $a/b$ and $c/d$, the mediant of $a/b$ and $c/d$ is
the Farey fraction representing

$$
  \frac{a+c}{b+d}.
$$

The mediant of any two adjacent Farey fractions representing reduced fractions
$a/b$ and $c/d$ represents again a reduced fraction.

Farey fractions appear in Chapter 3 of {{#cite HW08}}, but they are covered in
more detail in Chapter 6 of {{#cite NZM}}.

## Definitions

### The inductively generated Farey fractions

```agda
mutual

  data
    farey-fraction : UU lzero
    where

    zero-farey-fraction : farey-fraction
    one-farey-fraction : farey-fraction

    mediant-farey-fraction :
      (x y : farey-fraction) ‚Üí adjacent-farey-fraction x y ‚Üí farey-fraction

  data
    adjacent-farey-fraction : farey-fraction ‚Üí farey-fraction ‚Üí UU lzero
    where

    adjacent-zero-one-farey-fraction :
      adjacent-farey-fraction zero-farey-fraction one-farey-fraction

    right-adjacent-mediant-farey-fraction :
      (x y : farey-fraction) (H : adjacent-farey-fraction x y) ‚Üí
      adjacent-farey-fraction x (mediant-farey-fraction x y H)

    left-adjacent-mediant-farey-fraction :
      (x y : farey-fraction) (H : adjacent-farey-fraction x y) ‚Üí
      adjacent-farey-fraction (mediant-farey-fraction x y H) y
```

### The inclusion of Farey fractions into the integer fractions

```agda
numerator-farey-fraction :
  farey-fraction ‚Üí ‚Ñï
numerator-farey-fraction zero-farey-fraction =
  0
numerator-farey-fraction one-farey-fraction =
  1
numerator-farey-fraction (mediant-farey-fraction x y H) =
  numerator-farey-fraction x +‚Ñï numerator-farey-fraction y

denominator-farey-fraction :
  farey-fraction ‚Üí ‚Ñï
denominator-farey-fraction zero-farey-fraction =
  1
denominator-farey-fraction one-farey-fraction =
  1
denominator-farey-fraction (mediant-farey-fraction x y H) =
  denominator-farey-fraction x +‚Ñï denominator-farey-fraction y
```

## Properties

### Any two adjacent Farey fractions $a/b$ and $c/d$ satisfy $bc = ad + 1$

Equivalently, two adjacent Farey fractions $a/b$ and $c/d$ satisfy the relation

$$
  bc - ad = 1.
$$

This is also known as the **characteristic property of adjacent Farey
fractions**.

```agda
characteristic-property-adjacent-farey-fraction :
  (x y : farey-fraction) (r : adjacent-farey-fraction x y) ‚Üí
  denominator-farey-fraction x *‚Ñï numerator-farey-fraction y Ôºù
  numerator-farey-fraction x *‚Ñï denominator-farey-fraction y +‚Ñï 1
characteristic-property-adjacent-farey-fraction ._ ._
  adjacent-zero-one-farey-fraction =
  refl
characteristic-property-adjacent-farey-fraction x ._
  ( right-adjacent-mediant-farey-fraction .x y r) =
  left-distributive-mul-add-‚Ñï
    ( denominator-farey-fraction x)
    ( numerator-farey-fraction x)
    ( numerator-farey-fraction y) ‚àô
  ap-add-‚Ñï
    ( commutative-mul-‚Ñï
      ( denominator-farey-fraction x)
      ( numerator-farey-fraction x))
    ( characteristic-property-adjacent-farey-fraction x y r) ‚àô
  ap
    ( succ-‚Ñï)
    ( inv
      ( left-distributive-mul-add-‚Ñï
        ( numerator-farey-fraction x)
        ( denominator-farey-fraction x)
        ( denominator-farey-fraction y)))
characteristic-property-adjacent-farey-fraction ._ y
  ( left-adjacent-mediant-farey-fraction x .y r) =
  right-distributive-mul-add-‚Ñï
    ( denominator-farey-fraction x)
    ( denominator-farey-fraction y)
    ( numerator-farey-fraction y) ‚àô
  ap-add-‚Ñï
    ( characteristic-property-adjacent-farey-fraction x y r)
    ( commutative-mul-‚Ñï
      ( denominator-farey-fraction y)
      ( numerator-farey-fraction y)) ‚àô
  left-successor-law-add-‚Ñï
    ( numerator-farey-fraction x *‚Ñï denominator-farey-fraction y)
    ( numerator-farey-fraction y *‚Ñï denominator-farey-fraction y) ‚àô
  ap
    ( succ-‚Ñï)
    ( inv
      ( right-distributive-mul-add-‚Ñï
        ( numerator-farey-fraction x)
        ( numerator-farey-fraction y)
        ( denominator-farey-fraction y)))
```

### There is no adjacency from any Farey fraction to $0$

```agda
not-adjacent-zero-farey-fraction :
  (x : farey-fraction) ‚Üí ¬¨ adjacent-farey-fraction x zero-farey-fraction
not-adjacent-zero-farey-fraction ._
  ( left-adjacent-mediant-farey-fraction x ._ r) =
  not-adjacent-zero-farey-fraction x r
```

### There is no adjacency from $1$ to any Farey fraction

```agda
not-adjacent-one-farey-fraction :
  (x : farey-fraction) ‚Üí ¬¨ adjacent-farey-fraction one-farey-fraction x
not-adjacent-one-farey-fraction
  ( mediant-farey-fraction one-farey-fraction y s) r =
  not-adjacent-one-farey-fraction y s
```

## See also

- [Unbounded Farey fractions](elementary-number-theory.unbounded-farey-fractions.md)

## References

{{#bibliography}}
