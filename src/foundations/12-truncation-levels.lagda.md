---
title: Formalisation of the Symmetry Book
---

```agda
{-# OPTIONS --without-K --exact-split --safe #-}

module foundations.12-truncation-levels where

open import foundations.11-fundamental-theorem public

--------------------------------------------------------------------------------

-- Section 12 Propositions, Sets, and general truncation levels

--------------------------------------------------------------------------------

-- Section 12.1 Propositions

{- Definition 12.1.1 -}

is-prop :
  {i : Level} (A : UU i) â†’ UU i
is-prop A = (x y : A) â†’ is-contr (Id x y)

UU-Prop :
  (l : Level) â†’ UU (lsuc l)
UU-Prop l = Î£ (UU l) is-prop

module _
  {l : Level}
  where

  type-Prop : UU-Prop l â†’ UU l
  type-Prop P = pr1 P

  abstract
    is-prop-type-Prop : (P : UU-Prop l) â†’ is-prop (type-Prop P)
    is-prop-type-Prop P = pr2 P

{- Example 12.1.2 -}

abstract
  is-prop-unit : is-prop unit
  is-prop-unit = is-prop-is-contr is-contr-unit

unit-Prop : UU-Prop lzero
pr1 unit-Prop = unit
pr2 unit-Prop = is-prop-unit

abstract
  is-prop-empty : is-prop empty
  is-prop-empty ()

empty-Prop : UU-Prop lzero
pr1 empty-Prop = empty
pr2 empty-Prop = is-prop-empty

abstract
  is-prop-leq-Fin :
    {k : â„•} (x y : Fin k) â†’ is-prop (leq-Fin x y)
  is-prop-leq-Fin {succ-â„• k} (inl x) (inl y) = is-prop-leq-Fin x y
  is-prop-leq-Fin {succ-â„• k} (inl x) (inr star) = is-prop-unit
  is-prop-leq-Fin {succ-â„• k} (inr star) (inl y) = is-prop-empty
  is-prop-leq-Fin {succ-â„• k} (inr star) (inr star) = is-prop-unit

{- Proposition 12.1.3 -}

module _
  {l : Level} (A : UU l)
  where
  
  all-elements-equal : UU l
  all-elements-equal = (x y : A) â†’ Id x y
  
  is-proof-irrelevant : UU l
  is-proof-irrelevant = A â†’ is-contr A
  
  is-subterminal : UU l
  is-subterminal = is-emb (terminal-map {A = A})

module _
  {l : Level} {A : UU l}
  where
  
  abstract
    is-prop-all-elements-equal : all-elements-equal A â†’ is-prop A
    pr1 (is-prop-all-elements-equal H x y) = (inv (H x x)) âˆ™ (H x y)
    pr2 (is-prop-all-elements-equal H x .x) refl = left-inv (H x x)

  abstract
    eq-is-prop' : is-prop A â†’ all-elements-equal A
    eq-is-prop' H x y = pr1 (H x y)

  abstract
    eq-is-prop : is-prop A â†’ {x y : A} â†’ Id x y
    eq-is-prop H {x} {y} = eq-is-prop' H x y

  abstract
    is-proof-irrelevant-all-elements-equal :
      all-elements-equal A â†’ is-proof-irrelevant A
    pr1 (is-proof-irrelevant-all-elements-equal H a) = a
    pr2 (is-proof-irrelevant-all-elements-equal H a) = H a

  abstract
    is-proof-irrelevant-is-prop : is-prop A â†’ is-proof-irrelevant A
    is-proof-irrelevant-is-prop =
      is-proof-irrelevant-all-elements-equal âˆ˜ eq-is-prop'

  abstract
    is-prop-is-proof-irrelevant : is-proof-irrelevant A â†’ is-prop A
    is-prop-is-proof-irrelevant H x y = is-prop-is-contr (H x) x y

  abstract
    eq-is-proof-irrelevant : is-proof-irrelevant A â†’ all-elements-equal A
    eq-is-proof-irrelevant H = eq-is-prop' (is-prop-is-proof-irrelevant H)

  abstract
    is-emb-is-emb :
      {l2 : Level} {B : UU l2} {f : A â†’ B} â†’ (A â†’ is-emb f) â†’ is-emb f
    is-emb-is-emb H x y = H x x y

  abstract
    is-subterminal-is-proof-irrelevant :
      is-proof-irrelevant A â†’ is-subterminal A
    is-subterminal-is-proof-irrelevant H =
      is-emb-is-emb
        ( Î» x â†’ is-emb-is-equiv (is-equiv-is-contr _ (H x) is-contr-unit))

  abstract
    is-subterminal-all-elements-equal : all-elements-equal A â†’ is-subterminal A
    is-subterminal-all-elements-equal =
      is-subterminal-is-proof-irrelevant âˆ˜
      is-proof-irrelevant-all-elements-equal

  abstract
    is-subterminal-is-prop : is-prop A â†’ is-subterminal A
    is-subterminal-is-prop = is-subterminal-all-elements-equal âˆ˜ eq-is-prop'

  abstract
    is-prop-is-subterminal : is-subterminal A â†’ is-prop A
    is-prop-is-subterminal H x y =
      is-contr-is-equiv
        ( Id star star)
        ( ap terminal-map)
        ( H x y)
        ( is-prop-is-contr is-contr-unit star star)

  abstract
    eq-is-subterminal : is-subterminal A â†’ all-elements-equal A
    eq-is-subterminal = eq-is-prop' âˆ˜ is-prop-is-subterminal

  abstract
    is-proof-irrelevant-is-subterminal :
      is-subterminal A â†’ is-proof-irrelevant A
    is-proof-irrelevant-is-subterminal H =
      is-proof-irrelevant-all-elements-equal (eq-is-subterminal H)

{- Proposition 12.1.4 -}

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-equiv-is-prop :
      is-prop A â†’ is-prop B â†’ {f : A â†’ B} â†’ (B â†’ A) â†’ is-equiv f
    is-equiv-is-prop is-prop-A is-prop-B {f} g =
      is-equiv-has-inverse
        ( g)
        ( Î» y â†’ center (is-prop-B (f (g y)) y))
        ( Î» x â†’ center (is-prop-A (g (f x)) x))

  abstract
    equiv-prop : is-prop A â†’ is-prop B â†’ (A â†’ B) â†’ (B â†’ A) â†’ A â‰ƒ B
    pr1 (equiv-prop is-prop-A is-prop-B f g) = f
    pr2 (equiv-prop is-prop-A is-prop-B f g) =
      is-equiv-is-prop is-prop-A is-prop-B g

--------------------------------------------------------------------------------

-- Section 12.2 Subtypes

{- Definition 12.2.1 -}

module _
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2)
  where

  is-subtype : UU (l1 âŠ” l2)
  is-subtype = (x : A) â†’ is-prop (B x)

  is-property : UU (l1 âŠ” l2)
  is-property = is-subtype

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-prop-map : (A â†’ B) â†’ UU (l1 âŠ” l2)
  is-prop-map f = (b : B) â†’ is-prop (fib f b)

module _
  {l1 l2 : Level} {A : UU l1}
  where

  total-subtype : (A â†’ UU-Prop l2) â†’ UU (l1 âŠ” l2)
  total-subtype P = Î£ A (Î» x â†’ pr1 (P x))

{- Lemma 12.2.2 -}

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-prop-is-equiv : {f : A â†’ B} â†’ is-equiv f â†’ is-prop B â†’ is-prop A
    is-prop-is-equiv {f} E H x y =
      is-contr-is-equiv _ (ap f {x} {y}) (is-emb-is-equiv E x y) (H (f x) (f y))

  abstract
    is-prop-equiv : A â‰ƒ B â†’ is-prop B â†’ is-prop A
    is-prop-equiv (pair f is-equiv-f) = is-prop-is-equiv is-equiv-f

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-prop-is-equiv' : {f : A â†’ B} â†’ is-equiv f â†’ is-prop A â†’ is-prop B
    is-prop-is-equiv' E H =
      is-prop-is-equiv (is-equiv-map-inv-is-equiv E) H

  abstract
    is-prop-equiv' : A â‰ƒ B â†’ is-prop A â†’ is-prop B
    is-prop-equiv' (pair f is-equiv-f) = is-prop-is-equiv' is-equiv-f

{- Theorem 12.2.3 -}

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B}
  where

  abstract
    is-emb-is-prop-map : is-prop-map f â†’ is-emb f
    is-emb-is-prop-map is-prop-map-f x =
      fundamental-theorem-id x refl
        ( is-contr-equiv
          ( fib f (f x))
          ( equiv-tot (Î» y â†’ equiv-inv (f x) (f y)))
          ( is-proof-irrelevant-is-prop (is-prop-map-f (f x)) (pair x refl)))
        ( Î» y â†’ ap f)

  abstract
    is-prop-map-is-emb : is-emb f â†’ is-prop-map f
    is-prop-map-is-emb is-emb-f y =
      is-prop-is-proof-irrelevant Î±
      where
      Î± : (t : fib f y) â†’ is-contr (fib f y)
      Î± (pair x refl) =
        fundamental-theorem-id' x refl
          ( Î» y â†’ inv âˆ˜ ap f)
          ( Î» y â†’
            is-equiv-comp' inv (ap f)
              ( is-emb-f x y)
              ( is-equiv-inv (f x) (f y)))

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-prop-map-emb : (f : B â†ª A) â†’ is-prop-map (map-emb f)
    is-prop-map-emb f = is-prop-map-is-emb (is-emb-map-emb f)

  fib-emb-Prop : A â†ª B â†’ B â†’ UU-Prop (l1 âŠ” l2)
  pr1 (fib-emb-Prop f y) = fib (map-emb f) y
  pr2 (fib-emb-Prop f y) = is-prop-map-is-emb (is-emb-map-emb f) y

module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  abstract
    is-emb-pr1 : is-subtype B â†’ is-emb (pr1 {B = B})
    is-emb-pr1 H =
      is-emb-is-prop-map (Î» x â†’ is-prop-equiv (equiv-fib-pr1 B x) (H x))

  emb-pr1 : is-subtype B â†’ Î£ A B â†ª A
  pr1 (emb-pr1 H) = pr1
  pr2 (emb-pr1 H) = is-emb-pr1 H

  equiv-ap-pr1 : is-subtype B â†’ {s t : Î£ A B} â†’ Id s t â‰ƒ Id (pr1 s) (pr1 t)
  pr1 (equiv-ap-pr1 is-subtype-B {s} {t}) = ap pr1
  pr2 (equiv-ap-pr1 is-subtype-B {s} {t}) = is-emb-pr1 is-subtype-B s t

  abstract
    is-subtype-is-emb-pr1 : is-emb (pr1 {B = B}) â†’ is-subtype B
    is-subtype-is-emb-pr1 H x =
      is-prop-equiv' (equiv-fib-pr1 B x) (is-prop-map-is-emb H x)

{- Remark 12.2.5 -}

module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

{- The following is a general construction that will help us show that
   the identity type of a subtype agrees with the identity type of the 
   original type. We already know that the first projection of a family of
   propositions is an embedding, but the following lemma still has its uses. -}

  abstract
    is-contr-total-Eq-substructure :
      {l3 : Level} {P : A â†’ UU l3} â†’
      is-contr (Î£ A B) â†’ (is-subtype P) â†’ (a : A) (b : B a) (p : P a) â†’
      is-contr (Î£ (Î£ A P) (Î» t â†’ B (pr1 t)))
    is-contr-total-Eq-substructure {l3} {P}
      is-contr-AB is-subtype-P a b p =
      is-contr-equiv
        ( Î£ (Î£ A B) (Î» t â†’ P (pr1 t)))
        ( equiv-right-swap-Î£)
        ( is-contr-equiv
          ( P a)
          ( left-unit-law-Î£-is-contr
            ( is-contr-AB)
            ( pair a b))
          ( is-proof-irrelevant-is-prop (is-subtype-P a) p))

module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (H : is-subtype B)
  where

  Eq-total-subtype : (Î£ A B) â†’ (Î£ A B) â†’ UU l1
  Eq-total-subtype p p' = Id (pr1 p) (pr1 p') 

  refl-Eq-total-subtype : (p : Î£ A B) â†’ Eq-total-subtype p p
  refl-Eq-total-subtype (pair x y) = refl

  Eq-eq-total-subtype : (p p' : Î£ A B) â†’ Id p p' â†’ Eq-total-subtype p p'
  Eq-eq-total-subtype p .p refl = refl-Eq-total-subtype p

  abstract
    is-contr-total-Eq-total-subtype :
      (p : Î£ A B) â†’ is-contr (Î£ (Î£ A B) (Eq-total-subtype p))
    is-contr-total-Eq-total-subtype (pair x y) =
      is-contr-total-Eq-substructure (is-contr-total-path x) H x refl y

  abstract
    is-equiv-Eq-eq-total-subtype :
      (p p' : Î£ A B) â†’ is-equiv (Eq-eq-total-subtype p p')
    is-equiv-Eq-eq-total-subtype p =
      fundamental-theorem-id p
        ( refl-Eq-total-subtype p)
        ( is-contr-total-Eq-total-subtype p)
        ( Eq-eq-total-subtype p)

  equiv-Eq-eq-total-subtype :
    (p p' : Î£ A B) â†’ (Id p p') â‰ƒ (Eq-total-subtype p p')
  pr1 (equiv-Eq-eq-total-subtype p p') = Eq-eq-total-subtype p p'
  pr2 (equiv-Eq-eq-total-subtype p p') = is-equiv-Eq-eq-total-subtype p p'

  eq-subtype :
    {p p' : Î£ A B} â†’ Eq-total-subtype p p' â†’ Id p p'
  eq-subtype {p} {p'} =
    map-inv-is-equiv (is-equiv-Eq-eq-total-subtype p p')

--------------------------------------------------------------------------------

-- Section 12.3 Sets

is-set :
  {i : Level} â†’ UU i â†’ UU i
is-set A = (x y : A) â†’ is-prop (Id x y)

UU-Set :
  (i : Level) â†’ UU (lsuc i)
UU-Set i = Î£ (UU i) is-set

module _
  {l : Level} (X : UU-Set l)
  where

  type-Set : UU l
  type-Set = pr1 X

  abstract
    is-set-type-Set : is-set type-Set
    is-set-type-Set = pr2 X

  Id-Prop : (x y : type-Set) â†’ UU-Prop l
  pr1 (Id-Prop x y) = Id x y
  pr2 (Id-Prop x y) = is-set-type-Set x y

axiom-K :
  {i : Level} â†’ UU i â†’ UU i
axiom-K A = (x : A) (p : Id x x) â†’ Id refl p

module _
  {l : Level} {A : UU l}
  where

  abstract
    is-set-axiom-K' : axiom-K A â†’ (x y : A) â†’ all-elements-equal (Id x y)
    is-set-axiom-K' K x .x refl q with K x q
    ... | refl = refl

  abstract
    is-set-axiom-K : axiom-K A â†’ is-set A
    is-set-axiom-K H x y = is-prop-all-elements-equal (is-set-axiom-K' H x y) 

  abstract
    axiom-K-is-set : is-set A â†’ axiom-K A
    axiom-K-is-set H x p =
      ( inv (contraction (is-proof-irrelevant-is-prop (H x x) refl) refl)) âˆ™ 
      ( contraction (is-proof-irrelevant-is-prop (H x x) refl) p)

module _
  {l1 l2 : Level} {A : UU l1} (R : A â†’ A â†’ UU l2)
  (p : (x y : A) â†’ is-prop (R x y)) (Ï : (x : A) â†’ R x x)
  (i : (x y : A) â†’ R x y â†’ Id x y)
  where

  abstract
    is-equiv-prop-in-id : (x y : A) â†’ is-equiv (i x y)
    is-equiv-prop-in-id x =
      fundamental-theorem-id-retr x (i x)
        ( Î» y â†’
          pair
            ( ind-Id x (Î» z p â†’ R x z) (Ï x) y)
            ( Î» r â†’ eq-is-prop (p x y)))

  abstract
    is-set-prop-in-id : is-set A
    is-set-prop-in-id x y = is-prop-is-equiv' (is-equiv-prop-in-id x y) (p x y)

abstract
  is-prop-Eq-â„• :
    (n m : â„•) â†’ is-prop (Eq-â„• n m)
  is-prop-Eq-â„• zero-â„• zero-â„• = is-prop-unit
  is-prop-Eq-â„• zero-â„• (succ-â„• m) = is-prop-empty
  is-prop-Eq-â„• (succ-â„• n) zero-â„• = is-prop-empty
  is-prop-Eq-â„• (succ-â„• n) (succ-â„• m) = is-prop-Eq-â„• n m

abstract
  is-set-â„• : is-set â„•
  is-set-â„• =
    is-set-prop-in-id
      Eq-â„•
      is-prop-Eq-â„•
      refl-Eq-â„•
      eq-Eq-â„•

â„•-Set : UU-Set lzero
pr1 â„•-Set = â„•
pr2 â„•-Set = is-set-â„•

module _
  {l : Level} {A : UU l}
  where

  {- Next, we show that types with decidable equality are sets. To see this, we 
     will construct a fiberwise equivalence with the binary relation R that is
     defined by R x y := unit if (x = y), and empty otherwise. In order to
     define this relation, we first define a type family over
     ((x = y) + Â¬(x = y)) that returns unit on the left and empty on the right.   -}
   
  Eq-has-decidable-equality' :
    (x y : A) â†’ is-decidable (Id x y) â†’ UU lzero
  Eq-has-decidable-equality' x y (inl p) = unit
  Eq-has-decidable-equality' x y (inr f) = empty

  Eq-has-decidable-equality :
    (d : has-decidable-equality A) â†’ A â†’ A â†’ UU lzero
  Eq-has-decidable-equality d x y = Eq-has-decidable-equality' x y (d x y)

  abstract
    is-prop-Eq-has-decidable-equality' :
      (x y : A) (t : is-decidable (Id x y)) â†’
      is-prop (Eq-has-decidable-equality' x y t)
    is-prop-Eq-has-decidable-equality' x y (inl p) = is-prop-unit
    is-prop-Eq-has-decidable-equality' x y (inr f) = is-prop-empty

  abstract
    is-prop-Eq-has-decidable-equality :
      (d : has-decidable-equality A)
      {x y : A} â†’ is-prop (Eq-has-decidable-equality d x y)
    is-prop-Eq-has-decidable-equality d {x} {y} =
      is-prop-Eq-has-decidable-equality' x y (d x y)

  abstract
    refl-Eq-has-decidable-equality :
      (d : has-decidable-equality A) (x : A) â†’
      Eq-has-decidable-equality d x x 
    refl-Eq-has-decidable-equality d x with d x x
    ... | inl Î± = star
    ... | inr f = f refl

  abstract
    Eq-has-decidable-equality-eq :
      (d : has-decidable-equality A) {x y : A} â†’
      Id x y â†’ Eq-has-decidable-equality d x y
    Eq-has-decidable-equality-eq d {x} {.x} refl =
      refl-Eq-has-decidable-equality d x

  abstract
    eq-Eq-has-decidable-equality' :
      (x y : A) (t : is-decidable (Id x y)) â†’
      Eq-has-decidable-equality' x y t â†’ Id x y
    eq-Eq-has-decidable-equality' x y (inl p) t = p
    eq-Eq-has-decidable-equality' x y (inr f) t = ex-falso t

  abstract
    eq-Eq-has-decidable-equality :
      (d : has-decidable-equality A) {x y : A} â†’
      Eq-has-decidable-equality d x y â†’ Id x y
    eq-Eq-has-decidable-equality d {x} {y} =
      eq-Eq-has-decidable-equality' x y (d x y)

  abstract
    is-set-has-decidable-equality : has-decidable-equality A â†’ is-set A
    is-set-has-decidable-equality d =
      is-set-prop-in-id
        ( Î» x y â†’ Eq-has-decidable-equality d x y)
        ( Î» x y â†’ is-prop-Eq-has-decidable-equality d)
        ( Î» x â†’ refl-Eq-has-decidable-equality d x)
        ( Î» x y â†’ eq-Eq-has-decidable-equality d)

--------------------------------------------------------------------------------

-- Section 12.3 General truncation levels

data ğ•‹ : UU lzero where
  neg-two-ğ•‹ : ğ•‹
  succ-ğ•‹ : ğ•‹ â†’ ğ•‹

neg-one-ğ•‹ : ğ•‹
neg-one-ğ•‹ = succ-ğ•‹ (neg-two-ğ•‹)

zero-ğ•‹ : ğ•‹
zero-ğ•‹ = succ-ğ•‹ (neg-one-ğ•‹)

one-ğ•‹ : ğ•‹
one-ğ•‹ = succ-ğ•‹ (zero-ğ•‹)

truncation-level-â„• : â„• â†’ ğ•‹
truncation-level-â„• zero-â„• = zero-ğ•‹
truncation-level-â„• (succ-â„• k) = succ-ğ•‹ (truncation-level-â„• k)

truncation-level-minus-one-â„• : â„• â†’ ğ•‹
truncation-level-minus-one-â„• zero-â„• = neg-one-ğ•‹
truncation-level-minus-one-â„• (succ-â„• k) =
  succ-ğ•‹ (truncation-level-minus-one-â„• k)

truncation-level-minus-two-â„• : â„• â†’ ğ•‹
truncation-level-minus-two-â„• zero-â„• = neg-two-ğ•‹
truncation-level-minus-two-â„• (succ-â„• k) =
  succ-ğ•‹ (truncation-level-minus-two-â„• k)

-- Probably it is better to define this where we first need it.
add-ğ•‹ : ğ•‹ â†’ ğ•‹ â†’ ğ•‹
add-ğ•‹ neg-two-ğ•‹ neg-two-ğ•‹ = neg-two-ğ•‹
add-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) = neg-two-ğ•‹
add-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ y)) = y
add-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) neg-two-ğ•‹ = neg-two-ğ•‹
add-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) (succ-ğ•‹ y) = y
add-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹)) y = y
add-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ x))) y = succ-ğ•‹ (add-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ x)) y)

-- Definition 12.4.1

-- Truncated types

is-trunc : {i : Level} (k : ğ•‹) â†’ UU i â†’ UU i
is-trunc neg-two-ğ•‹ A = is-contr A
is-trunc (succ-ğ•‹ k) A = (x y : A) â†’ is-trunc k (Id x y)

-- Truncated maps

module _
  {l1 l2 : Level} (k : ğ•‹)
  where

  is-trunc-map : {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU (l1 âŠ” l2)
  is-trunc-map f = (y : _) â†’ is-trunc k (fib f y)
  
  trunc-map : (A : UU l1) (B : UU l2) â†’ UU (l1 âŠ” l2)
  trunc-map A B = Î£ (A â†’ B) is-trunc-map

module _
  {l1 l2 : Level} {k : ğ•‹} {A : UU l1} {B : UU l2}
  where

  map-trunc-map : trunc-map k A B â†’ A â†’ B
  map-trunc-map = pr1

  abstract
    is-trunc-map-map-trunc-map :
      (f : trunc-map k A B) â†’ is-trunc-map k (map-trunc-map f)
    is-trunc-map-map-trunc-map = pr2

module _
  {l1 l2 : Level}
  where

  is-0-map : {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU (l1 âŠ” l2)
  is-0-map {A} {B} f = (y : B) â†’ is-set (fib f y)

  0-map : (A : UU l1) (B : UU l2) â†’ UU (l1 âŠ” l2)
  0-map A B = Î£ (A â†’ B) is-0-map

  map-0-map : {A : UU l1} {B : UU l2} â†’ 0-map A B â†’ A â†’ B
  map-0-map = pr1

  is-0-map-map-0-map :
    {A : UU l1} {B : UU l2} (f : 0-map A B) â†’ is-0-map (map-0-map f)
  is-0-map-map-0-map = pr2

-- We introduce some notation for the special case of 1-types --

is-1-type : {l : Level} â†’ UU l â†’ UU l
is-1-type = is-trunc one-ğ•‹

UU-1-Type : (l : Level) â†’ UU (lsuc l)
UU-1-Type l = Î£ (UU l) is-1-type

type-1-Type : {l : Level} â†’ UU-1-Type l â†’ UU l
type-1-Type = pr1

abstract
  is-1-type-type-1-Type :
    {l : Level} (A : UU-1-Type l) â†’ is-1-type (type-1-Type A)
  is-1-type-type-1-Type = pr2

Id-Set : {l : Level} (X : UU-1-Type l) (x y : type-1-Type X) â†’ UU-Set l
pr1 (Id-Set X x y) = Id x y
pr2 (Id-Set X x y) = is-1-type-type-1-Type X x y

-- We introduce some notation for the special case of 2-types --

is-2-type : {l : Level} â†’ UU l â†’ UU l
is-2-type = is-trunc (succ-ğ•‹ one-ğ•‹)

UU-2-Type : (l : Level) â†’ UU (lsuc l)
UU-2-Type l = Î£ (UU l) is-2-type

type-2-Type :
  {l : Level} â†’ UU-2-Type l â†’ UU l
type-2-Type = pr1

abstract
  is-2-type-type-2-Type :
    {l : Level} (A : UU-2-Type l) â†’ is-2-type (type-2-Type A)
  is-2-type-type-2-Type = pr2

-- We introduce some notation for the universe of k-truncated types --

UU-Truncated-Type : ğ•‹ â†’ (l : Level) â†’ UU (lsuc l)
UU-Truncated-Type k l = Î£ (UU l) (is-trunc k)

module _
  {k : ğ•‹} {l : Level}
  where
  
  type-Truncated-Type : UU-Truncated-Type k l â†’ UU l
  type-Truncated-Type = pr1

  abstract
    is-trunc-type-Truncated-Type :
      (A : UU-Truncated-Type k l) â†’ is-trunc k (type-Truncated-Type A)
    is-trunc-type-Truncated-Type = pr2

{- Remark 12.4.2

We can't formalise this remark in Agda, because universes are handled 
differently. -}

-- Proposition 12.4.3

-- We show that if a type is k-truncated, then it is (k+1)-truncated. --

abstract
  is-trunc-succ-is-trunc :
    (k : ğ•‹) {i : Level} {A : UU i} â†’
    is-trunc k A â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-succ-is-trunc neg-two-ğ•‹ H =
    is-prop-is-contr H
  is-trunc-succ-is-trunc (succ-ğ•‹ k) H x y =
    is-trunc-succ-is-trunc k (H x y)

abstract
  is-trunc-map-succ-is-trunc-map :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2}
    (f : A â†’ B) â†’ is-trunc-map k f â†’ is-trunc-map (succ-ğ•‹ k) f
  is-trunc-map-succ-is-trunc-map k f is-trunc-f b =
    is-trunc-succ-is-trunc k (is-trunc-f b)

truncated-type-succ-Truncated-Type :
  (k : ğ•‹) {l : Level} â†’ UU-Truncated-Type k l â†’ UU-Truncated-Type (succ-ğ•‹ k) l
pr1 (truncated-type-succ-Truncated-Type k A) = type-Truncated-Type A
pr2 (truncated-type-succ-Truncated-Type k A) =
  is-trunc-succ-is-trunc k (is-trunc-type-Truncated-Type A)

abstract
  is-set-is-prop :
    {l : Level} {P : UU l} â†’ is-prop P â†’ is-set P
  is-set-is-prop = is-trunc-succ-is-trunc neg-one-ğ•‹

set-Prop :
  {l : Level} â†’ UU-Prop l â†’ UU-Set l
set-Prop P = truncated-type-succ-Truncated-Type neg-one-ğ•‹ P

1-type-Set :
  {l : Level} â†’ UU-Set l â†’ UU-1-Type l
1-type-Set A = truncated-type-succ-Truncated-Type zero-ğ•‹ A

-- We conclude that a contractible type is k-truncated for any k

abstract
  is-trunc-is-contr :
    {l : Level} (k : ğ•‹) {A : UU l} â†’ is-contr A â†’ is-trunc k A
  is-trunc-is-contr neg-two-ğ•‹ is-contr-A = is-contr-A
  is-trunc-is-contr (succ-ğ•‹ k) is-contr-A =
    is-trunc-succ-is-trunc k (is-trunc-is-contr k is-contr-A)

abstract
  is-set-is-contr :
    {l : Level} {A : UU l} â†’ is-contr A â†’ is-set A
  is-set-is-contr = is-trunc-is-contr zero-ğ•‹

-- We also conclude that a proposition is (k+1)-truncated for any k

abstract
  is-trunc-is-prop :
    { l : Level} (k : ğ•‹) {A : UU l} â†’ is-prop A â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-is-prop k is-prop-A x y = is-trunc-is-contr k (is-prop-A x y)

abstract
  is-trunc-empty : (k : ğ•‹) â†’ is-trunc (succ-ğ•‹ k) empty
  is-trunc-empty k = ind-empty

abstract
  is-trunc-is-empty :
    {l : Level} (k : ğ•‹) {A : UU l} â†’ is-empty A â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-is-empty k f = is-trunc-is-prop k (Î» x â†’ ex-falso (f x))

-- Corollary 12.4.4

abstract
  is-trunc-Id : {l : Level} (k : ğ•‹) {A : UU l} â†’
    is-trunc k A â†’ (x y : A) â†’ is-trunc k (Id x y)
  is-trunc-Id neg-two-ğ•‹ is-trunc-A = is-prop-is-contr is-trunc-A
  is-trunc-Id (succ-ğ•‹ k) is-trunc-A x y =
    is-trunc-succ-is-trunc k {A = Id x y} (is-trunc-A x y)

-- Proposition 12.4.5

-- We show that k-truncated types are closed under equivalences --

abstract
  is-trunc-is-equiv :
    {i j : Level} (k : ğ•‹) {A : UU i} (B : UU j) (f : A â†’ B) â†’ is-equiv f â†’
    is-trunc k B â†’ is-trunc k A
  is-trunc-is-equiv neg-two-ğ•‹ B f is-equiv-f H =
    is-contr-is-equiv B f is-equiv-f H
  is-trunc-is-equiv (succ-ğ•‹ k) B f is-equiv-f H x y =
    is-trunc-is-equiv k (Id (f x) (f y)) (ap f {x} {y})
      ( is-emb-is-equiv is-equiv-f x y) (H (f x) (f y))

abstract
  is-set-is-equiv :
    {i j : Level} {A : UU i} (B : UU j) (f : A â†’ B) â†’ is-equiv f â†’
    is-set B â†’ is-set A
  is-set-is-equiv = is-trunc-is-equiv zero-ğ•‹

abstract
  is-trunc-equiv :
    {i j : Level} (k : ğ•‹) {A : UU i} (B : UU  j) (e : A â‰ƒ B) â†’
    is-trunc k B â†’ is-trunc k A
  is-trunc-equiv k B (pair f is-equiv-f) =
    is-trunc-is-equiv k B f is-equiv-f

abstract
  is-set-equiv :
    {i j : Level} {A : UU i} (B : UU j) (e : A â‰ƒ B) â†’
    is-set B â†’ is-set A
  is-set-equiv = is-trunc-equiv zero-ğ•‹

abstract
  is-trunc-is-equiv' :
    {i j : Level} (k : ğ•‹) (A : UU i) {B : UU j} (f : A â†’ B) â†’
    is-equiv f â†’ is-trunc k A â†’ is-trunc k B
  is-trunc-is-equiv' k A  f is-equiv-f is-trunc-A =
    is-trunc-is-equiv k A
      ( map-inv-is-equiv is-equiv-f)
      ( is-equiv-map-inv-is-equiv is-equiv-f)
      ( is-trunc-A)

abstract
  is-set-is-equiv' :
    {i j : Level} (A : UU i) {B : UU j} (f : A â†’ B) â†’ is-equiv f â†’
    is-set A â†’ is-set B
  is-set-is-equiv' = is-trunc-is-equiv' zero-ğ•‹

abstract
  is-trunc-equiv' :
    {i j : Level} (k : ğ•‹) (A : UU i) {B : UU j} (e : A â‰ƒ B) â†’
    is-trunc k A â†’ is-trunc k B
  is-trunc-equiv' k A (pair f is-equiv-f) =
    is-trunc-is-equiv' k A f is-equiv-f

abstract
  is-set-equiv' :
    {i j : Level} (A : UU i) {B : UU j} (e : A â‰ƒ B) â†’
    is-set A â†’ is-set B
  is-set-equiv' = is-trunc-equiv' zero-ğ•‹

-- Corollary 12.4.6

-- We show that if A embeds into a (k+1)-type B, then A is a (k+1)-type. --

abstract
  is-trunc-is-emb :
    {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} (f : A â†’ B) â†’
    is-emb f â†’ is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-is-emb k f Ef H x y =
    is-trunc-is-equiv k (Id (f x) (f y)) (ap f {x} {y}) (Ef x y) (H (f x) (f y))

abstract
  is-trunc-emb :
    {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} (f : A â†ª B) â†’
    is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-emb k f = is-trunc-is-emb k (map-emb f) (is-emb-map-emb f)

-- Proposition 12.4.7

module _
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (f : A â†’ B)
  where
  
  abstract
    is-trunc-map-is-trunc-map-ap :
      ((x y : A) â†’ is-trunc-map k (ap f {x} {y})) â†’ is-trunc-map (succ-ğ•‹ k) f
    is-trunc-map-is-trunc-map-ap is-trunc-map-ap-f b (pair x p) (pair x' p') =
      is-trunc-is-equiv k
        ( fib (ap f) (p âˆ™ (inv p')))
        ( fib-ap-eq-fib f (pair x p) (pair x' p'))
        ( is-equiv-fib-ap-eq-fib f (pair x p) (pair x' p'))
        ( is-trunc-map-ap-f x x' (p âˆ™ (inv p')))      

  abstract
    is-trunc-map-ap-is-trunc-map :
      is-trunc-map (succ-ğ•‹ k) f â†’ (x y : A) â†’ is-trunc-map k (ap f {x} {y})
    is-trunc-map-ap-is-trunc-map is-trunc-map-f x y p =
      is-trunc-is-equiv' k
        ( Id (pair x p) (pair y refl))
        ( eq-fib-fib-ap f x y p)
        ( is-equiv-eq-fib-fib-ap f x y p)
        ( is-trunc-map-f (f y) (pair x p) (pair y refl))

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B}
  where

  is-0-map-is-faithful : is-faithful f â†’ is-0-map f
  is-0-map-is-faithful H =
    is-trunc-map-is-trunc-map-ap neg-one-ğ•‹ f
      ( Î» x y â†’ is-prop-map-is-emb (H x y))

  is-faithful-is-0-map : is-0-map f â†’ is-faithful f
  is-faithful-is-0-map H x y =
    is-emb-is-prop-map (is-trunc-map-ap-is-trunc-map neg-one-ğ•‹ f H x y)

--

module _
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1}
  where

  abstract
    is-trunc-map-pr1 :
      {B : A â†’ UU l2} â†’ ((x : A) â†’ is-trunc k (B x)) â†’
      is-trunc-map k (pr1 {l1} {l2} {A} {B})
    is-trunc-map-pr1 {B} H x =
      is-trunc-equiv k (B x) (equiv-fib-pr1 B x) (H x)

  pr1-trunc-map :
    (B : A â†’ UU-Truncated-Type k l2) â†’ trunc-map k (Î£ A (Î» x â†’ pr1 (B x))) A
  pr1 (pr1-trunc-map B) = pr1
  pr2 (pr1-trunc-map B) = is-trunc-map-pr1 (Î» x â†’ pr2 (B x))

  abstract
    is-trunc-is-trunc-map-pr1 :
      (B : A â†’ UU l2) â†’ is-trunc-map k (pr1 {l1} {l2} {A} {B}) â†’
      ((x : A) â†’ is-trunc k (B x))
    is-trunc-is-trunc-map-pr1 B is-trunc-map-pr1 x =
      is-trunc-equiv k (fib pr1 x) (inv-equiv-fib-pr1 B x) (is-trunc-map-pr1 x)
    
  abstract
    is-trunc-is-subtype :
      {P : A â†’ UU l2} â†’ is-subtype P â†’ is-trunc (succ-ğ•‹ k) A â†’
      is-trunc (succ-ğ•‹ k) (Î£ A P)
    is-trunc-is-subtype H is-trunc-A =
      is-trunc-is-emb k pr1 (is-emb-pr1 H) is-trunc-A

module _
  {l1 l2 : Level} {A : UU l1}
  where
  
  abstract
    is-0-map-pr1 :
      {B : A â†’ UU l2} â†’ ((x : A) â†’ is-set (B x)) â†’ is-0-map (pr1 {B = B})
    is-0-map-pr1 {B} H x =
      is-set-equiv (B x) (equiv-fib-pr1 B x) (H x)
                                                  
  pr1-0-map :
    (B : A â†’ UU-Set l2) â†’ 0-map (Î£ A (Î» x â†’ type-Set (B x))) A
  pr1 (pr1-0-map B) = pr1
  pr2 (pr1-0-map B) = is-0-map-pr1 (Î» x â†’ is-set-type-Set (B x))

  abstract
    is-faithful-pr1 :
      {B : A â†’ UU l2} â†’ ((x : A) â†’ is-set (B x)) â†’ is-faithful (pr1 {B = B})
    is-faithful-pr1 H = is-faithful-is-0-map (is-0-map-pr1 H)

  pr1-faithful-map :
    (B : A â†’ UU-Set l2) â†’ faithful-map (Î£ A (Î» x â†’ type-Set (B x))) A
  pr1 (pr1-faithful-map B) = pr1
  pr2 (pr1-faithful-map B) = is-faithful-pr1 (Î» x â†’ is-set-type-Set (B x))

module _
  {l1 l2 : Level} {A : UU l1} {P : A â†’ UU l2}
  where
  
  abstract
    is-prop-is-subtype : is-subtype P â†’ is-prop A â†’ is-prop (Î£ A P)
    is-prop-is-subtype = is-trunc-is-subtype neg-two-ğ•‹

  abstract
    is-set-is-subtype : is-subtype P â†’ is-set A â†’ is-set (Î£ A P)
    is-set-is-subtype = is-trunc-is-subtype neg-one-ğ•‹

  abstract
    is-1-type-is-subtype : is-subtype P â†’ is-1-type A â†’ is-1-type (Î£ A P)
    is-1-type-is-subtype = is-trunc-is-subtype zero-ğ•‹

subprop-Prop :
  {l1 l2 : Level} (A : UU-Prop l1) (P : (x : type-Prop A) â†’ UU-Prop l2) â†’
  UU-Prop (l1 âŠ” l2)
pr1 (subprop-Prop A P) = Î£ (type-Prop A) (Î» x â†’ type-Prop (P x))
pr2 (subprop-Prop A P) =
  is-prop-is-subtype (Î» x â†’ is-prop-type-Prop (P x)) (is-prop-type-Prop A)

subset-Set :
  {l1 l2 : Level} (A : UU-Set l1) (P : (x : type-Set A) â†’ UU-Prop l2) â†’
  UU-Set (l1 âŠ” l2)
pr1 (subset-Set A P) = Î£ (type-Set A) (Î» x â†’ type-Prop (P x))
pr2 (subset-Set A P) =
  is-set-is-subtype (Î» x â†’ is-prop-type-Prop (P x)) (is-set-type-Set A)

--------------------------------------------------------------------------------

-- Exercises

-- Exercise 12.1

abstract
  is-prop-Eq-bool : (x y : bool) â†’ is-prop (Eq-bool x y)
  is-prop-Eq-bool true true = is-prop-unit
  is-prop-Eq-bool true false = is-prop-empty
  is-prop-Eq-bool false true = is-prop-empty
  is-prop-Eq-bool false false = is-prop-unit

abstract
  is-set-bool : is-set bool
  is-set-bool =
    is-set-prop-in-id
      ( Eq-bool)
      ( is-prop-Eq-bool)
      ( refl-Eq-bool)
      ( Î» x y â†’ eq-Eq-bool)

bool-Set : UU-Set lzero
pr1 bool-Set = bool
pr2 bool-Set = is-set-bool

-- Exercise 12.2

-- Exercise 12.2 (a)

abstract
  is-emb-is-injective' : {l1 l2 : Level} {A : UU l1} (is-set-A : is-set A)
    {B : UU l2} (is-set-B : is-set B) (f : A â†’ B) â†’
    is-injective f â†’ is-emb f
  is-emb-is-injective' is-set-A is-set-B f is-injective-f x y =
    is-equiv-is-prop
      ( is-set-A x y)
      ( is-set-B (f x) (f y))
      ( is-injective-f)

  is-set-is-injective :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-set B â†’ is-injective f â†’ is-set A
  is-set-is-injective {f = f} H I =
    is-set-prop-in-id
      ( Î» x y â†’ Id (f x) (f y))
      ( Î» x y â†’ H (f x) (f y))
      ( Î» x â†’ refl)
      ( Î» x y â†’ I)

  is-emb-is-injective :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-set B â†’ is-injective f â†’ is-emb f
  is-emb-is-injective {f = f} H I =
    is-emb-is-injective' (is-set-is-injective H I) H f I

  is-prop-map-is-injective :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-set B â†’ is-injective f â†’ is-prop-map f
  is-prop-map-is-injective {f = f} H I =
    is-prop-map-is-emb (is-emb-is-injective H I)

-- Exercise 12.2 (b)

is-emb-add-â„• : (x : â„•) â†’ is-emb (add-â„• x)
is-emb-add-â„• x = is-emb-is-injective is-set-â„• (is-injective-add-â„• x)

is-emb-add-â„•' : (x : â„•) â†’ is-emb (add-â„•' x)
is-emb-add-â„•' x = is-emb-is-injective is-set-â„• (is-injective-add-â„•' x)

-- Exercise 12.2 (c)

is-emb-mul-â„• : (x : â„•) â†’ is-nonzero-â„• x â†’ is-emb (mul-â„• x)
is-emb-mul-â„• x H = is-emb-is-injective is-set-â„• (is-injective-mul-â„• x H)

is-emb-mul-â„•' : (x : â„•) â†’ is-nonzero-â„• x â†’ is-emb (mul-â„•' x)
is-emb-mul-â„•' x H = is-emb-is-injective is-set-â„• (is-injective-mul-â„•' x H)

-- We conclude that some maps, that were known to be injective, are embeddings
                                                                    
is-emb-nat-Fin : {k : â„•} â†’ is-emb (nat-Fin {k})
is-emb-nat-Fin {k} = is-emb-is-injective is-set-â„• is-injective-nat-Fin

emb-nat-Fin : (k : â„•) â†’ Fin k â†ª â„•
pr1 (emb-nat-Fin k) = nat-Fin
pr2 (emb-nat-Fin k) = is-emb-nat-Fin

-- Exercise 12.3

-- Exercise 12.3 (a)

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-not-contractible-coprod-is-contr :
      is-contr A â†’ is-contr B â†’ Â¬ (is-contr (coprod A B))
    is-not-contractible-coprod-is-contr HA HB HAB =
      is-empty-eq-coprod-inl-inr (center HA) (center HB) (eq-is-contr HAB)

-- Exercise 12.3 (b)

module _
  {l1 l2 : Level} {P : UU l1} {Q : UU l2}
  where

  abstract
    all-elements-equal-coprod :
      (P â†’ Â¬ Q) â†’ all-elements-equal P â†’ all-elements-equal Q â†’
      all-elements-equal (coprod P Q)
    all-elements-equal-coprod f is-prop-P is-prop-Q (inl p) (inl p') =
      ap inl (is-prop-P p p')
    all-elements-equal-coprod f is-prop-P is-prop-Q (inl p) (inr q') =
      ex-falso (f p q')
    all-elements-equal-coprod f is-prop-P is-prop-Q (inr q) (inl p') =
      ex-falso (f p' q)
    all-elements-equal-coprod f is-prop-P is-prop-Q (inr q) (inr q') =
      ap inr (is-prop-Q q q')
  
  abstract
    is-prop-coprod : (P â†’ Â¬ Q) â†’ is-prop P â†’ is-prop Q â†’ is-prop (coprod P Q)
    is-prop-coprod f is-prop-P is-prop-Q =
      is-prop-all-elements-equal
        ( all-elements-equal-coprod f
          ( eq-is-prop' is-prop-P)
          ( eq-is-prop' is-prop-Q))

-- Exercise 12.3 (c)

module _
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2}
  where

  abstract
    is-trunc-coprod :
      is-trunc (succ-ğ•‹ (succ-ğ•‹ k)) A â†’ is-trunc (succ-ğ•‹ (succ-ğ•‹ k)) B â†’
      is-trunc (succ-ğ•‹ (succ-ğ•‹ k)) (coprod A B)
    is-trunc-coprod is-trunc-A is-trunc-B (inl x) (inl y) =
      is-trunc-equiv (succ-ğ•‹ k)
        ( Id x y)
        ( compute-eq-coprod-inl-inl x y)
        ( is-trunc-A x y)
    is-trunc-coprod is-trunc-A is-trunc-B (inl x) (inr y) =
      is-trunc-is-empty k (is-empty-eq-coprod-inl-inr x y)
    is-trunc-coprod is-trunc-A is-trunc-B (inr x) (inl y) =
      is-trunc-is-empty k (is-empty-eq-coprod-inr-inl x y)
    is-trunc-coprod is-trunc-A is-trunc-B (inr x) (inr y) =
      is-trunc-equiv (succ-ğ•‹ k)
        ( Id x y)
        ( compute-eq-coprod-inr-inr x y)
        ( is-trunc-B x y)

abstract
  is-set-coprod : {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-set A â†’ is-set B â†’ is-set (coprod A B)
  is-set-coprod = is-trunc-coprod neg-two-ğ•‹

coprod-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : UU-Set l2) â†’ UU-Set (l1 âŠ” l2)
pr1 (coprod-Set (pair A is-set-A) (pair B is-set-B)) = coprod A B
pr2 (coprod-Set (pair A is-set-A) (pair B is-set-B)) =
  is-set-coprod is-set-A is-set-B

abstract
  is-set-unit : is-set unit
  is-set-unit = is-trunc-succ-is-trunc neg-one-ğ•‹ is-prop-unit

unit-Set : UU-Set lzero
pr1 unit-Set = unit
pr2 unit-Set = is-set-unit

abstract
  is-set-â„¤ : is-set â„¤
  is-set-â„¤ = is-set-coprod is-set-â„• (is-set-coprod is-set-unit is-set-â„•)

â„¤-Set : UU-Set lzero
pr1 â„¤-Set = â„¤
pr2 â„¤-Set = is-set-â„¤

is-set-empty : is-set empty
is-set-empty ()

empty-Set : UU-Set lzero
pr1 empty-Set = empty
pr2 empty-Set = is-set-empty

abstract
  is-set-Fin : (n : â„•) â†’ is-set (Fin n)
  is-set-Fin zero-â„• = is-set-empty
  is-set-Fin (succ-â„• n) =
    is-set-coprod (is-set-Fin n) is-set-unit

Fin-Set : (n : â„•) â†’ UU-Set lzero
pr1 (Fin-Set n) = Fin n
pr2 (Fin-Set n) = is-set-Fin n

abstract
  is-set-â„¤-Mod : (k : â„•) â†’ is-set (â„¤-Mod k)
  is-set-â„¤-Mod zero-â„• = is-set-â„¤
  is-set-â„¤-Mod (succ-â„• k) = is-set-Fin (succ-â„• k)

â„¤-Mod-Set : (k : â„•) â†’ UU-Set lzero
pr1 (â„¤-Mod-Set k) = â„¤-Mod k
pr2 (â„¤-Mod-Set k) = is-set-â„¤-Mod k    

-- Exercise 12.4

module _
  {l : Level} (A : UU l)
  where

  diagonal : A â†’ A Ã— A
  pr1 (diagonal x) = x
  pr2 (diagonal x) = x

  -- Exercise 12.4 (a)
  
  abstract
    is-prop-is-equiv-diagonal : is-equiv diagonal â†’ is-prop A
    is-prop-is-equiv-diagonal is-equiv-d =
      is-prop-all-elements-equal ( Î» x y â†’
        let Î± = issec-map-inv-is-equiv is-equiv-d (pair x y) in
        ( inv (ap pr1 Î±)) âˆ™ (ap pr2 Î±))
  
  eq-fib-diagonal : (t : A Ã— A) â†’ fib diagonal t â†’ Id (pr1 t) (pr2 t)
  eq-fib-diagonal (pair x y) (pair z Î±) = (inv (ap pr1 Î±)) âˆ™ (ap pr2 Î±)
  
  fib-diagonal-eq : (t : A Ã— A) â†’ Id (pr1 t) (pr2 t) â†’ fib diagonal t
  pr1 (fib-diagonal-eq (pair x y) Î²) = x
  pr2 (fib-diagonal-eq (pair x y) Î²) = eq-pair refl Î²
  
  issec-fib-diagonal-eq :
    (t : A Ã— A) â†’ ((eq-fib-diagonal t) âˆ˜ (fib-diagonal-eq t)) ~ id
  issec-fib-diagonal-eq (pair x .x) refl = refl
  
  isretr-fib-diagonal-eq :
    (t : A Ã— A) â†’ ((fib-diagonal-eq t) âˆ˜ (eq-fib-diagonal t)) ~ id
  isretr-fib-diagonal-eq .(pair z z) (pair z refl) = refl
  
  abstract
    is-equiv-eq-fib-diagonal : (t : A Ã— A) â†’ is-equiv (eq-fib-diagonal t)
    is-equiv-eq-fib-diagonal t =
      is-equiv-has-inverse
        ( fib-diagonal-eq t)
        ( issec-fib-diagonal-eq t)
        ( isretr-fib-diagonal-eq t)

-- Exercise 12.4 (c)

module _
  {l : Level} {A : UU l}
  where
  
  abstract
    is-trunc-is-trunc-map-diagonal :
      (k : ğ•‹) â†’ is-trunc-map k (diagonal A) â†’ is-trunc (succ-ğ•‹ k) A
    is-trunc-is-trunc-map-diagonal k is-trunc-d x y =
      is-trunc-is-equiv' k
        ( fib (diagonal A) (pair x y))
        ( eq-fib-diagonal A (pair x y))
        ( is-equiv-eq-fib-diagonal A (pair x y))
        ( is-trunc-d (pair x y))

  abstract
    is-prop-is-contr-map-diagonal : is-contr-map (diagonal A) â†’ is-prop A
    is-prop-is-contr-map-diagonal = is-trunc-is-trunc-map-diagonal neg-two-ğ•‹

  abstract
    is-set-is-prop-map-diagonal : is-prop-map (diagonal A) â†’ is-set A
    is-set-is-prop-map-diagonal = is-trunc-is-trunc-map-diagonal neg-one-ğ•‹

  abstract
    is-set-is-emb-diagonal : is-emb (diagonal A) â†’ is-set A
    is-set-is-emb-diagonal H =
      is-set-is-prop-map-diagonal (is-prop-map-is-emb H)

  abstract
    is-1-type-is-0-map-diagonal : is-0-map (diagonal A) â†’ is-1-type A
    is-1-type-is-0-map-diagonal = is-trunc-is-trunc-map-diagonal zero-ğ•‹

  abstract
    is-1-type-is-faithful-diagonal : is-faithful (diagonal A) â†’ is-1-type A
    is-1-type-is-faithful-diagonal H =
      is-1-type-is-0-map-diagonal (is-0-map-is-faithful H)
  
  abstract
    is-trunc-map-diagonal-is-trunc : 
      (k : ğ•‹) â†’ is-trunc (succ-ğ•‹ k) A â†’ is-trunc-map k (diagonal A)
    is-trunc-map-diagonal-is-trunc k is-trunc-A t =
      is-trunc-is-equiv k
        ( Id (pr1 t) (pr2 t))
        ( eq-fib-diagonal A t)
        ( is-equiv-eq-fib-diagonal A t)
          ( is-trunc-A (pr1 t) (pr2 t))

  abstract
    is-contr-map-diagonal-is-prop : is-prop A â†’ is-contr-map (diagonal A)
    is-contr-map-diagonal-is-prop = is-trunc-map-diagonal-is-trunc neg-two-ğ•‹

  abstract
    is-prop-map-diagonal-is-set : is-set A â†’ is-prop-map (diagonal A)
    is-prop-map-diagonal-is-set = is-trunc-map-diagonal-is-trunc neg-one-ğ•‹

  abstract
    is-emb-diagonal-is-set : is-set A â†’ is-emb (diagonal A)
    is-emb-diagonal-is-set H =
      is-emb-is-prop-map (is-prop-map-diagonal-is-set H)

  abstract
    is-0-map-diagonal-is-1-type : is-1-type A â†’ is-0-map (diagonal A)
    is-0-map-diagonal-is-1-type = is-trunc-map-diagonal-is-trunc zero-ğ•‹

  abstract
    is-faithful-diagonal-is-1-type : is-1-type A â†’ is-faithful (diagonal A)
    is-faithful-diagonal-is-1-type H =
      is-faithful-is-0-map (is-0-map-diagonal-is-1-type H)

diagonal-emb :
  {l : Level} (A : UU-Set l) â†’ (type-Set A) â†ª ((type-Set A) Ã— (type-Set A))
pr1 (diagonal-emb A) = diagonal (type-Set A)
pr2 (diagonal-emb A) = is-emb-diagonal-is-set (is-set-type-Set A)

diagonal-faithful-map :
  {l : Level} (A : UU-1-Type l) â†’
  faithful-map (type-1-Type A) (type-1-Type A Ã— type-1-Type A)
pr1 (diagonal-faithful-map A) = diagonal (type-1-Type A)
pr2 (diagonal-faithful-map A) =
  is-faithful-diagonal-is-1-type (is-1-type-type-1-Type A)

-- Exercise 12.5

-- Exercise 12.5 (a)

abstract
  is-trunc-Î£ : {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : A â†’ UU l2} â†’
    is-trunc k A â†’ ((x : A) â†’ is-trunc k (B x)) â†’ is-trunc k (Î£ A B)
  is-trunc-Î£ neg-two-ğ•‹ is-trunc-A is-trunc-B =
    is-contr-Î£' is-trunc-A is-trunc-B
  is-trunc-Î£ (succ-ğ•‹ k) {B = B} is-trunc-A is-trunc-B s t =
    is-trunc-is-equiv k
      ( Î£ (Id (pr1 s) (pr1 t)) (Î» p â†’ Id (tr B p (pr2 s)) (pr2 t)))
      ( pair-eq-Î£)
      ( is-equiv-pair-eq-Î£ s t)
      ( is-trunc-Î£ k
        ( is-trunc-A (pr1 s) (pr1 t))
        ( Î» p â†’ is-trunc-B (pr1 t) (tr B p (pr2 s)) (pr2 t)))

-- Exercise 12.5 (b)

-- Bureaucracy

abstract
  is-prop-Î£ : {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    is-prop A â†’ is-subtype B â†’ is-prop (Î£ A B)
  is-prop-Î£ = is-trunc-Î£ neg-one-ğ•‹

Î£-Prop :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : type-Prop P â†’ UU-Prop l2) â†’
  UU-Prop (l1 âŠ” l2)
pr1 (Î£-Prop P Q) = Î£ (type-Prop P) (Î» p â†’ type-Prop (Q p))
pr2 (Î£-Prop P Q) =
  is-prop-Î£
    ( is-prop-type-Prop P)
    ( Î» p â†’ is-prop-type-Prop (Q p))

abstract
  is-set-Î£ :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    is-set A â†’ ((x : A) â†’ is-set (B x)) â†’ is-set (Î£ A B)
  is-set-Î£ = is-trunc-Î£ zero-ğ•‹

Î£-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : pr1 A â†’ UU-Set l2) â†’ UU-Set (l1 âŠ” l2)
pr1 (Î£-Set A B) = Î£ (type-Set A) (Î» x â†’ (type-Set (B x)))
pr2 (Î£-Set A B) = is-set-Î£ (is-set-type-Set A) (Î» x â†’ is-set-type-Set (B x))

prod-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : UU-Set l2) â†’ UU-Set (l1 âŠ” l2)
prod-Set A B = Î£-Set A (Î» x â†’ B)

-- Exercise 12.5 (b)

abstract
  is-trunc-map-is-trunc-domain-codomain :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1}
    {B : UU l2} {f : A â†’ B} â†’ is-trunc k A â†’ is-trunc k B â†’ is-trunc-map k f
  is-trunc-map-is-trunc-domain-codomain k {f = f} is-trunc-A is-trunc-B b =
    is-trunc-Î£ k is-trunc-A (Î» x â†’ is-trunc-Id k is-trunc-B (f x) b)

-- Bureaucracy

abstract
  is-trunc-fam-is-trunc-Î£ :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : A â†’ UU l2} â†’
    is-trunc k A â†’ is-trunc k (Î£ A B) â†’ (x : A) â†’ is-trunc k (B x)
  is-trunc-fam-is-trunc-Î£ k {B = B} is-trunc-A is-trunc-Î£AB x =
    is-trunc-equiv' k
      ( fib pr1 x)
      ( equiv-fib-pr1 B x)
      ( is-trunc-map-is-trunc-domain-codomain k is-trunc-Î£AB is-trunc-A x)

-- Exercise 12.6

abstract
  is-trunc-prod :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
    is-trunc k A â†’ is-trunc k B â†’ is-trunc k (A Ã— B)
  is-trunc-prod k is-trunc-A is-trunc-B =
    is-trunc-Î£ k is-trunc-A (Î» x â†’ is-trunc-B)

is-trunc-prod' :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  (B â†’ is-trunc (succ-ğ•‹ k) A) â†’ (A â†’ is-trunc (succ-ğ•‹ k) B) â†’
  is-trunc (succ-ğ•‹ k) (A Ã— B)
is-trunc-prod' k f g (pair a b) (pair a' b') =
  is-trunc-equiv k
    ( Eq-prod (pair a b) (pair a' b'))
    ( equiv-pair-eq (pair a b) (pair a' b'))
    ( is-trunc-prod k (f b a a') (g a b b'))

is-trunc-left-factor-prod :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc k (A Ã— B) â†’ B â†’ is-trunc k A
is-trunc-left-factor-prod neg-two-ğ•‹ {A} {B} H b =
  is-contr-left-factor-prod A B H
is-trunc-left-factor-prod (succ-ğ•‹ k) H b a a' =
  is-trunc-left-factor-prod k {A = Id a a'} {B = Id b b}
    ( is-trunc-equiv' k
      ( Id (pair a b) (pair a' b))
      ( equiv-pair-eq (pair a b) (pair a' b))
      ( H (pair a b) (pair a' b)))
    ( refl)

is-trunc-right-factor-prod :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc k (A Ã— B) â†’ A â†’ is-trunc k B
is-trunc-right-factor-prod neg-two-ğ•‹ {A} {B} H a =
  is-contr-right-factor-prod A B H
is-trunc-right-factor-prod (succ-ğ•‹ k) {A} {B} H a b b' =
  is-trunc-right-factor-prod k {A = Id a a} {B = Id b b'}
    ( is-trunc-equiv' k
      ( Id (pair a b) (pair a b'))
      ( equiv-pair-eq (pair a b) (pair a b'))
      ( H (pair a b) (pair a b')))
    ( refl)

-- Bureaucracy

abstract
  is-prop-prod :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-prop A â†’ is-prop B â†’ is-prop (A Ã— B)
  is-prop-prod = is-trunc-prod neg-one-ğ•‹

prod-Prop : {l1 l2 : Level} â†’ UU-Prop l1 â†’ UU-Prop l2 â†’ UU-Prop (l1 âŠ” l2)
pr1 (prod-Prop P Q) = type-Prop P Ã— type-Prop Q
pr2 (prod-Prop P Q) = is-prop-prod (is-prop-type-Prop P) (is-prop-type-Prop Q)

abstract
  is-set-prod :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-set A â†’ is-set B â†’ is-set (A Ã— B)
  is-set-prod = is-trunc-prod zero-ğ•‹

-- Exercise 12.7

-- Exercise 12.7 (a)

{- In this exercise we show that if A is a retract of B, then so are its 
   identity types. -}

ap-retraction :
  {i j : Level} {A : UU i} {B : UU j}
  (i : A â†’ B) (r : B â†’ A) (H : (r âˆ˜ i) ~ id)
  (x y : A) â†’ Id (i x) (i y) â†’ Id x y
ap-retraction i r H x y p =
    ( inv (H x)) âˆ™ ((ap r p) âˆ™ (H y))

isretr-ap-retraction :
  {i j : Level} {A : UU i} {B : UU j}
  (i : A â†’ B) (r : B â†’ A) (H : (r âˆ˜ i) ~ id)
  (x y : A) â†’ ((ap-retraction i r H x y) âˆ˜ (ap i {x} {y})) ~ id
isretr-ap-retraction i r H x .x refl = left-inv (H x)

retr-ap :
  {i j : Level} {A : UU i} {B : UU j} (i : A â†’ B) â†’
  retr i â†’ (x y : A) â†’ retr (ap i {x} {y})
pr1 (retr-ap i (pair r H) x y) = ap-retraction i r H x y
pr2 (retr-ap i (pair r H) x y) = isretr-ap-retraction i r H x y

retract-eq :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (R : A retract-of B) â†’
  (x y : A) â†’ (Id x y) retract-of (Id (pr1 R x) (pr1 R y))
pr1 (retract-eq (pair i (pair r H)) x y) = ap i
pr2 (retract-eq (pair i (pair r H)) x y) = retr-ap i (pair r H) x y

-- Exercise 12.7 (b)

abstract
  is-trunc-retract-of : {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
    A retract-of B â†’ is-trunc k B â†’ is-trunc k A
  is-trunc-retract-of neg-two-ğ•‹ (pair i (pair r H)) is-trunc-B =
    is-contr-retract-of _ (pair i (pair r H)) is-trunc-B
  is-trunc-retract-of (succ-ğ•‹ k) (pair i retr-i) is-trunc-B x y =
    is-trunc-retract-of k
      ( retract-eq (pair i retr-i) x y)
      ( is-trunc-B (i x) (i y))

-- Exercise 12.8

module _
  {l : Level} {A : UU l}
  where

  fib-const : (x y : A) â†’ fib (const unit A x) y â‰ƒ (Id x y)
  fib-const x y = left-unit-law-prod

  abstract
    is-trunc-map-const-is-trunc :
      (k : ğ•‹) â†’ is-trunc (succ-ğ•‹ k) A â†’
      (x : A) â†’ is-trunc-map k (const unit A x)
    is-trunc-map-const-is-trunc k is-trunc-A x y =
      is-trunc-equiv k
        ( Id x y)
        ( fib-const x y)
        ( is-trunc-A x y)

  abstract
    is-contr-map-const-is-prop :
      is-prop A â†’ (x : A) â†’ is-contr-map (const unit A x)
    is-contr-map-const-is-prop = is-trunc-map-const-is-trunc neg-two-ğ•‹

  abstract
    is-equiv-const-is-prop :
      is-prop A â†’ (x : A) â†’ is-equiv (const unit A x)
    is-equiv-const-is-prop H x =
      is-equiv-is-contr-map (is-contr-map-const-is-prop H x)

  abstract
    is-prop-map-const-is-set :
      is-set A â†’ (x : A) â†’ is-prop-map (const unit A x)
    is-prop-map-const-is-set = is-trunc-map-const-is-trunc neg-one-ğ•‹

  abstract
    is-emb-const-is-set : is-set A â†’ (x : A) â†’ is-emb (const unit A x)
    is-emb-const-is-set H x = is-emb-is-prop-map (is-prop-map-const-is-set H x)

  abstract
    is-0-map-const-is-1-type : is-1-type A â†’ (x : A) â†’ is-0-map (const unit A x)
    is-0-map-const-is-1-type = is-trunc-map-const-is-trunc zero-ğ•‹

  abstract
    is-faithful-const-is-1-type :
      is-1-type A â†’ (x : A) â†’ is-faithful (const unit A x)
    is-faithful-const-is-1-type H x =
      is-faithful-is-0-map (is-0-map-const-is-1-type H x)

  abstract
    is-trunc-is-trunc-map-const :
      (k : ğ•‹) â†’ ((x : A) â†’ is-trunc-map k (const unit A x)) â†’
      is-trunc (succ-ğ•‹ k) A
    is-trunc-is-trunc-map-const k is-trunc-const x y =
      is-trunc-equiv' k
        ( Î£ unit (Î» t â†’ Id x y))
        ( left-unit-law-Î£ (Î» t â†’ Id x y))
        ( is-trunc-const x y)

  abstract
    is-prop-is-contr-map-const :
      ((x : A) â†’ is-contr-map (const unit A x)) â†’ is-prop A
    is-prop-is-contr-map-const = is-trunc-is-trunc-map-const neg-two-ğ•‹

  abstract
    is-prop-is-equiv-const :
      ((x : A) â†’ is-equiv (const unit A x)) â†’ is-prop A
    is-prop-is-equiv-const H =
      is-prop-is-contr-map-const (Î» x â†’ is-contr-map-is-equiv (H x))

  abstract
    is-set-is-prop-map-const :
      ((x : A) â†’ is-prop-map (const unit A x)) â†’ is-set A
    is-set-is-prop-map-const = is-trunc-is-trunc-map-const neg-one-ğ•‹

  abstract
    is-set-is-emb-const :
      ((x : A) â†’ is-emb (const unit A x)) â†’ is-set A
    is-set-is-emb-const H =
      is-set-is-prop-map-const (Î» x â†’ is-prop-map-is-emb (H x))

  abstract
    is-1-type-is-0-map-const :
      ((x : A) â†’ is-0-map (const unit A x)) â†’ is-1-type A
    is-1-type-is-0-map-const = is-trunc-is-trunc-map-const zero-ğ•‹

  abstract
    is-1-type-is-faithful-const :
      ((x : A) â†’ is-faithful (const unit A x)) â†’ is-1-type A
    is-1-type-is-faithful-const H =
      is-1-type-is-0-map-const (Î» x â†’ is-0-map-is-faithful (H x))

const-equiv :
  {l : Level} (A : UU-Prop l) (x : type-Prop A) â†’ unit â‰ƒ type-Prop A
pr1 (const-equiv A x) = const unit (type-Prop A) x
pr2 (const-equiv A x) = is-equiv-const-is-prop (is-prop-type-Prop A) x

const-emb :
  {l : Level} (A : UU-Set l) (x : type-Set A) â†’ unit â†ª type-Set A
pr1 (const-emb A x) = const unit (type-Set A) x
pr2 (const-emb A x) = is-emb-const-is-set (is-set-type-Set A) x

const-faithful-map :
  {l : Level} (A : UU-1-Type l) (x : type-1-Type A) â†’
  faithful-map unit (type-1-Type A)
pr1 (const-faithful-map A x) = const unit (type-1-Type A) x
pr2 (const-faithful-map A x) =
  is-faithful-const-is-1-type (is-1-type-type-1-Type A) x

-- Exercise 12.9

map-fib-comp : {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {X : UU l3} (g : B â†’ X) (h : A â†’ B) â†’
  (x : X) â†’ fib (g âˆ˜ h) x â†’ Î£ (fib g x) (Î» t â†’ fib h (pr1 t))
pr1 (pr1 (map-fib-comp g h x (pair a p))) = h a
pr2 (pr1 (map-fib-comp g h x (pair a p))) = p
pr1 (pr2 (map-fib-comp g h x (pair a p))) = a
pr2 (pr2 (map-fib-comp g h x (pair a p))) = refl

inv-map-fib-comp : {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {X : UU l3} (g : B â†’ X) (h : A â†’ B) â†’
  (x : X) â†’ Î£ (fib g x) (Î» t â†’ fib h (pr1 t)) â†’ fib (g âˆ˜ h) x
pr1 (inv-map-fib-comp g h c t) = pr1 (pr2 t)
pr2 (inv-map-fib-comp g h c t) = (ap g (pr2 (pr2 t))) âˆ™ (pr2 (pr1 t))

issec-inv-map-fib-comp : {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {X : UU l3} (g : B â†’ X) (h : A â†’ B) â†’
  (x : X) â†’
  ((map-fib-comp g h x) âˆ˜ (inv-map-fib-comp g h x)) ~ id
issec-inv-map-fib-comp g h x
  (pair (pair .(h a) refl) (pair a refl)) = refl

isretr-inv-map-fib-comp : {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {X : UU l3} (g : B â†’ X) (h : A â†’ B) (x : X) â†’
  ((inv-map-fib-comp g h x) âˆ˜ (map-fib-comp g h x)) ~ id
isretr-inv-map-fib-comp g h .(g (h a)) (pair a refl) = refl

abstract
  is-equiv-map-fib-comp : {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
    {X : UU l3} (g : B â†’ X) (h : A â†’ B) (x : X) â†’
    is-equiv (map-fib-comp g h x)
  is-equiv-map-fib-comp g h x =
    is-equiv-has-inverse
      ( inv-map-fib-comp g h x)
      ( issec-inv-map-fib-comp g h x)
      ( isretr-inv-map-fib-comp g h x)

abstract
  is-equiv-inv-map-fib-comp : {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
    {X : UU l3} (g : B â†’ X) (h : A â†’ B) (x : X) â†’
    is-equiv (inv-map-fib-comp g h x)
  is-equiv-inv-map-fib-comp g h x =
    is-equiv-has-inverse
      ( map-fib-comp g h x)
      ( isretr-inv-map-fib-comp g h x)
      ( issec-inv-map-fib-comp g h x)

abstract
  is-trunc-map-htpy :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2}
    {f g : A â†’ B} â†’ f ~ g â†’ is-trunc-map k g â†’ is-trunc-map k f
  is-trunc-map-htpy k {A} {B} {f} {g} H is-trunc-g b =
    is-trunc-is-equiv k
      ( Î£ A (Î» z â†’ Id (g z) b))
      ( fib-triangle f g id H b)
      ( is-fiberwise-equiv-is-equiv-triangle f g id H is-equiv-id b)
      ( is-trunc-g b)

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f g : A â†’ B} (H : f ~ g)
  where
  
  abstract
    is-contr-map-htpy : is-contr-map g â†’ is-contr-map f
    is-contr-map-htpy = is-trunc-map-htpy neg-two-ğ•‹ H

  abstract
    is-prop-map-htpy : is-prop-map g â†’ is-prop-map f
    is-prop-map-htpy = is-trunc-map-htpy neg-one-ğ•‹ H

  abstract
    is-0-map-htpy : is-0-map g â†’ is-0-map f
    is-0-map-htpy = is-trunc-map-htpy zero-ğ•‹ H

  abstract
    is-faithful-htpy : is-faithful g â†’ is-faithful f
    is-faithful-htpy K =
      is-faithful-is-0-map (is-0-map-htpy (is-0-map-is-faithful K))

abstract
  is-trunc-map-comp : {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2}
    {X : UU l3} (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
    is-trunc-map k g â†’ is-trunc-map k h â†’ is-trunc-map k f
  is-trunc-map-comp k f g h H is-trunc-g is-trunc-h =
    is-trunc-map-htpy k H
      ( Î» x â†’ is-trunc-is-equiv k
        ( Î£ (fib g x) (Î» t â†’ fib h (pr1 t)))
        ( map-fib-comp g h x)
        ( is-equiv-map-fib-comp g h x)
        ( is-trunc-Î£ k
          ( is-trunc-g x)
          ( Î» t â†’ is-trunc-h (pr1 t))))

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h))
  where

  abstract
    is-contr-map-comp : is-contr-map g â†’ is-contr-map h â†’ is-contr-map f
    is-contr-map-comp = is-trunc-map-comp neg-two-ğ•‹ f g h H

  abstract
    is-prop-map-comp : is-prop-map g â†’ is-prop-map h â†’ is-prop-map f
    is-prop-map-comp = is-trunc-map-comp neg-one-ğ•‹ f g h H

  abstract
    is-0-map-comp : is-0-map g â†’ is-0-map h â†’ is-0-map f
    is-0-map-comp = is-trunc-map-comp zero-ğ•‹ f g h H

  abstract
    is-faithful-comp : is-faithful g â†’ is-faithful h â†’ is-faithful f
    is-faithful-comp K L =
      is-faithful-is-0-map
        (is-0-map-comp (is-0-map-is-faithful K) (is-0-map-is-faithful L))

abstract
  is-trunc-map-right-factor : {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2}
    {X : UU l3} (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
    is-trunc-map k g â†’ is-trunc-map k f â†’ is-trunc-map k h
  is-trunc-map-right-factor k {A} f g h H is-trunc-g is-trunc-f b =
    is-trunc-fam-is-trunc-Î£ k
      ( is-trunc-g (g b))
      ( is-trunc-is-equiv' k
        ( Î£ A (Î» z â†’ Id (g (h z)) (g b)))
        ( map-fib-comp g h (g b))
        ( is-equiv-map-fib-comp g h (g b))
        ( is-trunc-map-htpy k (inv-htpy H) is-trunc-f (g b)))
      ( pair b refl)

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h))
  where

  is-contr-map-right-factor : is-contr-map g â†’ is-contr-map f â†’ is-contr-map h
  is-contr-map-right-factor = is-trunc-map-right-factor neg-two-ğ•‹ f g h H

  is-prop-map-right-factor : is-prop-map g â†’ is-prop-map f â†’ is-prop-map h
  is-prop-map-right-factor = is-trunc-map-right-factor neg-one-ğ•‹ f g h H

  is-0-map-right-factor : is-0-map g â†’ is-0-map f â†’ is-0-map h
  is-0-map-right-factor = is-trunc-map-right-factor zero-ğ•‹ f g h H

  is-faithful-right-factor : is-faithful g â†’ is-faithful f â†’ is-faithful h
  is-faithful-right-factor K L =
    is-faithful-is-0-map
      ( is-0-map-right-factor (is-0-map-is-faithful K) (is-0-map-is-faithful L))

-- Exercise 12.10

module _
  {l1 l2 l3 : Level} (k : ğ•‹)  {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  {f : (x : A) â†’ B x â†’ C x}
  where

  abstract
    is-trunc-map-tot : ((x : A) â†’ is-trunc-map k (f x)) â†’ is-trunc-map k (tot f)
    is-trunc-map-tot H y =
      is-trunc-equiv k
        ( fib (f (pr1 y)) (pr2 y))
        ( compute-fib-tot f y)
        ( H (pr1 y) (pr2 y))

  abstract
    is-trunc-map-is-trunc-map-tot : 
      is-trunc-map k (tot f) â†’ ((x : A) â†’ is-trunc-map k (f x))
    is-trunc-map-is-trunc-map-tot is-trunc-tot-f x z =
      is-trunc-equiv k
        ( fib (tot f) (pair x z))
        ( inv-compute-fib-tot f (pair x z))
        ( is-trunc-tot-f (pair x z))

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  {f : (x : A) â†’ B x â†’ C x}
  where

  abstract
    is-contr-map-tot :
      ((x : A) â†’ is-contr-map (f x)) â†’ is-contr-map (tot f)
    is-contr-map-tot =
      is-trunc-map-tot neg-two-ğ•‹

  abstract
    is-prop-map-tot : ((x : A) â†’ is-prop-map (f x)) â†’ is-prop-map (tot f)
    is-prop-map-tot = is-trunc-map-tot neg-one-ğ•‹

  is-emb-tot : ((x : A) â†’ is-emb (f x)) â†’ is-emb (tot f)
  is-emb-tot H =
    is-emb-is-prop-map (is-prop-map-tot (Î» x â†’ is-prop-map-is-emb (H x)))

  abstract
    is-0-map-tot : ((x : A) â†’ is-0-map (f x)) â†’ is-0-map (tot f)
    is-0-map-tot = is-trunc-map-tot zero-ğ•‹

  is-faithful-tot : ((x : A) â†’ is-faithful (f x)) â†’ is-faithful (tot f)
  is-faithful-tot H =
    is-faithful-is-0-map (is-0-map-tot (Î» x â†’ is-0-map-is-faithful (H x)))

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  where

  tot-emb : ((x : A) â†’ B x â†ª C x) â†’ Î£ A B â†ª Î£ A C
  pr1 (tot-emb f) = tot (Î» x â†’ map-emb (f x))
  pr2 (tot-emb f) = is-emb-tot (Î» x â†’ is-emb-map-emb (f x))

  tot-faithful-map :
    ((x : A) â†’ faithful-map (B x) (C x)) â†’ faithful-map (Î£ A B) (Î£ A C)
  pr1 (tot-faithful-map f) = tot (Î» x â†’ map-faithful-map (f x))
  pr2 (tot-faithful-map f) =
    is-faithful-tot (Î» x â†’ is-faithful-map-faithful-map (f x))

-- Exercise 12.11

module _
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2)
  where
  
  fiber-inclusion : (x : A) â†’ B x â†’ Î£ A B
  pr1 (fiber-inclusion x y) = x
  pr2 (fiber-inclusion x y) = y

  fib-fiber-inclusion :
    (a : A) (t : Î£ A B) â†’ fib (fiber-inclusion a) t â‰ƒ Id a (pr1 t)
  fib-fiber-inclusion a t =
    ( ( right-unit-law-Î£-is-contr
        ( Î» p â†’ is-contr-map-is-equiv (is-equiv-tr B p) (pr2 t))) âˆ˜e
      ( equiv-left-swap-Î£)) âˆ˜e
    ( equiv-tot (Î» b â†’ equiv-pair-eq-Î£ (pair a b) t))

module _
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1}
  where
  
  is-trunc-is-trunc-map-fiber-inclusion :
    ((B : A â†’ UU l2) (a : A) â†’ is-trunc-map k (fiber-inclusion B a)) â†’
    is-trunc (succ-ğ•‹ k) A
  is-trunc-is-trunc-map-fiber-inclusion H x y =
    is-trunc-equiv' k
      ( fib (fiber-inclusion B x) (pair y raise-star))
      ( fib-fiber-inclusion B x (pair y raise-star))
      ( H B x (pair y raise-star))
    where
    B : A â†’ UU l2
    B a = raise-unit l2

  is-trunc-map-fiber-inclusion-is-trunc :
    (B : A â†’ UU l2) (a : A) â†’
    is-trunc (succ-ğ•‹ k) A â†’ is-trunc-map k (fiber-inclusion B a)
  is-trunc-map-fiber-inclusion-is-trunc B a H t =
    is-trunc-equiv k
      ( Id a (pr1 t))
      ( fib-fiber-inclusion B a t)
      ( H a (pr1 t))

module _
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2)
  where

  is-contr-map-fiber-inclusion :
    (x : A) â†’ is-prop A â†’ is-contr-map (fiber-inclusion B x)
  is-contr-map-fiber-inclusion =
    is-trunc-map-fiber-inclusion-is-trunc neg-two-ğ•‹ B

  is-prop-map-fiber-inclusion :
    (x : A) â†’ is-set A â†’ is-prop-map (fiber-inclusion B x)
  is-prop-map-fiber-inclusion =
    is-trunc-map-fiber-inclusion-is-trunc neg-one-ğ•‹ B

  is-0-map-fiber-inclusion :
    (x : A) â†’ is-1-type A â†’ is-0-map (fiber-inclusion B x)
  is-0-map-fiber-inclusion =
    is-trunc-map-fiber-inclusion-is-trunc zero-ğ•‹ B

  is-emb-fiber-inclusion : is-set A â†’ (x : A) â†’ is-emb (fiber-inclusion B x)
  is-emb-fiber-inclusion H x =
    is-emb-is-prop-map (is-prop-map-fiber-inclusion x H)

  is-faithful-fiber-inclusion :
    is-1-type A â†’ (x : A) â†’ is-faithful (fiber-inclusion B x)
  is-faithful-fiber-inclusion H x =
    is-faithful-is-0-map (is-0-map-fiber-inclusion x H)

fiber-inclusion-emb :
  {l1 l2 : Level} (A : UU-Set l1) (B : type-Set A â†’ UU l2) â†’
  (x : type-Set A) â†’ B x â†ª Î£ (type-Set A) B
pr1 (fiber-inclusion-emb A B x) = fiber-inclusion B x
pr2 (fiber-inclusion-emb A B x) = is-emb-fiber-inclusion B (is-set-type-Set A) x

fiber-inclusion-faithful-map :
  {l1 l2 : Level} (A : UU-1-Type l1) (B : type-1-Type A â†’ UU l2) â†’
  (x : type-1-Type A) â†’ faithful-map (B x) (Î£ (type-1-Type A) B)
pr1 (fiber-inclusion-faithful-map A B x) = fiber-inclusion B x
pr2 (fiber-inclusion-faithful-map A B x) =
  is-faithful-fiber-inclusion B (is-1-type-type-1-Type A) x

-- Exercise 12.12

is-isolated :
  {l1 : Level} {X : UU l1} (x : X) â†’ UU l1
is-isolated {l1} {X} x = (y : X) â†’ is-decidable (Id x y)

isolated-point :
  {l1 : Level} (X : UU l1) â†’ UU l1
isolated-point X = Î£ X is-isolated

-- We will use a few facts about decidability in this exercise

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-decidable-map : (A â†’ B) â†’ UU (l1 âŠ” l2)
  is-decidable-map f = (y : B) â†’ is-decidable (fib f y)

  is-decidable-retract-of :
    A retract-of B â†’ is-decidable B â†’ is-decidable A
  is-decidable-retract-of (pair i (pair r H)) (inl b) = inl (r b)
  is-decidable-retract-of (pair i (pair r H)) (inr f) = inr (f âˆ˜ i)

  is-decidable-is-equiv :
    {f : A â†’ B} â†’ is-equiv f â†’ is-decidable B â†’ is-decidable A
  is-decidable-is-equiv {f} (pair (pair g G) (pair h H)) =
    is-decidable-retract-of (pair f (pair h H))

  is-decidable-equiv :
    (e : A â‰ƒ B) â†’ is-decidable B â†’ is-decidable A
  is-decidable-equiv e = is-decidable-iff (map-inv-equiv e) (map-equiv e)

is-decidable-equiv' :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
  is-decidable A â†’ is-decidable B
is-decidable-equiv' e = is-decidable-equiv (inv-equiv e)

module _
  {l1 : Level} {A : UU l1} (a : A)
  where
  
  -- Exercise 12.12 (a)
  
  is-decidable-map-const-is-isolated :
    is-isolated a â†’ is-decidable-map (const unit A a)
  is-decidable-map-const-is-isolated d x =
    is-decidable-equiv (fib-const a x) (d x)

  is-isolated-is-decidable-map-const :
    is-decidable-map (const unit A a) â†’ is-isolated a
  is-isolated-is-decidable-map-const d x =
    is-decidable-equiv' (fib-const a x) (d x)

  -- Exercise 12.12 (b)
  
  cases-Eq-isolated-point :
    is-isolated a â†’ (x : A) â†’ is-decidable (Id a x) â†’ UU lzero
  cases-Eq-isolated-point H x (inl p) = unit
  cases-Eq-isolated-point H x (inr f) = empty

  abstract
    is-prop-cases-Eq-isolated-point :
      (d : is-isolated a) (x : A) (dx : is-decidable (Id a x)) â†’
      is-prop (cases-Eq-isolated-point d x dx)
    is-prop-cases-Eq-isolated-point d x (inl p) = is-prop-unit
    is-prop-cases-Eq-isolated-point d x (inr f) = is-prop-empty

  Eq-isolated-point : is-isolated a â†’ A â†’ UU lzero
  Eq-isolated-point d x = cases-Eq-isolated-point d x (d x)

  abstract
    is-prop-Eq-isolated-point :
      (d : is-isolated a) (x : A) â†’ is-prop (Eq-isolated-point d x)
    is-prop-Eq-isolated-point d x =
      is-prop-cases-Eq-isolated-point d x (d x)

  decide-reflexivity :
    (d : is-decidable (Id a a)) â†’ Î£ (Id a a) (Î» p â†’ Id (inl p) d)
  pr1 (decide-reflexivity (inl p)) = p
  pr2 (decide-reflexivity (inl p)) = refl
  decide-reflexivity (inr f) = ex-falso (f refl)

  abstract
    refl-Eq-isolated-point : (d : is-isolated a) â†’ Eq-isolated-point d a
    refl-Eq-isolated-point d =
      tr ( cases-Eq-isolated-point d a)
        ( pr2 (decide-reflexivity (d a)))
        ( star)

  abstract
    Eq-eq-isolated-point :
      (d : is-isolated a) {x : A} â†’ Id a x â†’ Eq-isolated-point d x
    Eq-eq-isolated-point d refl = refl-Eq-isolated-point d

  abstract
    center-total-Eq-isolated-point :
      (d : is-isolated a) â†’ Î£ A (Eq-isolated-point d)
    pr1 (center-total-Eq-isolated-point d) = a
    pr2 (center-total-Eq-isolated-point d) = refl-Eq-isolated-point d
  
    cases-contraction-total-Eq-isolated-point :
      (d : is-isolated a) (x : A) (dx : is-decidable (Id a x))
      (e : cases-Eq-isolated-point d x dx) â†’ Id a x
    cases-contraction-total-Eq-isolated-point d x (inl p) e = p
  
    contraction-total-Eq-isolated-point :
      (d : is-isolated a) (t : Î£ A (Eq-isolated-point d)) â†’
      Id (center-total-Eq-isolated-point d) t
    contraction-total-Eq-isolated-point d (pair x e) =
      eq-subtype
        ( is-prop-Eq-isolated-point d)
        ( cases-contraction-total-Eq-isolated-point d x (d x) e)

    is-contr-total-Eq-isolated-point :
      (d : is-isolated a) â†’ is-contr (Î£ A (Eq-isolated-point d))
    pr1 (is-contr-total-Eq-isolated-point d) = center-total-Eq-isolated-point d
    pr2 (is-contr-total-Eq-isolated-point d) =
      contraction-total-Eq-isolated-point d

  abstract
    is-equiv-Eq-eq-isolated-point :
      (d : is-isolated a) (x : A) â†’ is-equiv (Eq-eq-isolated-point d {x})
    is-equiv-Eq-eq-isolated-point d =
      fundamental-theorem-id a
        ( refl-Eq-isolated-point d)
        ( is-contr-total-Eq-isolated-point d)
        ( Î» x â†’ Eq-eq-isolated-point d {x})

  abstract
    equiv-Eq-eq-isolated-point :
      (d : is-isolated a) (x : A) â†’ Id a x â‰ƒ Eq-isolated-point d x
    pr1 (equiv-Eq-eq-isolated-point d x) = Eq-eq-isolated-point d
    pr2 (equiv-Eq-eq-isolated-point d x) = is-equiv-Eq-eq-isolated-point d x

  abstract
    is-prop-eq-isolated-point : (d : is-isolated a) (x : A) â†’ is-prop (Id a x)
    is-prop-eq-isolated-point d x =
      is-prop-equiv
        ( equiv-Eq-eq-isolated-point d x)
        ( is-prop-Eq-isolated-point d x)

  abstract
    is-emb-const-is-isolated : is-isolated a â†’ is-emb (const unit A a)
    is-emb-const-is-isolated d star =
      fundamental-theorem-id star
        refl
        ( is-contr-equiv
          ( Id a a)
          ( left-unit-law-prod)
          ( is-proof-irrelevant-is-prop
            ( is-prop-eq-isolated-point d a)
            ( refl)))
        ( Î» x â†’ ap (Î» y â†’ a))

abstract
  has-decidable-equality-retract-of :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    A retract-of B â†’ has-decidable-equality B â†’ has-decidable-equality A
  has-decidable-equality-retract-of (pair i (pair r H)) d x y =
    is-decidable-retract-of
      ( retract-eq (pair i (pair r H)) x y)
      ( d (i x) (i y))

--------------------------------------------------------------------------------

-- Extra stuff

abstract
  has-decidable-equality-Î£ :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    has-decidable-equality A â†’ ((x : A) â†’ has-decidable-equality (B x)) â†’
    has-decidable-equality (Î£ A B)
  has-decidable-equality-Î£ dA dB (pair x y) (pair x' y') with dA x x'
  ... | inr np = inr (Î» r â†’ np (ap pr1 r))
  ... | inl p =
    is-decidable-iff eq-pair-Î£' pair-eq-Î£
      ( is-decidable-equiv
        ( left-unit-law-Î£-is-contr
          ( is-proof-irrelevant-is-prop
            ( is-set-has-decidable-equality dA x x') p)
          ( p))
        ( dB x' (tr _ p y) y'))

abstract
  has-decidable-equality-is-prop :
    {l1 : Level} {A : UU l1} â†’ is-prop A â†’ has-decidable-equality A
  has-decidable-equality-is-prop H x y = inl (eq-is-prop H)

abstract
  has-decidable-equality-equiv :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
    has-decidable-equality B â†’ has-decidable-equality A
  has-decidable-equality-equiv e dB x y =
    is-decidable-equiv (equiv-ap e x y) (dB (map-equiv e x) (map-equiv e y))
  
abstract
  has-decidable-equality-equiv' :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
    has-decidable-equality A â†’ has-decidable-equality B
  has-decidable-equality-equiv' e = has-decidable-equality-equiv (inv-equiv e)

abstract
  has-decidable-equality-fiber-has-decidable-equality-Î£ :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    has-decidable-equality A â†’ has-decidable-equality (Î£ A B) â†’
    (x : A) â†’ has-decidable-equality (B x)
  has-decidable-equality-fiber-has-decidable-equality-Î£ {B = B} dA dÎ£ x =
    has-decidable-equality-equiv'
      ( equiv-fib-pr1 B x)
      ( has-decidable-equality-Î£ dÎ£
        ( Î» t â†’
          has-decidable-equality-is-prop
            ( is-set-has-decidable-equality dA (pr1 t) x)))

is-injective-map-section :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (b : (x : A) â†’ B x) â†’
  is-injective (map-section b)
is-injective-map-section b = ap pr1

abstract
  has-decidable-equality-base-has-decidable-equality-Î£ :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (b : (x : A) â†’ B x) â†’
    has-decidable-equality (Î£ A B) â†’ ((x : A) â†’ has-decidable-equality (B x)) â†’
    has-decidable-equality A
  has-decidable-equality-base-has-decidable-equality-Î£ b dÎ£ dB =
    has-decidable-equality-equiv'
      ( equiv-total-fib (map-section b))
      ( has-decidable-equality-Î£ dÎ£
        ( Î» t â†’
          has-decidable-equality-is-prop
            ( is-prop-map-is-injective
              ( is-set-has-decidable-equality dÎ£)
              ( is-injective-map-section b)
              ( t))))

abstract
  is-injective-const-true : is-injective (const unit bool true)
  is-injective-const-true {star} {star} p = refl

abstract
  is-injective-const-false : is-injective (const unit bool false)
  is-injective-const-false {star} {star} p = refl

equiv-total-subtype :
  { l1 l2 l3 : Level} {A : UU l1} {P : A â†’ UU l2} {Q : A â†’ UU l3} â†’
  ( is-subtype-P : is-subtype P) (is-subtype-Q : is-subtype Q) â†’
  ( f : (x : A) â†’ P x â†’ Q x) â†’
  ( g : (x : A) â†’ Q x â†’ P x) â†’
  ( Î£ A P) â‰ƒ (Î£ A Q)
pr1 (equiv-total-subtype is-subtype-P is-subtype-Q f g) = tot f
pr2 (equiv-total-subtype is-subtype-P is-subtype-Q f g) =
  is-equiv-tot-is-fiberwise-equiv {f = f}
    ( Î» x â†’ is-equiv-is-prop (is-subtype-P x) (is-subtype-Q x) (g x))

{- We show that if f : A â†’ B is an embedding, then the induced map
   Î£ A (C âˆ˜ f) â†’ Î£ A C is also an embedding. -}

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  where

  is-trunc-map-map-Î£-map-base :
    (k : ğ•‹) {f : A â†’ B} (C : B â†’ UU l3) â†’
    is-trunc-map k f â†’ is-trunc-map k (map-Î£-map-base f C)
  is-trunc-map-map-Î£-map-base k {f} C H y =
    is-trunc-equiv' k
      ( fib f (pr1 y))
      ( equiv-fib-map-Î£-map-base-fib f C y)
      ( H (pr1 y))

  module _
    {f : A â†’ B} (C : B â†’ UU l3)
    where

    abstract
      is-prop-map-map-Î£-map-base :
        is-prop-map f â†’ is-prop-map (map-Î£-map-base f C)
      is-prop-map-map-Î£-map-base = is-trunc-map-map-Î£-map-base neg-one-ğ•‹ C

    abstract
      is-emb-map-Î£-map-base : is-emb f â†’ is-emb (map-Î£-map-base f C)
      is-emb-map-Î£-map-base H =
        is-emb-is-prop-map (is-prop-map-map-Î£-map-base (is-prop-map-is-emb H))

    abstract
      is-0-map-map-Î£-map-base : is-0-map f â†’ is-0-map (map-Î£-map-base f C)
      is-0-map-map-Î£-map-base = is-trunc-map-map-Î£-map-base zero-ğ•‹ C

    abstract
      is-faithful-map-Î£-map-base :
        is-faithful f â†’ is-faithful (map-Î£-map-base f C)
      is-faithful-map-Î£-map-base H =
        is-faithful-is-0-map (is-0-map-map-Î£-map-base (is-0-map-is-faithful H))

  emb-Î£-emb-base :
    (f : A â†ª B) (C : B â†’ UU l3) â†’ Î£ A (Î» a â†’ C (map-emb f a)) â†ª Î£ B C
  pr1 (emb-Î£-emb-base f C) = map-Î£-map-base (map-emb f) C
  pr2 (emb-Î£-emb-base f C) =
    is-emb-map-Î£-map-base C (is-emb-map-emb f)

  faithful-map-Î£-faithful-map-base :
    (f : faithful-map A B) (C : B â†’ UU l3) â†’
    faithful-map (Î£ A (Î» a â†’ C (map-faithful-map f a))) (Î£ B C)
  pr1 (faithful-map-Î£-faithful-map-base f C) =
    map-Î£-map-base (map-faithful-map f) C
  pr2 (faithful-map-Î£-faithful-map-base f C) =
    is-faithful-map-Î£-map-base C (is-faithful-map-faithful-map f)
    
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : A â†’ UU l3}
  where

  is-trunc-map-map-Î£ :
    (k : ğ•‹) (D : B â†’ UU l4) {f : A â†’ B} {g : (x : A) â†’ C x â†’ D (f x)} â†’
    is-trunc-map k f â†’ ((x : A) â†’ is-trunc-map k (g x)) â†’
    is-trunc-map k (map-Î£ D f g)
  is-trunc-map-map-Î£ k D {f} {g} H K = 
    is-trunc-map-comp k
      ( map-Î£ D f g)
      ( map-Î£-map-base f D)
      ( tot g)
      ( triangle-map-Î£ D f g)
      ( is-trunc-map-map-Î£-map-base k D H)
      ( is-trunc-map-tot k K)

  module _
    (D : B â†’ UU l4) {f : A â†’ B} {g : (x : A) â†’ C x â†’ D (f x)}
    where

    is-contr-map-map-Î£ :
      is-contr-map f â†’ ((x : A) â†’ is-contr-map (g x)) â†’
      is-contr-map (map-Î£ D f g)
    is-contr-map-map-Î£ = is-trunc-map-map-Î£ neg-two-ğ•‹ D

    is-prop-map-map-Î£ :
      is-prop-map f â†’ ((x : A) â†’ is-prop-map (g x)) â†’ is-prop-map (map-Î£ D f g)
    is-prop-map-map-Î£ = is-trunc-map-map-Î£ neg-one-ğ•‹ D

    is-emb-map-Î£ :
      is-emb f â†’ ((x : A) â†’ is-emb (g x)) â†’ is-emb (map-Î£ D f g)
    is-emb-map-Î£ H K =
      is-emb-is-prop-map
        ( is-prop-map-map-Î£
          ( is-prop-map-is-emb H)
          ( Î» x â†’ is-prop-map-is-emb (K x)))

    is-0-map-map-Î£ :
      is-0-map f â†’ ((x : A) â†’ is-0-map (g x)) â†’ is-0-map (map-Î£ D f g)
    is-0-map-map-Î£ = is-trunc-map-map-Î£ zero-ğ•‹ D

    is-faithful-map-Î£ :
      is-faithful f â†’ ((x : A) â†’ is-faithful (g x)) â†’ is-faithful (map-Î£ D f g)
    is-faithful-map-Î£ H K =
      is-faithful-is-0-map
        ( is-0-map-map-Î£
          ( is-0-map-is-faithful H)
          ( Î» x â†’ is-0-map-is-faithful (K x)))

  emb-Î£ :
    (D : B â†’ UU l4) (f : A â†ª B) (g : (x : A) â†’ C x â†ª D (map-emb f x)) â†’
    Î£ A C â†ª Î£ B D
  pr1 (emb-Î£ D f g) = map-Î£ D (map-emb f) (Î» x â†’ map-emb (g x))
  pr2 (emb-Î£ D f g) =
    is-emb-map-Î£ D (is-emb-map-emb f) (Î» x â†’ is-emb-map-emb (g x))
