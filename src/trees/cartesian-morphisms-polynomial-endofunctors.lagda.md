# Cartesian morphisms of polynomial endofunctors

```agda
module trees.cartesian-morphisms-polynomial-endofunctors where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.binary-homotopies
open import foundation.commuting-squares-of-homotopies
open import foundation.commuting-squares-of-maps
open import foundation.cones-over-cospan-diagrams
open import foundation.contractible-maps
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equality-dependent-function-types
open import foundation.equivalences
open import foundation.equivalences-arrows
open import foundation.fibers-of-maps
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.functoriality-fibers-of-maps
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.implicit-function-types
open import foundation.morphisms-arrows
open import foundation.precomposition-functions
open import foundation.propositions
open import foundation.pullbacks
open import foundation.raising-universe-levels
open import foundation.sections
open import foundation.structure-identity-principle
open import foundation.subtypes
open import foundation.transport-along-identifications
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.type-theoretic-principle-of-choice
open import foundation.unit-type
open import foundation.universal-property-equivalences
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.equality-dependent-pair-types
open import foundation-core.retractions
open import foundation-core.torsorial-type-families

open import trees.cartesian-natural-transformations-polynomial-endofunctors
open import trees.morphisms-polynomial-endofunctors
open import trees.natural-transformations-polynomial-endofunctors
open import trees.polynomial-endofunctors
```

</details>

## Idea

Given two [polynomial endofunctors](trees.polynomial-endofunctors.md)
$P â‰ (A â—ƒ B)$ and $Q â‰ (C â—ƒ D)$, a
[morphism](trees.morphisms-polynomial-endofunctors.md) $Î± : P â†’ Q$ is
{{#concept "cartesian" Disambiguation="morphism of polynomial endofunctors of types" Agda=is-cartesian-hom-polynomial-endofunctor Agda=cartesian-hom-polynomial-endofunctor}}
if the family of maps

$$Î±â‚ : (a : A) â†’ D (Î±â‚€ a) â†’ B a$$

is a family of [equivalences](foundation-core.equivalences.md).

## Definitions

### The predicate of being cartesian

```agda
module _
  {l1 l2 l3 l4 : Level}
  (P : polynomial-endofunctor l1 l2)
  (Q : polynomial-endofunctor l3 l4)
  (Î± : hom-polynomial-endofunctor P Q)
  where

  is-cartesian-hom-polynomial-endofunctor : UU (l1 âŠ” l2 âŠ” l4)
  is-cartesian-hom-polynomial-endofunctor =
    (a : shape-polynomial-endofunctor P) â†’
    is-equiv (position-hom-polynomial-endofunctor P Q Î± a)

  is-prop-is-cartesian-hom-polynomial-endofunctor :
    is-prop is-cartesian-hom-polynomial-endofunctor
  is-prop-is-cartesian-hom-polynomial-endofunctor =
    is-prop-Î 
      ( Î» a â†’
        is-property-is-equiv (position-hom-polynomial-endofunctor P Q Î± a))

  is-cartesian-hom-polynomial-endofunctor-Prop : Prop (l1 âŠ” l2 âŠ” l4)
  is-cartesian-hom-polynomial-endofunctor-Prop =
    ( is-cartesian-hom-polynomial-endofunctor ,
      is-prop-is-cartesian-hom-polynomial-endofunctor)
```

### The type of cartesian morphisms

```agda
cartesian-hom-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level}
  (P : polynomial-endofunctor l1 l2)
  (Q : polynomial-endofunctor l3 l4) â†’
  UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
cartesian-hom-polynomial-endofunctor P Q =
  Î£ ( hom-polynomial-endofunctor P Q)
    ( is-cartesian-hom-polynomial-endofunctor P Q)

module _
  {l1 l2 l3 l4 : Level}
  (P : polynomial-endofunctor l1 l2)
  (Q : polynomial-endofunctor l3 l4)
  (Î± : cartesian-hom-polynomial-endofunctor P Q)
  where

  hom-cartesian-hom-polynomial-endofunctor : hom-polynomial-endofunctor P Q
  hom-cartesian-hom-polynomial-endofunctor = pr1 Î±

  shape-cartesian-hom-polynomial-endofunctor :
    shape-polynomial-endofunctor P â†’ shape-polynomial-endofunctor Q
  shape-cartesian-hom-polynomial-endofunctor =
    shape-hom-polynomial-endofunctor P Q
      ( hom-cartesian-hom-polynomial-endofunctor)

  position-cartesian-hom-polynomial-endofunctor :
    (a : shape-polynomial-endofunctor P) â†’
    position-polynomial-endofunctor Q
      ( shape-hom-polynomial-endofunctor P Q
        ( hom-cartesian-hom-polynomial-endofunctor)
        ( a)) â†’
    position-polynomial-endofunctor P a
  position-cartesian-hom-polynomial-endofunctor =
    position-hom-polynomial-endofunctor P Q
      hom-cartesian-hom-polynomial-endofunctor

  type-cartesian-hom-polynomial-endofunctor :
    {l5 : Level} {X : UU l5} â†’
    type-polynomial-endofunctor P X â†’ type-polynomial-endofunctor Q X
  type-cartesian-hom-polynomial-endofunctor =
    type-hom-polynomial-endofunctor P Q
      hom-cartesian-hom-polynomial-endofunctor

  is-cartesian-cartesian-hom-polynomial-endofunctor :
    is-cartesian-hom-polynomial-endofunctor P Q
      hom-cartesian-hom-polynomial-endofunctor
  is-cartesian-cartesian-hom-polynomial-endofunctor = pr2 Î±

  equiv-position-cartesian-hom-polynomial-endofunctor :
    (a : shape-polynomial-endofunctor P) â†’
    position-polynomial-endofunctor Q
      ( shape-hom-polynomial-endofunctor P Q
        ( hom-cartesian-hom-polynomial-endofunctor)
        ( a)) â‰ƒ
    position-polynomial-endofunctor P a
  equiv-position-cartesian-hom-polynomial-endofunctor a =
    ( position-cartesian-hom-polynomial-endofunctor a ,
      is-cartesian-cartesian-hom-polynomial-endofunctor a)

  naturality-cartesian-hom-polynomial-endofunctor :
    {l5 l6 : Level} {X : UU l5} {Y : UU l6} (f : X â†’ Y) â†’
    coherence-square-maps
      ( map-polynomial-endofunctor P f)
      ( type-cartesian-hom-polynomial-endofunctor)
      ( type-cartesian-hom-polynomial-endofunctor)
      ( map-polynomial-endofunctor Q f)
  naturality-cartesian-hom-polynomial-endofunctor =
    naturality-hom-polynomial-endofunctor P Q
      hom-cartesian-hom-polynomial-endofunctor

  natural-transformation-cartesian-hom-polynomial-endofunctor :
    {l : Level} â†’ natural-transformation-polynomial-endofunctor l P Q
  natural-transformation-cartesian-hom-polynomial-endofunctor =
    natural-transformation-hom-polynomial-endofunctor P Q
      hom-cartesian-hom-polynomial-endofunctor

  hom-arrow-cartesian-hom-polynomial-endofunctor :
    {l5 l6 : Level} {X : UU l5} {Y : UU l6} (f : X â†’ Y) â†’
    hom-arrow (map-polynomial-endofunctor P f) (map-polynomial-endofunctor Q f)
  hom-arrow-cartesian-hom-polynomial-endofunctor =
    hom-arrow-hom-polynomial-endofunctor P Q
      hom-cartesian-hom-polynomial-endofunctor

  cone-cartesian-hom-polynomial-endofunctor :
    {l5 l6 : Level} {X : UU l5} {Y : UU l6} (f : X â†’ Y) â†’
    cone
      ( type-cartesian-hom-polynomial-endofunctor)
      ( map-polynomial-endofunctor Q f)
      ( type-polynomial-endofunctor P X)
  cone-cartesian-hom-polynomial-endofunctor =
    cone-hom-polynomial-endofunctor P Q hom-cartesian-hom-polynomial-endofunctor
```

### The identity cartesian morphism

```agda
id-cartesian-hom-polynomial-endofunctor :
  {l1 l2 : Level}
  (P : polynomial-endofunctor l1 l2) â†’
  cartesian-hom-polynomial-endofunctor P P
id-cartesian-hom-polynomial-endofunctor P =
  ( id-hom-polynomial-endofunctor P , (Î» a â†’ is-equiv-id))
```

### Composition of cartesian morphisms

```agda
comp-cartesian-hom-polynomial-endofunctor :
  {l1 l2 l3 l4 l5 l6 : Level}
  (P : polynomial-endofunctor l1 l2)
  (Q : polynomial-endofunctor l3 l4)
  (R : polynomial-endofunctor l5 l6) â†’
  cartesian-hom-polynomial-endofunctor Q R â†’
  cartesian-hom-polynomial-endofunctor P Q â†’
  cartesian-hom-polynomial-endofunctor P R
comp-cartesian-hom-polynomial-endofunctor
  P Q R ((Î²â‚€ , Î²â‚) , H) ((Î±â‚€ , Î±â‚) , K) =
  ( ( comp-hom-polynomial-endofunctor P Q R (Î²â‚€ , Î²â‚) (Î±â‚€ , Î±â‚)) ,
    ( Î» a â†’ is-equiv-comp (Î±â‚ a) (Î²â‚ (Î±â‚€ a)) (H (Î±â‚€ a)) (K a)))
```

## Properties

### A computation of the type of cartesian morphisms

```agda
module _
  {l1 l2 l3 l4 : Level}
  (P : polynomial-endofunctor l1 l2)
  (Q : polynomial-endofunctor l3 l4)
  where

  cartesian-hom-polynomial-endofunctor' : UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  cartesian-hom-polynomial-endofunctor' =
    Î£ ( shape-polynomial-endofunctor P â†’ shape-polynomial-endofunctor Q)
      ( Î» Î±â‚€ â†’
        ((a : shape-polynomial-endofunctor P) â†’
          position-polynomial-endofunctor Q (Î±â‚€ a) â‰ƒ
          position-polynomial-endofunctor P a))

  reassociate-type-cartesian-hom-polynomial-endofunctor :
    cartesian-hom-polynomial-endofunctor P Q â‰ƒ
    cartesian-hom-polynomial-endofunctor'
  reassociate-type-cartesian-hom-polynomial-endofunctor =
    equiv-tot (Î» _ â†’ inv-distributive-Î -Î£) âˆ˜e associative-Î£
```

### Truncatedness of the type of cartesian morphisms

If the shapes and positions of the codomain $Q$ are $k$-truncated, for $k â‰¥ -1$,
then the type of cartesian morphisms $P â†’ Q$ is $k$-truncated.

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  (P : polynomial-endofunctor l1 l2)
  (Q : polynomial-endofunctor l3 l4)
  where

  abstract
    is-trunc-succ-cartesian-hom-polynomial-endofunctor' :
      (k : ð•‹) â†’
      is-trunc (succ-ð•‹ k) (shape-polynomial-endofunctor Q) â†’
      ( (c : shape-polynomial-endofunctor Q) â†’
        is-trunc (succ-ð•‹ k) (position-polynomial-endofunctor Q c)) â†’
      is-trunc (succ-ð•‹ k) (cartesian-hom-polynomial-endofunctor' P Q)
    is-trunc-succ-cartesian-hom-polynomial-endofunctor' k hQ hQ' =
      is-trunc-Î£
        ( is-trunc-function-type (succ-ð•‹ k) hQ)
        ( Î» f â†’
          is-trunc-Î 
            ( succ-ð•‹ k)
            ( Î» e â†’ is-trunc-equiv-is-trunc-domain k (hQ' (f e))))

  abstract
    is-trunc-succ-cartesian-hom-polynomial-endofunctor :
      (k : ð•‹) â†’
      is-trunc (succ-ð•‹ k) (shape-polynomial-endofunctor Q) â†’
      ( (c : shape-polynomial-endofunctor Q) â†’
        is-trunc (succ-ð•‹ k) (position-polynomial-endofunctor Q c)) â†’
      is-trunc (succ-ð•‹ k) (cartesian-hom-polynomial-endofunctor P Q)
    is-trunc-succ-cartesian-hom-polynomial-endofunctor k hQ hQ' =
      is-trunc-equiv
        ( succ-ð•‹ k)
        ( cartesian-hom-polynomial-endofunctor' P Q)
        ( reassociate-type-cartesian-hom-polynomial-endofunctor P Q)
        ( is-trunc-succ-cartesian-hom-polynomial-endofunctor' k hQ hQ')
```

### Computing the fibers of the induced natural transformation

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  (P : polynomial-endofunctor l1 l2)
  (Q : polynomial-endofunctor l3 l4)
  (Î± : cartesian-hom-polynomial-endofunctor P Q)
  (let Î±â‚€ = shape-cartesian-hom-polynomial-endofunctor P Q Î±)
  (let Î±â‚ = position-cartesian-hom-polynomial-endofunctor P Q Î±)
  {X : UU l5}
  where

  compute-fiber-type-cartesian-hom-polynomial-endofunctor :
    (c : shape-polynomial-endofunctor Q)
    (x : position-polynomial-endofunctor Q c â†’ X) â†’
    fiber (type-cartesian-hom-polynomial-endofunctor P Q Î± {X = X}) q@(c , x) â‰ƒ
    fiber Î±â‚€ c
  compute-fiber-type-cartesian-hom-polynomial-endofunctor c x =
    equivalence-reasoning
      fiber (type-cartesian-hom-polynomial-endofunctor P Q Î± {X = X}) q
      â‰ƒ Î£ ( fiber Î±â‚€ c)
          ( Î» (a , p) â†’
            fiber
              ( precomp (Î±â‚ a) X)
              ( inv-tr (Î» c â†’ position-polynomial-endofunctor Q c â†’ X) p x))
        by
        compute-fiber-map-Î£
          ( Î» c â†’ position-polynomial-endofunctor Q c â†’ X)
          ( Î±â‚€)
          ( Î» a â†’ precomp (Î±â‚ a) X)
          ( c , x)
      â‰ƒ fiber Î±â‚€ c
        by
        right-unit-law-Î£-is-contr
          ( Î» (a , p) â†’
            is-contr-map-is-equiv
              ( is-equiv-precomp-is-equiv
                ( Î±â‚ a)
                ( is-cartesian-cartesian-hom-polynomial-endofunctor P Q Î± a)
                ( X))
              ( inv-tr (Î» c â†’ position-polynomial-endofunctor Q c â†’ X) p x))
```

### The associated natural transformation of a cartesian morphism is cartesian

```agda
module _
  {l1 l2 l3 l4 : Level}
  (P : polynomial-endofunctor l1 l2)
  (Q : polynomial-endofunctor l3 l4)
  (Î± : cartesian-hom-polynomial-endofunctor P Q)
  where

  abstract
    is-cartesian-natural-transformation-cartesian-hom-polynomial-endofunctor :
      {l : Level} â†’
      is-cartesian-natural-transformation-polynomial-endofunctor P Q
        ( natural-transformation-cartesian-hom-polynomial-endofunctor P Q Î± {l})
    is-cartesian-natural-transformation-cartesian-hom-polynomial-endofunctor f =
      is-pullback-is-fiberwise-equiv-map-fiber-horizontal-map-cone
        ( type-cartesian-hom-polynomial-endofunctor P Q Î±)
        ( map-polynomial-endofunctor Q f)
        ( cone-cartesian-hom-polynomial-endofunctor P Q Î± f)
        ( Î» (a , y) â†’
          is-equiv-source-is-equiv-target-equiv-arrow
            ( map-fiber-horizontal-map-cone
              ( type-cartesian-hom-polynomial-endofunctor P Q Î±)
              ( map-polynomial-endofunctor Q f)
              ( cone-cartesian-hom-polynomial-endofunctor P Q Î± f)
              ( a , y))
            ( id)
            ( ( compute-fiber-type-cartesian-hom-polynomial-endofunctor P Q Î± a
                ( y)) ,
              ( compute-fiber-type-cartesian-hom-polynomial-endofunctor P Q Î± a
                ( f âˆ˜ y)) ,
              ( Î» where (u , refl) â†’ refl))
            ( is-equiv-id))
```

### Equivalence between cartesian morphisms and cartesian natural transformations

This is proved in a slightly different setting as part of Lemma 2.3.3 in
{{#cite GHK22}}.

> This remains to be formalized.

## References

{{#bibliography}}
