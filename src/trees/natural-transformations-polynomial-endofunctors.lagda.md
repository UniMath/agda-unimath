# Natural transformations between polynomial endofunctors

```agda
module trees.natural-transformations-polynomial-endofunctors where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-binary-functions
open import foundation.action-on-identifications-functions
open import foundation.binary-homotopies
open import foundation.commuting-squares-of-homotopies
open import foundation.commuting-squares-of-maps
open import foundation.cones-over-cospan-diagrams
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equality-dependent-function-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopies-morphisms-arrows
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.implicit-function-types
open import foundation.morphisms-arrows
open import foundation.precomposition-functions
open import foundation.pullbacks
open import foundation.sections
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.equality-dependent-pair-types
open import foundation-core.retractions
open import foundation-core.torsorial-type-families

open import trees.polynomial-endofunctors
```

</details>

## Idea

Given two [polynomial endofunctors](trees.polynomial-endofunctors.md)
$ğ‘ƒ â‰ (A â—ƒ B)$ and $ğ‘„ â‰ (C â—ƒ D)$, a
{{#concept "natural transformation" Disambiguation="of polynomial endofunctors of types" Agda=natural-transformation-polynomial-endofunctor}}
$Î±$ from $ğ‘ƒ$ to $ğ‘„$ is a family of maps $Î± : (X : Type) â†’ ğ‘ƒ(X) â†’ ğ‘„(X)$ such that
for every map of types $f : X â†’ Y$, the following square commutes

```text
              Î±(X)
     ğ‘ƒ(X) -----------> ğ‘„(X)
       |                |
       |                |
  ğ‘ƒ(f) |                | ğ‘„(f)
       |                |
       âˆ¨                âˆ¨
     ğ‘ƒ(Y) -----------> ğ‘„(Y).
              Î±(Y)
```

## Definitions

### Natural transformations between polynomial endofunctors

```agda
coherence-natural-transformation-polynomial-endofunctor :
  {l1 l2 l3 l4 l : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4) â†’
  ( {X : UU l} â†’
    type-polynomial-endofunctor ğ‘ƒ X â†’
    type-polynomial-endofunctor ğ‘„ X) â†’
  UU (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” lsuc l)
coherence-natural-transformation-polynomial-endofunctor {l = l} ğ‘ƒ ğ‘„ Î±â‚€ =
  {X Y : UU l} (f : X â†’ Y) â†’
  coherence-square-maps
    ( Î±â‚€)
    ( map-polynomial-endofunctor ğ‘ƒ f)
    ( map-polynomial-endofunctor ğ‘„ f)
  ( Î±â‚€)

natural-transformation-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (l : Level) â†’
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4) â†’
    UU (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” lsuc l)
natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„ =
  Î£ ( {X : UU l} â†’
      type-polynomial-endofunctor ğ‘ƒ X â†’
      type-polynomial-endofunctor ğ‘„ X)
    ( coherence-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„)

module _
  {l1 l2 l3 l4 l5 : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î± : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„)
  where

  type-natural-transformation-polynomial-endofunctor :
    {X : UU l5} â†’
    type-polynomial-endofunctor ğ‘ƒ X â†’
    type-polynomial-endofunctor ğ‘„ X
  type-natural-transformation-polynomial-endofunctor = pr1 Î±

  naturality-natural-transformation-polynomial-endofunctor :
    coherence-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( type-natural-transformation-polynomial-endofunctor)
  naturality-natural-transformation-polynomial-endofunctor = pr2 Î±
```

### The associated family of morphisms of arrows

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î±@(Î±â‚€ , Î±â‚) : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„)
  {X Y : UU l5} (f : X â†’ Y)
  where

  hom-arrow-natural-transformation-polynomial-endofunctor :
    hom-arrow (map-polynomial-endofunctor ğ‘ƒ f) (map-polynomial-endofunctor ğ‘„ f)
  hom-arrow-natural-transformation-polynomial-endofunctor =
    ( Î±â‚€ , Î±â‚€ , Î±â‚ f)

  cone-natural-transformation-polynomial-endofunctor :
    cone Î±â‚€ (map-polynomial-endofunctor ğ‘„ f) (type-polynomial-endofunctor ğ‘ƒ X)
  cone-natural-transformation-polynomial-endofunctor =
    cone-hom-arrow
      ( map-polynomial-endofunctor ğ‘ƒ f)
      ( map-polynomial-endofunctor ğ‘„ f)
      ( hom-arrow-natural-transformation-polynomial-endofunctor)
```

### The identity natural transformation

```agda
id-natural-transformation-polynomial-endofunctor :
  {l1 l2 l3 : Level} (ğ‘ƒ : polynomial-endofunctor l1 l2) â†’
  natural-transformation-polynomial-endofunctor l3 ğ‘ƒ ğ‘ƒ
pr1 (id-natural-transformation-polynomial-endofunctor ğ‘ƒ) = id
pr2 (id-natural-transformation-polynomial-endofunctor ğ‘ƒ) f x = refl
```

## Properties

### Characterizing equality of natural transformations

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  where

  htpy-natural-transformation-polynomial-endofunctor :
    (Î± Î² : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„) â†’
    UU (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” lsuc l5)
  htpy-natural-transformation-polynomial-endofunctor Î± Î² =
    Î£ ( (X : UU l5) â†’
        type-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î± {X} ~
        type-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î² {X})
      ( Î» H â†’
        (X Y : UU l5) (f : X â†’ Y) â†’
        coherence-square-homotopies
          ( naturality-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î± f)
          ( H Y Â·r map-polynomial-endofunctor ğ‘ƒ f)
          ( map-polynomial-endofunctor ğ‘„ f Â·l H X)
          ( naturality-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î² f))

  refl-htpy-natural-transformation-polynomial-endofunctor :
    (Î± : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„) â†’
    htpy-natural-transformation-polynomial-endofunctor Î± Î±
  refl-htpy-natural-transformation-polynomial-endofunctor Î± =
    ( (Î» X x â†’ refl) , (Î» X Y f x â†’ inv right-unit))

  htpy-eq-natural-transformation-polynomial-endofunctor :
    (Î± Î² : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„) â†’
    (Î± ï¼ Î²) â†’ htpy-natural-transformation-polynomial-endofunctor Î± Î²
  htpy-eq-natural-transformation-polynomial-endofunctor Î± .Î± refl =
    refl-htpy-natural-transformation-polynomial-endofunctor Î±

  is-torsorial-htpy-natural-transformation-polynomial-endofunctor :
    (Î± : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„) â†’
    is-torsorial (htpy-natural-transformation-polynomial-endofunctor Î±)
  is-torsorial-htpy-natural-transformation-polynomial-endofunctor Î± =
    is-torsorial-Eq-structure
      ( is-torsorial-Eq-implicit-Î '
        ( Î» X â†’
          is-torsorial-htpy
            ( type-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±)))
      ( type-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î± ,
        ( Î» _ _ â†’ refl))
      ( is-torsorial-Eq-implicit-Î '
        ( Î» X â†’
          is-torsorial-Eq-implicit-Î '
            ( Î» Y â†’
              is-torsorial-Eq-Î 
                ( Î» f â†’
                  is-torsorial-htpy'
                    ( ( naturality-natural-transformation-polynomial-endofunctor
                          ğ‘ƒ ğ‘„ Î± f) âˆ™h
                      ( refl-htpy))))))

  is-equiv-htpy-eq-natural-transformation-polynomial-endofunctor :
    (Î± Î² : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„) â†’
    is-equiv (htpy-eq-natural-transformation-polynomial-endofunctor Î± Î²)
  is-equiv-htpy-eq-natural-transformation-polynomial-endofunctor Î± =
    fundamental-theorem-id
      ( is-torsorial-htpy-natural-transformation-polynomial-endofunctor Î±)
      ( htpy-eq-natural-transformation-polynomial-endofunctor Î±)

  equiv-htpy-eq-natural-transformation-polynomial-endofunctor :
    (Î± Î² : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„) â†’
    (Î± ï¼ Î²) â‰ƒ htpy-natural-transformation-polynomial-endofunctor Î± Î²
  equiv-htpy-eq-natural-transformation-polynomial-endofunctor Î± Î² =
    ( htpy-eq-natural-transformation-polynomial-endofunctor Î± Î² ,
      is-equiv-htpy-eq-natural-transformation-polynomial-endofunctor Î± Î²)

  eq-htpy-natural-transformation-polynomial-endofunctor :
    (Î± Î² : natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„) â†’
    htpy-natural-transformation-polynomial-endofunctor Î± Î² â†’
    Î± ï¼ Î²
  eq-htpy-natural-transformation-polynomial-endofunctor Î± Î² =
    map-inv-equiv
      ( equiv-htpy-eq-natural-transformation-polynomial-endofunctor Î± Î²)
```

### Composition of natural transformations

```agda
comp-natural-transformation-polynomial-endofunctor :
  {l1 l2 l3 l4 l5 l6 l : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (ğ‘… : polynomial-endofunctor l5 l6) â†’
  natural-transformation-polynomial-endofunctor l ğ‘„ ğ‘… â†’
  natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„ â†’
  natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘…
comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ ğ‘… (Î²â‚€ , Î²â‚) (Î±â‚€ , Î±â‚) =
  ( ( Î²â‚€ âˆ˜ Î±â‚€) ,
    ( Î» f â†’
      pasting-horizontal-coherence-square-maps
        ( Î±â‚€)
        ( Î²â‚€)
        ( map-polynomial-endofunctor ğ‘ƒ f)
        ( map-polynomial-endofunctor ğ‘„ f)
        ( map-polynomial-endofunctor ğ‘… f)
        ( Î±â‚€)
        ( Î²â‚€)
        ( Î±â‚ f)
        ( Î²â‚ f)))
```

### Unit laws for composition of natural transformations

```agda
module _
  {l1 l2 l3 l4 l : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î±@(Î±â‚€ , Î±â‚) : natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„)
  where

  htpy-left-unit-law-comp-natural-transformation-polynomial-endofunctor :
    htpy-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ ğ‘„
        ( id-natural-transformation-polynomial-endofunctor ğ‘„)
        ( Î±))
      ( Î±)
  htpy-left-unit-law-comp-natural-transformation-polynomial-endofunctor =
      ( ( Î» X x â†’ refl) ,
        ( Î» X Y f x â†’ inv (right-unit âˆ™ right-unit âˆ™ ap-id (Î±â‚ f x))))

  left-unit-law-comp-natural-transformation-polynomial-endofunctor :
    ( comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ ğ‘„
      ( id-natural-transformation-polynomial-endofunctor ğ‘„)
      ( Î±)) ï¼
    ( Î±)
  left-unit-law-comp-natural-transformation-polynomial-endofunctor =
    eq-htpy-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ ğ‘„
        ( id-natural-transformation-polynomial-endofunctor ğ‘„)
        ( Î±))
      ( Î±)
      ( htpy-left-unit-law-comp-natural-transformation-polynomial-endofunctor)

  htpy-right-unit-law-comp-natural-transformation-polynomial-endofunctor :
    htpy-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘ƒ ğ‘„
        ( Î±)
        ( id-natural-transformation-polynomial-endofunctor ğ‘ƒ))
      ( Î±)
  htpy-right-unit-law-comp-natural-transformation-polynomial-endofunctor =
      ( ( Î» X x â†’ refl) , (Î» X Y f x â†’ inv right-unit))

  right-unit-law-comp-natural-transformation-polynomial-endofunctor :
    ( comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘ƒ ğ‘„
      ( Î±)
      ( id-natural-transformation-polynomial-endofunctor ğ‘ƒ)) ï¼
    ( Î±)
  right-unit-law-comp-natural-transformation-polynomial-endofunctor =
    eq-htpy-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘ƒ ğ‘„
        ( Î±)
        ( id-natural-transformation-polynomial-endofunctor ğ‘ƒ))
      ( Î±)
      ( htpy-right-unit-law-comp-natural-transformation-polynomial-endofunctor)
```

## See also

- [Morphisms of polynomial endofunctors](trees.morphisms-polynomial-endofunctors.md)
