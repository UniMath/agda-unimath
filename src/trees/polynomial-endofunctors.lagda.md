# Polynomial endofunctors

```agda
module trees.polynomial-endofunctors where
```

<details><summary>Imports</summary>

```agda
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.fibers-of-maps
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.postcomposition-functions
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.commuting-triangles-of-maps
open import foundation-core.retractions
open import foundation-core.torsorial-type-families
```

</details>

## Idea

Given a type `A` [equipped](foundation.structure.md) with a type family `B` over
`A`, the
{{#concept "polynomial endofunctor" WD="polynomial functor" WDID=Q48842893 Agda=polynomial-endofunctor}}
`ğ‘ƒ A B` is defined by

```text
  X â†¦ Î£ (x : A), (B x â†’ X).
```

Polynomial endofunctors are important in the study of
[W-types](trees.w-types.md), and also in the study of
[combinatorial species](species.md).

## Definitions

### The type of polynomial endofunctors

```agda
polynomial-endofunctor : (l1 l2 : Level) â†’ UU (lsuc l1 âŠ” lsuc l2)
polynomial-endofunctor l1 l2 = Î£ (UU l1) (Î» A â†’ (A â†’ UU l2))

module _
  {l1 l2 : Level} (ğ‘ƒ : polynomial-endofunctor l1 l2)
  where

  shape-polynomial-endofunctor : UU l1
  shape-polynomial-endofunctor = pr1 ğ‘ƒ

  position-polynomial-endofunctor : shape-polynomial-endofunctor â†’ UU l2
  position-polynomial-endofunctor = pr2 ğ‘ƒ

make-polynomial-endofunctor :
  {l1 l2 : Level} {A : UU l1} â†’ (A â†’ UU l2) â†’ polynomial-endofunctor l1 l2
make-polynomial-endofunctor B = (_ , B)
```

### The action on types of a polynomial endofunctor

```agda
type-polynomial-endofunctor' :
  {l1 l2 l3 : Level} (A : UU l1) (B : A â†’ UU l2) (X : UU l3) â†’
  UU (l1 âŠ” l2 âŠ” l3)
type-polynomial-endofunctor' A B X = Î£ A (Î» x â†’ B x â†’ X)

type-polynomial-endofunctor :
  {l1 l2 l3 : Level} â†’ polynomial-endofunctor l1 l2 â†’ UU l3 â†’ UU (l1 âŠ” l2 âŠ” l3)
type-polynomial-endofunctor (A , B) = type-polynomial-endofunctor' A B
```

### The action on maps of the polynomial endofunctor

```agda
map-polynomial-endofunctor' :
  {l1 l2 l3 l4 : Level} (A : UU l1) (B : A â†’ UU l2) {X : UU l3} {Y : UU l4}
  (f : X â†’ Y) â†’
  type-polynomial-endofunctor' A B X â†’ type-polynomial-endofunctor' A B Y
map-polynomial-endofunctor' A B f = tot (Î» x Î± â†’ f âˆ˜ Î±)

map-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (ğ‘ƒ : polynomial-endofunctor l1 l2)
  {X : UU l3} {Y : UU l4} (f : X â†’ Y) â†’
  type-polynomial-endofunctor ğ‘ƒ X â†’ type-polynomial-endofunctor ğ‘ƒ Y
map-polynomial-endofunctor (A , B) = map-polynomial-endofunctor' A B
```

## Properties

### Characterizing identity in the image of polynomial endofunctors

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {X : UU l3}
  where

  Eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor' A B X) â†’ UU (l1 âŠ” l2 âŠ” l3)
  Eq-type-polynomial-endofunctor x y =
    Î£ (pr1 x ï¼ pr1 y) (Î» p â†’ coherence-triangle-maps (pr2 x) (pr2 y) (tr B p))

  refl-Eq-type-polynomial-endofunctor :
    (x : type-polynomial-endofunctor' A B X) â†’
    Eq-type-polynomial-endofunctor x x
  refl-Eq-type-polynomial-endofunctor (x , Î±) = (refl , refl-htpy)

  Eq-eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor' A B X) â†’
    x ï¼ y â†’ Eq-type-polynomial-endofunctor x y
  Eq-eq-type-polynomial-endofunctor x .x refl =
    refl-Eq-type-polynomial-endofunctor x

  is-torsorial-Eq-type-polynomial-endofunctor :
    (x : type-polynomial-endofunctor' A B X) â†’
    is-torsorial (Eq-type-polynomial-endofunctor x)
  is-torsorial-Eq-type-polynomial-endofunctor (x , Î±) =
    is-torsorial-Eq-structure
      ( is-torsorial-Id x)
      ( x , refl)
      ( is-torsorial-htpy Î±)

  is-equiv-Eq-eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor' A B X) â†’
    is-equiv (Eq-eq-type-polynomial-endofunctor x y)
  is-equiv-Eq-eq-type-polynomial-endofunctor x =
    fundamental-theorem-id
      ( is-torsorial-Eq-type-polynomial-endofunctor x)
      ( Eq-eq-type-polynomial-endofunctor x)

  eq-Eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor' A B X) â†’
    Eq-type-polynomial-endofunctor x y â†’ x ï¼ y
  eq-Eq-type-polynomial-endofunctor x y =
    map-inv-is-equiv (is-equiv-Eq-eq-type-polynomial-endofunctor x y)

  is-retraction-eq-Eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor' A B X) â†’
    is-retraction
      ( Eq-eq-type-polynomial-endofunctor x y)
      ( eq-Eq-type-polynomial-endofunctor x y)
  is-retraction-eq-Eq-type-polynomial-endofunctor x y =
    is-retraction-map-inv-is-equiv
      ( is-equiv-Eq-eq-type-polynomial-endofunctor x y)

  coh-refl-eq-Eq-type-polynomial-endofunctor :
    (x : type-polynomial-endofunctor' A B X) â†’
    ( eq-Eq-type-polynomial-endofunctor x x
      ( refl-Eq-type-polynomial-endofunctor x)) ï¼ refl
  coh-refl-eq-Eq-type-polynomial-endofunctor x =
    is-retraction-eq-Eq-type-polynomial-endofunctor x x refl
```

### The action on homotopies of the polynomial endofunctor

```agda
htpy-polynomial-endofunctor' :
  {l1 l2 l3 l4 : Level} (A : UU l1) (B : A â†’ UU l2) {X : UU l3} {Y : UU l4}
  {f g : X â†’ Y} â†’
  f ~ g â†’ map-polynomial-endofunctor' A B f ~ map-polynomial-endofunctor' A B g
htpy-polynomial-endofunctor' A B {f = f} {g} H (x , Î±) =
  eq-Eq-type-polynomial-endofunctor
    ( map-polynomial-endofunctor' A B f (x , Î±))
    ( map-polynomial-endofunctor' A B g (x , Î±))
    ( refl , H Â·r Î±)

htpy-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (ğ‘ƒ : polynomial-endofunctor l1 l2)
  {X : UU l3} {Y : UU l4} {f g : X â†’ Y} â†’
  f ~ g â†’ map-polynomial-endofunctor ğ‘ƒ f ~ map-polynomial-endofunctor ğ‘ƒ g
htpy-polynomial-endofunctor (A , B) = htpy-polynomial-endofunctor' A B

coh-refl-htpy-polynomial-endofunctor' :
  {l1 l2 l3 l4 : Level} (A : UU l1) (B : A â†’ UU l2)
  {X : UU l3} {Y : UU l4} (f : X â†’ Y) â†’
  htpy-polynomial-endofunctor' A B (refl-htpy' f) ~ refl-htpy
coh-refl-htpy-polynomial-endofunctor' A B f (x , Î±) =
  coh-refl-eq-Eq-type-polynomial-endofunctor
    ( map-polynomial-endofunctor' A B f (x , Î±))

coh-refl-htpy-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (ğ‘ƒ : polynomial-endofunctor l1 l2)
  {X : UU l3} {Y : UU l4} (f : X â†’ Y) â†’
  htpy-polynomial-endofunctor ğ‘ƒ (refl-htpy' f) ~ refl-htpy
coh-refl-htpy-polynomial-endofunctor (A , B) =
  coh-refl-htpy-polynomial-endofunctor' A B
```

### Computing the fibers of the action on maps

```agda
module _
  {l1 l2 l3 l4 : Level} (ğ‘ƒ : polynomial-endofunctor l1 l2)
  {X : UU l3} {Y : UU l4} (f : X â†’ Y)
  where

  compute-fiber-map-polynomial-endofunctor :
    (a : shape-polynomial-endofunctor ğ‘ƒ)
    (y : position-polynomial-endofunctor ğ‘ƒ a â†’ Y) â†’
    fiber (map-polynomial-endofunctor ğ‘ƒ f) (a , y) â‰ƒ
    ( (b : position-polynomial-endofunctor ğ‘ƒ a) â†’ fiber f (y b))
  compute-fiber-map-polynomial-endofunctor a y =
    equivalence-reasoning
    fiber (map-polynomial-endofunctor ğ‘ƒ f) (a , y)
    â‰ƒ fiber (postcomp (position-polynomial-endofunctor ğ‘ƒ a) f) y
      by
        compute-fiber-tot
          ( Î» a â†’ postcomp (position-polynomial-endofunctor ğ‘ƒ a) f)
          ( a , y)
    â‰ƒ ((b : position-polynomial-endofunctor ğ‘ƒ a) â†’ fiber f (y b))
      by inv-compute-Î -fiber-postcomp (position-polynomial-endofunctor ğ‘ƒ a) f y
```

## See also

- [Multivariable polynomial functors](trees.multivariable-polynomial-functors.md)
  for the generalization of polynomial endofunctors to type families.
- [Cauchy series of species of types](species.cauchy-series-species-of-types.md)
  are polynomial endofunctors of the form
  ```text
    X â†¦ Î£ (U : Type), S(U) Ã— (U â†’ X)
  ```
  In other words, given a [species of types](species.species-of-types.md) `S`,
  the shapes are types equipped with `S`-structure, and the positions are
  points.
- Via [type duality](foundation.type-duality.md), polynomial endofunctors are
  classified by maps of types.
