# Polynomial endofunctors

```agda
module trees.polynomial-endofunctors where
```

<details><summary>Imports</summary>

```agda
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.commuting-triangles-of-maps
open import foundation-core.retractions
open import foundation-core.torsorial-type-families
```

</details>

## Idea

Given a type `A` equipped with a type family `B` over `A`, the
{{#concept "polynomial endofunctor"}} `ùëÉ A B` is defined by

```text
  X ‚Ü¶ Œ£ (x : A), (B x ‚Üí X)
```

Polynomial endofunctors are important in the study of
[W-types](trees.w-types.md), and also in the study of
[combinatorial species](species.md).

## Definitions

### The action on types of a polynomial endofunctor

```agda
type-polynomial-endofunctor :
  {l1 l2 l3 : Level} (A : UU l1) (B : A ‚Üí UU l2) (X : UU l3) ‚Üí
  UU (l1 ‚äî l2 ‚äî l3)
type-polynomial-endofunctor A B X = Œ£ A (Œª x ‚Üí B x ‚Üí X)
```

### The identity type of `type-polynomial-endofunctor`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A ‚Üí UU l2} {X : UU l3}
  where

  Eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor A B X) ‚Üí UU (l1 ‚äî l2 ‚äî l3)
  Eq-type-polynomial-endofunctor x y =
    Œ£ (pr1 x Ôºù pr1 y) (Œª p ‚Üí coherence-triangle-maps (pr2 x) (pr2 y) (tr B p))

  refl-Eq-type-polynomial-endofunctor :
    (x : type-polynomial-endofunctor A B X) ‚Üí
    Eq-type-polynomial-endofunctor x x
  refl-Eq-type-polynomial-endofunctor (x , Œ±) = (refl , refl-htpy)

  Eq-eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor A B X) ‚Üí
    x Ôºù y ‚Üí Eq-type-polynomial-endofunctor x y
  Eq-eq-type-polynomial-endofunctor x .x refl =
    refl-Eq-type-polynomial-endofunctor x

  is-torsorial-Eq-type-polynomial-endofunctor :
    (x : type-polynomial-endofunctor A B X) ‚Üí
    is-torsorial (Eq-type-polynomial-endofunctor x)
  is-torsorial-Eq-type-polynomial-endofunctor (x , Œ±) =
    is-torsorial-Eq-structure
      ( is-torsorial-Id x)
      ( x , refl)
      ( is-torsorial-htpy Œ±)

  is-equiv-Eq-eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor A B X) ‚Üí
    is-equiv (Eq-eq-type-polynomial-endofunctor x y)
  is-equiv-Eq-eq-type-polynomial-endofunctor x =
    fundamental-theorem-id
      ( is-torsorial-Eq-type-polynomial-endofunctor x)
      ( Eq-eq-type-polynomial-endofunctor x)

  eq-Eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor A B X) ‚Üí
    Eq-type-polynomial-endofunctor x y ‚Üí x Ôºù y
  eq-Eq-type-polynomial-endofunctor x y =
    map-inv-is-equiv (is-equiv-Eq-eq-type-polynomial-endofunctor x y)

  is-retraction-eq-Eq-type-polynomial-endofunctor :
    (x y : type-polynomial-endofunctor A B X) ‚Üí
    is-retraction
      ( Eq-eq-type-polynomial-endofunctor x y)
      ( eq-Eq-type-polynomial-endofunctor x y)
  is-retraction-eq-Eq-type-polynomial-endofunctor x y =
    is-retraction-map-inv-is-equiv
      ( is-equiv-Eq-eq-type-polynomial-endofunctor x y)

  coh-refl-eq-Eq-type-polynomial-endofunctor :
    (x : type-polynomial-endofunctor A B X) ‚Üí
    ( eq-Eq-type-polynomial-endofunctor x x
      ( refl-Eq-type-polynomial-endofunctor x)) Ôºù refl
  coh-refl-eq-Eq-type-polynomial-endofunctor x =
    is-retraction-eq-Eq-type-polynomial-endofunctor x x refl
```

### The action on morphisms of the polynomial endofunctor

```agda
map-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (A : UU l1) (B : A ‚Üí UU l2) {X : UU l3} {Y : UU l4}
  (f : X ‚Üí Y) ‚Üí
  type-polynomial-endofunctor A B X ‚Üí type-polynomial-endofunctor A B Y
map-polynomial-endofunctor A B f = tot (Œª x Œ± ‚Üí f ‚àò Œ±)
```

### The action on homotopies of the polynomial endofunctor

```agda
htpy-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (A : UU l1) (B : A ‚Üí UU l2) {X : UU l3} {Y : UU l4}
  {f g : X ‚Üí Y} ‚Üí
  f ~ g ‚Üí map-polynomial-endofunctor A B f ~ map-polynomial-endofunctor A B g
htpy-polynomial-endofunctor A B {f = f} {g} H (x , Œ±) =
  eq-Eq-type-polynomial-endofunctor
    ( map-polynomial-endofunctor A B f (x , Œ±))
    ( map-polynomial-endofunctor A B g (x , Œ±))
    ( refl , H ¬∑r Œ±)

coh-refl-htpy-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (A : UU l1) (B : A ‚Üí UU l2) {X : UU l3} {Y : UU l4}
  (f : X ‚Üí Y) ‚Üí
  htpy-polynomial-endofunctor A B (refl-htpy {f = f}) ~ refl-htpy
coh-refl-htpy-polynomial-endofunctor A B f (x , Œ±) =
  coh-refl-eq-Eq-type-polynomial-endofunctor
    ( map-polynomial-endofunctor A B f (x , Œ±))
```

## See also

- [Multivariable polynomial functors](trees.multivariable-polynomial-functors.md)
  for the generalization of polynomial endofunctors to type families.
