# Multivariable polynomial functors

```agda
module trees.multivariable-polynomial-functors where
```

<details><summary>Imports</summary>

```agda
open import foundation.binary-homotopies
open import foundation.dependent-pair-types
open import foundation.equality-dependent-function-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.type-arithmetic-unit-type
open import foundation.unit-type
open import foundation.universal-property-identity-types
open import foundation.universe-levels

open import foundation-core.commuting-squares-of-maps
open import foundation-core.commuting-triangles-of-maps
open import foundation-core.retractions
open import foundation-core.torsorial-type-families
```

</details>

## Idea

{{#concept "Multivariable polynomial functors" Agda=polynomial-functor}} are a
generalization of the notion of
[polynomial endofunctors](trees.polynomial-endofunctors.md) to the case of
families of types (variables). Given a family of types `A : J â†’ Type` and a type
family `B : I â†’ {j : J} â†’ A j â†’ Type` over `A`, we have a multivariable
polynomial functor `ğ‘ƒ A B` with action on type families given by

```text
  ğ‘ƒ A B X j := Î£ (a : A j), ((i : I) â†’ B i a â†’ X i).
```

## Definitions

### The type of multivariable polynomial functors

```agda
polynomial-functor :
  {l1 l2 : Level} (l3 l4 : Level) â†’
  UU l1 â†’ UU l2 â†’ UU (l1 âŠ” l2 âŠ” lsuc l3 âŠ” lsuc l4)
polynomial-functor l3 l4 I J =
  Î£ (J â†’ UU l3) (Î» A â†’ (I â†’ {j : J} â†’ A j â†’ UU l4))

module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {J : UU l2}
  (ğ‘ƒ : polynomial-functor l3 l4 I J)
  where

  shape-polynomial-functor : J â†’ UU l3
  shape-polynomial-functor = pr1 ğ‘ƒ

  position-polynomial-functor :
    I â†’ {j : J} â†’ shape-polynomial-functor j â†’ UU l4
  position-polynomial-functor = pr2 ğ‘ƒ
```

### The action on type families of a multivariable polynomial functor

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {I : UU l1} {J : UU l2}
  where

  type-polynomial-functor' :
    (A : J â†’ UU l3) (B : I â†’ {j : J} â†’ A j â†’ UU l4) â†’
    (I â†’ UU l5) â†’ (J â†’ UU (l1 âŠ” l3 âŠ” l4 âŠ” l5))
  type-polynomial-functor' A B X j =
    Î£ (A j) (Î» a â†’ (i : I) â†’ B i a â†’ X i)

  type-polynomial-functor :
    (ğ‘ƒ : polynomial-functor l3 l4 I J) â†’
    (I â†’ UU l5) â†’ (J â†’ UU (l1 âŠ” l3 âŠ” l4 âŠ” l5))
  type-polynomial-functor (A , B) =
    type-polynomial-functor' A B
```

### Characterizing equality in `type-polynomial-functor`

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  {I : UU l1} {J : UU l2}
  {ğ‘ƒ@(A , B) : polynomial-functor l3 l4 I J}
  {X : I â†’ UU l5}
  where

  Eq-type-polynomial-functor :
    (x y : (j : J) â†’ type-polynomial-functor ğ‘ƒ X j) â†’
    UU (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” l5)
  Eq-type-polynomial-functor x y =
    (j : J) â†’
    Î£ ( pr1 (x j) ï¼ pr1 (y j))
      ( Î» p â†’
        (i : I) â†’
        coherence-triangle-maps (pr2 (x j) i) (pr2 (y j) i) (tr (B i {j}) p))

  refl-Eq-type-polynomial-functor :
    (x : (j : J) â†’ type-polynomial-functor ğ‘ƒ X j) â†’
    Eq-type-polynomial-functor x x
  refl-Eq-type-polynomial-functor x j = (refl , (Î» i â†’ refl-htpy))

  Eq-eq-type-polynomial-functor :
    (x y : (j : J) â†’ type-polynomial-functor ğ‘ƒ X j) â†’
    x ï¼ y â†’ Eq-type-polynomial-functor x y
  Eq-eq-type-polynomial-functor x .x refl =
    refl-Eq-type-polynomial-functor x

  is-torsorial-Eq-type-polynomial-functor :
    (x : (j : J) â†’ type-polynomial-functor ğ‘ƒ X j) â†’
    is-torsorial (Eq-type-polynomial-functor x)
  is-torsorial-Eq-type-polynomial-functor x =
    is-torsorial-Eq-Î 
      ( Î» j â†’
        is-torsorial-Eq-structure
          { D =
            Î» a y p â†’
            (i : I) â†’
            coherence-triangle-maps (pr2 (x j) i) (y i) (tr (B i {j}) p)}
          ( is-torsorial-Id (pr1 (x j)))
          ( pr1 (x j) , refl)
          (is-torsorial-binary-htpy (pr2 (x j))))

  is-equiv-Eq-eq-type-polynomial-functor :
    (x y : (j : J) â†’ type-polynomial-functor ğ‘ƒ X j) â†’
    is-equiv (Eq-eq-type-polynomial-functor x y)
  is-equiv-Eq-eq-type-polynomial-functor x =
    fundamental-theorem-id
      ( is-torsorial-Eq-type-polynomial-functor x)
      ( Eq-eq-type-polynomial-functor x)

  eq-Eq-type-polynomial-functor :
    (x y : (j : J) â†’ type-polynomial-functor ğ‘ƒ X j) â†’
    Eq-type-polynomial-functor x y â†’ x ï¼ y
  eq-Eq-type-polynomial-functor x y =
    map-inv-is-equiv (is-equiv-Eq-eq-type-polynomial-functor x y)

  is-retraction-eq-Eq-type-polynomial-functor :
    (x y : (j : J) â†’ type-polynomial-functor ğ‘ƒ X j) â†’
    is-retraction
      ( Eq-eq-type-polynomial-functor x y)
      ( eq-Eq-type-polynomial-functor x y)
  is-retraction-eq-Eq-type-polynomial-functor x y =
    is-retraction-map-inv-is-equiv
      ( is-equiv-Eq-eq-type-polynomial-functor x y)

  coh-refl-eq-Eq-type-polynomial-functor :
    (x : (j : J) â†’ type-polynomial-functor ğ‘ƒ X j) â†’
    ( eq-Eq-type-polynomial-functor x x
      ( refl-Eq-type-polynomial-functor x)) ï¼ refl
  coh-refl-eq-Eq-type-polynomial-functor x =
    is-retraction-eq-Eq-type-polynomial-functor x x refl
```

### An action on dependent functions of multivariable polynomial functors

The following construction is not quite right for "the" action on dependent
functions, since given a type family `Y` over a type family `X`, the
construction gives only a dependent function of approximately type

```text
  (x : ğ‘ƒ X) â†’ ğ‘ƒ (Î£ B Y x)
```

rather than

```text
  (x : ğ‘ƒ X) â†’ ğ‘ƒ (Y x).
```

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level} {I : UU l1} {J : UU l2}
  where

  dmap-Î£-polynomial-functor' :
    (A : J â†’ UU l3) (B : I â†’ {j : J} â†’ A j â†’ UU l4)
    {X : I â†’ UU l5} {Y : (i : I) â†’ X i â†’ UU l6}
    (f : (i : I) (x : X i) â†’ Y i x) â†’
    (x : (j : J) â†’ type-polynomial-functor' A B X j) â†’
    (j : J) â†’
    type-polynomial-functor' A B
      ( Î» i â†’ Î£ (B i (pr1 (x j))) (Y i âˆ˜ pr2 (x j) i))
      ( j)
  dmap-Î£-polynomial-functor' A B f x j =
    ( pr1 (x j) , (Î» i b â†’ (b , f i (pr2 (x j) i b))))
```

### The action on functions of multivariable polynomial functors

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level} {I : UU l1} {J : UU l2}
  where

  map-polynomial-functor' :
    (A : J â†’ UU l3) (B : I â†’ {j : J} â†’ A j â†’ UU l4)
    {X : I â†’ UU l5} {Y : I â†’ UU l6}
    (f : (i : I) â†’ X i â†’ Y i) â†’
    (j : J) â†’
    type-polynomial-functor' A B X j â†’ type-polynomial-functor' A B Y j
  map-polynomial-functor' A B f j (a , x) = (a , (Î» i b â†’ f i (x i b)))

  map-polynomial-functor :
    (ğ‘ƒ : polynomial-functor l3 l4 I J)
    {X : I â†’ UU l5} {Y : I â†’ UU l6}
    (f : (i : I) â†’ X i â†’ Y i) â†’
    (j : J) â†’ type-polynomial-functor ğ‘ƒ X j â†’ type-polynomial-functor ğ‘ƒ Y j
  map-polynomial-functor (A , B) = map-polynomial-functor' A B
```

### The action on homotopies of multivariable polynomial functors

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level} {I : UU l1} {J : UU l2}
  where

  binary-htpy-polynomial-functor' :
    (A : J â†’ UU l3) (B : I â†’ {j : J} â†’ A j â†’ UU l4)
    {X : I â†’ UU l5} {Y : I â†’ UU l6} {f g : (i : I) â†’ X i â†’ Y i} â†’
    binary-htpy f g â†’
    binary-htpy (map-polynomial-functor' A B f) (map-polynomial-functor' A B g)
  binary-htpy-polynomial-functor' A B H j x =
    eq-pair-eq-fiber (eq-binary-htpy _ _ (Î» i â†’ H i âˆ˜ pr2 x i))

  binary-htpy-polynomial-functor :
    (ğ‘ƒ : polynomial-functor l3 l4 I J)
    {X : I â†’ UU l5} {Y : I â†’ UU l6} {f g : (i : I) â†’ X i â†’ Y i} â†’
    binary-htpy f g â†’
    binary-htpy (map-polynomial-functor ğ‘ƒ f) (map-polynomial-functor ğ‘ƒ g)
  binary-htpy-polynomial-functor (A , B) = binary-htpy-polynomial-functor' A B
```

### The identity multivariable polynomial functor

```agda
module _
  {l1 : Level} {I : UU l1}
  where

  id-polynomial-functor : polynomial-functor lzero l1 I I
  id-polynomial-functor = (Î» i' â†’ unit) , (Î» i {i'} * â†’ (i' ï¼ i))

  compute-type-id-polynomial-functor :
    {l2 : Level} (X : I â†’ UU l2) (i : I) â†’
    type-polynomial-functor id-polynomial-functor X i â‰ƒ X i
  compute-type-id-polynomial-functor X i =
    equivalence-reasoning
      Î£ unit (Î» * â†’ (i' : I) â†’ i ï¼ i' â†’ X i')
      â‰ƒ ((i' : I) â†’ i ï¼ i' â†’ X i')
        by left-unit-law-Î£ (Î» * â†’ (i' : I) â†’ i ï¼ i' â†’ X i')
      â‰ƒ X i
        by equiv-ev-refl i

  map-compute-type-id-polynomial-functor :
    {l2 : Level} (X : I â†’ UU l2) (i : I) â†’
    type-polynomial-functor id-polynomial-functor X i â†’ X i
  map-compute-type-id-polynomial-functor X i =
    map-equiv (compute-type-id-polynomial-functor X i)

  coh-map-id-polynomial-functor :
    {l2 l3 : Level} {X : I â†’ UU l2} {Y : I â†’ UU l3} (f : (i : I) â†’ X i â†’ Y i)
    (i : I) â†’
    coherence-square-maps
      ( map-compute-type-id-polynomial-functor X i)
      ( map-polynomial-functor id-polynomial-functor f i)
      ( f i)
      ( map-compute-type-id-polynomial-functor Y i)
  coh-map-id-polynomial-functor f i = refl-htpy
```

### Composition of multivariable polynomial functors

Given two multivariable polynomial functors `ğ‘ƒ A B : (I â†’ Type) â†’ (J â†’ Type)`
and `ğ‘ƒ C D : (J â†’ Type) â†’ (K â†’ Type)`, then the composite functor
`ğ‘ƒ C D âˆ˜ ğ‘ƒ A B` is again a polynomial functor.

```agda
module _
  {l1 l2 l3 l4 l5 l6 l7 : Level}
  {I : UU l1} {J : UU l2} {K : UU l3}
  (ğ‘„@(C , D) : polynomial-functor l6 l7 J K)
  (ğ‘ƒ@(A , B) : polynomial-functor l4 l5 I J)
  where

  shape-comp-polynomial-functor : K â†’ UU (l2 âŠ” l4 âŠ” l6 âŠ” l7)
  shape-comp-polynomial-functor k =
    Î£ (C k) (Î» c â†’ (j : J) â†’ D j {k} c â†’ A j)

  position-comp-polynomial-functor :
    I â†’ {k : K} â†’ shape-comp-polynomial-functor k â†’ UU (l2 âŠ” l5 âŠ” l7)
  position-comp-polynomial-functor i {k} (c , a) =
    Î£ J (Î» j â†’ Î£ (D j {k} c) (Î» d â†’ B i {j} (a j d)))

  comp-polynomial-functor :
    polynomial-functor (l2 âŠ” l4 âŠ” l6 âŠ” l7) (l2 âŠ” l5 âŠ” l7) I K
  comp-polynomial-functor =
    ( shape-comp-polynomial-functor , position-comp-polynomial-functor)

  map-compute-type-comp-polynomial-functor :
    {l8 : Level} (X : I â†’ UU l8) (k : K) â†’
    type-polynomial-functor comp-polynomial-functor X k â†’
    type-polynomial-functor ğ‘„ (type-polynomial-functor ğ‘ƒ X) k
  map-compute-type-comp-polynomial-functor X k ((c , a) , x) =
    (c , (Î» j d â†’ (a j d , (Î» i b â†’ x i (j , d , b)))))

  map-inv-compute-type-comp-polynomial-functor :
    {l8 : Level} (X : I â†’ UU l8) (k : K) â†’
    type-polynomial-functor ğ‘„ (type-polynomial-functor ğ‘ƒ X) k â†’
    type-polynomial-functor comp-polynomial-functor X k
  map-inv-compute-type-comp-polynomial-functor X k (c , q) =
    ((c , (Î» j d â†’ pr1 (q j d))) , (Î» i (j , d , b) â†’ pr2 (q j d) i b))

  is-equiv-map-compute-type-comp-polynomial-functor :
    {l8 : Level} (X : I â†’ UU l8) (k : K) â†’
    is-equiv (map-compute-type-comp-polynomial-functor X k)
  is-equiv-map-compute-type-comp-polynomial-functor X k =
    is-equiv-is-invertible
      ( map-inv-compute-type-comp-polynomial-functor X k)
      ( refl-htpy)
      ( refl-htpy)

  compute-type-comp-polynomial-functor :
    {l8 : Level} (X : I â†’ UU l8) (k : K) â†’
    type-polynomial-functor comp-polynomial-functor X k â‰ƒ
    type-polynomial-functor ğ‘„ (type-polynomial-functor ğ‘ƒ X) k
  compute-type-comp-polynomial-functor X k =
    ( map-compute-type-comp-polynomial-functor X k ,
      is-equiv-map-compute-type-comp-polynomial-functor X k)

  coh-map-comp-polynomial-functor :
    {l8 l9 : Level} {X : I â†’ UU l8} {Y : I â†’ UU l9}
    (f : (i : I) â†’ X i â†’ Y i) (k : K) â†’
    coherence-square-maps
      ( map-compute-type-comp-polynomial-functor X k)
      ( map-polynomial-functor comp-polynomial-functor f k)
      ( map-polynomial-functor ğ‘„ (map-polynomial-functor ğ‘ƒ f) k)
      ( map-compute-type-comp-polynomial-functor Y k)
  coh-map-comp-polynomial-functor f k x = refl

  compute-map-comp-polynomial-functor :
    {l8 l9 : Level} {X : I â†’ UU l8} {Y : I â†’ UU l9}
    (f : (i : I) â†’ X i â†’ Y i) (k : K) â†’
    ( map-polynomial-functor comp-polynomial-functor f k) ~
    ( map-inv-compute-type-comp-polynomial-functor Y k) âˆ˜
    ( map-polynomial-functor ğ‘„ (map-polynomial-functor ğ‘ƒ f) k) âˆ˜
    ( map-compute-type-comp-polynomial-functor X k)
  compute-map-comp-polynomial-functor f k x = refl

  compute-map-comp-polynomial-functor' :
    {l8 l9 : Level} {X : I â†’ UU l8} {Y : I â†’ UU l9}
    (f : (i : I) â†’ X i â†’ Y i) (k : K) â†’
    ( map-polynomial-functor ğ‘„ (map-polynomial-functor ğ‘ƒ f) k) ~
    ( map-compute-type-comp-polynomial-functor Y k âˆ˜
      map-polynomial-functor comp-polynomial-functor f k âˆ˜
      map-inv-compute-type-comp-polynomial-functor X k)
  compute-map-comp-polynomial-functor' f k x = refl
```
