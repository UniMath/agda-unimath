# Cartesian natural transformations between polynomial endofunctors

```agda
module trees.cartesian-natural-transformations-polynomial-endofunctors where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-binary-functions
open import foundation.action-on-identifications-functions
open import foundation.binary-homotopies
open import foundation.cartesian-morphisms-arrows
open import foundation.commuting-squares-of-homotopies
open import foundation.commuting-squares-of-maps
open import foundation.cones-over-cospan-diagrams
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equality-dependent-function-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopies-morphisms-arrows
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.implicit-function-types
open import foundation.lifts-morphisms-arrows
open import foundation.morphisms-arrows
open import foundation.precomposition-functions
open import foundation.propositions
open import foundation.pullbacks
open import foundation.raising-universe-levels
open import foundation.sections
open import foundation.sets
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.unit-type
open import foundation.universal-property-cartesian-morphisms-arrows
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.equality-dependent-pair-types
open import foundation-core.retractions
open import foundation-core.torsorial-type-families

open import trees.natural-transformations-polynomial-endofunctors
open import trees.polynomial-endofunctors
open import trees.morphisms-polynomial-endofunctors
```

</details>

## Idea

Given two [polynomial endofunctors](trees.polynomial-endofunctors.md)
$ğ‘ƒ â‰ (A â—ƒ B)$ and $ğ‘„ â‰ (C â—ƒ D)$, then a
[natural transformation](trees.natural-transformations-polynomial-endofunctors.md)
$Î±$ between them is
{{#concept "cartesian" Disambiguation="natural transformations of polynomial endofunctors of types" Agda=is-cartesian-natural-transformation-polynomial-endofunctor}}

if every naturality square

```text
              Î±(X)
     ğ‘ƒ(X) -----------> ğ‘„(X)
       |                |
       |                |
  ğ‘ƒ(f) |                | ğ‘„(f)
       |                |
       âˆ¨                âˆ¨
     ğ‘ƒ(Y) -----------> ğ‘„(Y)
              Î±(Y)
```

is a [pullback](foundation-core.pullbacks.md).

## Definitions

### The cartesianness predicate on natural transformations between polynomial endofunctors

```agda
module _
  {l1 l2 l3 l4 l : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î± : natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„)
  where

  is-cartesian-natural-transformation-polynomial-endofunctor :
    UU (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” lsuc l)
  is-cartesian-natural-transformation-polynomial-endofunctor =
    {X Y : UU l} (f : X â†’ Y) â†’
    is-cartesian-hom-arrow
      ( map-polynomial-endofunctor ğ‘ƒ f)
      ( map-polynomial-endofunctor ğ‘„ f)
      ( hom-arrow-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î± f)

  is-prop-is-cartesian-natural-transformation-polynomial-endofunctor :
    is-prop is-cartesian-natural-transformation-polynomial-endofunctor
  is-prop-is-cartesian-natural-transformation-polynomial-endofunctor =
    is-prop-implicit-Î 
      ( Î» X â†’
        is-prop-implicit-Î 
          ( Î» Y â†’
            is-prop-Î 
              ( Î» f â†’
                is-prop-is-cartesian-hom-arrow
                  ( map-polynomial-endofunctor ğ‘ƒ f)
                  ( map-polynomial-endofunctor ğ‘„ f)
                  ( hom-arrow-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
                    ( Î±)
                    ( f)))))

  is-cartesian-natural-transformation-polynomial-endofunctor-Prop :
    Prop (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” lsuc l)
  is-cartesian-natural-transformation-polynomial-endofunctor-Prop =
    ( is-cartesian-natural-transformation-polynomial-endofunctor ,
      is-prop-is-cartesian-natural-transformation-polynomial-endofunctor)
```

### Cartesian natural transformations between polynomial endofunctors

```agda
cartesian-natural-transformation-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (l : Level) â†’
  polynomial-endofunctor l1 l2 â†’
  polynomial-endofunctor l3 l4 â†’
  UU (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” lsuc l)
cartesian-natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„ =
  Î£ ( natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„)
    ( is-cartesian-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„)

module _
  {l1 l2 l3 l4 l : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î± : cartesian-natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„)
  where

  natural-transformation-cartesian-natural-transformation-polynomial-endofunctor :
    natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„
  natural-transformation-cartesian-natural-transformation-polynomial-endofunctor =
    pr1 Î±

  type-cartesian-natural-transformation-polynomial-endofunctor :
    {X : UU l} â†’
    type-polynomial-endofunctor ğ‘ƒ X â†’
    type-polynomial-endofunctor ğ‘„ X
  type-cartesian-natural-transformation-polynomial-endofunctor =
    type-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      natural-transformation-cartesian-natural-transformation-polynomial-endofunctor

  naturality-cartesian-natural-transformation-polynomial-endofunctor :
    coherence-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( type-cartesian-natural-transformation-polynomial-endofunctor)
  naturality-cartesian-natural-transformation-polynomial-endofunctor =
    naturality-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      natural-transformation-cartesian-natural-transformation-polynomial-endofunctor

  is-cartesian-cartesian-natural-transformation-polynomial-endofunctor :
    is-cartesian-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      natural-transformation-cartesian-natural-transformation-polynomial-endofunctor
  is-cartesian-cartesian-natural-transformation-polynomial-endofunctor = pr2 Î±
```

### The criterion of being cartesian at terminal maps

```agda
module _
  {l1 l2 l3 l4 l : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î± : natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„)
  where

  is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor :
    UU (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” lsuc l)
  is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor =
    {X : UU l} â†’
    is-cartesian-hom-arrow
      ( map-polynomial-endofunctor ğ‘ƒ (raise-terminal-map X))
      ( map-polynomial-endofunctor ğ‘„ (raise-terminal-map X))
      ( hom-arrow-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±
        ( raise-terminal-map X))

  is-prop-is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor :
    is-prop
      is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor
  is-prop-is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor =
    is-prop-implicit-Î 
      ( Î» X â†’
        is-prop-is-cartesian-hom-arrow
          ( map-polynomial-endofunctor ğ‘ƒ (raise-terminal-map X))
          ( map-polynomial-endofunctor ğ‘„ (raise-terminal-map X))
          ( hom-arrow-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±
            ( raise-terminal-map X)))

  is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor-Prop :
    Prop (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” lsuc l)
  is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor-Prop =
    ( is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor ,
      is-prop-is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor)
```

### The associated family of cartesian morphisms of arrows

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î±@(Î±' , H) : cartesian-natural-transformation-polynomial-endofunctor l5 ğ‘ƒ ğ‘„)
  where

  hom-arrow-cartesian-natural-transformation-polynomial-endofunctor :
    {X Y : UU l5} (f : X â†’ Y) â†’
    hom-arrow (map-polynomial-endofunctor ğ‘ƒ f) (map-polynomial-endofunctor ğ‘„ f)
  hom-arrow-cartesian-natural-transformation-polynomial-endofunctor =
    hom-arrow-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±'

  cone-cartesian-natural-transformation-polynomial-endofunctor :
    {X Y : UU l5} (f : X â†’ Y) â†’
    cone
      ( type-cartesian-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±)
      ( map-polynomial-endofunctor ğ‘„ f)
      ( type-polynomial-endofunctor ğ‘ƒ X)
  cone-cartesian-natural-transformation-polynomial-endofunctor =
    cone-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±'

  cartesian-hom-arrow-cartesian-natural-transformation-polynomial-endofunctor :
    {X Y : UU l5} (f : X â†’ Y) â†’
    cartesian-hom-arrow
      ( map-polynomial-endofunctor ğ‘ƒ f)
      ( map-polynomial-endofunctor ğ‘„ f)
  cartesian-hom-arrow-cartesian-natural-transformation-polynomial-endofunctor
    f =
    ( hom-arrow-cartesian-natural-transformation-polynomial-endofunctor f , H f)
```

### The associated morphism of polynomial endofunctors

```agda
module _
  {l1 l2 l3 l4 : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î± : cartesian-natural-transformation-polynomial-endofunctor l2 ğ‘ƒ ğ‘„)
  (let ğ‘ƒâ‚€ = shape-polynomial-endofunctor ğ‘ƒ)
  (let ğ‘ƒâ‚ = position-polynomial-endofunctor ğ‘ƒ)
  (let Î±â‚€ = type-cartesian-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±)
  where

  shape-cartesian-natural-transformation-polynomial-endofunctor :
    shape-polynomial-endofunctor ğ‘ƒ â†’ shape-polynomial-endofunctor ğ‘„
  shape-cartesian-natural-transformation-polynomial-endofunctor =
    shape-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( natural-transformation-cartesian-natural-transformation-polynomial-endofunctor
          ğ‘ƒ ğ‘„ Î±)

  position-cartesian-natural-transformation-polynomial-endofunctor :
    (a : shape-polynomial-endofunctor ğ‘ƒ) â†’
    position-polynomial-endofunctor ğ‘„
      ( shape-cartesian-natural-transformation-polynomial-endofunctor a) â†’
    position-polynomial-endofunctor ğ‘ƒ a
  position-cartesian-natural-transformation-polynomial-endofunctor =
    position-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( natural-transformation-cartesian-natural-transformation-polynomial-endofunctor
          ğ‘ƒ ğ‘„ Î±)

  hom-cartesian-natural-transformation-polynomial-endofunctor :
    hom-polynomial-endofunctor ğ‘ƒ ğ‘„
  hom-cartesian-natural-transformation-polynomial-endofunctor =
    hom-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„
      ( natural-transformation-cartesian-natural-transformation-polynomial-endofunctor
          ğ‘ƒ ğ‘„ Î±)
```

### The identity cartesian natural transformation

```agda
id-cartesian-natural-transformation-polynomial-endofunctor :
  {l1 l2 l3 : Level} (ğ‘ƒ : polynomial-endofunctor l1 l2) â†’
  cartesian-natural-transformation-polynomial-endofunctor l3 ğ‘ƒ ğ‘ƒ
pr1 (id-cartesian-natural-transformation-polynomial-endofunctor ğ‘ƒ) =
  id-natural-transformation-polynomial-endofunctor ğ‘ƒ
pr2 (id-cartesian-natural-transformation-polynomial-endofunctor ğ‘ƒ) f =
  is-cartesian-id-hom-arrow
```

### Composition of cartesian natural transformations

```agda
module _
  {l1 l2 l3 l4 l5 l6 l : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (ğ‘… : polynomial-endofunctor l5 l6)
  (Î²@(Î²' , H) : cartesian-natural-transformation-polynomial-endofunctor l ğ‘„ ğ‘…)
  (Î±@(Î±' , K) : cartesian-natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„)
  where

  is-cartesian-comp-cartesian-natural-transformation-polynomial-endofunctor :
    is-cartesian-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘…
      ( comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ ğ‘… Î²' Î±')
  is-cartesian-comp-cartesian-natural-transformation-polynomial-endofunctor f =
    is-cartesian-comp-hom-arrow
      ( map-polynomial-endofunctor ğ‘ƒ f)
      ( map-polynomial-endofunctor ğ‘„ f)
      ( map-polynomial-endofunctor ğ‘… f)
      ( hom-arrow-natural-transformation-polynomial-endofunctor ğ‘„ ğ‘… Î²' f)
      ( hom-arrow-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±' f)
      ( H f)
      ( K f)

  comp-cartesian-natural-transformation-polynomial-endofunctor :
    cartesian-natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘…
  comp-cartesian-natural-transformation-polynomial-endofunctor =
    ( comp-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ ğ‘… Î²' Î±' ,
      is-cartesian-comp-cartesian-natural-transformation-polynomial-endofunctor)
```

### A natural transformation into a polynomial endofunctor with a set of shapes is cartesian if and only if it is cartesian at terminal maps

**Proof.** One direction is trivial. For the other direction, given a natural
transformation of polynomial endofunctors $Î± : ğ‘ƒ â‡’ ğ‘„$ and an arbitrary function
$f : X â†’ Y$, since the type of shapes of $ğ‘„$ is a set, the following prism
commutes and we have a morphism of arrows in the slice above $Î±_{*}$:

```text
         Î±X
  ğ‘ƒX -----------> ğ‘„X   â†’
   \ âŒŸ â†’   ğ‘ƒY ---- \ ----> ğ‘„Y
    \     / âŒŸ   Î±Y  \     /
     \   /           \   /
      âˆ¨ âˆ¨             âˆ¨ âˆ¨
       ğ‘ƒ* -----------> ğ‘„*
              Î±*
```

and so by the right-cancellation property of cartesian squares the naturality
square at $f$ is cartesian. âˆ

This holds more generally for coherent natural transformations between arbitrary
polynomial functors, as mentioned in Remark 2.1.4 in {{#cite GHK22}}.

```agda
module _
  {l1 l2 l3 l4 l : Level}
  (ğ‘ƒ : polynomial-endofunctor l1 l2)
  (ğ‘„ : polynomial-endofunctor l3 l4)
  (Î± : natural-transformation-polynomial-endofunctor l ğ‘ƒ ğ‘„)
  where

  is-cartesian-is-cartesian-at-terminal-map-natural-transformation-is-set-shape-polynomial-endofunctor :
    is-set (shape-polynomial-endofunctor ğ‘„) â†’
    is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor
      ğ‘ƒ ğ‘„ Î± â†’
    is-cartesian-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±
  is-cartesian-is-cartesian-at-terminal-map-natural-transformation-is-set-shape-polynomial-endofunctor
    Hğ‘„ HÎ± {X} {Y} f =
    is-pullback-top-square-vertical-triangle
      (type-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±)
      (map-polynomial-endofunctor ğ‘„ (raise-terminal-map Y))
      (map-polynomial-endofunctor ğ‘„ f)
      (map-polynomial-endofunctor ğ‘„ (raise-terminal-map X))
      ( cone-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±
        ( raise-terminal-map Y))
      ( cone-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î± f)
      ( cone-natural-transformation-polynomial-endofunctor ğ‘ƒ ğ‘„ Î±
        ( raise-terminal-map X))
      ( refl-htpy)
      ( refl-htpy ,
        refl-htpy ,
        Î» x â†’
        eq-is-prop
          ( is-set-Î£ Hğ‘„ (Î» _ â†’ is-set-function-type is-set-raise-unit) _ _))
      ( HÎ± {Y})
      ( HÎ± {X})
```

## References

{{#bibliography}}
