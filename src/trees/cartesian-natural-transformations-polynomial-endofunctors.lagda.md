# Cartesian natural transformations between polynomial endofunctors

```agda
module trees.cartesian-natural-transformations-polynomial-endofunctors where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-binary-functions
open import foundation.action-on-identifications-functions
open import foundation.binary-homotopies
open import foundation.cartesian-morphisms-arrows
open import foundation.commuting-squares-of-homotopies
open import foundation.commuting-squares-of-maps
open import foundation.cones-over-cospan-diagrams
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equality-dependent-function-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopies-morphisms-arrows
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.implicit-function-types
open import foundation.lifts-morphisms-arrows
open import foundation.morphisms-arrows
open import foundation.precomposition-functions
open import foundation.propositions
open import foundation.pullbacks
open import foundation.raising-universe-levels
open import foundation.sections
open import foundation.structure-identity-principle
open import foundation.transport-along-identifications
open import foundation.unit-type
open import foundation.universal-property-cartesian-morphisms-arrows
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.equality-dependent-pair-types
open import foundation-core.retractions
open import foundation-core.torsorial-type-families

open import trees.natural-transformations-polynomial-endofunctors
open import trees.polynomial-endofunctors
```

</details>

## Idea

Given two [polynomial endofunctors](trees.polynomial-endofunctors.md)
$ùëÉ ‚âê (A ‚óÉ B)$ and $ùëÑ ‚âê (C ‚óÉ D)$, then a
[natural transformation](trees.natural-transformations-polynomial-endofunctors.md)
$Œ±$ between them is
{{#concept "cartesian" Disambiguation="natural transformations of polynomial endofunctors of types" Agda=is-cartesian-natural-transformation-polynomial-endofunctor}}

if every naturality square

```text
              Œ±(X)
     ùëÉ(X) -----------> ùëÑ(X)
       |                |
       |                |
  ùëÉ(f) |                | ùëÑ(f)
       |                |
       ‚à®                ‚à®
     ùëÉ(Y) -----------> ùëÑ(Y)
              Œ±(Y)
```

is a [pullback](foundation-core.pullbacks.md).

## Definitions

### The cartesianness predicate on natural transformations between polynomial endofunctors

```agda
module _
  {l1 l2 l3 l4 l : Level}
  (ùëÉ : polynomial-endofunctor l1 l2)
  (ùëÑ : polynomial-endofunctor l3 l4)
  (Œ± : natural-transformation-polynomial-endofunctor l ùëÉ ùëÑ)
  where

  is-cartesian-natural-transformation-polynomial-endofunctor :
    UU (l1 ‚äî l2 ‚äî l3 ‚äî l4 ‚äî lsuc l)
  is-cartesian-natural-transformation-polynomial-endofunctor =
    {X Y : UU l} (f : X ‚Üí Y) ‚Üí
    is-cartesian-hom-arrow
      ( map-polynomial-endofunctor ùëÉ f)
      ( map-polynomial-endofunctor ùëÑ f)
      ( hom-arrow-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ Œ± f)

  is-prop-is-cartesian-natural-transformation-polynomial-endofunctor :
    is-prop is-cartesian-natural-transformation-polynomial-endofunctor
  is-prop-is-cartesian-natural-transformation-polynomial-endofunctor =
    is-prop-implicit-Œ†
      ( Œª X ‚Üí
        is-prop-implicit-Œ†
          ( Œª Y ‚Üí
            is-prop-Œ†
              ( Œª f ‚Üí
                is-prop-is-cartesian-hom-arrow
                  ( map-polynomial-endofunctor ùëÉ f)
                  ( map-polynomial-endofunctor ùëÑ f)
                  ( hom-arrow-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ
                    ( Œ±)
                    ( f)))))

  is-cartesian-natural-transformation-polynomial-endofunctor-Prop :
    Prop (l1 ‚äî l2 ‚äî l3 ‚äî l4 ‚äî lsuc l)
  is-cartesian-natural-transformation-polynomial-endofunctor-Prop =
    ( is-cartesian-natural-transformation-polynomial-endofunctor ,
      is-prop-is-cartesian-natural-transformation-polynomial-endofunctor)
```

### Cartesian natural transformations between polynomial endofunctors

```agda
cartesian-natural-transformation-polynomial-endofunctor :
  {l1 l2 l3 l4 : Level} (l : Level) ‚Üí
  polynomial-endofunctor l1 l2 ‚Üí
  polynomial-endofunctor l3 l4 ‚Üí
  UU (l1 ‚äî l2 ‚äî l3 ‚äî l4 ‚äî lsuc l)
cartesian-natural-transformation-polynomial-endofunctor l ùëÉ ùëÑ =
  Œ£ ( natural-transformation-polynomial-endofunctor l ùëÉ ùëÑ)
    ( is-cartesian-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ)

module _
  {l1 l2 l3 l4 l : Level}
  (ùëÉ : polynomial-endofunctor l1 l2)
  (ùëÑ : polynomial-endofunctor l3 l4)
  (Œ± : cartesian-natural-transformation-polynomial-endofunctor l ùëÉ ùëÑ)
  where

  natural-transformation-cartesian-natural-transformation-polynomial-endofunctor :
    natural-transformation-polynomial-endofunctor l ùëÉ ùëÑ
  natural-transformation-cartesian-natural-transformation-polynomial-endofunctor =
    pr1 Œ±

  type-cartesian-natural-transformation-polynomial-endofunctor :
    {X : UU l} ‚Üí
    type-polynomial-endofunctor ùëÉ X ‚Üí
    type-polynomial-endofunctor ùëÑ X
  type-cartesian-natural-transformation-polynomial-endofunctor =
    type-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ
      natural-transformation-cartesian-natural-transformation-polynomial-endofunctor

  naturality-cartesian-natural-transformation-polynomial-endofunctor :
    coherence-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ
      ( type-cartesian-natural-transformation-polynomial-endofunctor)
  naturality-cartesian-natural-transformation-polynomial-endofunctor =
    naturality-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ
      natural-transformation-cartesian-natural-transformation-polynomial-endofunctor

  is-cartesian-cartesian-natural-transformation-polynomial-endofunctor :
    is-cartesian-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ
      natural-transformation-cartesian-natural-transformation-polynomial-endofunctor
  is-cartesian-cartesian-natural-transformation-polynomial-endofunctor = pr2 Œ±
```

### The criterion of being cartesian at terminal maps

```agda
module _
  {l1 l2 l3 l4 l : Level}
  (ùëÉ : polynomial-endofunctor l1 l2)
  (ùëÑ : polynomial-endofunctor l3 l4)
  (Œ± : natural-transformation-polynomial-endofunctor l ùëÉ ùëÑ)
  where

  is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor :
    UU (l1 ‚äî l2 ‚äî l3 ‚äî l4 ‚äî lsuc l)
  is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor =
    {X : UU l} ‚Üí
    is-cartesian-hom-arrow
      ( map-polynomial-endofunctor ùëÉ (raise-terminal-map X))
      ( map-polynomial-endofunctor ùëÑ (raise-terminal-map X))
      ( hom-arrow-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ Œ±
        ( raise-terminal-map X))

  is-prop-is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor :
    is-prop
      is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor
  is-prop-is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor =
    is-prop-implicit-Œ†
      ( Œª X ‚Üí
        is-prop-is-cartesian-hom-arrow
          ( map-polynomial-endofunctor ùëÉ (raise-terminal-map X))
          ( map-polynomial-endofunctor ùëÑ (raise-terminal-map X))
          ( hom-arrow-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ Œ±
            ( raise-terminal-map X)))

  is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor-Prop :
    Prop (l1 ‚äî l2 ‚äî l3 ‚äî l4 ‚äî lsuc l)
  is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor-Prop =
    ( is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor ,
      is-prop-is-cartesian-at-terminal-map-natural-transformation-polynomial-endofunctor)
```

### The associated family of cartesian morphisms of arrows

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  (ùëÉ : polynomial-endofunctor l1 l2)
  (ùëÑ : polynomial-endofunctor l3 l4)
  (Œ±@(Œ±' , H) : cartesian-natural-transformation-polynomial-endofunctor l5 ùëÉ ùëÑ)
  where

  hom-arrow-cartesian-natural-transformation-polynomial-endofunctor :
    {X Y : UU l5} (f : X ‚Üí Y) ‚Üí
    hom-arrow (map-polynomial-endofunctor ùëÉ f) (map-polynomial-endofunctor ùëÑ f)
  hom-arrow-cartesian-natural-transformation-polynomial-endofunctor =
    hom-arrow-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ Œ±'

  cone-cartesian-natural-transformation-polynomial-endofunctor :
    {X Y : UU l5} (f : X ‚Üí Y) ‚Üí
    cone
      ( type-cartesian-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ Œ±)
      ( map-polynomial-endofunctor ùëÑ f)
      ( type-polynomial-endofunctor ùëÉ X)
  cone-cartesian-natural-transformation-polynomial-endofunctor =
    cone-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ Œ±'

  cartesian-hom-arrow-cartesian-natural-transformation-polynomial-endofunctor :
    {X Y : UU l5} (f : X ‚Üí Y) ‚Üí
    cartesian-hom-arrow
      ( map-polynomial-endofunctor ùëÉ f)
      ( map-polynomial-endofunctor ùëÑ f)
  cartesian-hom-arrow-cartesian-natural-transformation-polynomial-endofunctor
    f =
    ( hom-arrow-cartesian-natural-transformation-polynomial-endofunctor f , H f)
```

### The identity cartesian natural transformation

```agda
id-cartesian-natural-transformation-polynomial-endofunctor :
  {l1 l2 l3 : Level} (ùëÉ : polynomial-endofunctor l1 l2) ‚Üí
  cartesian-natural-transformation-polynomial-endofunctor l3 ùëÉ ùëÉ
pr1 (id-cartesian-natural-transformation-polynomial-endofunctor ùëÉ) =
  id-natural-transformation-polynomial-endofunctor ùëÉ
pr2 (id-cartesian-natural-transformation-polynomial-endofunctor ùëÉ) f =
  is-cartesian-id-hom-arrow
```

### Composition of cartesian natural transformations

```agda
module _
  {l1 l2 l3 l4 l5 l6 l : Level}
  (ùëÉ : polynomial-endofunctor l1 l2)
  (ùëÑ : polynomial-endofunctor l3 l4)
  (ùëÖ : polynomial-endofunctor l5 l6)
  (Œ≤@(Œ≤' , H) : cartesian-natural-transformation-polynomial-endofunctor l ùëÑ ùëÖ)
  (Œ±@(Œ±' , K) : cartesian-natural-transformation-polynomial-endofunctor l ùëÉ ùëÑ)
  where

  is-cartesian-comp-cartesian-natural-transformation-polynomial-endofunctor :
    is-cartesian-natural-transformation-polynomial-endofunctor ùëÉ ùëÖ
      ( comp-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ ùëÖ Œ≤' Œ±')
  is-cartesian-comp-cartesian-natural-transformation-polynomial-endofunctor f =
    is-cartesian-comp-hom-arrow
      ( map-polynomial-endofunctor ùëÉ f)
      ( map-polynomial-endofunctor ùëÑ f)
      ( map-polynomial-endofunctor ùëÖ f)
      ( hom-arrow-natural-transformation-polynomial-endofunctor ùëÑ ùëÖ Œ≤' f)
      ( hom-arrow-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ Œ±' f)
      ( H f)
      ( K f)

  comp-cartesian-natural-transformation-polynomial-endofunctor :
    cartesian-natural-transformation-polynomial-endofunctor l ùëÉ ùëÖ
  comp-cartesian-natural-transformation-polynomial-endofunctor =
    ( comp-natural-transformation-polynomial-endofunctor ùëÉ ùëÑ ùëÖ Œ≤' Œ±' ,
      is-cartesian-comp-cartesian-natural-transformation-polynomial-endofunctor)
```

### A natural transformation is cartesian if and only if it is cartesian at terminal maps

**Proof.** One direction is trivial. For the other direction, given a natural
transformation of polynomial endofunctors $Œ± : ùëÉ ‚áí ùëÑ$ and an arbitrary function
$f : X ‚Üí Y$, we have a morphism of arrows in the slice above $Œ±_{*}$:

```text
         Œ±X
  ùëÉX -----------> ùëÑX   ‚Üí
   \ ‚åü ‚Üí   ùëÉY ---- \ ----> ùëÑY
    \     / ‚åü   Œ±Y  \     /
     \   /           \   /
      ‚à® ‚à®             ‚à® ‚à®
       ùëÉ* -----------> ùëÑ*
              Œ±*
```

and so by the right-cancellation property of cartesian squares the naturality
square at $f$ is cartesian. ‚àé

This is mentioned as Remark 2.1.4 in {{#cite GHK22}}.

## References

{{#bibliography}}
