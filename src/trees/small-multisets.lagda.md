# Small multisets

```agda
module trees.small-multisets where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.empty-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.propositions
open import foundation.raising-universe-levels
open import foundation.small-types
open import foundation.subtypes
open import foundation.transport-along-identifications
open import foundation.univalence
open import foundation.universe-levels

open import trees.multisets
open import trees.w-types
```

</details>

## Idea

A [multiset](trees.multisets.md) `X := tree-ğ• A Î±` is said to be
{{#concept "small" Disambiguation="multiset" Agda=is-small-ğ•}} with respect to a
[universe](foundation.universe-levels.md) `UU l` if its symbol `A` is a
[small type](foundation-core.small-types.md) with respect to `UU l`, and if
recursively each `Î± x` is a small multiset with respect to `UU l`.

## Definition

### Small multisets

```agda
is-small-ğ•-Prop : (l : Level) {l1 : Level} â†’ ğ• l1 â†’ Prop (l1 âŠ” lsuc l)
is-small-ğ•-Prop l (tree-ğ• A Î±) =
  product-Prop (is-small-Prop l A) (Î -Prop A (Î» x â†’ is-small-ğ•-Prop l (Î± x)))

is-small-ğ• : (l : Level) {l1 : Level} â†’ ğ• l1 â†’ UU (l1 âŠ” lsuc l)
is-small-ğ• l X = type-Prop (is-small-ğ•-Prop l X)

is-prop-is-small-ğ• : {l l1 : Level} (X : ğ• l1) â†’ is-prop (is-small-ğ• l X)
is-prop-is-small-ğ• {l} X = is-prop-type-Prop (is-small-ğ•-Prop l X)
```

### Resizing small multisets

```agda
resize-ğ• :
  {l1 l2 : Level} (X : ğ• l1) â†’ is-small-ğ• l2 X â†’ ğ• l2
resize-ğ• (tree-ğ• A Î±) (pair (pair A' e) H2) =
  tree-ğ• A'
    ( Î» x' â†’ resize-ğ• (Î± (map-inv-equiv e x')) (H2 (map-inv-equiv e x')))
```

## Properties

### The comprehension of a small multiset equipped with a small predicate is small

```agda
is-small-comprehension-ğ• :
  (l : Level) {l1 : Level} {X : ğ• l1} {P : shape-ğ• X â†’ UU l1} â†’
  is-small-ğ• l X â†’ ((x : shape-ğ• X) â†’ is-small l (P x)) â†’
  is-small-ğ• l (comprehension-ğ• X P)
is-small-comprehension-ğ• l {l1} {tree-ğ• A Î±} {P} (pair (pair X e) H) K =
  pair
    ( is-small-Î£ (pair X e) K)
    ( Î» t â†’ H (pr1 t))
```

### The identity type between small multisets is small

```agda
is-small-eq-ğ• :
  (l : Level) {l1 : Level} {X Y : ğ• l1} â†’
  is-small-ğ• l X â†’ is-small-ğ• l Y â†’ is-small l (X ï¼ Y)
is-small-eq-ğ• l
  {l1} {tree-ğ• A Î±} {tree-ğ• B Î²} (pair (pair X e) H) (pair (pair Y f) K) =
  is-small-equiv
    ( Eq-ğ• (tree-ğ• A Î±) (tree-ğ• B Î²))
    ( equiv-Eq-ğ•-eq (tree-ğ• A Î±) (tree-ğ• B Î²))
    ( is-small-Î£
      ( is-small-equiv
        ( A â‰ƒ B)
        ( equiv-univalence)
        ( pair
          ( X â‰ƒ Y)
          ( equiv-precomp-equiv (inv-equiv e) Y âˆ˜e equiv-postcomp-equiv f A)))
      ( Ïƒ))
  where
  Ïƒ : (x : A ï¼ B) â†’ is-small l ((z : A) â†’ Eq-ğ• (Î± z) (Î² (tr id x z)))
  Ïƒ refl =
    is-small-Î 
      ( pair X e)
      ( Î» x â†’
        is-small-equiv
          ( Î± x ï¼ Î² x)
          ( inv-equiv (equiv-Eq-ğ•-eq (Î± x) (Î² x)))
          ( is-small-eq-ğ• l (H x) (K x)))
```

### The elementhood relation between small multisets is small

```agda
is-small-âˆˆ-ğ• :
  (l : Level) {l1 : Level} {X Y : ğ• l1} â†’
  is-small-ğ• l X â†’ is-small-ğ• l Y â†’ is-small l (X âˆˆ-ğ• Y)
is-small-âˆˆ-ğ• l {l1} {tree-ğ• A Î±} {tree-ğ• B Î²} H (pair (pair Y f) K) =
  is-small-Î£
    ( pair Y f)
    ( Î» b â†’ is-small-eq-ğ• l (K b) H)

is-small-âˆ‰-ğ• :
  (l : Level) {l1 : Level} {X Y : ğ• l1} â†’
  is-small-ğ• l X â†’ is-small-ğ• l Y â†’ is-small l (X âˆ‰-ğ• Y)
is-small-âˆ‰-ğ• l {l1} {X} {Y} H K =
  is-small-Î 
    ( is-small-âˆˆ-ğ• l {l1} {X} {Y} H K)
    ( Î» x â†’ Raise l empty)
```

### The resizing of a small multiset is small

```agda
is-small-resize-ğ• :
  {l1 l2 : Level} (X : ğ• l1) (H : is-small-ğ• l2 X) â†’
  is-small-ğ• l1 (resize-ğ• X H)
is-small-resize-ğ• (tree-ğ• A Î±) (pair (pair A' e) H2) =
  pair
    ( pair A (inv-equiv e))
    ( Î» a' â†’
      is-small-resize-ğ•
        ( Î± (map-inv-equiv e a'))
        ( H2 (map-inv-equiv e a')))
```

### The type of `UU l2`-small multisets in `ğ• l1` is equivalent to the type of `UU l1`-small multisets in `ğ• l2`

```agda
resize-ğ•' :
  {l1 l2 : Level} â†’
  Î£ (ğ• l1) (is-small-ğ• l2) â†’ Î£ (ğ• l2) (is-small-ğ• l1)
resize-ğ•' (pair X H) = pair (resize-ğ• X H) (is-small-resize-ğ• X H)

abstract
  resize-resize-ğ• :
    {l1 l2 : Level} {x : ğ• l1} (H : is-small-ğ• l2 x) â†’
    resize-ğ• (resize-ğ• x H) (is-small-resize-ğ• x H) ï¼ x
  resize-resize-ğ• {x = tree-ğ• A Î±} ((A' , e) , H) =
    eq-Eq-ğ•
      ( resize-ğ•
        ( resize-ğ• (tree-ğ• A Î±) ((A' , e) , H))
        ( is-small-resize-ğ• (tree-ğ• A Î±) ((A' , e) , H)))
      ( tree-ğ• A Î±)
      ( pair
        ( refl)
        ( Î» z â†’
          Eq-ğ•-eq
            ( resize-ğ•
              ( resize-ğ•
                ( Î± (map-inv-equiv e (map-equiv e z)))
                ( H (map-inv-equiv e (map-equiv e z))))
              ( is-small-resize-ğ•
                ( Î± (map-inv-equiv e (map-equiv e z)))
                ( H (map-inv-equiv e (map-equiv e z)))))
            ( Î± z)
            ( ( ap
                ( Î» t â†’
                  resize-ğ•
                    ( resize-ğ• (Î± t) (H t))
                    ( is-small-resize-ğ• (Î± t) (H t)))
                ( is-retraction-map-inv-equiv e z)) âˆ™
              ( resize-resize-ğ• (H z)))))

abstract
  resize-resize-ğ•' :
    {l1 l2 : Level} â†’ (resize-ğ•' {l2} {l1} âˆ˜ resize-ğ•' {l1} {l2}) ~ id
  resize-resize-ğ•' {l1} {l2} (pair X H) =
    eq-type-subtype
      ( is-small-ğ•-Prop l2)
      ( resize-resize-ğ• H)

is-equiv-resize-ğ•' :
  {l1 l2 : Level} â†’ is-equiv (resize-ğ•' {l1} {l2})
is-equiv-resize-ğ•' {l1} {l2} =
  is-equiv-is-invertible
    ( resize-ğ•' {l2} {l1})
    ( resize-resize-ğ•')
    ( resize-resize-ğ•')

equiv-resize-ğ•' :
  {l1 l2 : Level} â†’ Î£ (ğ• l1) (is-small-ğ• l2) â‰ƒ Î£ (ğ• l2) (is-small-ğ• l1)
equiv-resize-ğ•' {l1} {l2} = pair resize-ğ•' is-equiv-resize-ğ•'
```

The resizing operation on small multisets is an embedding

```agda
eq-resize-ğ• :
  {l1 l2 : Level} {x y : ğ• l1} (H : is-small-ğ• l2 x) (K : is-small-ğ• l2 y) â†’
  (x ï¼ y) â‰ƒ (resize-ğ• x H ï¼ resize-ğ• y K)
eq-resize-ğ• {l1} {l2} H K =
  ( extensionality-type-subtype'
    ( is-small-ğ•-Prop l1)
    ( resize-ğ•' (pair _ H))
    ( resize-ğ•' (pair _ K))) âˆ˜e
  ( ( equiv-ap (equiv-resize-ğ•') (pair _ H) (pair _ K)) âˆ˜e
    ( inv-equiv
      ( extensionality-type-subtype'
        ( is-small-ğ•-Prop l2)
        ( pair _ H)
        ( pair _ K))))
```

### The resizing operation on small multisets respects the elementhood relation

```agda
abstract
  equiv-elementhood-resize-ğ• :
    {l1 l2 : Level} {x y : ğ• l1} (H : is-small-ğ• l2 x) (K : is-small-ğ• l2 y) â†’
    (x âˆˆ-ğ• y) â‰ƒ (resize-ğ• x H âˆˆ-ğ• resize-ğ• y K)
  equiv-elementhood-resize-ğ• {x = X} {tree-ğ• B Î²} H (pair (pair B' e) K) =
    equiv-Î£
      ( Î» y' â†’
        ( component-ğ• (resize-ğ• (tree-ğ• B Î²) (pair (pair B' e) K)) y') ï¼
        ( resize-ğ• X H))
      ( e)
      ( Î» b â†’
        ( equiv-concat
          ( ap
            ( Î» t â†’ resize-ğ• (Î² t) (K t))
            ( is-retraction-map-inv-equiv e b))
          ( resize-ğ• X H)) âˆ˜e
        ( eq-resize-ğ• (K b) H))
```

### The type of all multisets of universe level `l1` is `UU l2`-small if each type in `UU l1` is `UU l2`-small

```agda
is-small-multiset-ğ• :
  {l1 l2 : Level} â†’
  ((A : UU l1) â†’ is-small l2 A) â†’ (X : ğ• l1) â†’ is-small-ğ• l2 X
is-small-multiset-ğ• {l1} {l2} H (tree-ğ• A Î±) =
  pair (H A) (Î» x â†’ is-small-multiset-ğ• H (Î± x))
```
