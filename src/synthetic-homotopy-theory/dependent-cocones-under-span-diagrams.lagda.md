# Dependent cocones under span diagrams

```agda
module synthetic-homotopy-theory.dependent-cocones-under-span-diagrams where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-dependent-functions
open import foundation.action-on-identifications-functions
open import foundation.contractible-types
open import foundation.dependent-homotopies
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.retractions
open import foundation.sections
open import foundation.span-diagrams
open import foundation.structure-identity-principle
open import foundation.torsorial-type-families
open import foundation.transport-along-identifications
open import foundation.universe-levels

open import synthetic-homotopy-theory.cocones-under-span-diagrams
open import synthetic-homotopy-theory.descent-property-families-of-types-pushouts
open import synthetic-homotopy-theory.sections-families-of-types-pushouts
```

</details>

## Idea

Consider a [span diagram](foundation.span-diagrams.md) `ğ’® := (A <-- S --> B)`
and a
[cocone structure](synthetic-homotopy-theory.cocones-under-span-diagrams.md) `c`
under `ğ’®` with codomain `X`. Furthermore, consider a type family `P` over `X`.
In this case we may consider _dependent_ cocone structures on `P` over `c`.

A {{#concept "dependent cocone" Disambiguation="span diagram"}} `d` over
`(i , j , H)` on `P` consists of two dependent functions

```text
  i' : (a : A) â†’ P (i a)
  j' : (b : B) â†’ P (j b)
```

and a [dependent homotopy](foundation.dependent-homotopies.md)

```text
  i' âˆ˜ f ~ j' âˆ˜ g
```

over `H`.

## Definitions

### Dependent cocones with respect to type families over the codomain of a cocone

```agda
module _
  { l1 l2 l3 l4 l5 : Level} (ğ’® : span-diagram l1 l2 l3)
  {X : UU l4} (c : cocone-span-diagram ğ’® X) (Y : X â†’ UU l5)
  where

  left-family-of-elements-dependent-cocone-span-diagram : UU (l1 âŠ” l5)
  left-family-of-elements-dependent-cocone-span-diagram =
    (a : domain-span-diagram ğ’®) â†’ Y (left-map-cocone-span-diagram ğ’® c a)

  right-family-of-elements-dependent-cocone-span-diagram : UU (l2 âŠ” l5)
  right-family-of-elements-dependent-cocone-span-diagram =
    (b : codomain-span-diagram ğ’®) â†’ Y (right-map-cocone-span-diagram ğ’® c b)

  dependent-homotopy-dependent-cocone-span-diagram :
    left-family-of-elements-dependent-cocone-span-diagram â†’
    right-family-of-elements-dependent-cocone-span-diagram â†’ UU (l3 âŠ” l5)
  dependent-homotopy-dependent-cocone-span-diagram hA hB =
    dependent-homotopy
      ( Î» _ â†’ Y)
      ( coherence-square-cocone-span-diagram ğ’® c)
      ( hA âˆ˜ left-map-span-diagram ğ’®)
      ( hB âˆ˜ right-map-span-diagram ğ’®)

  dependent-cocone-span-diagram : UU (l1 âŠ” l2 âŠ” l3 âŠ” l5)
  dependent-cocone-span-diagram =
    structure-section-type-family-pushout ğ’®
      ( descent-data-type-family-pushout ğ’® c Y)

  module _
    (d : dependent-cocone-span-diagram)
    where

    left-map-dependent-cocone-span-diagram :
      left-family-of-elements-dependent-cocone-span-diagram
    left-map-dependent-cocone-span-diagram = pr1 d

    right-map-dependent-cocone-span-diagram :
      right-family-of-elements-dependent-cocone-span-diagram
    right-map-dependent-cocone-span-diagram = pr1 (pr2 d)

    coherence-square-dependent-cocone-span-diagram :
      dependent-homotopy-dependent-cocone-span-diagram
        ( left-map-dependent-cocone-span-diagram)
        ( right-map-dependent-cocone-span-diagram)
    coherence-square-dependent-cocone-span-diagram = pr2 (pr2 d)
```

### Postcomposing dependent cocones with maps

```agda
module _
  {l1 l2 l3 l4 l5 : Level} (ğ’® : span-diagram l1 l2 l3)
  {X : UU l4} (c : cocone-span-diagram ğ’® X) (P : X â†’ UU l5)
  where

  dependent-cocone-map-span-diagram :
    ((x : X) â†’ P x) â†’ dependent-cocone-span-diagram ğ’® c P
  pr1 (dependent-cocone-map-span-diagram h) a =
    h (left-map-cocone-span-diagram ğ’® c a)
  pr1 (pr2 (dependent-cocone-map-span-diagram h)) b =
    h (right-map-cocone-span-diagram ğ’® c b)
  pr2 (pr2 (dependent-cocone-map-span-diagram h)) s =
    apd h (coherence-square-cocone-span-diagram ğ’® c s)
```

## Properties

### Characterization of identifications of dependent cocones

```agda
module _
  {l1 l2 l3 l4 l5 : Level} (ğ’® : span-diagram l1 l2 l3)
  {X : UU l4} (c : cocone-span-diagram ğ’® X) (P : X â†’ UU l5)
  (d : dependent-cocone-span-diagram ğ’® c P)
  where

  coherence-htpy-dependent-cocone-span-diagram :
    ( d' : dependent-cocone-span-diagram ğ’® c P)
    ( K :
      left-map-dependent-cocone-span-diagram ğ’® c P d ~
      left-map-dependent-cocone-span-diagram ğ’® c P d')
    ( L :
      right-map-dependent-cocone-span-diagram ğ’® c P d ~
      right-map-dependent-cocone-span-diagram ğ’® c P d') â†’
    UU (l3 âŠ” l5)
  coherence-htpy-dependent-cocone-span-diagram d' K L =
    ( s : spanning-type-span-diagram ğ’®) â†’
    ( ( coherence-square-dependent-cocone-span-diagram ğ’® c P d s) âˆ™
      ( L (right-map-span-diagram ğ’® s))) ï¼
    ( ( ap
        ( tr P (coherence-square-cocone-span-diagram ğ’® c s))
        ( K (left-map-span-diagram ğ’® s))) âˆ™
      ( coherence-square-dependent-cocone-span-diagram ğ’® c P d' s))

  htpy-dependent-cocone-span-diagram :
    (d' : dependent-cocone-span-diagram ğ’® c P) â†’ UU (l1 âŠ” l2 âŠ” l3 âŠ” l5)
  htpy-dependent-cocone-span-diagram d' =
    Î£ ( left-map-dependent-cocone-span-diagram ğ’® c P d ~
        left-map-dependent-cocone-span-diagram ğ’® c P d')
      ( Î» K â†’
        Î£ ( right-map-dependent-cocone-span-diagram ğ’® c P d ~
            right-map-dependent-cocone-span-diagram ğ’® c P d')
          ( coherence-htpy-dependent-cocone-span-diagram d' K))

  refl-htpy-dependent-cocone-span-diagram :
    htpy-dependent-cocone-span-diagram d
  pr1 refl-htpy-dependent-cocone-span-diagram = refl-htpy
  pr1 (pr2 refl-htpy-dependent-cocone-span-diagram) = refl-htpy
  pr2 (pr2 refl-htpy-dependent-cocone-span-diagram) = right-unit-htpy

  htpy-eq-dependent-cocone-span-diagram :
    (d' : dependent-cocone-span-diagram ğ’® c P) â†’
    d ï¼ d' â†’ htpy-dependent-cocone-span-diagram d'
  htpy-eq-dependent-cocone-span-diagram .d refl =
    refl-htpy-dependent-cocone-span-diagram

  module _
    (d' : dependent-cocone-span-diagram ğ’® c P)
    (p : d ï¼ d')
    where

    left-htpy-eq-dependent-cocone-span-diagram :
      left-map-dependent-cocone-span-diagram ğ’® c P d ~
      left-map-dependent-cocone-span-diagram ğ’® c P d'
    left-htpy-eq-dependent-cocone-span-diagram =
      pr1 (htpy-eq-dependent-cocone-span-diagram d' p)

    right-htpy-eq-dependent-cocone-span-diagram :
      right-map-dependent-cocone-span-diagram ğ’® c P d ~
      right-map-dependent-cocone-span-diagram ğ’® c P d'
    right-htpy-eq-dependent-cocone-span-diagram =
      pr1 (pr2 (htpy-eq-dependent-cocone-span-diagram d' p))

    coherence-square-htpy-eq-dependent-cocone-span-diagram :
      coherence-htpy-dependent-cocone-span-diagram d'
        ( left-htpy-eq-dependent-cocone-span-diagram)
        ( right-htpy-eq-dependent-cocone-span-diagram)
    coherence-square-htpy-eq-dependent-cocone-span-diagram =
      pr2 (pr2 (htpy-eq-dependent-cocone-span-diagram d' p))

  abstract
    is-torsorial-htpy-dependent-cocone-span-diagram :
      is-torsorial htpy-dependent-cocone-span-diagram
    is-torsorial-htpy-dependent-cocone-span-diagram =
      is-torsorial-Eq-structure
        ( is-torsorial-htpy
          ( left-map-dependent-cocone-span-diagram ğ’® c P d))
        ( left-map-dependent-cocone-span-diagram ğ’® c P d , refl-htpy)
        ( is-torsorial-Eq-structure
          ( is-torsorial-htpy
            ( right-map-dependent-cocone-span-diagram ğ’® c P d))
          ( right-map-dependent-cocone-span-diagram ğ’® c P d , refl-htpy)
          ( is-contr-equiv
            ( Î£ ( dependent-homotopy
                  ( Î» _ â†’ P)
                  ( coherence-square-cocone-span-diagram ğ’® c)
                  ( left-map-dependent-cocone-span-diagram ğ’® c P d âˆ˜
                    left-map-span-diagram ğ’®)
                  ( right-map-dependent-cocone-span-diagram ğ’® c P d âˆ˜
                    right-map-span-diagram ğ’®))
                ( Î» Î³ â†’
                  coherence-square-dependent-cocone-span-diagram ğ’® c P d ~ Î³))
            ( equiv-tot (equiv-concat-htpy inv-htpy-right-unit-htpy))
            ( is-torsorial-htpy
              ( coherence-square-dependent-cocone-span-diagram ğ’® c P d))))

  abstract
    is-equiv-htpy-eq-dependent-cocone-span-diagram :
      (d' : dependent-cocone-span-diagram ğ’® c P) â†’
      is-equiv (htpy-eq-dependent-cocone-span-diagram d')
    is-equiv-htpy-eq-dependent-cocone-span-diagram =
      fundamental-theorem-id
        ( is-torsorial-htpy-dependent-cocone-span-diagram)
        ( htpy-eq-dependent-cocone-span-diagram)

    eq-htpy-dependent-cocone-span-diagram :
      (d' : dependent-cocone-span-diagram ğ’® c P) â†’
      htpy-dependent-cocone-span-diagram d' â†’ d ï¼ d'
    eq-htpy-dependent-cocone-span-diagram d' =
      map-inv-is-equiv (is-equiv-htpy-eq-dependent-cocone-span-diagram d')

    is-section-eq-htpy-dependent-cocone-span-diagram :
      (d' : dependent-cocone-span-diagram ğ’® c P) â†’
      is-section
        ( htpy-eq-dependent-cocone-span-diagram d')
        ( eq-htpy-dependent-cocone-span-diagram d')
    is-section-eq-htpy-dependent-cocone-span-diagram d' =
      is-section-map-inv-is-equiv
        ( is-equiv-htpy-eq-dependent-cocone-span-diagram d')

    is-retraction-eq-htpy-dependent-cocone-span-diagram :
      (d' : dependent-cocone-span-diagram ğ’® c P) â†’
      is-retraction
        ( htpy-eq-dependent-cocone-span-diagram d')
        ( eq-htpy-dependent-cocone-span-diagram d')
    is-retraction-eq-htpy-dependent-cocone-span-diagram d' =
      is-retraction-map-inv-is-equiv
        ( is-equiv-htpy-eq-dependent-cocone-span-diagram d')

  extensionality-dependent-cocone-span-diagram :
    (d' : dependent-cocone-span-diagram ğ’® c P) â†’
    (d ï¼ d') â‰ƒ htpy-dependent-cocone-span-diagram d'
  pr1 (extensionality-dependent-cocone-span-diagram d') =
    htpy-eq-dependent-cocone-span-diagram d'
  pr2 (extensionality-dependent-cocone-span-diagram d') =
    is-equiv-htpy-eq-dependent-cocone-span-diagram d'
```
