# Identity systems of descent data for pushouts

```agda
{-# OPTIONS --lossy-unification #-}

open import foundation.function-extensionality-axiom

module
  synthetic-homotopy-theory.identity-systems-descent-data-pushouts
  (funext : function-extensionality)
  where
```

<details><summary>Imports</summary>

```agda
open import foundation.commuting-squares-of-maps funext
open import foundation.contractible-types funext
open import foundation.dependent-pair-types
open import foundation.equivalences funext
open import foundation.function-types funext
open import foundation.functoriality-dependent-pair-types funext
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies funext
open import foundation.identity-systems
open import foundation.identity-types funext
open import foundation.sections funext
open import foundation.singleton-induction
open import foundation.span-diagrams funext
open import foundation.torsorial-type-families funext
open import foundation.transposition-identifications-along-equivalences funext
open import foundation.universal-property-dependent-pair-types funext
open import foundation.universal-property-identity-types funext
open import foundation.universe-levels

open import synthetic-homotopy-theory.cocones-under-spans funext
open import synthetic-homotopy-theory.dependent-universal-property-pushouts funext
open import synthetic-homotopy-theory.descent-data-equivalence-types-over-pushouts funext
open import synthetic-homotopy-theory.descent-data-identity-types-over-pushouts funext
open import synthetic-homotopy-theory.descent-data-pushouts funext
open import synthetic-homotopy-theory.descent-property-pushouts funext
open import synthetic-homotopy-theory.equivalences-descent-data-pushouts funext
open import synthetic-homotopy-theory.families-descent-data-pushouts funext
open import synthetic-homotopy-theory.flattening-lemma-pushouts funext
open import synthetic-homotopy-theory.morphisms-descent-data-pushouts funext
open import synthetic-homotopy-theory.sections-descent-data-pushouts funext
open import synthetic-homotopy-theory.universal-property-pushouts funext
```

</details>

## Idea

We define a universal property of
[descent data](synthetic-homotopy-theory.descent-data-pushouts.md) for the
[identity types](foundation-core.identity-types.md) of
[pushouts](synthetic-homotopy-theory.pushouts.md), which allows their
alternative characterizations. The property is analogous to being an
[identity system](foundation.identity-systems.md); in fact, we show that a type
family over a pushout is an identity system if and only if the corresponding
descent data satisfies this universal property.

Given descent data `(PA, PB, PS)` for the pushout

```text
        g
    S -----> B
    |        |
  f |   H    | j
    ‚à®        ‚à®
    A -----> X
        i
```

and a point `p‚ÇÄ : PA a‚ÇÄ` over a basepoint `a‚ÇÄ : A`, we would like to mirror the
definition of identity systems. A na√Øve translation would lead us to define
dependent descent data and its sections. We choose to sidestep building that
technical infrastructure. By the
[descent property](synthetic-homotopy-theory.descent-property-pushouts.md),
there is a [unique](foundation-core.contractible-types.md) type family
`P : X ‚Üí ùí∞`
[corresponding](synthetic-homotopy-theory.families-descent-data-pushouts.md) to
`(PA, PB, PS)`. Observe that the type of dependent type families
`(x : X) ‚Üí (p : P x) ‚Üí ùí∞` is [equivalent](foundation-core.equivalences.md) to
the [uncurried](foundation.universal-property-dependent-pair-types.md) form
`(Œ£ X P) ‚Üí ùí∞`. By the
[flattening lemma](synthetic-homotopy-theory.flattening-lemma-pushouts.md), the
total space `Œ£ X P` is the pushout of the
[span diagram](foundation.span-diagrams.md) of total spaces

```text
  Œ£ A PA <----- Œ£ S (PA ‚àò f) -----> Œ£ B PB,
```

so, again by the descent property, descent data over it correspond to type
families over `Œ£ X P`. Hence we can talk about descent data `(RŒ£A, RŒ£B, RŒ£S)`
over the total span diagram instead of dependent descent data.

Every such descent data induces an evaluation map `ev-refl` on its
[sections](synthetic-homotopy-theory.sections-descent-data-pushouts.md), which
takes a section `(tA, tB, tS)` of `(RŒ£A, RŒ£B, RŒ£S)` to the point
`tA (a‚ÇÄ, p‚ÇÄ) : RŒ£A (a‚ÇÄ, p‚ÇÄ)`. We say that `(PA, PB, PS)` is an
{{#concept "identity system" Disambiguation="descent data for pushouts" Agda=is-identity-system-descent-data-pushout}}
based at `p‚ÇÄ` if `ev-refl` has a [section](foundation-core.sections.md), in the
sense that there is a converse map
`ind-R : RŒ£A (a‚ÇÄ, p‚ÇÄ) ‚Üí section-descent-data (RŒ£A, RŒ£B, RŒ£S)` such that
`(ind-R r)A (a‚ÇÄ, p‚ÇÄ) = r` for all `r : RŒ£A (a‚ÇÄ, p‚ÇÄ)`. Mind the unfortunate
terminology clash between "sections of descent data" and "sections of a map".

Note that this development is biased towards to left --- we pick a basepoint in
the domain `a‚ÇÄ : A`, a point in the left type family `p‚ÇÄ : PA a‚ÇÄ`, and the
evaluation map evaluates the left map of the section. By symmetry of pushouts we
could just as well work with the points `b‚ÇÄ : B`, `p‚ÇÄ : PB b‚ÇÄ`, and the
evaluation map evaluating the right map of the section.

By showing that the canonical
[descent data for identity types](synthetic-homotopy-theory.descent-data-identity-types-over-pushouts.md)
is an identity system, we recover the "induction principle for pushout equality"
stated and proved by Kraus and von Raumer in {{#cite KvR19}}.

First observe that the type of sections of the evaluation map is

```text
  Œ£ (ind-R : (r : RŒ£A (a‚ÇÄ, p‚ÇÄ)) ‚Üí section (RŒ£A, RŒ£B, RŒ£S))
    (is-sect : (r : RŒ£A (a‚ÇÄ, p‚ÇÄ)) ‚Üí (ind-R r)A (a‚ÇÄ, p‚ÇÄ) = r),
```

which is equivalent to the type

```text
  (r : RŒ£A (a‚ÇÄ, p‚ÇÄ)) ‚Üí
  Œ£ (ind : section (RŒ£A, RŒ£B, RŒ£S))
    (preserves-pt : indA (a‚ÇÄ, p‚ÇÄ) = r)
```

by
[distributivity of Œ† over Œ£](foundation-core.type-theoretic-principle-of-choice.md).

Then the induction terms from {{#cite KvR19}} (with names changed to fit our
naming scheme)

```text
  ind·¥¨ : (a : A) (q : ia‚ÇÄ = ia) ‚Üí RŒ£A (a, q)
  ind·¥Æ : (b : B) (q : ia‚ÇÄ = jb) ‚Üí RŒ£B (b, q)
```

are the first and second components of the section of `(RŒ£A, RŒ£B, RŒ£S)` induced
by `r`, and their computation rules

```text
  ind·¥¨ a‚ÇÄ refl = r
  (s : S) (q : ia‚ÇÄ = ifa) ‚Üí RŒ£S (s, q) (ind·¥¨ fs q) = ind·¥Æ gs (q ‚àô H s)
```

arise as the `preserves-pt` component above, and the coherence condition of a
section of `(RŒ£A, RŒ£B, RŒ£S)`, respectively.

## Definitions

### The evaluation map of a section of descent data for pushouts

```agda
module _
  {l1 l2 l3 l4 : Level} {ùíÆ : span-diagram l1 l2 l3}
  (P : descent-data-pushout ùíÆ l4) {a‚ÇÄ : domain-span-diagram ùíÆ}
  (p‚ÇÄ : left-family-descent-data-pushout P a‚ÇÄ)
  where

  ev-refl-section-descent-data-pushout :
    {l5 : Level}
    (R :
      descent-data-pushout (span-diagram-flattening-descent-data-pushout P) l5)
    (t : section-descent-data-pushout R) ‚Üí
    left-family-descent-data-pushout R (a‚ÇÄ , p‚ÇÄ)
  ev-refl-section-descent-data-pushout R t =
    left-map-section-descent-data-pushout R t (a‚ÇÄ , p‚ÇÄ)
```

### The predicate of being an identity system on descent data for pushouts

```agda
module _
  {l1 l2 l3 l4 : Level} {ùíÆ : span-diagram l1 l2 l3}
  (P : descent-data-pushout ùíÆ l4) {a‚ÇÄ : domain-span-diagram ùíÆ}
  (p‚ÇÄ : left-family-descent-data-pushout P a‚ÇÄ)
  where

  is-identity-system-descent-data-pushout : UUœâ
  is-identity-system-descent-data-pushout =
    {l5 : Level}
    (R :
      descent-data-pushout
        ( span-diagram-flattening-descent-data-pushout P)
        ( l5)) ‚Üí
    section (ev-refl-section-descent-data-pushout P p‚ÇÄ R)
```

## Properties

### A type family over a pushout is an identity system if and only if the corresponding descent data is an identity system

Given a family with descent data `P ‚âÉ (PA, PB, PS)` and a point `p‚ÇÄ : PA a‚ÇÄ`, we
show that `(PA, PB, PS)` is an identity system at `p‚ÇÄ` if an only if `P` is an
identity system at `(e·¥æA a)‚Åª¬π p‚ÇÄ : P (ia‚ÇÄ)`.

**Proof:** Consider a family with descent data `RŒ£ ‚âà (RŒ£A, RŒ£B, RŒ£S)`. Recall
that this datum consists of a type family `RŒ£ : Œ£ X P ‚Üí ùí∞`, descent data

```text
  RŒ£A : Œ£ A PA ‚Üí ùí∞
  RŒ£B : Œ£ B PB ‚Üí ùí∞
  RŒ£S : ((s, p) : (Œ£ (s : S) (p : PA fs))) ‚Üí RŒ£A (fs, p) ‚âÉ RŒ£B (gs, PS s p),
```

a pair of equivalences

```text
  e·¥øA : ((a, p) : Œ£ A PA) ‚Üí RŒ£ (ia, (e·¥æA a)‚Åª¬π p) ‚âÉ RŒ£A (a, p)
  e·¥øB : ((b, p) : Œ£ B PB) ‚Üí RŒ£ (jb, (e·¥æB b)‚Åª¬π p) ‚âÉ RŒ£B (b, p),
```

and a coherence between them that isn't relevant here. Then there is a
[commuting square](foundation-core.commuting-squares-of-maps.md)

```text
  (x : X) ‚Üí (p : P x) ‚Üí RŒ£ (x, p) ---> (u : Œ£ X P) ‚Üí RŒ£ u ----> section (RŒ£A, RŒ£B, RŒ£S)
                |                                                           |
                | ev-refl ((e·¥æA a)‚Åª¬π p‚ÇÄ)                                    | ev-refl p‚ÇÄ
                |                                                           |
                ‚à®                                                           ‚à®
      RŒ£ (ia‚ÇÄ, (e·¥æA a)‚Åª¬π p‚ÇÄ) ---------------------------------------> RŒ£A (a‚ÇÄ, p‚ÇÄ).
                                      e·¥øA (a‚ÇÄ, p‚ÇÄ)
```

Since the top and bottom maps are equivalences, we get that the left map has a
section if and only if the right map has a section.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ùíÆ : span-diagram l1 l2 l3}
  {X : UU l4} {c : cocone-span-diagram ùíÆ X}
  (P : family-with-descent-data-pushout c l5)
  {a‚ÇÄ : domain-span-diagram ùíÆ}
  (p‚ÇÄ : left-family-family-with-descent-data-pushout P a‚ÇÄ)
  where

  private
    cocone-flattening :
      cocone-span-diagram
        ( span-diagram-flattening-descent-data-pushout
          ( descent-data-family-with-descent-data-pushout P))
        ( Œ£ X (family-cocone-family-with-descent-data-pushout P))
    cocone-flattening =
      cocone-flattening-descent-data-pushout _ _ c
        ( descent-data-family-with-descent-data-pushout P)
        ( family-cocone-family-with-descent-data-pushout P)
        ( inv-equiv-descent-data-family-with-descent-data-pushout P)

  square-ev-refl-section-descent-data-pushout :
    {l5 : Level}
    (R :
      family-with-descent-data-pushout
        ( cocone-flattening-descent-data-pushout _ _ c
          ( descent-data-family-with-descent-data-pushout P)
          ( family-cocone-family-with-descent-data-pushout P)
          ( inv-equiv-descent-data-pushout _ _
            ( equiv-descent-data-family-with-descent-data-pushout P)))
        ( l5)) ‚Üí
    coherence-square-maps
      ( section-descent-data-section-family-cocone-span-diagram R ‚àò ind-Œ£)
      ( ev-refl-identity-system
        ( inv-left-map-family-with-descent-data-pushout P a‚ÇÄ p‚ÇÄ))
      ( ev-refl-section-descent-data-pushout
        ( descent-data-family-with-descent-data-pushout P)
        ( p‚ÇÄ)
        ( descent-data-family-with-descent-data-pushout R))
      ( left-map-family-with-descent-data-pushout R (a‚ÇÄ , p‚ÇÄ))
  square-ev-refl-section-descent-data-pushout R = refl-htpy
```

To show the forward implication, assume that `(PA, PB, PS)` is an identity
system at `p‚ÇÄ`. We need to show that for every `R : (x : X) (p : P x) ‚Üí ùí∞`, the
evaluation map `ev-refl ((e·¥æA a)‚Åª¬π p‚ÇÄ)` has a section. By the descent property,
there is unique descent data `(RŒ£A, RŒ£B, RŒ£S)` for the uncurried family
`RŒ£ := Œª (x, p) ‚Üí R x p`. Then we get the above square, and by assumption the
right map has a section, hence the left map has a section.

```agda
  abstract
    is-identity-system-is-identity-system-descent-data-pushout :
      universal-property-pushout _ _ c ‚Üí
      is-identity-system-descent-data-pushout
        ( descent-data-family-with-descent-data-pushout P)
        ( p‚ÇÄ) ‚Üí
      is-identity-system
        ( family-cocone-family-with-descent-data-pushout P)
        ( horizontal-map-cocone _ _ c a‚ÇÄ)
        ( inv-left-map-family-with-descent-data-pushout P a‚ÇÄ p‚ÇÄ)
    is-identity-system-is-identity-system-descent-data-pushout
      up-c id-system-P {l} R =
      section-left-map-triangle _ _ _
        ( square-ev-refl-section-descent-data-pushout fam-R)
        ( section-is-equiv
          ( is-equiv-comp _ _
            ( is-equiv-ind-Œ£)
            ( is-equiv-section-descent-data-section-family-cocone-span-diagram
              ( fam-R)
              ( flattening-lemma-descent-data-pushout _ _ c
                ( descent-data-family-with-descent-data-pushout P)
                ( family-cocone-family-with-descent-data-pushout P)
                ( inv-equiv-descent-data-family-with-descent-data-pushout P)
                ( up-c)))))
        ( id-system-P (descent-data-family-with-descent-data-pushout fam-R))
      where
        fam-R : family-with-descent-data-pushout cocone-flattening l
        fam-R =
          family-with-descent-data-pushout-family-cocone
            ( cocone-flattening)
            ( ind-Œ£ R)
```

Similarly, assume `P` is an identity system at `(e·¥æA a)‚Åª¬π p‚ÇÄ`, and assume
descent data `(RŒ£A, RŒ£B, RŒ£S)`. There is a unique corresponding type family
`RŒ£`. Then the square above commutes, and the left map has a section by
assumption, so the right map has a section.

```agda
  abstract
    is-identity-system-descent-data-pushout-is-identity-system :
      universal-property-pushout _ _ c ‚Üí
      is-identity-system
        ( family-cocone-family-with-descent-data-pushout P)
        ( horizontal-map-cocone _ _ c a‚ÇÄ)
        ( inv-left-map-family-with-descent-data-pushout P a‚ÇÄ p‚ÇÄ) ‚Üí
      is-identity-system-descent-data-pushout
        ( descent-data-family-with-descent-data-pushout P)
        ( p‚ÇÄ)
    is-identity-system-descent-data-pushout-is-identity-system
      up-c id-system-P {l} R =
      section-right-map-triangle _ _ _
        ( square-ev-refl-section-descent-data-pushout fam-R)
        ( section-comp _ _
          ( id-system-P
            ( ev-pair (family-cocone-family-with-descent-data-pushout fam-R)))
          ( section-map-equiv
            ( left-equiv-family-with-descent-data-pushout fam-R (a‚ÇÄ , p‚ÇÄ))))
      where
        fam-R : family-with-descent-data-pushout cocone-flattening l
        fam-R =
          family-with-descent-data-pushout-descent-data-pushout
            ( flattening-lemma-descent-data-pushout _ _ c
              ( descent-data-family-with-descent-data-pushout P)
              ( family-cocone-family-with-descent-data-pushout P)
              ( inv-equiv-descent-data-family-with-descent-data-pushout P)
              ( up-c))
            ( R)
```

### The canonical descent data for families of identity types is an identity system

**Proof:** By the above property, the descent data `(IA, IB, IS)` is an identity
system at `refl : ia‚ÇÄ = ia‚ÇÄ` if and only if the corresponding type family
`Id (ia‚ÇÄ) : X ‚Üí ùí∞` is an identity system at `refl`, which is already
established.

```agda
module _
  {l1 l2 l3 l4 : Level} {ùíÆ : span-diagram l1 l2 l3}
  {X : UU l4} {c : cocone-span-diagram ùíÆ X}
  (up-c : universal-property-pushout _ _ c)
  (a‚ÇÄ : domain-span-diagram ùíÆ)
  where

  abstract
    induction-principle-descent-data-pushout-identity-type :
      is-identity-system-descent-data-pushout
        ( descent-data-identity-type-pushout c (horizontal-map-cocone _ _ c a‚ÇÄ))
        ( refl)
    induction-principle-descent-data-pushout-identity-type =
      is-identity-system-descent-data-pushout-is-identity-system
        ( family-with-descent-data-identity-type-pushout c
          ( horizontal-map-cocone _ _ c a‚ÇÄ))
        ( refl)
        ( up-c)
        ( is-identity-system-is-torsorial
          ( horizontal-map-cocone _ _ c a‚ÇÄ)
          ( refl)
          ( is-torsorial-Id _))
```

### Unique uniqueness of identity systems

For any identity system `(PA, PB, PS)` at `p‚ÇÄ`, there is a unique
[equivalence of descent data](synthetic-homotopy-theory.equivalences-descent-data-pushouts.md)
`(IA, IB, IS) ‚âÉ (PA, PB, PS)` sending `refl` to `p‚ÇÄ`.

**Proof:** Consider the unique type family `P : X ‚Üí ùí∞` corresponding to
`(PA, PB, PS).` The type of point preserving equivalences between `(IA, IB, IS)`
and `(PA, PB, PS)` is equivalent to the type of
[fiberwise equivalences](foundation-core.families-of-equivalences.md)
`(x : X) ‚Üí (ia‚ÇÄ = x) ‚âÉ P x` that send `refl` to `(e·¥æA a‚ÇÄ)‚Åª¬π p‚ÇÄ`. To show that
this type is contractible, it suffices to show that `P` is
[torsorial](foundation-core.torsorial-type-families.md). A type family is
torsorial if it is an identity system, and we have shown that `(PA, PB, PS)`
being an identity system implies that `P` is an identity system.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ùíÆ : span-diagram l1 l2 l3}
  {X : UU l4} {c : cocone-span-diagram ùíÆ X}
  (up-c : universal-property-pushout _ _ c)
  (P : descent-data-pushout ùíÆ l5) {a‚ÇÄ : domain-span-diagram ùíÆ}
  (p‚ÇÄ : left-family-descent-data-pushout P a‚ÇÄ)
  (id-system-P : is-identity-system-descent-data-pushout P p‚ÇÄ)
  where

  abstract
    unique-uniqueness-identity-system-descent-data-pushout :
      is-contr
        ( Œ£ ( equiv-descent-data-pushout
              ( descent-data-identity-type-pushout c
                ( horizontal-map-cocone _ _ c a‚ÇÄ))
              ( P))
            ( Œª e ‚Üí left-map-equiv-descent-data-pushout _ _ e a‚ÇÄ refl Ôºù p‚ÇÄ))
    unique-uniqueness-identity-system-descent-data-pushout =
      is-contr-is-equiv'
        ( Œ£ ( (x : X) ‚Üí
              (horizontal-map-cocone _ _ c a‚ÇÄ Ôºù x) ‚âÉ
              family-cocone-family-with-descent-data-pushout fam-P x)
            ( Œª e ‚Üí map-equiv (e (horizontal-map-cocone _ _ c a‚ÇÄ)) refl Ôºù p‚ÇÄ'))
        ( _)
        ( is-equiv-map-Œ£ _
          ( is-equiv-equiv-descent-data-equiv-family-cocone-span-diagram
            ( family-with-descent-data-identity-type-pushout c
              ( horizontal-map-cocone _ _ c a‚ÇÄ))
            ( fam-P)
            ( up-c))
          ( Œª e ‚Üí
            is-equiv-map-inv-equiv
              ( eq-transpose-equiv
                ( left-equiv-family-with-descent-data-pushout fam-P a‚ÇÄ)
                ( _)
                ( p‚ÇÄ))))
        ( is-torsorial-pointed-fam-equiv-is-torsorial p‚ÇÄ'
          ( is-torsorial-is-identity-system
            ( horizontal-map-cocone _ _ c a‚ÇÄ)
            ( p‚ÇÄ')
            ( is-identity-system-is-identity-system-descent-data-pushout
              ( fam-P)
              ( p‚ÇÄ)
              ( up-c)
              ( id-system-P))))
      where
      fam-P : family-with-descent-data-pushout c l5
      fam-P = family-with-descent-data-pushout-descent-data-pushout up-c P
      p‚ÇÄ' :
        family-cocone-family-with-descent-data-pushout
          ( fam-P)
          ( horizontal-map-cocone _ _ c a‚ÇÄ)
      p‚ÇÄ' =
        map-compute-inv-left-family-cocone-descent-data-pushout up-c P a‚ÇÄ p‚ÇÄ
```

### Descent data with a converse to the evaluation map of sections of descent data is an identity system

To show that `(PA, PB, PS)` is an identity system at `p‚ÇÄ : PA a‚ÇÄ`, it suffices
to provide an element of the type `H : RŒ£A (a‚ÇÄ, p‚ÇÄ) ‚Üí section (RŒ£A, RŒ£B, RŒ£S)`
for all `(RŒ£A, RŒ£B, RŒ£S)`.

**Proof:** Consider the unique family `P : X ‚Üí ùí∞` for `(PA, PB, PS)`. It
suffices to show that `P` is an identity system. As above, we can do that by
showing that it is torsorial. By definition, that means that the total space
`Œ£ X P` is contractible. We can prove that using the property that a type is
contractible if we provide a point, here `(ia‚ÇÄ, (e·¥æA a)‚Åª¬π p‚ÇÄ)`, and a map

```text
  H' : (RŒ£ : Œ£ X P ‚Üí ùí∞) ‚Üí (r‚ÇÄ : RŒ£ (ia‚ÇÄ, (e·¥æA a)‚Åª¬π p‚ÇÄ)) ‚Üí (u : Œ£ X P) ‚Üí RŒ£ u.
```

Assume such `RŒ£` and `r‚ÇÄ`. A section `(u : Œ£ X P) ‚Üí RŒ£ u` is given by a section
of `(RŒ£A, RŒ£B, RŒ£S)`, and we can get one by applying `H` to
`e·¥øA (a‚ÇÄ, p‚ÇÄ) r‚ÇÄ : RŒ£A (a‚ÇÄ, p‚ÇÄ)`.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ùíÆ : span-diagram l1 l2 l3}
  {X : UU l4} {c : cocone-span-diagram ùíÆ X}
  (up-c : universal-property-pushout _ _ c)
  (P : descent-data-pushout ùíÆ l5) {a‚ÇÄ : domain-span-diagram ùíÆ}
  (p‚ÇÄ : left-family-descent-data-pushout P a‚ÇÄ)
  where

  abstract
    is-identity-system-descent-data-pushout-ind-singleton :
      (H :
        {l6 : Level}
        (R :
          descent-data-pushout
            ( span-diagram-flattening-descent-data-pushout P)
            ( l6))
        (r‚ÇÄ : left-family-descent-data-pushout R (a‚ÇÄ , p‚ÇÄ)) ‚Üí
        section-descent-data-pushout R) ‚Üí
      is-identity-system-descent-data-pushout P p‚ÇÄ
    is-identity-system-descent-data-pushout-ind-singleton H =
      is-identity-system-descent-data-pushout-is-identity-system
        ( family-with-descent-data-pushout-descent-data-pushout up-c P)
        ( p‚ÇÄ)
        ( up-c)
        ( is-identity-system-is-torsorial
          ( horizontal-map-cocone _ _ c a‚ÇÄ)
          ( p‚ÇÄ')
          ( is-contr-ind-singleton _
            ( horizontal-map-cocone _ _ c a‚ÇÄ , p‚ÇÄ')
            ( Œª R r‚ÇÄ ‚Üí
              section-family-section-descent-data-pushout
                ( flattening-lemma-descent-data-pushout _ _ c P
                  ( family-cocone-descent-data-pushout up-c P)
                  ( inv-equiv-family-cocone-descent-data-pushout up-c P)
                  ( up-c))
                ( family-with-descent-data-pushout-family-cocone _ R)
                ( H (descent-data-family-cocone-span-diagram _ R) r‚ÇÄ))))
      where
        p‚ÇÄ' :
          family-cocone-descent-data-pushout up-c P
            ( horizontal-map-cocone _ _ c a‚ÇÄ)
        p‚ÇÄ' =
          map-compute-inv-left-family-cocone-descent-data-pushout up-c P a‚ÇÄ p‚ÇÄ
```
