# Identity systems of descent data for pushouts

```agda
{-# OPTIONS --lossy-unification #-}

module synthetic-homotopy-theory.identity-systems-descent-data-pushouts where
```

<details><summary>Imports</summary>

```agda
open import foundation.commuting-squares-of-maps
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.identity-systems
open import foundation.identity-types
open import foundation.sections
open import foundation.singleton-induction
open import foundation.span-diagrams
open import foundation.torsorial-type-families
open import foundation.transposition-identifications-along-equivalences
open import foundation.universal-property-dependent-pair-types
open import foundation.universal-property-identity-types
open import foundation.universe-levels

open import synthetic-homotopy-theory.cocones-under-spans
open import synthetic-homotopy-theory.dependent-universal-property-pushouts
open import synthetic-homotopy-theory.descent-data-equivalence-types-over-pushouts
open import synthetic-homotopy-theory.descent-data-identity-types-over-pushouts
open import synthetic-homotopy-theory.descent-data-pushouts
open import synthetic-homotopy-theory.descent-pushouts
open import synthetic-homotopy-theory.equivalences-descent-data-pushouts
open import synthetic-homotopy-theory.families-descent-data-pushouts
open import synthetic-homotopy-theory.flattening-lemma-pushouts
open import synthetic-homotopy-theory.morphisms-descent-data-pushouts
open import synthetic-homotopy-theory.sections-descent-data-pushouts
open import synthetic-homotopy-theory.universal-property-pushouts
```

</details>

## Idea

We define a universal property of
[descent data](synthetic-homotopy-theory.descent-data-pushouts.md) for the
[identity types](foundation-core.identity-types.md) of
[pushouts](synthetic-homotopy-theory.pushouts.md), which allows their
alternative characterizations. The property is analogous to being an
[identity system](foundation.identity-systems.md); in fact, we show that a type
family over a pushout is an identity system if and only if the corresponding
descent data satisfies this universal property.

Given descent data `(PA, PB, PS)` for the pushout

```text
        g
    S -----> B
    |        |
  f |   H    | j
    âˆ¨        âˆ¨
    A -----> X
        i
```

and a point `pâ‚€ : PA aâ‚€` over a basepoint `aâ‚€ : A`, we would like to mirror the
definition of identity systems. A naÃ¯ve translation would lead us to define
dependent descent data and its sections. We choose to sidestep building that
technical infrastructure. By the
[descent property](synthetic-homotopy-theory.descent-pushouts.md), there is a
[unique](foundation-core.contractible-types.md) type family `P : X â†’ ğ’°`
[corresponding](synthetic-homotopy-theory.families-descent-data-pushouts.md) to
`(PA, PB, PS)`. Observe that the type of dependent type families
`(x : X) â†’ (p : P x) â†’ ğ’°` is [equivalent](foundation-core.equivalences.md) to
the [uncurried](foundation.universal-property-dependent-pair-types.md) form
`(Î£ X P) â†’ ğ’°`. By the
[flattening lemma](synthetic-homotopy-theory.flattening-lemma-pushouts.md), the
total space `Î£ X P` is the pushout of the
[span diagram](foundation.span-diagrams.md) of total spaces

```text
  Î£ A PA <----- Î£ S (PA âˆ˜ f) -----> Î£ B PB,
```

so, again by the descent property, descent data over it correspond to type
families over `Î£ X P`. Hence we can talk about descent data `(RÎ£A, RÎ£B, RÎ£S)`
over the total span diagram instead of dependent descent data.

Every such descent data induces an evaluation map `ev-refl` on its
[sections](synthetic-homotopy-theory.sections-descent-data-pushouts.md), which
takes a section `(tA, tB, tS)` of `(RÎ£A, RÎ£B, RÎ£S)` to the point
`tA (aâ‚€, pâ‚€) : RÎ£A (aâ‚€, pâ‚€)`. We say that `(PA, PB, PS)` is an
{{#concept "identity system" Disambiguation="descent data for pushouts" Agda=is-identity-system-descent-data-pushout}}
based at `pâ‚€` if `ev-refl` has a [section](foundation-core.sections.md), in the
sense that there is a converse map
`ind-R : RÎ£A (aâ‚€, pâ‚€) â†’ section-descent-data (RÎ£A, RÎ£B, RÎ£S)` such that
`(ind-R r)A (aâ‚€, pâ‚€) = r` for all `r : RÎ£A (aâ‚€, pâ‚€)`. Mind the unfortunate
terminology clash between "sections of descent data" and "sections of a map".

Note that this development is biased towards to left --- we pick a basepoint in
the domain `aâ‚€ : A`, a point in the left type family `pâ‚€ : PA aâ‚€`, and the
evaluation map evaluates the left map of the section. By symmetry of pushouts we
could just as well work with the points `bâ‚€ : B`, `pâ‚€ : PB bâ‚€`, and the
evaluation map evaluating the right map of the section.

By showing that the canonical
[descent data for identity types](synthetic-homotopy-theory.descent-data-identity-types-over-pushouts.md)
is an identity system, we recover the "induction principle for pushout equality"
stated and proved by Kraus and von Raumer in {{#cite KvR19}}.

First observe that the type of sections of the evaluation map is

```text
  Î£ (ind-R : (r : RÎ£A (aâ‚€, pâ‚€)) â†’ section (RÎ£A, RÎ£B, RÎ£S))
    (is-sect : (r : RÎ£A (aâ‚€, pâ‚€)) â†’ (ind-R r)A (aâ‚€, pâ‚€) = r),
```

which is equivalent to the type

```text
  (r : RÎ£A (aâ‚€, pâ‚€)) â†’
  Î£ (ind : section (RÎ£A, RÎ£B, RÎ£S))
    (preserves-pt : indA (aâ‚€, pâ‚€) = r)
```

by
[distributivity of Î  over Î£](foundation-core.type-theoretic-principle-of-choice.md).

Then the induction terms from {{#cite KvR19}} (with names changed to fit our
naming scheme)

```text
  indá´¬ : (a : A) (q : iaâ‚€ = ia) â†’ RÎ£A (a, q)
  indá´® : (b : B) (q : iaâ‚€ = jb) â†’ RÎ£B (b, q)
```

are the first and second components of the section of `(RÎ£A, RÎ£B, RÎ£S)` induced
by `r`, and their computation rules

```text
  indá´¬ aâ‚€ refl = r
  (s : S) (q : iaâ‚€ = ifa) â†’ RÎ£S (s, q) (indá´¬ fs q) = indá´® gs (q âˆ™ H s)
```

arise as the `preserves-pt` component above, and the coherence condition of a
section of `(RÎ£A, RÎ£B, RÎ£S)`, respectively.

## Definitions

### The evaluation map of a section of descent data for pushouts

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ğ’® : span-diagram l1 l2 l3}
  (P : descent-data-pushout ğ’® l4 l5) {aâ‚€ : domain-span-diagram ğ’®}
  (pâ‚€ : left-family-descent-data-pushout P aâ‚€)
  where

  ev-refl-section-descent-data-pushout :
    {l6 l7 : Level}
    (R :
      descent-data-pushout
        ( span-diagram-flattening-descent-data-pushout P)
        ( l6)
        ( l7))
    (t : section-descent-data-pushout R) â†’
    left-family-descent-data-pushout R (aâ‚€ , pâ‚€)
  ev-refl-section-descent-data-pushout R t =
    left-map-section-descent-data-pushout R t (aâ‚€ , pâ‚€)
```

### The predicate of being an identity system on descent data for pushouts

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ğ’® : span-diagram l1 l2 l3}
  (P : descent-data-pushout ğ’® l4 l5) {aâ‚€ : domain-span-diagram ğ’®}
  (pâ‚€ : left-family-descent-data-pushout P aâ‚€)
  where

  is-identity-system-descent-data-pushout : UUÏ‰
  is-identity-system-descent-data-pushout =
    {l6 : Level}
    (R :
      descent-data-pushout
        ( span-diagram-flattening-descent-data-pushout P)
        ( l6)
        ( l6)) â†’
    section (ev-refl-section-descent-data-pushout P pâ‚€ R)
```

## Properties

### A type family over a pushout is an identity system if and only if the corresponding descent data is an identity system

Given a family with descent data `P â‰ƒ (PA, PB, PS)` and a point `pâ‚€ : PA aâ‚€`, we
show that `(PA, PB, PS)` is an identity system at `pâ‚€` if an only if `P` is an
identity system at `(eá´¾A a)â»Â¹ pâ‚€ : P (iaâ‚€)`.

**Proof:** Consider a family with descent data `RÎ£ â‰ˆ (RÎ£A, RÎ£B, RÎ£S)`. Recall
that this datum consists of a type family `RÎ£ : Î£ X P â†’ ğ’°`, descent data

```text
  RÎ£A : Î£ A PA â†’ ğ’°
  RÎ£B : Î£ B PB â†’ ğ’°
  RÎ£S : ((s, p) : (Î£ (s : S) (p : PA fs))) â†’ RÎ£A (fs, p) â‰ƒ RÎ£B (gs, PS s p),
```

a pair of equivalences

```text
  eá´¿A : ((a, p) : Î£ A PA) â†’ RÎ£ (ia, (eá´¾A a)â»Â¹ p) â‰ƒ RÎ£A (a, p)
  eá´¿B : ((b, p) : Î£ B PB) â†’ RÎ£ (jb, (eá´¾B b)â»Â¹ p) â‰ƒ RÎ£B (b, p),
```

and a coherence between them that isn't relevant here. Then there is a
[commuting square](foundation-core.commuting-squares-of-maps.md)

```text
  (x : X) â†’ (p : P x) â†’ RÎ£ (x, p) ---> (u : Î£ X P) â†’ RÎ£ u ----> section (RÎ£A, RÎ£B, RÎ£S)
                |                                                           |
                | ev-refl ((eá´¾A a)â»Â¹ pâ‚€)                                    | ev-refl pâ‚€
                |                                                           |
                âˆ¨                                                           âˆ¨
      RÎ£ (iaâ‚€, (eá´¾A a)â»Â¹ pâ‚€) ---------------------------------------> RÎ£A (aâ‚€, pâ‚€).
                                      eá´¿A (aâ‚€, pâ‚€)
```

Since the top and bottom maps are equivalences, we get that the left map has a
section if and only if the right map has a section.

```agda
module _
  {l1 l2 l3 l4 l5 l6 l7 : Level} {ğ’® : span-diagram l1 l2 l3}
  {X : UU l4} {c : cocone-span-diagram ğ’® X}
  (P : family-with-descent-data-pushout c l5 l6 l7)
  {aâ‚€ : domain-span-diagram ğ’®}
  (pâ‚€ : left-family-family-with-descent-data-pushout P aâ‚€)
  where

  private
    cocone-flattening :
      cocone-span-diagram
        ( span-diagram-flattening-descent-data-pushout
          ( descent-data-family-with-descent-data-pushout P))
        ( Î£ X (family-cocone-family-with-descent-data-pushout P))
    cocone-flattening =
      cocone-flattening-descent-data-pushout _ _ c
        ( descent-data-family-with-descent-data-pushout P)
        ( family-cocone-family-with-descent-data-pushout P)
        ( inv-equiv-descent-data-family-with-descent-data-pushout P)

  square-ev-refl-section-descent-data-pushout :
    {l5 l6 l7 : Level}
    (R :
      family-with-descent-data-pushout
        ( cocone-flattening-descent-data-pushout _ _ c
          ( descent-data-family-with-descent-data-pushout P)
          ( family-cocone-family-with-descent-data-pushout P)
          ( inv-equiv-descent-data-pushout _ _
            ( equiv-descent-data-family-with-descent-data-pushout P)))
        ( l5)
        ( l6)
        ( l7)) â†’
    coherence-square-maps
      ( section-descent-data-section-family-cocone-span-diagram R âˆ˜ ind-Î£)
      ( ev-refl-identity-system
        ( inv-left-map-family-with-descent-data-pushout P aâ‚€ pâ‚€))
      ( ev-refl-section-descent-data-pushout
        ( descent-data-family-with-descent-data-pushout P)
        ( pâ‚€)
        ( descent-data-family-with-descent-data-pushout R))
      ( left-map-family-with-descent-data-pushout R (aâ‚€ , pâ‚€))
  square-ev-refl-section-descent-data-pushout R = refl-htpy
```

To show the forward implication, assume that `(PA, PB, PS)` is an identity
system at `pâ‚€`. We need to show that for every `R : (x : X) (p : P x) â†’ ğ’°`, the
evaluation map `ev-refl ((eá´¾A a)â»Â¹ pâ‚€)` has a section. By the descent property,
there is unique descent data `(RÎ£A, RÎ£B, RÎ£S)` for the uncurried family
`RÎ£ := Î» (x, p) â†’ R x p`. Then we get the above square, and by assumption the
right map has a section, hence the left map has a section.

```agda
  abstract
    is-identity-system-is-identity-system-descent-data-pushout :
      universal-property-pushout _ _ c â†’
      is-identity-system-descent-data-pushout
        ( descent-data-family-with-descent-data-pushout P)
        ( pâ‚€) â†’
      is-identity-system
        ( family-cocone-family-with-descent-data-pushout P)
        ( horizontal-map-cocone _ _ c aâ‚€)
        ( inv-left-map-family-with-descent-data-pushout P aâ‚€ pâ‚€)
    is-identity-system-is-identity-system-descent-data-pushout
      up-c id-system-P {l} R =
      section-left-map-triangle _ _ _
        ( square-ev-refl-section-descent-data-pushout fam-R)
        ( section-is-equiv
          ( is-equiv-comp _ _
            ( is-equiv-ind-Î£)
            ( is-equiv-section-descent-data-section-family-cocone-span-diagram
              ( fam-R)
              ( flattening-lemma-descent-data-pushout _ _ c
                ( descent-data-family-with-descent-data-pushout P)
                ( family-cocone-family-with-descent-data-pushout P)
                ( inv-equiv-descent-data-family-with-descent-data-pushout P)
                ( up-c)))))
        ( id-system-P (descent-data-family-with-descent-data-pushout fam-R))
      where
        fam-R : family-with-descent-data-pushout cocone-flattening l l l
        fam-R =
          family-with-descent-data-pushout-family-cocone
            ( cocone-flattening)
            ( ind-Î£ R)
```

Similarly, assume `P` is an identity system at `(eá´¾A a)â»Â¹ pâ‚€`, and assume
descent data `(RÎ£A, RÎ£B, RÎ£S)`. There is a unique corresponding type family
`RÎ£`. Then the square above commutes, and the left map has a section by
assumption, so the right map has a section.

```agda
  abstract
    is-identity-system-descent-data-pushout-is-identity-system :
      universal-property-pushout _ _ c â†’
      is-identity-system
        ( family-cocone-family-with-descent-data-pushout P)
        ( horizontal-map-cocone _ _ c aâ‚€)
        ( inv-left-map-family-with-descent-data-pushout P aâ‚€ pâ‚€) â†’
      is-identity-system-descent-data-pushout
        ( descent-data-family-with-descent-data-pushout P)
        ( pâ‚€)
    is-identity-system-descent-data-pushout-is-identity-system
      up-c id-system-P {l} R =
      section-right-map-triangle _ _ _
        ( square-ev-refl-section-descent-data-pushout fam-R)
        ( section-comp _ _
          ( id-system-P
            ( ev-pair (family-cocone-family-with-descent-data-pushout fam-R)))
          ( section-map-equiv
            ( left-equiv-family-with-descent-data-pushout fam-R (aâ‚€ , pâ‚€))))
      where
        fam-R : family-with-descent-data-pushout cocone-flattening l l l
        fam-R =
          family-with-descent-data-pushout-descent-data-pushout
            ( flattening-lemma-descent-data-pushout _ _ c
              ( descent-data-family-with-descent-data-pushout P)
              ( family-cocone-family-with-descent-data-pushout P)
              ( inv-equiv-descent-data-family-with-descent-data-pushout P)
              ( up-c))
            ( R)
```

### The canonical descent data for families of identity types is an identity system

**Proof:** By the above property, the descent data `(IA, IB, IS)` is an identity
system at `refl : iaâ‚€ = iaâ‚€` if and only if the corresponding type family
`Id (iaâ‚€) : X â†’ ğ’°` is an identity system at `refl`, which is already
established.

```agda
module _
  {l1 l2 l3 l4 : Level} {ğ’® : span-diagram l1 l2 l3}
  {X : UU l4} {c : cocone-span-diagram ğ’® X}
  (up-c : universal-property-pushout _ _ c)
  (aâ‚€ : domain-span-diagram ğ’®)
  where

  abstract
    induction-principle-descent-data-pushout-identity-type :
      is-identity-system-descent-data-pushout
        ( descent-data-identity-type-pushout c (horizontal-map-cocone _ _ c aâ‚€))
        ( refl)
    induction-principle-descent-data-pushout-identity-type =
      is-identity-system-descent-data-pushout-is-identity-system
        ( family-with-descent-data-identity-type-pushout c
          ( horizontal-map-cocone _ _ c aâ‚€))
        ( refl)
        ( up-c)
        ( is-identity-system-is-torsorial
          ( horizontal-map-cocone _ _ c aâ‚€)
          ( refl)
          ( is-torsorial-Id _))
```

### Unique uniqueness of identity systems

For any identity system `(PA, PB, PS)` at `pâ‚€`, there is a unique
[equivalence of descent data](synthetic-homotopy-theory.equivalences-descent-data-pushouts.md)
`(IA, IB, IS) â‰ƒ (PA, PB, PS)` sending `refl` to `pâ‚€`.

**Proof:** Consider the unique type family `P : X â†’ ğ’°` corresponding to
`(PA, PB, PS).` The type of point preserving equivalences between `(IA, IB, IS)`
and `(PA, PB, PS)` is equivalent to the type of
[fiberwise equivalences](foundation-core.families-of-equivalences.md)
`(x : X) â†’ (iaâ‚€ = x) â‰ƒ P x` that send `refl` to `(eá´¾A aâ‚€)â»Â¹ pâ‚€`. To show that
this type is contractible, it suffices to show that `P` is
[torsorial](foundation-core.torsorial-type-families.md). A type family is
torsorial if it is an identity system, and we have shown that `(PA, PB, PS)`
being an identity system implies that `P` is an identity system.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ğ’® : span-diagram l1 l2 l3}
  {X : UU l4} {c : cocone-span-diagram ğ’® X}
  (up-c : universal-property-pushout _ _ c)
  (P : descent-data-pushout ğ’® l5 l5) {aâ‚€ : domain-span-diagram ğ’®}
  (pâ‚€ : left-family-descent-data-pushout P aâ‚€)
  (id-system-P : is-identity-system-descent-data-pushout P pâ‚€)
  where

  abstract
    unique-uniqueness-identity-system-descent-data-pushout :
      is-contr
        ( Î£ ( equiv-descent-data-pushout
              ( descent-data-identity-type-pushout c
                ( horizontal-map-cocone _ _ c aâ‚€))
              ( P))
            ( Î» e â†’ left-map-equiv-descent-data-pushout _ _ e aâ‚€ refl ï¼ pâ‚€))
    unique-uniqueness-identity-system-descent-data-pushout =
      is-contr-is-equiv'
        ( Î£ ( (x : X) â†’
              (horizontal-map-cocone _ _ c aâ‚€ ï¼ x) â‰ƒ
              family-cocone-family-with-descent-data-pushout fam-P x)
            ( Î» e â†’ map-equiv (e (horizontal-map-cocone _ _ c aâ‚€)) refl ï¼ pâ‚€'))
        ( _)
        ( is-equiv-map-Î£ _
          ( is-equiv-equiv-descent-data-equiv-family-cocone-span-diagram
            ( family-with-descent-data-identity-type-pushout c
              ( horizontal-map-cocone _ _ c aâ‚€))
            ( fam-P)
            ( up-c))
          ( Î» e â†’
            is-equiv-map-inv-equiv
              ( eq-transpose-equiv
                ( left-equiv-family-with-descent-data-pushout fam-P aâ‚€)
                ( _)
                ( pâ‚€))))
        ( is-torsorial-pointed-fam-equiv-is-torsorial pâ‚€'
          ( is-torsorial-is-identity-system
            ( horizontal-map-cocone _ _ c aâ‚€)
            ( pâ‚€')
            ( is-identity-system-is-identity-system-descent-data-pushout
              ( fam-P)
              ( pâ‚€)
              ( up-c)
              ( id-system-P))))
      where
      fam-P : family-with-descent-data-pushout c l5 l5 l5
      fam-P = family-with-descent-data-pushout-descent-data-pushout up-c P
      pâ‚€' :
        family-cocone-family-with-descent-data-pushout
          ( fam-P)
          ( horizontal-map-cocone _ _ c aâ‚€)
      pâ‚€' =
        map-compute-inv-left-family-cocone-descent-data-pushout up-c P aâ‚€ pâ‚€
```

### Descent data with a converse to the evaluation map of sections of descent data is an identity system

To show that `(PA, PB, PS)` is an identity system at `pâ‚€ : PA aâ‚€`, it suffices
to provide an element of the type `H : RÎ£A (aâ‚€, pâ‚€) â†’ section (RÎ£A, RÎ£B, RÎ£S)`
for all `(RÎ£A, RÎ£B, RÎ£S)`.

**Proof:** Consider the unique family `P : X â†’ ğ’°` for `(PA, PB, PS)`. It
suffices to show that `P` is an identity system. As above, we can do that by
showing that it is torsorial. By definition, that means that the total space
`Î£ X P` is contractible. We can prove that using the property that a type is
contractible if we provide a point, here `(iaâ‚€, (eá´¾A a)â»Â¹ pâ‚€)`, and a map

```text
  H' : (RÎ£ : Î£ X P â†’ ğ’°) â†’ (râ‚€ : RÎ£ (iaâ‚€, (eá´¾A a)â»Â¹ pâ‚€)) â†’ (u : Î£ X P) â†’ RÎ£ u.
```

Assume such `RÎ£` and `râ‚€`. A section `(u : Î£ X P) â†’ RÎ£ u` is given by a section
of `(RÎ£A, RÎ£B, RÎ£S)`, and we can get one by applying `H` to
`eá´¿A (aâ‚€, pâ‚€) râ‚€ : RÎ£A (aâ‚€, pâ‚€)`.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ğ’® : span-diagram l1 l2 l3}
  {X : UU l4} {c : cocone-span-diagram ğ’® X}
  (up-c : universal-property-pushout _ _ c)
  (P : descent-data-pushout ğ’® l5 l5) {aâ‚€ : domain-span-diagram ğ’®}
  (pâ‚€ : left-family-descent-data-pushout P aâ‚€)
  where

  abstract
    is-identity-system-descent-data-pushout-ind-singleton :
      (H :
        {l6 : Level}
        (R :
          descent-data-pushout
            ( span-diagram-flattening-descent-data-pushout P)
            ( l6)
            ( l6))
        (râ‚€ : left-family-descent-data-pushout R (aâ‚€ , pâ‚€)) â†’
        section-descent-data-pushout R) â†’
      is-identity-system-descent-data-pushout P pâ‚€
    is-identity-system-descent-data-pushout-ind-singleton H =
      is-identity-system-descent-data-pushout-is-identity-system
        ( family-with-descent-data-pushout-descent-data-pushout up-c P)
        ( pâ‚€)
        ( up-c)
        ( is-identity-system-is-torsorial
          ( horizontal-map-cocone _ _ c aâ‚€)
          ( pâ‚€')
          ( is-contr-ind-singleton _
            ( horizontal-map-cocone _ _ c aâ‚€ , pâ‚€')
            ( Î» R râ‚€ â†’
              section-family-section-descent-data-pushout
                ( flattening-lemma-descent-data-pushout _ _ c P
                  ( family-cocone-descent-data-pushout up-c P)
                  ( inv-equiv-family-cocone-descent-data-pushout up-c P)
                  ( up-c))
                ( family-with-descent-data-pushout-family-cocone _ R)
                ( H (descent-data-family-cocone-span-diagram _ R) râ‚€))))
      where
        pâ‚€' :
          family-cocone-descent-data-pushout up-c P
            ( horizontal-map-cocone _ _ c aâ‚€)
        pâ‚€' =
          map-compute-inv-left-family-cocone-descent-data-pushout up-c P aâ‚€ pâ‚€
```
