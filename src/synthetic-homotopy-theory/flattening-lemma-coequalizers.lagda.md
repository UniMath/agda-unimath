# The flattening lemma for coequalizers

```agda
module synthetic-homotopy-theory.flattening-lemma-coequalizers where
```

<details><summary>Imports</summary>

```agda
open import foundation.commuting-triangles-of-maps
open import foundation.dependent-pair-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.transport-along-identifications
open import foundation.universal-property-dependent-pair-types
open import foundation.universe-levels

open import synthetic-homotopy-theory.coforks
open import synthetic-homotopy-theory.dependent-coforks
open import synthetic-homotopy-theory.dependent-universal-property-coequalizers
open import synthetic-homotopy-theory.universal-property-coequalizers
```

</details>

## Idea

The **flattening lemma** for
[coequalizers](synthetic-homotopy-theory.coequalizers.md) states that
coequalizers commute with
[dependent pair types](foundation.dependent-pair-types.md). More precisely,
given a coequalizer

```text
     g
   ----->     e
 A -----> B -----> X
     f
```

with homotopy `H : e ‚àò f ~ e ‚àò g`, and a type family `P : X ‚Üí ùì§` over `X`, the
cofork

```text
                 ----->
Œ£ (a : A) P(efa) -----> Œ£ (b : B) P(eb) ---> Œ£ (x : X) P(x)
```

is again a coequalizer.

## Definitions

### The statement of the flattening lemma for coequalizers

```agda
module _
  { l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} (f g : A ‚Üí B) {X : UU l3}
  ( P : X ‚Üí UU l4) (e : cofork f g X)
  where

  bottom-map-cofork-flattening-lemma-coequalizer :
    Œ£ A (P ‚àò map-cofork f g e ‚àò f) ‚Üí Œ£ B (P ‚àò map-cofork f g e)
  bottom-map-cofork-flattening-lemma-coequalizer =
    map-Œ£-map-base f (P ‚àò map-cofork f g e)

  top-map-cofork-flattening-lemma-coequalizer :
    Œ£ A (P ‚àò map-cofork f g e ‚àò f) ‚Üí Œ£ B (P ‚àò map-cofork f g e)
  top-map-cofork-flattening-lemma-coequalizer =
    map-Œ£ (P ‚àò map-cofork f g e) g (Œª a ‚Üí tr P (coherence-cofork f g e a))

  cofork-flattening-lemma-coequalizer :
    cofork
      ( bottom-map-cofork-flattening-lemma-coequalizer)
      ( top-map-cofork-flattening-lemma-coequalizer)
      ( Œ£ X P)
  pr1 cofork-flattening-lemma-coequalizer = map-Œ£-map-base (map-cofork f g e) P
  pr2 cofork-flattening-lemma-coequalizer =
    coherence-square-maps-map-Œ£-map-base P g f
      ( map-cofork f g e)
      ( map-cofork f g e)
      ( coherence-cofork f g e)

  flattening-lemma-coequalizer-statement : UUœâ
  flattening-lemma-coequalizer-statement =
    ( {l : Level} ‚Üí dependent-universal-property-coequalizer l f g e) ‚Üí
    { l : Level} ‚Üí
    universal-property-coequalizer l
      ( bottom-map-cofork-flattening-lemma-coequalizer)
      ( top-map-cofork-flattening-lemma-coequalizer)
      ( cofork-flattening-lemma-coequalizer)
```

## Properties

### Proof of the flattening lemma for coequalizers

The proof is analogous to the one of the
[flattening lemma for pushouts](synthetic-homotopy-theory.flattening-lemma-pushouts.md).

```agda
module _
  { l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} (f g : A ‚Üí B) {X : UU l3}
  ( P : X ‚Üí UU l4) (e : cofork f g X)
  where

  cofork-map-flattening-coequalizer :
    { l : Level} (Y : UU l) ‚Üí
    ( Œ£ X P ‚Üí Y) ‚Üí
    cofork
      ( bottom-map-cofork-flattening-lemma-coequalizer f g P e)
      ( top-map-cofork-flattening-lemma-coequalizer f g P e)
      ( Y)
  cofork-map-flattening-coequalizer Y =
    cofork-map
      ( bottom-map-cofork-flattening-lemma-coequalizer f g P e)
      ( top-map-cofork-flattening-lemma-coequalizer f g P e)
      ( cofork-flattening-lemma-coequalizer f g P e)

  comparison-dependent-cofork-ind-Œ£-cofork :
    { l : Level} (Y : UU l) ‚Üí
    Œ£ ( (b : B) ‚Üí P (map-cofork f g e b) ‚Üí Y)
      ( Œª k ‚Üí
        ( a : A) (t : P (map-cofork f g e (f a))) ‚Üí
        ( k (f a) t) Ôºù
        ( k (g a) (tr P (coherence-cofork f g e a) t))) ‚âÉ
    dependent-cofork f g e (Œª x ‚Üí P x ‚Üí Y)
  comparison-dependent-cofork-ind-Œ£-cofork Y =
    equiv-tot
      ( Œª k ‚Üí
        equiv-Œ†-equiv-family
          ( equiv-htpy-dependent-function-dependent-identification-function-type
            ( Y)
            ( coherence-cofork f g e)
            ( k ‚àò f)
            ( k ‚àò g)))

  triangle-comparison-dependent-cofork-ind-Œ£-cofork :
    { l : Level} (Y : UU l) ‚Üí
    coherence-triangle-maps
      ( dependent-cofork-map f g e {P = (Œª x ‚Üí P x ‚Üí Y)})
      ( map-equiv (comparison-dependent-cofork-ind-Œ£-cofork Y))
      ( map-equiv equiv-ev-pair¬≤ ‚àò cofork-map-flattening-coequalizer Y ‚àò ind-Œ£)
  triangle-comparison-dependent-cofork-ind-Œ£-cofork Y h =
    eq-pair-Œ£
      ( refl)
      ( eq-htpy
        ( inv-htpy
          ( compute-equiv-htpy-dependent-function-dependent-identification-function-type
            ( Y)
            ( coherence-cofork f g e)
            ( h))))

  flattening-lemma-coequalizer :
    flattening-lemma-coequalizer-statement f g P e
  flattening-lemma-coequalizer dup-coequalizer Y =
    is-equiv-left-factor
      ( cofork-map-flattening-coequalizer Y)
      ( ind-Œ£)
      ( is-equiv-right-factor
        ( map-equiv equiv-ev-pair¬≤)
        ( cofork-map-flattening-coequalizer Y ‚àò ind-Œ£)
        ( is-equiv-map-equiv equiv-ev-pair¬≤)
        ( is-equiv-right-factor-htpy
          ( dependent-cofork-map f g e {P = (Œª x ‚Üí P x ‚Üí Y)})
          ( map-equiv (comparison-dependent-cofork-ind-Œ£-cofork Y))
          ( ( map-equiv equiv-ev-pair¬≤) ‚àò
            ( cofork-map-flattening-coequalizer Y) ‚àò
            ( ind-Œ£))
          ( triangle-comparison-dependent-cofork-ind-Œ£-cofork Y)
          ( is-equiv-map-equiv (comparison-dependent-cofork-ind-Œ£-cofork Y))
          ( dup-coequalizer (Œª x ‚Üí P x ‚Üí Y))))
      ( is-equiv-ind-Œ£)
```
