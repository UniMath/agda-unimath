# Pushouts

```agda
module synthetic-homotopy-theory.pushouts where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-dependent-functions
open import foundation.action-on-identifications-functions
open import foundation.commuting-squares-of-maps
open import foundation.constant-type-families
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.fibers-of-maps
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.propositions
open import foundation.retractions
open import foundation.sections
open import foundation.span-diagrams
open import foundation.transport-along-homotopies
open import foundation.transport-along-identifications
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import reflection.erasing-equality

open import synthetic-homotopy-theory.cocones-under-spans
open import synthetic-homotopy-theory.dependent-cocones-under-spans
open import synthetic-homotopy-theory.dependent-universal-property-pushouts
open import synthetic-homotopy-theory.flattening-lemma-pushouts
open import synthetic-homotopy-theory.induction-principle-pushouts
open import synthetic-homotopy-theory.pullback-property-pushouts
open import synthetic-homotopy-theory.universal-property-pushouts
```

</details>

## Idea

Consider a span `ğ’®` of types

```text
      f     g
  A <--- S ---> B.
```

A **pushout** of `ğ’®` is an initial type `X` equipped with a
[cocone structure](synthetic-homotopy-theory.cocones-under-spans.md) of `ğ’®` in
`X`. In other words, a pushout `X` of `ğ’®` comes equipped with a cocone structure
`(i , j , H)` where

```text
        g
    S -----> B
    |        |
  f |   H    | j
    âˆ¨        âˆ¨
    A -----> X,
        i
```

such that for any type `Y`, the following evaluation map is an equivalence

```text
  (X â†’ Y) â†’ cocone ğ’® Y.
```

This condition is the
[universal property of the pushout](synthetic-homotopy-theory.universal-property-pushouts.md)
of `ğ’®`.

The idea is that the pushout of `ğ’®` is the universal type that contains the
elements of the types `A` and `B` via the 'inclusions' `i : A â†’ X` and
`j : B â†’ X`, and furthermore an identification `i a ï¼ j b` for every `s : S`
such that `f s ï¼ a` and `g s ï¼ b`.

Examples of pushouts include
[suspensions](synthetic-homotopy-theory.suspensions-of-types.md),
[spheres](synthetic-homotopy-theory.spheres.md),
[joins](synthetic-homotopy-theory.joins-of-types.md), and the
[smash product](synthetic-homotopy-theory.smash-products-of-pointed-types.md).

## Postulates

### The standard pushout type

We will assume that for any span

```text
      f     g
  A <--- S ---> B,
```

where `S : UU l1`, `A : UU l2`, and `B : UU l3` there is a pushout in
`UU (l1 âŠ” l2 âŠ” l3)`.

```agda
postulate
  pushout :
    {l1 l2 l3 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
    (f : S â†’ A) (g : S â†’ B) â†’ UU (l1 âŠ” l2 âŠ” l3)

postulate
  inl-pushout :
    {l1 l2 l3 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
    (f : S â†’ A) (g : S â†’ B) â†’ A â†’ pushout f g

postulate
  inr-pushout :
    {l1 l2 l3 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
    (f : S â†’ A) (g : S â†’ B) â†’ B â†’ pushout f g

postulate
  glue-pushout :
    {l1 l2 l3 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
    (f : S â†’ A) (g : S â†’ B) â†’ inl-pushout f g âˆ˜ f ~ inr-pushout f g âˆ˜ g

cocone-pushout :
  {l1 l2 l3 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) â†’ cocone f g (pushout f g)
pr1 (cocone-pushout f g) = inl-pushout f g
pr1 (pr2 (cocone-pushout f g)) = inr-pushout f g
pr2 (pr2 (cocone-pushout f g)) = glue-pushout f g

module _
  {l1 l2 l3 : Level} (ğ’® : span-diagram l1 l2 l3)
  where

  standard-pushout : UU (l1 âŠ” l2 âŠ” l3)
  standard-pushout =
    pushout (left-map-span-diagram ğ’®) (right-map-span-diagram ğ’®)

  inl-standard-pushout : domain-span-diagram ğ’® â†’ standard-pushout
  inl-standard-pushout =
    inl-pushout (left-map-span-diagram ğ’®) (right-map-span-diagram ğ’®)

  inr-standard-pushout : codomain-span-diagram ğ’® â†’ standard-pushout
  inr-standard-pushout =
    inr-pushout (left-map-span-diagram ğ’®) (right-map-span-diagram ğ’®)

  glue-standard-pushout :
    coherence-square-maps
      ( right-map-span-diagram ğ’®)
      ( left-map-span-diagram ğ’®)
      ( inr-standard-pushout)
      ( inl-standard-pushout)
  glue-standard-pushout =
    glue-pushout (left-map-span-diagram ğ’®) (right-map-span-diagram ğ’®)

  cocone-pushout-span-diagram :
    cocone-span-diagram ğ’® standard-pushout
  pr1 cocone-pushout-span-diagram = inl-standard-pushout
  pr1 (pr2 cocone-pushout-span-diagram) = inr-standard-pushout
  pr2 (pr2 cocone-pushout-span-diagram) = glue-standard-pushout
```

### The dependent cogap map

We postulate the constituents of a section of
`dependent-cocone-map f g (cocone-pushout f g)` up to homotopy of dependent
cocones. This is, assuming
[function extensionality](foundation.function-extensionality.md), precisely the
data of the induction principle of pushouts. We write out each component
separately to accommodate
[optional rewrite rules for the standard pushouts](synthetic-homotopy-theory.rewriting-pushouts.md).

```agda
module _
  {l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) {P : pushout f g â†’ UU l4}
  (c : dependent-cocone f g (cocone-pushout f g) P)
  where

  postulate
    dependent-cogap : (x : pushout f g) â†’ P x

  compute-inl-dependent-cogap :
    (a : A) â†’
    dependent-cogap (inl-pushout f g a) ï¼
    horizontal-map-dependent-cocone f g (cocone-pushout f g) P c a
  compute-inl-dependent-cogap a = primEraseEquality compute-inl-dependent-cogap'
    where postulate
      compute-inl-dependent-cogap' :
        dependent-cogap (inl-pushout f g a) ï¼
        horizontal-map-dependent-cocone f g (cocone-pushout f g) P c a

  compute-inr-dependent-cogap :
    (b : B) â†’
    dependent-cogap (inr-pushout f g b) ï¼
    vertical-map-dependent-cocone f g (cocone-pushout f g) P c b
  compute-inr-dependent-cogap b = primEraseEquality compute-inr-dependent-cogap'
    where postulate
      compute-inr-dependent-cogap' :
        dependent-cogap (inr-pushout f g b) ï¼
        vertical-map-dependent-cocone f g (cocone-pushout f g) P c b

  postulate
    compute-glue-dependent-cogap :
      coherence-htpy-dependent-cocone f g
        ( cocone-pushout f g)
        ( P)
        ( dependent-cocone-map f g (cocone-pushout f g) P dependent-cogap)
        ( c)
        ( compute-inl-dependent-cogap)
        ( compute-inr-dependent-cogap)

  htpy-compute-dependent-cogap :
    htpy-dependent-cocone f g
      ( cocone-pushout f g)
      ( P)
      ( dependent-cocone-map f g (cocone-pushout f g) P dependent-cogap)
      ( c)
  htpy-compute-dependent-cogap =
    ( compute-inl-dependent-cogap ,
      compute-inr-dependent-cogap ,
      compute-glue-dependent-cogap)
```

## Definitions

### The induction principle of standard pushouts

```agda
module _
  {l1 l2 l3 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B)
  where

  is-section-dependent-cogap :
    {l : Level} {P : pushout f g â†’ UU l} â†’
    is-section
      ( dependent-cocone-map f g (cocone-pushout f g) P)
      ( dependent-cogap f g)
  is-section-dependent-cogap {P = P} c =
    eq-htpy-dependent-cocone f g
      ( cocone-pushout f g)
      ( P)
      ( dependent-cocone-map f g (cocone-pushout f g) P (dependent-cogap f g c))
      ( c)
      ( htpy-compute-dependent-cogap f g c)

  induction-principle-pushout' :
    induction-principle-pushout f g (cocone-pushout f g)
  induction-principle-pushout' P =
    ( dependent-cogap f g , is-section-dependent-cogap)

  is-retraction-dependent-cogap :
    {l : Level} {P : pushout f g â†’ UU l} â†’
    is-retraction
      ( dependent-cocone-map f g (cocone-pushout f g) P)
      ( dependent-cogap f g)
  is-retraction-dependent-cogap {P = P} =
    is-retraction-ind-induction-principle-pushout f g
      ( cocone-pushout f g)
      ( induction-principle-pushout')
      ( P)
```

### The dependent universal property of standard pushouts

```agda
module _
  {l1 l2 l3 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B)
  where

  dup-pushout :
    dependent-universal-property-pushout f g (cocone-pushout f g)
  dup-pushout =
    dependent-universal-property-pushout-induction-principle-pushout f g
      ( cocone-pushout f g)
      ( induction-principle-pushout' f g)

  equiv-dup-pushout :
    {l : Level} (P : pushout f g â†’ UU l) â†’
    ((x : pushout f g) â†’ P x) â‰ƒ dependent-cocone f g (cocone-pushout f g) P
  pr1 (equiv-dup-pushout P) = dependent-cocone-map f g (cocone-pushout f g) P
  pr2 (equiv-dup-pushout P) = dup-pushout P
```

### The cogap map

We define `cogap` and its computation rules in terms of `dependent-cogap` to
ensure the proper computational behavior when in the presence of strict
computation laws on the point constructors of the standard pushouts.

```agda
module _
  {l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) {X : UU l4}
  where

  cogap : cocone f g X â†’ pushout f g â†’ X
  cogap =
    dependent-cogap f g âˆ˜
    dependent-cocone-constant-type-family-cocone f g (cocone-pushout f g)

  is-section-cogap : is-section (cocone-map f g (cocone-pushout f g)) cogap
  is-section-cogap =
    ( ( triangle-dependent-cocone-map-constant-type-family' f g
        ( cocone-pushout f g)) Â·r
      ( cogap)) âˆ™h
    ( ( cocone-dependent-cocone-constant-type-family f g
        ( cocone-pushout f g)) Â·l
      ( is-section-dependent-cogap f g) Â·r
      ( dependent-cocone-constant-type-family-cocone f g
        ( cocone-pushout f g))) âˆ™h
    ( is-retraction-cocone-dependent-cocone-constant-type-family f g
      ( cocone-pushout f g))

  is-retraction-cogap :
    is-retraction (cocone-map f g (cocone-pushout f g)) cogap
  is-retraction-cogap =
    ( ( cogap) Â·l
      ( triangle-dependent-cocone-map-constant-type-family' f g
        ( cocone-pushout f g))) âˆ™h
    ( ( dependent-cogap f g) Â·l
      ( is-section-cocone-dependent-cocone-constant-type-family f g
        ( cocone-pushout f g)) Â·r
      ( dependent-cocone-map f g (cocone-pushout f g) (Î» _ â†’ X))) âˆ™h
    ( is-retraction-dependent-cogap f g)
```

### The universal property of standard pushouts

```agda
up-pushout :
  {l1 l2 l3 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) â†’
  universal-property-pushout f g (cocone-pushout f g)
up-pushout f g P =
  is-equiv-is-invertible
    ( cogap f g)
    ( is-section-cogap f g)
    ( is-retraction-cogap f g)

equiv-up-pushout :
  {l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) (X : UU l4) â†’ (pushout f g â†’ X) â‰ƒ (cocone f g X)
pr1 (equiv-up-pushout f g X) = cocone-map f g (cocone-pushout f g)
pr2 (equiv-up-pushout f g X) = up-pushout f g X
```

### Computation with the cogap map

We define the computation witnesses for the cogap map in terms of the
computation witnesses for the dependent cogap map so that when
[rewriting is enabled for pushouts](synthetic-homotopy-theory.rewriting-pushouts.md),
these witnesses reduce to reflexivities.

```agda
module _
  { l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  ( f : S â†’ A) (g : S â†’ B)
  { X : UU l4} (c : cocone f g X)
  where

  compute-inl-cogap :
    cogap f g c âˆ˜ inl-pushout f g ~ horizontal-map-cocone f g c
  compute-inl-cogap =
    compute-inl-dependent-cogap f g
      ( dependent-cocone-constant-type-family-cocone f g (cocone-pushout f g) c)

  compute-inr-cogap :
    cogap f g c âˆ˜ inr-pushout f g ~ vertical-map-cocone f g c
  compute-inr-cogap =
    compute-inr-dependent-cogap f g
      ( dependent-cocone-constant-type-family-cocone f g (cocone-pushout f g) c)
```

<!-- TODO: find the right infrastructure to make the proof below just an application of a more general construction. Note that this is very almost `coherence-htpy-cocone-coherence-htpy-dependent-cocone-constant-type-family`, but an `apd-constant-type-family` has snuck its way into the proof Issue#1120 -->

```agda
  abstract
    compute-glue-cogap :
      statement-coherence-htpy-cocone f g
        ( cocone-map f g (cocone-pushout f g) (cogap f g c))
        ( c)
        ( compute-inl-cogap)
        ( compute-inr-cogap)
    compute-glue-cogap x =
      is-injective-concat
        ( tr-constant-type-family
          ( glue-pushout f g x)
          ( cogap f g c ((inl-pushout f g âˆ˜ f) x)))
        ( ( inv
            ( assoc
              ( tr-constant-type-family
                ( glue-pushout f g x)
                ( cogap f g c
                  ( horizontal-map-cocone f g (cocone-pushout f g) (f x))))
              ( ap (cogap f g c) (glue-pushout f g x))
              ( compute-inr-cogap (g x)))) âˆ™
          ( ap
            ( _âˆ™ compute-inr-cogap (g x))
            ( inv
              ( apd-constant-type-family (cogap f g c) (glue-pushout f g x)))) âˆ™
          ( compute-glue-dependent-cogap f g
            ( dependent-cocone-constant-type-family-cocone f g
              ( cocone-pushout f g)
              ( c))
            ( x)) âˆ™
          ( inv
            ( assoc
              ( ap
                ( tr (Î» _ â†’ X) (glue-pushout f g x))
                ( compute-inl-cogap (f x)))
              ( tr-constant-type-family
                ( glue-pushout f g x)
                ( pr1 c (f x)))
              ( coherence-square-cocone f g c x))) âˆ™
          ( ap
            ( _âˆ™ coherence-square-cocone f g c x)
            ( inv
              ( naturality-tr-constant-type-family
                ( glue-pushout f g x)
                ( compute-inl-cogap (f x))))) âˆ™
            ( assoc
              ( tr-constant-type-family
                ( glue-pushout f g x)
                ( cogap f g c (inl-pushout f g (f x))))
              ( compute-inl-cogap (f x))
              ( coherence-square-cocone f g c x)))

  htpy-compute-cogap :
    htpy-cocone f g
      ( cocone-map f g (cocone-pushout f g) (cogap f g c))
      ( c)
  htpy-compute-cogap =
    ( compute-inl-cogap , compute-inr-cogap , compute-glue-cogap)
```

### The small predicate of being a pushout cocone

```agda
module _
  {l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) {X : UU l4} (c : cocone f g X)
  where

  is-pushout : UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  is-pushout = is-equiv (cogap f g c)

  is-prop-is-pushout : is-prop is-pushout
  is-prop-is-pushout = is-property-is-equiv (cogap f g c)

  is-pushout-Prop : Prop (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  pr1 is-pushout-Prop = is-pushout
  pr2 is-pushout-Prop = is-prop-is-pushout
```

## Properties

### Pushout cocones satisfy the universal property of the pushout

```agda
module _
  {l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) {X : UU l4} (c : cocone f g X)
  where

  universal-property-pushout-is-pushout :
    is-pushout f g c â†’ universal-property-pushout f g c
  universal-property-pushout-is-pushout po =
    up-pushout-up-pushout-is-equiv f g
      ( cocone-pushout f g)
      ( c)
      ( cogap f g c)
      ( htpy-cocone-map-universal-property-pushout f g
        ( cocone-pushout f g)
        ( up-pushout f g)
        ( c))
      ( po)
      ( up-pushout f g)

  is-pushout-universal-property-pushout :
    universal-property-pushout f g c â†’ is-pushout f g c
  is-pushout-universal-property-pushout =
    is-equiv-up-pushout-up-pushout f g
      ( cocone-pushout f g)
      ( c)
      ( cogap f g c)
      ( htpy-cocone-map-universal-property-pushout f g
        ( cocone-pushout f g)
        ( up-pushout f g)
        ( c))
      ( up-pushout f g)
```

### Pushout cocones satisfy the pullback property of the pushout

```agda
module _
  {l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) {X : UU l4} (c : cocone f g X)
  where

  abstract
    pullback-property-pushout-is-pushout :
      is-pushout f g c â†’ pullback-property-pushout f g c
    pullback-property-pushout-is-pushout po =
      pullback-property-pushout-universal-property-pushout f g c
        ( universal-property-pushout-is-pushout f g c po)

  abstract
    is-pushout-pullback-property-pushout :
      pullback-property-pushout f g c â†’ is-pushout f g c
    is-pushout-pullback-property-pushout pb =
      is-pushout-universal-property-pushout f g c
        ( universal-property-pushout-pullback-property-pushout f g c pb)
```

### Fibers of the cogap map

We characterize the [fibers](foundation-core.fibers-of-maps.md) of the cogap map
as a pushout of fibers. This is an application of the
[flattening lemma for pushouts](synthetic-homotopy-theory.flattening-lemma-pushouts.md).

Given a pushout square with a
[cocone](synthetic-homotopy-theory.cocones-under-spans.md)

```text
       g
   S ----> B
   |       | \
 f |    inr|  \  n
   âˆ¨     âŒœ âˆ¨   \
   A ----> âˆ™    \
    \ inl   \   |
  m  \  cogap\  |
      \       âˆ¨ âˆ¨
       \-----> X
```

we have, for every `x : X`, a pushout square of fibers:

```text
    fiber (m âˆ˜ f) x ---> fiber (cogap âˆ˜ inr) x
           |                       |
           |                       |
           âˆ¨                     âŒœ âˆ¨
 fiber (cogap âˆ˜ inl) x ----> fiber cogap x
```

```agda
module _
  { l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  ( f : S â†’ A) (g : S â†’ B)
  { X : UU l4} (c : cocone f g X) (x : X)
  where

  equiv-fiber-horizontal-map-cocone-cogap-inl-horizontal-span :
    fiber (horizontal-map-cocone f g c âˆ˜ f) x â‰ƒ
    fiber (cogap f g c âˆ˜ inl-pushout f g âˆ˜ f) x
  equiv-fiber-horizontal-map-cocone-cogap-inl-horizontal-span =
    equiv-tot (Î» s â†’ equiv-concat (compute-inl-cogap f g c (f s)) x)

  equiv-fiber-horizontal-map-cocone-cogap-inl :
    fiber (horizontal-map-cocone f g c) x â‰ƒ
    fiber (cogap f g c âˆ˜ inl-pushout f g) x
  equiv-fiber-horizontal-map-cocone-cogap-inl =
    equiv-tot (Î» a â†’ equiv-concat (compute-inl-cogap f g c a) x)

  equiv-fiber-vertical-map-cocone-cogap-inr :
    fiber (vertical-map-cocone f g c) x â‰ƒ
    fiber (cogap f g c âˆ˜ inr-pushout f g) x
  equiv-fiber-vertical-map-cocone-cogap-inr =
    equiv-tot (Î» b â†’ equiv-concat (compute-inr-cogap f g c b) x)

  horizontal-map-span-cogap-fiber :
    fiber (horizontal-map-cocone f g c âˆ˜ f) x â†’
    fiber (horizontal-map-cocone f g c) x
  horizontal-map-span-cogap-fiber =
    map-Î£-map-base f (Î» a â†’ horizontal-map-cocone f g c a ï¼ x)
```

Since our pushout square of fibers has `fiber (m âˆ˜ f) x` as its top-left corner
and not `fiber (n âˆ˜ g) x`, things are "left-biased". For this reason, the
following map is constructed as a composition which makes a later coherence
square commute (almost) trivially.

```agda
  vertical-map-span-cogap-fiber :
    fiber (horizontal-map-cocone f g c âˆ˜ f) x â†’
    fiber (vertical-map-cocone f g c) x
  vertical-map-span-cogap-fiber =
    ( map-inv-equiv equiv-fiber-vertical-map-cocone-cogap-inr) âˆ˜
    ( horizontal-map-span-flattening-pushout
      ( Î» y â†’ (cogap f g c y) ï¼ x) f g (cocone-pushout f g)) âˆ˜
    ( map-equiv equiv-fiber-horizontal-map-cocone-cogap-inl-horizontal-span)

  statement-universal-property-pushout-cogap-fiber : UUÏ‰
  statement-universal-property-pushout-cogap-fiber =
    { l : Level} â†’
    Î£ ( cocone
        ( horizontal-map-span-cogap-fiber)
        ( vertical-map-span-cogap-fiber)
        ( fiber (cogap f g c) x))
      ( universal-property-pushout-Level l
        ( horizontal-map-span-cogap-fiber)
        ( vertical-map-span-cogap-fiber))

  universal-property-pushout-cogap-fiber :
    statement-universal-property-pushout-cogap-fiber
  universal-property-pushout-cogap-fiber =
    universal-property-pushout-extension-by-equivalences
      ( vertical-map-span-flattening-pushout
        ( Î» y â†’ cogap f g c y ï¼ x)
        ( f)
        ( g)
        ( cocone-pushout f g))
      ( horizontal-map-span-flattening-pushout
        ( Î» y â†’ cogap f g c y ï¼ x)
        ( f)
        ( g)
        ( cocone-pushout f g))
      ( horizontal-map-span-cogap-fiber)
      ( vertical-map-span-cogap-fiber)
      ( map-equiv equiv-fiber-horizontal-map-cocone-cogap-inl)
      ( map-equiv equiv-fiber-vertical-map-cocone-cogap-inr)
      ( map-equiv equiv-fiber-horizontal-map-cocone-cogap-inl-horizontal-span)
      ( cocone-flattening-pushout
        ( Î» y â†’ cogap f g c y ï¼ x)
        ( f)
        ( g)
        ( cocone-pushout f g))
      ( flattening-lemma-pushout
        ( Î» y â†’ cogap f g c y ï¼ x)
        ( f)
        ( g)
        ( cocone-pushout f g)
        ( up-pushout f g))
      ( refl-htpy)
      ( Î» _ â†’
        inv
          ( is-section-map-inv-equiv
            ( equiv-fiber-vertical-map-cocone-cogap-inr)
            ( _)))
      ( is-equiv-map-equiv equiv-fiber-horizontal-map-cocone-cogap-inl)
      ( is-equiv-map-equiv equiv-fiber-vertical-map-cocone-cogap-inr)
      ( is-equiv-map-equiv
        ( equiv-fiber-horizontal-map-cocone-cogap-inl-horizontal-span))
```

We record the following auxiliary lemma which says that if we have types `T`,
`F` and `G` such that `T â‰ƒ fiber (m âˆ˜ f) x`, `F â‰ƒ fiber (cogap âˆ˜ inl) x` and
`G â‰ƒ fiber (cogap âˆ˜ inr) x`, together with suitable maps `u : T â†’ F` and
`v : T â†’ G`, then we get a pushout square:

```text
          v
   T ----------> G
   |             |
 u |             |
   âˆ¨           âŒœ âˆ¨
   F ----> fiber cogap x
```

Thus, this lemma is useful in case we have convenient descriptions of the
fibers.

```agda
  module _
    { l5 l6 l7 : Level} (T : UU l5) (F : UU l6) (G : UU l7)
    ( i : F â‰ƒ fiber (horizontal-map-cocone f g c) x)
    ( j : G â‰ƒ fiber (vertical-map-cocone f g c) x)
    ( k : T â‰ƒ fiber (horizontal-map-cocone f g c âˆ˜ f) x)
    ( u : T â†’ F)
    ( v : T â†’ G)
    ( coh-l :
      coherence-square-maps
        ( map-equiv k)
        ( u)
        ( horizontal-map-span-cogap-fiber)
        ( map-equiv i))
    ( coh-r :
      coherence-square-maps
        ( v)
        ( map-equiv k)
        ( map-equiv j)
        ( vertical-map-span-cogap-fiber))
    where

    universal-property-pushout-cogap-fiber-up-to-equiv :
      { l : Level} â†’
      ( Î£ ( cocone u v (fiber (cogap f g c) x))
          ( Î» c â†’ universal-property-pushout-Level l u v c))
    universal-property-pushout-cogap-fiber-up-to-equiv {l} =
      universal-property-pushout-extension-by-equivalences
        ( horizontal-map-span-cogap-fiber)
        ( vertical-map-span-cogap-fiber)
        ( u)
        ( v)
        ( map-equiv i)
        ( map-equiv j)
        ( map-equiv k)
        ( pr1 ( universal-property-pushout-cogap-fiber {l}))
        ( pr2 universal-property-pushout-cogap-fiber)
        ( coh-l)
        ( coh-r)
        ( is-equiv-map-equiv i)
        ( is-equiv-map-equiv j)
        ( is-equiv-map-equiv k)
```

### Swapping a pushout cocone yields another pushout cocone

```agda
module _
  {l1 l2 l3 l4 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  (f : S â†’ A) (g : S â†’ B) (X : UU l4) (c : cocone f g X)
  where

  universal-property-pushout-swap-cocone-universal-property-pushout :
    universal-property-pushout f g c â†’
    universal-property-pushout g f (swap-cocone f g X c)
  universal-property-pushout-swap-cocone-universal-property-pushout up Y =
    is-equiv-equiv'
      ( id-equiv)
      ( equiv-swap-cocone f g Y)
      ( Î» h â†’
        eq-htpy-cocone g f
          ( swap-cocone f g Y (cocone-map f g c h))
          ( cocone-map g f (swap-cocone f g X c) h)
          ( ( refl-htpy) ,
            ( refl-htpy) ,
            ( Î» s â†’
              right-unit âˆ™ inv (ap-inv h (coherence-square-cocone f g c s)))))
      ( up Y)

  is-pushout-swap-cocone-is-pushout :
    is-pushout f g c â†’ is-pushout g f (swap-cocone f g X c)
  is-pushout-swap-cocone-is-pushout po =
    is-pushout-universal-property-pushout g f
      ( swap-cocone f g X c)
      ( universal-property-pushout-swap-cocone-universal-property-pushout
        ( universal-property-pushout-is-pushout f g c po))
```
