# Equivalences of cocones under equivalences of span diagrams

```agda
module
  synthetic-homotopy-theory.equivalences-cocones-under-equivalences-span-diagrams
  where
```

<details><summary>Imports</summary>

```agda
open import foundation.commuting-cubes-of-maps
open import foundation.commuting-squares-of-maps
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.equivalences-span-diagrams
open import foundation.homotopies
open import foundation.span-diagrams
open import foundation.universe-levels

open import synthetic-homotopy-theory.cocones-under-span-diagrams
open import synthetic-homotopy-theory.morphisms-cocones-under-morphisms-span-diagrams
```

</details>

## Idea

Consider an
[equivalence of span diagrams](foundation.equivalences-span-diagrams.md)
`e : equiv-span-diagram s t` and
[cocones](synthetic-homotopy-theory.cocones-under-span-diagrams.md) `c` with
vertex `X` and `d` with vertex `Y` on `ğ’®` and `t`, respectively. An
{{#concept "equivalence of cocones under an equivalence of span diagrams"}} from
`c` to `d` under `e` consists of an
[equivalence](foundation-core.equivalences.md) `e : X â‰ƒ Y` such that the cube

```text
              S
            / | \
          /   |   \
        /   hS|     \
      âˆ¨       âˆ¨       âˆ¨
     A        T        B
     | \    /   \    / |
  hA |   \/       \/   | hB
     |  /  \     /  \  |
     âˆ¨âˆ¨      âˆ¨ âˆ¨      âˆ¨âˆ¨
     A'       X        B'
       \      |      /
         \    |u   /
           \  |  /
             âˆ¨âˆ¨âˆ¨
              Y
```

[commutes](foundation.commuting-cubes-of-maps.md).

## Definitions

```agda
module _
  {l1 l2 l3 l4 l5 l6 l7 l8 : Level}
  (ğ’® : span-diagram l1 l2 l3) {X : UU l4} (c : cocone-span-diagram ğ’® X)
  (ğ’¯ : span-diagram l5 l6 l7) {Y : UU l8} (d : cocone-span-diagram ğ’¯ Y)
  (e : equiv-span-diagram ğ’® ğ’¯)
  where

  left-coherence-square-equiv-cocone-equiv-span-diagram :
    (X â‰ƒ Y) â†’ UU (l1 âŠ” l8)
  left-coherence-square-equiv-cocone-equiv-span-diagram u =
    coherence-square-maps
      ( left-map-cocone-span-diagram ğ’® c)
      ( map-domain-equiv-span-diagram ğ’® ğ’¯ e)
      ( map-equiv u)
      ( left-map-cocone-span-diagram ğ’¯ d)

  right-coherence-square-equiv-cocone-equiv-span-diagram :
    (X â‰ƒ Y) â†’ UU (l2 âŠ” l8)
  right-coherence-square-equiv-cocone-equiv-span-diagram u =
    coherence-square-maps
      ( right-map-cocone-span-diagram ğ’® c)
      ( map-codomain-equiv-span-diagram ğ’® ğ’¯ e)
      ( map-equiv u)
      ( right-map-cocone-span-diagram ğ’¯ d)

  coherence-cube-equiv-cocone-equiv-span-diagram :
    (u : X â‰ƒ Y) â†’
    left-coherence-square-equiv-cocone-equiv-span-diagram u â†’
    right-coherence-square-equiv-cocone-equiv-span-diagram u â†’ UU (l3 âŠ” l8)
  coherence-cube-equiv-cocone-equiv-span-diagram u L R =
    coherence-cube-maps
      ( left-map-span-diagram ğ’¯)
      ( right-map-span-diagram ğ’¯)
      ( left-map-cocone-span-diagram ğ’¯ d)
      ( right-map-cocone-span-diagram ğ’¯ d)
      ( left-map-span-diagram ğ’®)
      ( right-map-span-diagram ğ’®)
      ( left-map-cocone-span-diagram ğ’® c)
      ( right-map-cocone-span-diagram ğ’® c)
      ( spanning-map-equiv-span-diagram ğ’® ğ’¯ e)
      ( map-domain-equiv-span-diagram ğ’® ğ’¯ e)
      ( map-codomain-equiv-span-diagram ğ’® ğ’¯ e)
      ( map-equiv u)
      ( coherence-square-cocone-span-diagram ğ’® c)
      ( inv-htpy (left-square-equiv-span-diagram ğ’® ğ’¯ e))
      ( inv-htpy (right-square-equiv-span-diagram ğ’® ğ’¯ e))
      ( L)
      ( R)
      ( coherence-square-cocone-span-diagram ğ’¯ d)

  equiv-cocone-equiv-span-diagram : UU (l1 âŠ” l2 âŠ” l3 âŠ” l4 âŠ” l8)
  equiv-cocone-equiv-span-diagram =
    Î£ ( X â‰ƒ Y)
      ( Î» u â†’
        Î£ ( left-coherence-square-equiv-cocone-equiv-span-diagram u)
          ( Î» L â†’
            Î£ ( right-coherence-square-equiv-cocone-equiv-span-diagram u)
              ( coherence-cube-equiv-cocone-equiv-span-diagram u L)))

  module _
    (u : equiv-cocone-equiv-span-diagram)
    where

    equiv-equiv-cocone-equiv-span-diagram : X â‰ƒ Y
    equiv-equiv-cocone-equiv-span-diagram = pr1 u

    map-equiv-cocone-equiv-span-diagram : X â†’ Y
    map-equiv-cocone-equiv-span-diagram =
      map-equiv equiv-equiv-cocone-equiv-span-diagram

    is-equiv-equiv-cocone-equiv-span-diagram :
      is-equiv map-equiv-cocone-equiv-span-diagram
    is-equiv-equiv-cocone-equiv-span-diagram =
      is-equiv-map-equiv equiv-equiv-cocone-equiv-span-diagram

    left-square-equiv-cocone-equiv-span-diagram :
      left-coherence-square-equiv-cocone-equiv-span-diagram
        ( equiv-equiv-cocone-equiv-span-diagram)
    left-square-equiv-cocone-equiv-span-diagram =
      pr1 (pr2 u)

    right-square-equiv-cocone-equiv-span-diagram :
      right-coherence-square-equiv-cocone-equiv-span-diagram
        ( equiv-equiv-cocone-equiv-span-diagram)
    right-square-equiv-cocone-equiv-span-diagram =
      pr1 (pr2 (pr2 u))

    cube-equiv-cocone-equiv-span-diagram :
      coherence-cube-equiv-cocone-equiv-span-diagram
        ( equiv-equiv-cocone-equiv-span-diagram)
        ( left-square-equiv-cocone-equiv-span-diagram)
        ( right-square-equiv-cocone-equiv-span-diagram)
    cube-equiv-cocone-equiv-span-diagram =
      pr2 (pr2 (pr2 u))

    hom-cocone-equiv-cocone-equiv-span-diagram :
      hom-cocone-hom-span-diagram ğ’® c ğ’¯ d (hom-equiv-span-diagram ğ’® ğ’¯ e)
    pr1 hom-cocone-equiv-cocone-equiv-span-diagram =
      map-equiv-cocone-equiv-span-diagram
    pr1 (pr2 hom-cocone-equiv-cocone-equiv-span-diagram) =
      left-square-equiv-cocone-equiv-span-diagram
    pr1 (pr2 (pr2 hom-cocone-equiv-cocone-equiv-span-diagram)) =
      right-square-equiv-cocone-equiv-span-diagram
    pr2 (pr2 (pr2 hom-cocone-equiv-cocone-equiv-span-diagram)) =
      cube-equiv-cocone-equiv-span-diagram
```

## Properties

### For any equivalence of cocones under an equivalence of span diagrams, there is a naturality square involving `cocone-map`

This remains to be formalized.
