# Null cocones under pointed span diagrams

```agda
module synthetic-homotopy-theory.null-cocones-under-pointed-span-diagrams where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.commuting-squares-of-maps
open import foundation.dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.universe-levels
open import foundation.whiskering-identifications-concatenation

open import structured-types.commuting-squares-of-pointed-maps
open import structured-types.constant-pointed-maps
open import structured-types.pointed-homotopies
open import structured-types.pointed-maps
open import structured-types.pointed-span-diagrams
open import structured-types.pointed-types

open import synthetic-homotopy-theory.cocones-under-pointed-span-diagrams
```

</details>

## Idea

The {{#concept "null cocone" Disambiguation="pointed span diagram"}} under a
[pointed span diagram](structured-types.pointed-span-diagrams.md) `ğ’®` given by

```text
      f       g
  A <---- S ----> B
```

with codomain `X` is the
[cocone](synthetic-homotopy-theory.cocones-under-pointed-span-diagrams.md) under
`ğ’®` consisting of the
[constant pointed maps](structured-types.constant-pointed-maps.md) `A â†’âˆ— X` and
`B â†’âˆ— X` and the canonical homotopy witnessing that the square of pointed maps

```text
        g
    S -----> B
    |        |
  f |        | const
    âˆ¨        âˆ¨
    A -----> X
      const
```

[commutes](structured-types.commuting-squares-of-pointed-maps.md). The null
cocone under `ğ’®` provides a canonical pointing of the type
`cocone-Pointed-Type f g`.

## Definitions

### Null cocones under pointed span diagrams

```agda
module _
  {l1 l2 l3 l4 : Level} (ğ’® : pointed-span-diagram l1 l2 l3)
  (X : Pointed-Type l4)
  where

  left-pointed-map-null-cocone-Pointed-Type :
    pointed-domain-pointed-span-diagram ğ’® â†’âˆ— X
  left-pointed-map-null-cocone-Pointed-Type = constant-pointed-map _ X

  left-map-null-cocone-Pointed-Type :
    domain-pointed-span-diagram ğ’® â†’ type-Pointed-Type X
  left-map-null-cocone-Pointed-Type =
    map-pointed-map left-pointed-map-null-cocone-Pointed-Type

  preserves-point-left-map-null-cocone-Pointed-Type :
    left-map-null-cocone-Pointed-Type (point-domain-pointed-span-diagram ğ’®) ï¼
    point-Pointed-Type X
  preserves-point-left-map-null-cocone-Pointed-Type =
    preserves-point-pointed-map left-pointed-map-null-cocone-Pointed-Type

  right-pointed-map-null-cocone-Pointed-Type :
    pointed-codomain-pointed-span-diagram ğ’® â†’âˆ— X
  right-pointed-map-null-cocone-Pointed-Type = constant-pointed-map _ X

  right-map-null-cocone-Pointed-Type :
    codomain-pointed-span-diagram ğ’® â†’ type-Pointed-Type X
  right-map-null-cocone-Pointed-Type =
    map-pointed-map right-pointed-map-null-cocone-Pointed-Type

  preserves-point-right-map-null-cocone-Pointed-Type :
    right-map-null-cocone-Pointed-Type
      ( point-codomain-pointed-span-diagram ğ’®) ï¼
    point-Pointed-Type X
  preserves-point-right-map-null-cocone-Pointed-Type =
    preserves-point-pointed-map right-pointed-map-null-cocone-Pointed-Type

  htpy-coherence-square-null-cocone-Pointed-Type :
    coherence-square-maps
      ( map-pointed-map (right-pointed-map-pointed-span-diagram ğ’®))
      ( map-pointed-map (left-pointed-map-pointed-span-diagram ğ’®))
      ( map-constant-pointed-map (pointed-codomain-pointed-span-diagram ğ’®) X)
      ( map-constant-pointed-map (pointed-domain-pointed-span-diagram ğ’®) X)
  htpy-coherence-square-null-cocone-Pointed-Type = refl-htpy

  coherence-point-coherence-square-null-cocone-Pointed-Type :
    coherence-point-unpointed-htpy-pointed-Î 
      ( constant-pointed-map _ X âˆ˜âˆ— (left-pointed-map-pointed-span-diagram ğ’®))
      ( constant-pointed-map _ X âˆ˜âˆ— (right-pointed-map-pointed-span-diagram ğ’®))
      ( htpy-coherence-square-null-cocone-Pointed-Type)
  coherence-point-coherence-square-null-cocone-Pointed-Type =
    right-whisker-concat
      ( ( ap-const
          ( point-Pointed-Type X)
          ( preserves-point-left-map-pointed-span-diagram ğ’®)) âˆ™
        ( inv
          ( ap-const
            ( point-Pointed-Type X)
            ( preserves-point-right-map-pointed-span-diagram ğ’®))))
      ( refl)

  coherence-square-null-cocone-Pointed-Type :
    coherence-square-pointed-maps
      ( right-pointed-map-pointed-span-diagram ğ’®)
      ( left-pointed-map-pointed-span-diagram ğ’®)
      ( right-pointed-map-null-cocone-Pointed-Type)
      ( left-pointed-map-null-cocone-Pointed-Type)
  pr1 coherence-square-null-cocone-Pointed-Type =
    htpy-coherence-square-null-cocone-Pointed-Type
  pr2 coherence-square-null-cocone-Pointed-Type =
    coherence-point-coherence-square-null-cocone-Pointed-Type

  null-cocone-Pointed-Type :
    cocone-Pointed-Type
      ( left-pointed-map-pointed-span-diagram ğ’®)
      ( right-pointed-map-pointed-span-diagram ğ’®)
      ( X)
  pr1 null-cocone-Pointed-Type =
    left-pointed-map-null-cocone-Pointed-Type
  pr1 (pr2 null-cocone-Pointed-Type) =
    right-pointed-map-null-cocone-Pointed-Type
  pr2 (pr2 null-cocone-Pointed-Type) =
    coherence-square-null-cocone-Pointed-Type
```

### The pointed type of cocones under pointed span diagrams

```agda
module _
  {l1 l2 l3 l4 : Level} (ğ’® : pointed-span-diagram l1 l2 l3)
  (X : Pointed-Type l4)
  where

  type-cocone-pointed-type-Pointed-Type : UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  type-cocone-pointed-type-Pointed-Type =
    cocone-Pointed-Type
      ( left-pointed-map-pointed-span-diagram ğ’®)
      ( right-pointed-map-pointed-span-diagram ğ’®)
      ( X)

  cocone-pointed-type-Pointed-Type : Pointed-Type (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  pr1 cocone-pointed-type-Pointed-Type = type-cocone-pointed-type-Pointed-Type
  pr2 cocone-pointed-type-Pointed-Type = null-cocone-Pointed-Type ğ’® X
```
