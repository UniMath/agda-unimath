# Dependent sequential diagrams

```agda
module synthetic-homotopy-theory.dependent-sequential-diagrams where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.dependent-pair-types
open import foundation.function-types
open import foundation.homotopies
open import foundation.universe-levels

open import synthetic-homotopy-theory.sequential-diagrams
```

</details>

## Idea

A **dependent sequential diagram** over a
[sequential diagram](synthetic-homotopy-theory.sequential-diagrams.md) `(A, a)`
is a [sequence](lists.dependent-sequences.md) of families of types
`B : (n : ‚Ñï) ‚Üí A‚Çô ‚Üí ùí∞` over the types in the base sequential diagram, equipped
with fiberwise maps

```text
b‚Çô : (x : A‚Çô) ‚Üí B‚Çô x ‚Üí B‚Çô‚Çä‚ÇÅ (a‚Çô x).
```

They can be thought of as a family of sequential diagrams

```text
       b‚Çô(x)           b‚Çô‚Çä‚ÇÅ(a‚Çô(x))
 B‚Çô(x) ----> B‚Çô‚Çä‚ÇÅ(a‚Çô(x)) -------> B‚Çô‚Çä‚ÇÇ(a‚Çô‚Çä‚ÇÅ(a‚Çô(x))) ----> ‚ãØ,
```

one for each `x : A‚Çô`, or as a sequence fibered over `(A, a)`, visualized as

```text
     b‚ÇÄ      b‚ÇÅ      b‚ÇÇ
 B‚ÇÄ ---> B‚ÇÅ ---> B‚ÇÇ ---> ‚ãØ
 |       |       |
 |       |       |
 ‚Ü°       ‚Ü°       ‚Ü°
 A‚ÇÄ ---> A‚ÇÅ ---> A‚ÇÇ ---> ‚ãØ.
     a‚ÇÄ      a‚ÇÅ      a‚ÇÇ
```

## Definitions

### Dependent sequential diagrams

```agda
dependent-sequential-diagram :
  { l1 : Level} ‚Üí (A : sequential-diagram l1) ‚Üí
  ( l2 : Level) ‚Üí UU (l1 ‚äî lsuc l2)
dependent-sequential-diagram A l2 =
  Œ£ ( ( n : ‚Ñï) ‚Üí family-sequential-diagram A n ‚Üí UU l2)
    ( Œª B ‚Üí
      ( n : ‚Ñï) (x : family-sequential-diagram A n) ‚Üí
      B n x ‚Üí B (succ-‚Ñï n) (map-sequential-diagram A n x))
```

### Components of a dependent sequential diagram

```agda
module _
  { l1 l2 : Level} {A : sequential-diagram l1}
  ( B : dependent-sequential-diagram A l2)
  where

  family-dependent-sequential-diagram :
    ( n : ‚Ñï) ‚Üí family-sequential-diagram A n ‚Üí UU l2
  family-dependent-sequential-diagram = pr1 B

  map-dependent-sequential-diagram :
    ( n : ‚Ñï) (x : family-sequential-diagram A n) ‚Üí
    family-dependent-sequential-diagram n x ‚Üí
    family-dependent-sequential-diagram
      ( succ-‚Ñï n)
      ( map-sequential-diagram A n x)
  map-dependent-sequential-diagram = pr2 B
```

### Constant dependent sequential diagrams

Constant dependent sequential diagrams are dependent sequential diagrams where
the dependent type family `B` is [constant](foundation.constant-maps.md).

```agda
module _
  { l1 l2 : Level} (A : sequential-diagram l1) (B : sequential-diagram l2)
  where

  constant-dependent-sequential-diagram : dependent-sequential-diagram A l2
  pr1 constant-dependent-sequential-diagram n _ = family-sequential-diagram B n
  pr2 constant-dependent-sequential-diagram n _ = map-sequential-diagram B n
```

### Sections of dependent sequential diagrams

A **section of a dependent sequential diagram** `(B, b)` is a
[sequence](lists.dependent-sequences.md) of sections `s‚Çô : (x : A‚Çô) ‚Üí B‚Çô(x)`
satisfying the naturality condition that all squares of the form

```text
          b‚Çô(x)
  B‚Çô(x) -------> B‚Çô‚Çä‚ÇÅ(a‚Çô(x))
    ‚àß                ‚àß
 s‚Çô |                | s‚Çô‚Çä‚ÇÅ
    |                |
 (x : A‚Çô) ---> (a‚Çô(x) : A‚Çô‚Çä‚ÇÅ)
           a‚Çô
```

[commute](foundation.commuting-squares-of-maps.md).

```agda
module _
  { l1 l2 : Level} (A : sequential-diagram l1)
  ( B : dependent-sequential-diagram A l2)
  where

  naturality-section-dependent-sequential-diagram :
    ( s :
      ( n : ‚Ñï) (x : family-sequential-diagram A n) ‚Üí
      family-dependent-sequential-diagram B n x) ‚Üí
    UU (l1 ‚äî l2)
  naturality-section-dependent-sequential-diagram s =
    ( n : ‚Ñï) ‚Üí
    ( map-dependent-sequential-diagram B n _ ‚àò s n) ~
    ( s (succ-‚Ñï n) ‚àò map-sequential-diagram A n)

  section-dependent-sequential-diagram : UU (l1 ‚äî l2)
  section-dependent-sequential-diagram =
    Œ£ ( ( n : ‚Ñï) (x : family-sequential-diagram A n) ‚Üí
        family-dependent-sequential-diagram B n x)
      ( Œª s ‚Üí naturality-section-dependent-sequential-diagram s)
```

### Components of sections of dependent sequential diagrams

```agda
module _
  { l1 l2 : Level} (A : sequential-diagram l1)
  ( B : dependent-sequential-diagram A l2)
  ( s : section-dependent-sequential-diagram A B)
  where

  map-section-dependent-sequential-diagram :
    ( n : ‚Ñï) (x : family-sequential-diagram A n) ‚Üí
    family-dependent-sequential-diagram B n x
  map-section-dependent-sequential-diagram = pr1 s

  naturality-map-section-dependent-sequential-diagram :
    naturality-section-dependent-sequential-diagram A B
      map-section-dependent-sequential-diagram
  naturality-map-section-dependent-sequential-diagram = pr2 s
```

## References

{{#bibliography}} {{#reference SvDR20}}
