# The flattening lemma for pushouts

```agda
module synthetic-homotopy-theory.flattening-lemma-pushouts where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.commuting-cubes-of-maps
open import foundation.commuting-squares-of-maps
open import foundation.commuting-triangles-of-maps
open import foundation.dependent-pair-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.span-diagrams
open import foundation.transport-along-identifications
open import foundation.universal-property-dependent-pair-types
open import foundation.universe-levels

open import synthetic-homotopy-theory.cocones-under-spans
open import synthetic-homotopy-theory.dependent-cocones-under-spans
open import synthetic-homotopy-theory.dependent-universal-property-pushouts
open import synthetic-homotopy-theory.descent-data-pushouts
open import synthetic-homotopy-theory.equifibered-span-diagrams
open import synthetic-homotopy-theory.equivalences-descent-data-pushouts
open import synthetic-homotopy-theory.equivalences-equifibered-span-diagrams
open import synthetic-homotopy-theory.universal-property-pushouts
```

</details>

## Idea

The
{{#concept "flattening lemma" Disambiguation="for pushouts" Agda=flattening-lemma-pushout Agda=flattening-lemma-descent-data-pushout}}
for [pushouts](synthetic-homotopy-theory.pushouts.md) states that pushouts
commute with [dependent pair types](foundation.dependent-pair-types.md). More
precisely, given a pushout square

```text
         g
    S ------> B
    |         |
  f |         | j
    ‚à®       ‚åú ‚à®
    A ------> X
         i
```

with homotopy `H : i ‚àò f ~ j ‚àò g`, and for any type family `P` over `X`, the
commuting square

```text
  Œ£ (s : S), P(if(s)) ---> Œ£ (s : S), P(jg(s)) ---> Œ£ (b : B), P(j(b))
           |                                                 |
           |                                                 |
           ‚à®                                               ‚åú ‚à®
  Œ£ (a : A), P(i(a)) -----------------------------> Œ£ (x : X), P(x)
```

is again a pushout square.

## Definitions

### The statement of the flattening lemma for pushouts

```agda
module _
  { l1 l2 l3 l4 l5 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  { X : UU l4} (P : X ‚Üí UU l5)
  ( f : S ‚Üí A) (g : S ‚Üí B) (c : cocone f g X)
  where

  vertical-map-span-flattening-pushout :
    Œ£ S (P ‚àò horizontal-map-cocone f g c ‚àò f) ‚Üí
    Œ£ A (P ‚àò horizontal-map-cocone f g c)
  vertical-map-span-flattening-pushout =
    map-Œ£-map-base f (P ‚àò horizontal-map-cocone f g c)

  horizontal-map-span-flattening-pushout :
    Œ£ S (P ‚àò horizontal-map-cocone f g c ‚àò f) ‚Üí
    Œ£ B (P ‚àò vertical-map-cocone f g c)
  horizontal-map-span-flattening-pushout =
    map-Œ£
      ( P ‚àò vertical-map-cocone f g c)
      ( g)
      ( Œª s ‚Üí tr P (coherence-square-cocone f g c s))

  horizontal-map-cocone-flattening-pushout :
    Œ£ A (P ‚àò horizontal-map-cocone f g c) ‚Üí Œ£ X P
  horizontal-map-cocone-flattening-pushout =
    map-Œ£-map-base (horizontal-map-cocone f g c) P

  vertical-map-cocone-flattening-pushout :
    Œ£ B (P ‚àò vertical-map-cocone f g c) ‚Üí Œ£ X P
  vertical-map-cocone-flattening-pushout =
    map-Œ£-map-base (vertical-map-cocone f g c) P

  coherence-square-cocone-flattening-pushout :
    coherence-square-maps
      ( horizontal-map-span-flattening-pushout)
      ( vertical-map-span-flattening-pushout)
      ( vertical-map-cocone-flattening-pushout)
      ( horizontal-map-cocone-flattening-pushout)
  coherence-square-cocone-flattening-pushout =
    coherence-square-maps-map-Œ£-map-base P g f
      ( vertical-map-cocone f g c)
      ( horizontal-map-cocone f g c)
      ( coherence-square-cocone f g c)

  cocone-flattening-pushout :
    cocone
      ( vertical-map-span-flattening-pushout)
      ( horizontal-map-span-flattening-pushout)
      ( Œ£ X P)
  pr1 cocone-flattening-pushout =
    horizontal-map-cocone-flattening-pushout
  pr1 (pr2 cocone-flattening-pushout) =
    vertical-map-cocone-flattening-pushout
  pr2 (pr2 cocone-flattening-pushout) =
    coherence-square-cocone-flattening-pushout

  flattening-lemma-pushout-statement : UUœâ
  flattening-lemma-pushout-statement =
    universal-property-pushout f g c ‚Üí
    universal-property-pushout
      ( vertical-map-span-flattening-pushout)
      ( horizontal-map-span-flattening-pushout)
      ( cocone-flattening-pushout)

module _
  {l1 l2 l3 l4 l5 : Level} {ùíÆ : span-diagram l1 l2 l3}
  {X : UU l4} (c : cocone-span-diagram ùíÆ X)
  (P : X ‚Üí UU l5)
  where

  span-diagram-flattening-pushout : span-diagram (l1 ‚äî l5) (l2 ‚äî l5) (l3 ‚äî l5)
  span-diagram-flattening-pushout =
    make-span-diagram
      ( vertical-map-span-flattening-pushout P _ _ c)
      ( horizontal-map-span-flattening-pushout P _ _ c)
```

### The statement of the flattening lemma for pushouts, using descent data

The above statement of the flattening lemma works with a provided type family
over the pushout. We can instead accept a definition of this family via descent
data for the pushout.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ùíÆ : span-diagram l1 l2 l3}
  (P : descent-data-pushout ùíÆ l4 l5)
  where

  vertical-map-span-flattening-descent-data-pushout :
    Œ£ ( spanning-type-span-diagram ùíÆ)
      ( left-family-descent-data-pushout P ‚àò left-map-span-diagram ùíÆ) ‚Üí
    Œ£ ( domain-span-diagram ùíÆ) (left-family-descent-data-pushout P)
  vertical-map-span-flattening-descent-data-pushout =
    map-Œ£-map-base
      ( left-map-span-diagram ùíÆ)
      ( left-family-descent-data-pushout P)

  horizontal-map-span-flattening-descent-data-pushout :
    Œ£ ( spanning-type-span-diagram ùíÆ)
      ( left-family-descent-data-pushout P ‚àò left-map-span-diagram ùíÆ) ‚Üí
    Œ£ ( codomain-span-diagram ùíÆ) (right-family-descent-data-pushout P)
  horizontal-map-span-flattening-descent-data-pushout =
    map-Œ£
      ( right-family-descent-data-pushout P)
      ( right-map-span-diagram ùíÆ)
      ( map-family-descent-data-pushout P)

  span-diagram-flattening-descent-data-pushout :
    span-diagram (l1 ‚äî l4) (l2 ‚äî l5) (l3 ‚äî l4)
  span-diagram-flattening-descent-data-pushout =
    make-span-diagram
      ( vertical-map-span-flattening-descent-data-pushout)
      ( horizontal-map-span-flattening-descent-data-pushout)

module _
  { l1 l2 l3 l4 l5 l6 l7 : Level}
  {S : UU l1} {A : UU l2} {B : UU l3} {X : UU l4}
  ( f : S ‚Üí A) (g : S ‚Üí B) (c : cocone f g X)
  ( P : descent-data-pushout (make-span-diagram f g) l5 l6)
  ( Q : X ‚Üí UU l7)
  ( e :
    equiv-descent-data-pushout P (descent-data-family-cocone-span-diagram c Q))
  where

  horizontal-map-cocone-flattening-descent-data-pushout :
    Œ£ A (left-family-descent-data-pushout P) ‚Üí Œ£ X Q
  horizontal-map-cocone-flattening-descent-data-pushout =
    map-Œ£ Q
      ( horizontal-map-cocone f g c)
      ( left-map-equiv-descent-data-pushout
        ( P)
        ( descent-data-family-cocone-span-diagram c Q)
        ( e))

  vertical-map-cocone-flattening-descent-data-pushout :
    Œ£ B (right-family-descent-data-pushout P) ‚Üí Œ£ X Q
  vertical-map-cocone-flattening-descent-data-pushout =
    map-Œ£ Q
      ( vertical-map-cocone f g c)
      ( right-map-equiv-descent-data-pushout
        ( P)
        ( descent-data-family-cocone-span-diagram c Q)
        ( e))

  coherence-square-cocone-flattening-descent-data-pushout :
    coherence-square-maps
      ( horizontal-map-span-flattening-descent-data-pushout P)
      ( vertical-map-span-flattening-descent-data-pushout P)
      ( vertical-map-cocone-flattening-descent-data-pushout)
      ( horizontal-map-cocone-flattening-descent-data-pushout)
  coherence-square-cocone-flattening-descent-data-pushout =
    htpy-map-Œ£ Q
      ( coherence-square-cocone f g c)
      ( ( left-map-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e)) ‚àò
        ( f))
      ( inv-htpy ‚àò
        coherence-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e))

  cocone-flattening-descent-data-pushout :
    cocone
      ( vertical-map-span-flattening-descent-data-pushout P)
      ( horizontal-map-span-flattening-descent-data-pushout P)
      ( Œ£ X Q)
  pr1 cocone-flattening-descent-data-pushout =
    horizontal-map-cocone-flattening-descent-data-pushout
  pr1 (pr2 cocone-flattening-descent-data-pushout) =
    vertical-map-cocone-flattening-descent-data-pushout
  pr2 (pr2 cocone-flattening-descent-data-pushout) =
    coherence-square-cocone-flattening-descent-data-pushout

  flattening-lemma-descent-data-pushout-statement : UUœâ
  flattening-lemma-descent-data-pushout-statement =
    universal-property-pushout f g c ‚Üí
    universal-property-pushout
      ( vertical-map-span-flattening-descent-data-pushout P)
      ( horizontal-map-span-flattening-descent-data-pushout P)
      ( cocone-flattening-descent-data-pushout)
```

### The statement of the flattening lemma for pushouts, using equifibered span diagrams

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level} {ùíÆ : span-diagram l1 l2 l3}
  (P : equifibered-span-diagram ùíÆ l4 l5 l6)
  where

  vertical-map-span-flattening-equifibered-span-diagram :
    Œ£ ( spanning-type-span-diagram ùíÆ)
      ( spanning-type-family-equifibered-span-diagram P) ‚Üí
    Œ£ ( domain-span-diagram ùíÆ)
      ( left-family-equifibered-span-diagram P)
  vertical-map-span-flattening-equifibered-span-diagram =
    map-Œ£
      ( left-family-equifibered-span-diagram P)
      ( left-map-span-diagram ùíÆ)
      ( map-left-family-equifibered-span-diagram P)

  horizontal-map-span-flattening-equifibered-span-diagram :
    Œ£ ( spanning-type-span-diagram ùíÆ)
      ( spanning-type-family-equifibered-span-diagram P) ‚Üí
    Œ£ ( codomain-span-diagram ùíÆ)
      ( right-family-equifibered-span-diagram P)
  horizontal-map-span-flattening-equifibered-span-diagram =
    map-Œ£
      ( right-family-equifibered-span-diagram P)
      ( right-map-span-diagram ùíÆ)
      ( map-right-family-equifibered-span-diagram P)

  span-diagram-flattening-equifibered-span-diagram :
    span-diagram (l1 ‚äî l4) (l2 ‚äî l5) (l3 ‚äî l6)
  span-diagram-flattening-equifibered-span-diagram =
    make-span-diagram
      ( vertical-map-span-flattening-equifibered-span-diagram)
      ( horizontal-map-span-flattening-equifibered-span-diagram)

module _
  { l1 l2 l3 l4 l5 l6 l7 l8 : Level}
  { S : UU l1} {A : UU l2} {B : UU l3} {X : UU l4}
  ( f : S ‚Üí A) (g : S ‚Üí B) (c : cocone f g X)
  ( P : equifibered-span-diagram (make-span-diagram f g) l5 l6 l7)
  ( Q : X ‚Üí UU l8)
  ( e :
    equiv-equifibered-span-diagram
      ( P)
      ( equifibered-span-diagram-family-cocone-span-diagram c Q))
  where

  horizontal-map-cocone-flattening-equifibered-span-diagram :
    Œ£ A (left-family-equifibered-span-diagram P) ‚Üí Œ£ X Q
  horizontal-map-cocone-flattening-equifibered-span-diagram =
    map-Œ£ Q
      ( horizontal-map-cocone f g c)
      ( left-map-equiv-equifibered-span-diagram
        ( P)
        ( equifibered-span-diagram-family-cocone-span-diagram c Q)
        ( e))

  vertical-map-cocone-flattening-equifibered-span-diagram :
    Œ£ B (right-family-equifibered-span-diagram P) ‚Üí Œ£ X Q
  vertical-map-cocone-flattening-equifibered-span-diagram =
    map-Œ£ Q
      ( vertical-map-cocone f g c)
      ( right-map-equiv-equifibered-span-diagram
        ( P)
        ( equifibered-span-diagram-family-cocone-span-diagram c Q)
        ( e))
```

> The rest remains to be formalized.

```text
  coherence-square-cocone-flattening-equifibered-span-diagram :
    coherence-square-maps
      ( horizontal-map-span-flattening-equifibered-span-diagram P)
      ( vertical-map-span-flattening-equifibered-span-diagram P)
      ( vertical-map-cocone-flattening-equifibered-span-diagram)
      ( horizontal-map-cocone-flattening-equifibered-span-diagram)
  coherence-square-cocone-flattening-equifibered-span-diagram =
    htpy-map-Œ£ Q
      ( coherence-square-cocone f g c)
      ( Œª s t ‚Üí
        left-map-equiv-equifibered-span-diagram
          ( P)
          ( equifibered-span-diagram-family-cocone-span-diagram c Q)
          ( e)
          ( f s)
          ( map-left-family-equifibered-span-diagram P s t))
      ( Œª s t ‚Üí {!   !})

  cocone-flattening-equifibered-span-diagram :
    cocone
      ( vertical-map-span-flattening-equifibered-span-diagram P)
      ( horizontal-map-span-flattening-equifibered-span-diagram P)
      ( Œ£ X Q)
  pr1 cocone-flattening-equifibered-span-diagram =
    horizontal-map-cocone-flattening-equifibered-span-diagram
  pr1 (pr2 cocone-flattening-equifibered-span-diagram) =
    vertical-map-cocone-flattening-equifibered-span-diagram
  pr2 (pr2 cocone-flattening-equifibered-span-diagram) =
    coherence-square-cocone-flattening-equifibered-span-diagram

  flattening-lemma-equifibered-span-diagram-statement : UUœâ
  flattening-lemma-equifibered-span-diagram-statement =
    universal-property-pushout f g c ‚Üí
    universal-property-pushout
      ( vertical-map-span-flattening-equifibered-span-diagram P)
      ( horizontal-map-span-flattening-equifibered-span-diagram P)
      ( cocone-flattening-equifibered-span-diagram)
```

## Properties

### Proof of the flattening lemma for pushouts

The proof uses the theorem that maps from sigma types are equivalent to
dependent maps over the index type, for which we can invoke the dependent
universal property of the indexing pushout.

```agda
module _
  { l1 l2 l3 l4 l5 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  { X : UU l4} (P : X ‚Üí UU l5)
  ( f : S ‚Üí A) (g : S ‚Üí B) (c : cocone f g X)
  where

  cocone-map-flattening-pushout :
    { l : Level} (Y : UU l) ‚Üí
    ( Œ£ X P ‚Üí Y) ‚Üí
    cocone
      ( vertical-map-span-flattening-pushout P f g c)
      ( horizontal-map-span-flattening-pushout P f g c)
      ( Y)
  cocone-map-flattening-pushout Y =
    cocone-map
      ( vertical-map-span-flattening-pushout P f g c)
      ( horizontal-map-span-flattening-pushout P f g c)
      ( cocone-flattening-pushout P f g c)

  comparison-dependent-cocone-ind-Œ£-cocone :
    { l : Level} (Y : UU l) ‚Üí
    Œ£ ( (a : A) ‚Üí P (horizontal-map-cocone f g c a) ‚Üí Y)
      ( Œª k ‚Üí
        Œ£ ( (b : B) ‚Üí P (vertical-map-cocone f g c b) ‚Üí Y)
          ( Œª l ‚Üí
            ( s : S) (t : P (horizontal-map-cocone f g c (f s))) ‚Üí
            ( k (f s) t) Ôºù
            ( l (g s) (tr P (coherence-square-cocone f g c s) t)))) ‚âÉ
    dependent-cocone f g c (Œª x ‚Üí P x ‚Üí Y)
  comparison-dependent-cocone-ind-Œ£-cocone Y =
    equiv-tot
      ( Œª k ‚Üí
        equiv-tot
          ( Œª l ‚Üí
            equiv-Œ†-equiv-family
              ( equiv-htpy-dependent-function-dependent-identification-function-type
                ( Y)
                ( coherence-square-cocone f g c)
                ( k ‚àò f)
                ( l ‚àò g))))

  triangle-comparison-dependent-cocone-ind-Œ£-cocone :
    { l : Level} (Y : UU l) ‚Üí
    coherence-triangle-maps
      ( dependent-cocone-map f g c (Œª x ‚Üí P x ‚Üí Y))
      ( map-equiv (comparison-dependent-cocone-ind-Œ£-cocone Y))
      ( map-equiv equiv-ev-pair¬≥ ‚àò cocone-map-flattening-pushout Y ‚àò ind-Œ£)
  triangle-comparison-dependent-cocone-ind-Œ£-cocone Y h =
    eq-pair-eq-fiber
      ( eq-pair-eq-fiber
        ( eq-htpy
          ( inv-htpy
            ( compute-equiv-htpy-dependent-function-dependent-identification-function-type
              ( Y)
              ( coherence-square-cocone f g c)
              ( h)))))
  abstract
    flattening-lemma-pushout :
      flattening-lemma-pushout-statement P f g c
    flattening-lemma-pushout up-c Y =
      is-equiv-left-factor
        ( cocone-map-flattening-pushout Y)
        ( ind-Œ£)
        ( is-equiv-right-factor
          ( map-equiv equiv-ev-pair¬≥)
          ( cocone-map-flattening-pushout Y ‚àò ind-Œ£)
          ( is-equiv-map-equiv equiv-ev-pair¬≥)
          ( is-equiv-top-map-triangle
            ( dependent-cocone-map f g c (Œª x ‚Üí P x ‚Üí Y))
            ( map-equiv (comparison-dependent-cocone-ind-Œ£-cocone Y))
            ( ( map-equiv equiv-ev-pair¬≥) ‚àò
              ( cocone-map-flattening-pushout Y) ‚àò
              ( ind-Œ£))
            ( triangle-comparison-dependent-cocone-ind-Œ£-cocone Y)
            ( is-equiv-map-equiv (comparison-dependent-cocone-ind-Œ£-cocone Y))
            ( dependent-universal-property-universal-property-pushout _ _ _ up-c
              ( Œª x ‚Üí P x ‚Üí Y))))
        ( is-equiv-ind-Œ£)
```

### Proof of the descent data statement of the flattening lemma

The proof is carried out by constructing a commuting cube, which has
equivalences for vertical maps, the `cocone-flattening-pushout` square for the
bottom, and the `cocone-flattening-descent-data-pushout` square for the top.

The bottom is a pushout by the above flattening lemma, which implies that the
top is also a pushout.

The other parts of the cube are defined naturally, and come from the following
map of spans:

```text
  Œ£ (a : A) (PA a) <------- Œ£ (s : S) (PA (f s)) -----> Œ£ (b : B) (PB b)
         |                           |                         |
         |                           |                         |
         ‚à®                           ‚à®                         ‚à®
Œ£ (a : A) (P (i a)) <---- Œ£ (s : S) (P (i (f s))) ---> Œ£ (b : B) (P (j b))
```

where the vertical maps are equivalences given fiberwise by the equivalence of
descent data.

```agda
module _
  { l1 l2 l3 l4 l5 l6 l7 : Level}
  { S : UU l1} {A : UU l2} {B : UU l3} {X : UU l4}
  ( f : S ‚Üí A) (g : S ‚Üí B) (c : cocone f g X)
  ( P : descent-data-pushout (make-span-diagram f g) l5 l6)
  ( Q : X ‚Üí UU l7)
  ( e :
    equiv-descent-data-pushout P (descent-data-family-cocone-span-diagram c Q))
  where

  coherence-cube-flattening-lemma-descent-data-pushout :
    coherence-cube-maps
      ( vertical-map-span-flattening-pushout Q f g c)
      ( horizontal-map-span-flattening-pushout Q f g c)
      ( horizontal-map-cocone-flattening-pushout Q f g c)
      ( vertical-map-cocone-flattening-pushout Q f g c)
      ( vertical-map-span-flattening-descent-data-pushout P)
      ( horizontal-map-span-flattening-descent-data-pushout P)
      ( horizontal-map-cocone-flattening-descent-data-pushout f g c P Q e)
      ( vertical-map-cocone-flattening-descent-data-pushout f g c P Q e)
      ( tot
        ( ( left-map-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)) ‚àò
          ( f)))
      ( tot
        ( left-map-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e)))
      ( tot
        ( right-map-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e)))
      ( id)
      ( coherence-square-cocone-flattening-descent-data-pushout f g c P Q e)
      ( refl-htpy)
      ( htpy-map-Œ£
        ( Q ‚àò vertical-map-cocone f g c)
        ( refl-htpy)
        ( Œª s ‚Üí
          ( tr Q (coherence-square-cocone f g c s)) ‚àò
          ( left-map-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)
            ( f s)))
        ( inv-htpy ‚àò
          coherence-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)))
      ( refl-htpy)
      ( refl-htpy)
      ( coherence-square-cocone-flattening-pushout Q f g c)
  coherence-cube-flattening-lemma-descent-data-pushout (s , t) =
    ( ap-id
      ( coherence-square-cocone-flattening-descent-data-pushout f g c P Q e
        ( s , t))) ‚àô
    ( triangle-eq-pair-Œ£ Q
      ( coherence-square-cocone f g c s)
      ( inv
        ( coherence-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e)
          ( s)
          ( t)))) ‚àô
    ( ap
      ( eq-pair-Œ£ (coherence-square-cocone f g c s) refl ‚àô_)
      ( inv
        ( ( right-unit) ‚àô
          ( compute-ap-map-Œ£-map-base-eq-pair-Œ£
            ( vertical-map-cocone f g c)
            ( Q)
            ( refl)
            ( inv
              ( coherence-equiv-descent-data-pushout
                ( P)
                ( descent-data-family-cocone-span-diagram c Q)
                ( e)
                ( s)
                ( t)))))))

  abstract
    flattening-lemma-descent-data-pushout :
      flattening-lemma-descent-data-pushout-statement f g c P Q e
    flattening-lemma-descent-data-pushout up-c =
      universal-property-pushout-top-universal-property-pushout-bottom-cube-equiv
        ( vertical-map-span-flattening-pushout Q f g c)
        ( horizontal-map-span-flattening-pushout Q f g c)
        ( horizontal-map-cocone-flattening-pushout Q f g c)
        ( vertical-map-cocone-flattening-pushout Q f g c)
        ( vertical-map-span-flattening-descent-data-pushout P)
        ( horizontal-map-span-flattening-descent-data-pushout P)
        ( horizontal-map-cocone-flattening-descent-data-pushout f g c P Q e)
        ( vertical-map-cocone-flattening-descent-data-pushout f g c P Q e)
        ( equiv-tot
          ( ( left-equiv-equiv-descent-data-pushout
              ( P)
              ( descent-data-family-cocone-span-diagram c Q)
              ( e)) ‚àò
            ( f)))
        ( equiv-tot
          ( left-equiv-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)))
        ( equiv-tot
          ( right-equiv-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)))
        ( id-equiv)
        ( coherence-square-cocone-flattening-descent-data-pushout f g c P Q e)
        ( refl-htpy)
        ( htpy-map-Œ£
          ( Q ‚àò vertical-map-cocone f g c)
          ( refl-htpy)
          ( Œª s ‚Üí
            ( tr Q (coherence-square-cocone f g c s)) ‚àò
            ( left-map-equiv-descent-data-pushout
              ( P)
              ( descent-data-family-cocone-span-diagram c Q)
              ( e)
              ( f s)))
          ( inv-htpy ‚àò
            coherence-equiv-descent-data-pushout
              ( P)
              ( descent-data-family-cocone-span-diagram c Q)
              ( e)))
        ( refl-htpy)
        ( refl-htpy)
        ( coherence-square-cocone-flattening-pushout Q f g c)
        ( coherence-cube-flattening-lemma-descent-data-pushout)
        ( flattening-lemma-pushout Q f g c up-c)
```

## See also

- [Mather's second cube theorem](synthetic-homotopy-theory.mathers-second-cube-theorem.md)
  is the [unstraightened](foundation.type-duality.md) version of the flattening
  lemma for pushouts.
