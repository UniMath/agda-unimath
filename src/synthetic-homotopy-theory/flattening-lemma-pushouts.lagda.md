# The flattening lemma for pushouts

```agda
module synthetic-homotopy-theory.flattening-lemma-pushouts where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.commuting-cubes-of-maps
open import foundation.commuting-squares-of-maps
open import foundation.commuting-triangles-of-maps
open import foundation.dependent-pair-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.span-diagrams
open import foundation.transport-along-identifications
open import foundation.universal-property-dependent-pair-types
open import foundation.universe-levels

open import synthetic-homotopy-theory.cocones-under-spans
open import synthetic-homotopy-theory.dependent-cocones-under-spans
open import synthetic-homotopy-theory.dependent-universal-property-pushouts
open import synthetic-homotopy-theory.descent-data-pushouts
open import synthetic-homotopy-theory.equifibered-span-diagrams
open import synthetic-homotopy-theory.equivalences-descent-data-pushouts
open import synthetic-homotopy-theory.equivalences-equifibered-span-diagrams
open import synthetic-homotopy-theory.universal-property-pushouts
```

</details>

## Idea

The
{{#concept "flattening lemma" Disambiguation="for pushouts" Agda=flattening-lemma-pushout Agda=flattening-lemma-descent-data-pushout}}
for [pushouts](synthetic-homotopy-theory.pushouts.md) states that pushouts
commute with [dependent pair types](foundation.dependent-pair-types.md). More
precisely, given a pushout square

```text
         g
    S ------> B
    |         |
  f |         | j
    âˆ¨       âŒœ âˆ¨
    A ------> X
         i
```

with homotopy `H : i âˆ˜ f ~ j âˆ˜ g`, and for any type family `P` over `X`, the
commuting square

```text
  Î£ (s : S), P(if(s)) ---> Î£ (s : S), P(jg(s)) ---> Î£ (b : B), P(j(b))
           |                                                 |
           |                                                 |
           âˆ¨                                               âŒœ âˆ¨
  Î£ (a : A), P(i(a)) -----------------------------> Î£ (x : X), P(x)
```

is again a pushout square.

## Definitions

### The statement of the flattening lemma for pushouts

```agda
module _
  { l1 l2 l3 l4 l5 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  { X : UU l4} (P : X â†’ UU l5)
  ( f : S â†’ A) (g : S â†’ B) (c : cocone f g X)
  where

  vertical-map-span-flattening-pushout :
    Î£ S (P âˆ˜ horizontal-map-cocone f g c âˆ˜ f) â†’
    Î£ A (P âˆ˜ horizontal-map-cocone f g c)
  vertical-map-span-flattening-pushout =
    map-Î£-map-base f (P âˆ˜ horizontal-map-cocone f g c)

  horizontal-map-span-flattening-pushout :
    Î£ S (P âˆ˜ horizontal-map-cocone f g c âˆ˜ f) â†’
    Î£ B (P âˆ˜ vertical-map-cocone f g c)
  horizontal-map-span-flattening-pushout =
    map-Î£
      ( P âˆ˜ vertical-map-cocone f g c)
      ( g)
      ( Î» s â†’ tr P (coherence-square-cocone f g c s))

  horizontal-map-cocone-flattening-pushout :
    Î£ A (P âˆ˜ horizontal-map-cocone f g c) â†’ Î£ X P
  horizontal-map-cocone-flattening-pushout =
    map-Î£-map-base (horizontal-map-cocone f g c) P

  vertical-map-cocone-flattening-pushout :
    Î£ B (P âˆ˜ vertical-map-cocone f g c) â†’ Î£ X P
  vertical-map-cocone-flattening-pushout =
    map-Î£-map-base (vertical-map-cocone f g c) P

  coherence-square-cocone-flattening-pushout :
    coherence-square-maps
      ( horizontal-map-span-flattening-pushout)
      ( vertical-map-span-flattening-pushout)
      ( vertical-map-cocone-flattening-pushout)
      ( horizontal-map-cocone-flattening-pushout)
  coherence-square-cocone-flattening-pushout =
    coherence-square-maps-map-Î£-map-base P g f
      ( vertical-map-cocone f g c)
      ( horizontal-map-cocone f g c)
      ( coherence-square-cocone f g c)

  cocone-flattening-pushout :
    cocone
      ( vertical-map-span-flattening-pushout)
      ( horizontal-map-span-flattening-pushout)
      ( Î£ X P)
  pr1 cocone-flattening-pushout =
    horizontal-map-cocone-flattening-pushout
  pr1 (pr2 cocone-flattening-pushout) =
    vertical-map-cocone-flattening-pushout
  pr2 (pr2 cocone-flattening-pushout) =
    coherence-square-cocone-flattening-pushout

  flattening-lemma-pushout-statement : UUÏ‰
  flattening-lemma-pushout-statement =
    universal-property-pushout f g c â†’
    universal-property-pushout
      ( vertical-map-span-flattening-pushout)
      ( horizontal-map-span-flattening-pushout)
      ( cocone-flattening-pushout)

module _
  {l1 l2 l3 l4 l5 : Level} {ğ’® : span-diagram l1 l2 l3}
  {X : UU l4} (c : cocone-span-diagram ğ’® X)
  (P : X â†’ UU l5)
  where

  span-diagram-flattening-pushout : span-diagram (l1 âŠ” l5) (l2 âŠ” l5) (l3 âŠ” l5)
  span-diagram-flattening-pushout =
    make-span-diagram
      ( vertical-map-span-flattening-pushout P _ _ c)
      ( horizontal-map-span-flattening-pushout P _ _ c)
```

### The statement of the flattening lemma for pushouts, using descent data

The above statement of the flattening lemma works with a provided type family
over the pushout. We can instead accept a definition of this family via descent
data for the pushout.

```agda
module _
  {l1 l2 l3 l4 l5 : Level} {ğ’® : span-diagram l1 l2 l3}
  (P : descent-data-pushout ğ’® l4 l5)
  where

  vertical-map-span-flattening-descent-data-pushout :
    Î£ ( spanning-type-span-diagram ğ’®)
      ( left-family-descent-data-pushout P âˆ˜ left-map-span-diagram ğ’®) â†’
    Î£ ( domain-span-diagram ğ’®) (left-family-descent-data-pushout P)
  vertical-map-span-flattening-descent-data-pushout =
    map-Î£-map-base
      ( left-map-span-diagram ğ’®)
      ( left-family-descent-data-pushout P)

  horizontal-map-span-flattening-descent-data-pushout :
    Î£ ( spanning-type-span-diagram ğ’®)
      ( left-family-descent-data-pushout P âˆ˜ left-map-span-diagram ğ’®) â†’
    Î£ ( codomain-span-diagram ğ’®) (right-family-descent-data-pushout P)
  horizontal-map-span-flattening-descent-data-pushout =
    map-Î£
      ( right-family-descent-data-pushout P)
      ( right-map-span-diagram ğ’®)
      ( map-family-descent-data-pushout P)

  span-diagram-flattening-descent-data-pushout :
    span-diagram (l1 âŠ” l4) (l2 âŠ” l5) (l3 âŠ” l4)
  span-diagram-flattening-descent-data-pushout =
    make-span-diagram
      ( vertical-map-span-flattening-descent-data-pushout)
      ( horizontal-map-span-flattening-descent-data-pushout)

module _
  { l1 l2 l3 l4 l5 l6 l7 : Level}
  {S : UU l1} {A : UU l2} {B : UU l3} {X : UU l4}
  ( f : S â†’ A) (g : S â†’ B) (c : cocone f g X)
  ( P : descent-data-pushout (make-span-diagram f g) l5 l6)
  ( Q : X â†’ UU l7)
  ( e :
    equiv-descent-data-pushout P (descent-data-family-cocone-span-diagram c Q))
  where

  horizontal-map-cocone-flattening-descent-data-pushout :
    Î£ A (left-family-descent-data-pushout P) â†’ Î£ X Q
  horizontal-map-cocone-flattening-descent-data-pushout =
    map-Î£ Q
      ( horizontal-map-cocone f g c)
      ( left-map-equiv-descent-data-pushout
        ( P)
        ( descent-data-family-cocone-span-diagram c Q)
        ( e))

  vertical-map-cocone-flattening-descent-data-pushout :
    Î£ B (right-family-descent-data-pushout P) â†’ Î£ X Q
  vertical-map-cocone-flattening-descent-data-pushout =
    map-Î£ Q
      ( vertical-map-cocone f g c)
      ( right-map-equiv-descent-data-pushout
        ( P)
        ( descent-data-family-cocone-span-diagram c Q)
        ( e))

  coherence-square-cocone-flattening-descent-data-pushout :
    coherence-square-maps
      ( horizontal-map-span-flattening-descent-data-pushout P)
      ( vertical-map-span-flattening-descent-data-pushout P)
      ( vertical-map-cocone-flattening-descent-data-pushout)
      ( horizontal-map-cocone-flattening-descent-data-pushout)
  coherence-square-cocone-flattening-descent-data-pushout =
    htpy-map-Î£ Q
      ( coherence-square-cocone f g c)
      ( ( left-map-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e)) âˆ˜
        ( f))
      ( inv-htpy âˆ˜
        coherence-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e))

  cocone-flattening-descent-data-pushout :
    cocone
      ( vertical-map-span-flattening-descent-data-pushout P)
      ( horizontal-map-span-flattening-descent-data-pushout P)
      ( Î£ X Q)
  pr1 cocone-flattening-descent-data-pushout =
    horizontal-map-cocone-flattening-descent-data-pushout
  pr1 (pr2 cocone-flattening-descent-data-pushout) =
    vertical-map-cocone-flattening-descent-data-pushout
  pr2 (pr2 cocone-flattening-descent-data-pushout) =
    coherence-square-cocone-flattening-descent-data-pushout

  flattening-lemma-descent-data-pushout-statement : UUÏ‰
  flattening-lemma-descent-data-pushout-statement =
    universal-property-pushout f g c â†’
    universal-property-pushout
      ( vertical-map-span-flattening-descent-data-pushout P)
      ( horizontal-map-span-flattening-descent-data-pushout P)
      ( cocone-flattening-descent-data-pushout)
```

### The statement of the flattening lemma for pushouts, using equifibered diagrams

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level} {ğ’® : span-diagram l1 l2 l3}
  (P : equifibered-span-diagram ğ’® l4 l5 l6)
  where

  vertical-map-span-flattening-equifibered-span-diagram :
    Î£ ( spanning-type-span-diagram ğ’®)
      ( spanning-type-family-equifibered-span-diagram P) â†’
    Î£ ( domain-span-diagram ğ’®)
      ( left-family-equifibered-span-diagram P)
  vertical-map-span-flattening-equifibered-span-diagram =
    map-Î£
      ( left-family-equifibered-span-diagram P)
      ( left-map-span-diagram ğ’®)
      ( map-left-family-equifibered-span-diagram P)

  horizontal-map-span-flattening-equifibered-span-diagram :
    Î£ ( spanning-type-span-diagram ğ’®)
      ( spanning-type-family-equifibered-span-diagram P) â†’
    Î£ ( codomain-span-diagram ğ’®)
      ( right-family-equifibered-span-diagram P)
  horizontal-map-span-flattening-equifibered-span-diagram =
    map-Î£
      ( right-family-equifibered-span-diagram P)
      ( right-map-span-diagram ğ’®)
      ( map-right-family-equifibered-span-diagram P)

  span-diagram-flattening-equifibered-span-diagram :
    span-diagram (l1 âŠ” l4) (l2 âŠ” l5) (l3 âŠ” l6)
  span-diagram-flattening-equifibered-span-diagram =
    make-span-diagram
      ( vertical-map-span-flattening-equifibered-span-diagram)
      ( horizontal-map-span-flattening-equifibered-span-diagram)

module _
  { l1 l2 l3 l4 l5 l6 l7 l8 : Level}
  { S : UU l1} {A : UU l2} {B : UU l3} {X : UU l4}
  ( f : S â†’ A) (g : S â†’ B) (c : cocone f g X)
  ( P : equifibered-span-diagram (make-span-diagram f g) l5 l6 l7)
  ( Q : X â†’ UU l8)
  ( e :
    equiv-equifibered-span-diagram
      ( P)
      ( equifibered-span-diagram-family-cocone-span-diagram c Q))
  where

  horizontal-map-cocone-flattening-equifibered-span-diagram :
    Î£ A (left-family-equifibered-span-diagram P) â†’ Î£ X Q
  horizontal-map-cocone-flattening-equifibered-span-diagram =
    map-Î£ Q
      ( horizontal-map-cocone f g c)
      ( left-map-equiv-equifibered-span-diagram
        ( P)
        ( equifibered-span-diagram-family-cocone-span-diagram c Q)
        ( e))

  vertical-map-cocone-flattening-equifibered-span-diagram :
    Î£ B (right-family-equifibered-span-diagram P) â†’ Î£ X Q
  vertical-map-cocone-flattening-equifibered-span-diagram =
    map-Î£ Q
      ( vertical-map-cocone f g c)
      ( right-map-equiv-equifibered-span-diagram
        ( P)
        ( equifibered-span-diagram-family-cocone-span-diagram c Q)
        ( e))

  map-left-family-flattening-equifibered-span-diagram :
    (s : S) â†’
    spanning-type-family-equifibered-span-diagram P s â†’
    Q (horizontal-map-cocone f g c (f s))
  map-left-family-flattening-equifibered-span-diagram s =
    left-map-equiv-equifibered-span-diagram
      ( P)
      ( equifibered-span-diagram-family-cocone-span-diagram c Q)
      ( e)
      ( f s) âˆ˜
    map-left-family-equifibered-span-diagram P s

  coherence-map-flattening-equifibered-span-diagram :
    (s : S) (t : spanning-type-family-equifibered-span-diagram P s) â†’
    tr Q
      ( coherence-square-cocone f g c s)
      ( map-left-family-flattening-equifibered-span-diagram s t) ï¼
    right-map-equiv-equifibered-span-diagram
      ( P)
      ( equifibered-span-diagram-family-cocone-span-diagram c Q)
      ( e)
      ( g s)
      ( map-right-family-equifibered-span-diagram P s t)
  coherence-map-flattening-equifibered-span-diagram s t =
    ap
      ( tr Q (coherence-square-cocone f g c s))
      ( coherence-left-equiv-equifibered-span-diagram
        ( P)
        ( equifibered-span-diagram-family-cocone-span-diagram c Q)
        ( e)
        ( s)
        ( t)) âˆ™
    inv
      ( coherence-right-equiv-equifibered-span-diagram
        ( P)
        ( equifibered-span-diagram-family-cocone-span-diagram c Q)
        ( e)
        ( s)
        ( t))

  coherence-square-cocone-flattening-equifibered-span-diagram :
    coherence-square-maps
      ( horizontal-map-span-flattening-equifibered-span-diagram P)
      ( vertical-map-span-flattening-equifibered-span-diagram P)
      ( vertical-map-cocone-flattening-equifibered-span-diagram)
      ( horizontal-map-cocone-flattening-equifibered-span-diagram)
  coherence-square-cocone-flattening-equifibered-span-diagram =
    htpy-map-Î£ Q
      ( coherence-square-cocone f g c)
      ( map-left-family-flattening-equifibered-span-diagram)
      ( coherence-map-flattening-equifibered-span-diagram)

  cocone-flattening-equifibered-span-diagram :
    cocone
      ( vertical-map-span-flattening-equifibered-span-diagram P)
      ( horizontal-map-span-flattening-equifibered-span-diagram P)
      ( Î£ X Q)
  cocone-flattening-equifibered-span-diagram =
    ( horizontal-map-cocone-flattening-equifibered-span-diagram ,
      vertical-map-cocone-flattening-equifibered-span-diagram ,
      coherence-square-cocone-flattening-equifibered-span-diagram)

  flattening-lemma-equifibered-span-diagram-statement : UUÏ‰
  flattening-lemma-equifibered-span-diagram-statement =
    universal-property-pushout f g c â†’
    universal-property-pushout
      ( vertical-map-span-flattening-equifibered-span-diagram P)
      ( horizontal-map-span-flattening-equifibered-span-diagram P)
      ( cocone-flattening-equifibered-span-diagram)
```

## Properties

### Proof of the flattening lemma for pushouts, using descent data

The proof uses the theorem that maps from sigma types are equivalent to
dependent maps over the index type, for which we can invoke the dependent
universal property of the indexing pushout.

```agda
module _
  { l1 l2 l3 l4 l5 : Level} {S : UU l1} {A : UU l2} {B : UU l3}
  { X : UU l4} (P : X â†’ UU l5)
  ( f : S â†’ A) (g : S â†’ B) (c : cocone f g X)
  where

  cocone-map-flattening-pushout :
    { l : Level} (Y : UU l) â†’
    ( Î£ X P â†’ Y) â†’
    cocone
      ( vertical-map-span-flattening-pushout P f g c)
      ( horizontal-map-span-flattening-pushout P f g c)
      ( Y)
  cocone-map-flattening-pushout Y =
    cocone-map
      ( vertical-map-span-flattening-pushout P f g c)
      ( horizontal-map-span-flattening-pushout P f g c)
      ( cocone-flattening-pushout P f g c)

  comparison-dependent-cocone-ind-Î£-cocone :
    { l : Level} (Y : UU l) â†’
    Î£ ( (a : A) â†’ P (horizontal-map-cocone f g c a) â†’ Y)
      ( Î» k â†’
        Î£ ( (b : B) â†’ P (vertical-map-cocone f g c b) â†’ Y)
          ( Î» l â†’
            ( s : S) (t : P (horizontal-map-cocone f g c (f s))) â†’
            ( k (f s) t) ï¼
            ( l (g s) (tr P (coherence-square-cocone f g c s) t)))) â‰ƒ
    dependent-cocone f g c (Î» x â†’ P x â†’ Y)
  comparison-dependent-cocone-ind-Î£-cocone Y =
    equiv-tot
      ( Î» k â†’
        equiv-tot
          ( Î» l â†’
            equiv-Î -equiv-family
              ( equiv-htpy-dependent-function-dependent-identification-function-type
                ( Y)
                ( coherence-square-cocone f g c)
                ( k âˆ˜ f)
                ( l âˆ˜ g))))

  triangle-comparison-dependent-cocone-ind-Î£-cocone :
    { l : Level} (Y : UU l) â†’
    coherence-triangle-maps
      ( dependent-cocone-map f g c (Î» x â†’ P x â†’ Y))
      ( map-equiv (comparison-dependent-cocone-ind-Î£-cocone Y))
      ( map-equiv equiv-ev-pairÂ³ âˆ˜ cocone-map-flattening-pushout Y âˆ˜ ind-Î£)
  triangle-comparison-dependent-cocone-ind-Î£-cocone Y h =
    eq-pair-eq-fiber
      ( eq-pair-eq-fiber
        ( eq-htpy
          ( inv-htpy
            ( compute-equiv-htpy-dependent-function-dependent-identification-function-type
              ( Y)
              ( coherence-square-cocone f g c)
              ( h)))))
  abstract
    flattening-lemma-pushout :
      flattening-lemma-pushout-statement P f g c
    flattening-lemma-pushout up-c Y =
      is-equiv-left-factor
        ( cocone-map-flattening-pushout Y)
        ( ind-Î£)
        ( is-equiv-right-factor
          ( map-equiv equiv-ev-pairÂ³)
          ( cocone-map-flattening-pushout Y âˆ˜ ind-Î£)
          ( is-equiv-map-equiv equiv-ev-pairÂ³)
          ( is-equiv-top-map-triangle
            ( dependent-cocone-map f g c (Î» x â†’ P x â†’ Y))
            ( map-equiv (comparison-dependent-cocone-ind-Î£-cocone Y))
            ( ( map-equiv equiv-ev-pairÂ³) âˆ˜
              ( cocone-map-flattening-pushout Y) âˆ˜
              ( ind-Î£))
            ( triangle-comparison-dependent-cocone-ind-Î£-cocone Y)
            ( is-equiv-map-equiv (comparison-dependent-cocone-ind-Î£-cocone Y))
            ( dependent-universal-property-universal-property-pushout _ _ _ up-c
              ( Î» x â†’ P x â†’ Y))))
        ( is-equiv-ind-Î£)
```

### Proof of the descent data statement of the flattening lemma

The proof is carried out by constructing a commuting cube, which has
equivalences for vertical maps, the `cocone-flattening-pushout` square for the
bottom, and the `cocone-flattening-descent-data-pushout` square for the top.

The bottom is a pushout by the above flattening lemma, which implies that the
top is also a pushout.

The other parts of the cube are defined naturally, and come from the following
map of spans:

```text
  Î£ (a : A) (PA a) <------- Î£ (s : S) (PA (f s)) -----> Î£ (b : B) (PB b)
         |                           |                         |
         |                           |                         |
         âˆ¨                           âˆ¨                         âˆ¨
Î£ (a : A) (P (i a)) <---- Î£ (s : S) (P (i (f s))) ---> Î£ (b : B) (P (j b))
```

where the vertical maps are equivalences given fiberwise by the equivalence of
descent data.

```agda
module _
  { l1 l2 l3 l4 l5 l6 l7 : Level}
  { S : UU l1} {A : UU l2} {B : UU l3} {X : UU l4}
  ( f : S â†’ A) (g : S â†’ B) (c : cocone f g X)
  ( P : descent-data-pushout (make-span-diagram f g) l5 l6)
  ( Q : X â†’ UU l7)
  ( e :
    equiv-descent-data-pushout P (descent-data-family-cocone-span-diagram c Q))
  where

  coherence-cube-flattening-lemma-descent-data-pushout :
    coherence-cube-maps
      ( vertical-map-span-flattening-pushout Q f g c)
      ( horizontal-map-span-flattening-pushout Q f g c)
      ( horizontal-map-cocone-flattening-pushout Q f g c)
      ( vertical-map-cocone-flattening-pushout Q f g c)
      ( vertical-map-span-flattening-descent-data-pushout P)
      ( horizontal-map-span-flattening-descent-data-pushout P)
      ( horizontal-map-cocone-flattening-descent-data-pushout f g c P Q e)
      ( vertical-map-cocone-flattening-descent-data-pushout f g c P Q e)
      ( tot
        ( ( left-map-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)) âˆ˜
          ( f)))
      ( tot
        ( left-map-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e)))
      ( tot
        ( right-map-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e)))
      ( id)
      ( coherence-square-cocone-flattening-descent-data-pushout f g c P Q e)
      ( refl-htpy)
      ( htpy-map-Î£
        ( Q âˆ˜ vertical-map-cocone f g c)
        ( refl-htpy)
        ( Î» s â†’
          ( tr Q (coherence-square-cocone f g c s)) âˆ˜
          ( left-map-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)
            ( f s)))
        ( inv-htpy âˆ˜
          coherence-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)))
      ( refl-htpy)
      ( refl-htpy)
      ( coherence-square-cocone-flattening-pushout Q f g c)
  coherence-cube-flattening-lemma-descent-data-pushout (s , t) =
    ( ap-id
      ( coherence-square-cocone-flattening-descent-data-pushout f g c P Q e
        ( s , t))) âˆ™
    ( triangle-eq-pair-Î£ Q
      ( coherence-square-cocone f g c s)
      ( inv
        ( coherence-equiv-descent-data-pushout
          ( P)
          ( descent-data-family-cocone-span-diagram c Q)
          ( e)
          ( s)
          ( t)))) âˆ™
    ( ap
      ( eq-pair-Î£ (coherence-square-cocone f g c s) refl âˆ™_)
      ( inv
        ( ( right-unit) âˆ™
          ( compute-ap-map-Î£-map-base-eq-pair-Î£
            ( vertical-map-cocone f g c)
            ( Q)
            ( refl)
            ( inv
              ( coherence-equiv-descent-data-pushout
                ( P)
                ( descent-data-family-cocone-span-diagram c Q)
                ( e)
                ( s)
                ( t)))))))

  abstract
    flattening-lemma-descent-data-pushout :
      flattening-lemma-descent-data-pushout-statement f g c P Q e
    flattening-lemma-descent-data-pushout up-c =
      universal-property-pushout-top-universal-property-pushout-bottom-cube-equiv
        ( vertical-map-span-flattening-pushout Q f g c)
        ( horizontal-map-span-flattening-pushout Q f g c)
        ( horizontal-map-cocone-flattening-pushout Q f g c)
        ( vertical-map-cocone-flattening-pushout Q f g c)
        ( vertical-map-span-flattening-descent-data-pushout P)
        ( horizontal-map-span-flattening-descent-data-pushout P)
        ( horizontal-map-cocone-flattening-descent-data-pushout f g c P Q e)
        ( vertical-map-cocone-flattening-descent-data-pushout f g c P Q e)
        ( equiv-tot
          ( ( left-equiv-equiv-descent-data-pushout
              ( P)
              ( descent-data-family-cocone-span-diagram c Q)
              ( e)) âˆ˜
            ( f)))
        ( equiv-tot
          ( left-equiv-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)))
        ( equiv-tot
          ( right-equiv-equiv-descent-data-pushout
            ( P)
            ( descent-data-family-cocone-span-diagram c Q)
            ( e)))
        ( id-equiv)
        ( coherence-square-cocone-flattening-descent-data-pushout f g c P Q e)
        ( refl-htpy)
        ( htpy-map-Î£
          ( Q âˆ˜ vertical-map-cocone f g c)
          ( refl-htpy)
          ( Î» s â†’
            ( tr Q (coherence-square-cocone f g c s)) âˆ˜
            ( left-map-equiv-descent-data-pushout
              ( P)
              ( descent-data-family-cocone-span-diagram c Q)
              ( e)
              ( f s)))
          ( inv-htpy âˆ˜
            coherence-equiv-descent-data-pushout
              ( P)
              ( descent-data-family-cocone-span-diagram c Q)
              ( e)))
        ( refl-htpy)
        ( refl-htpy)
        ( coherence-square-cocone-flattening-pushout Q f g c)
        ( coherence-cube-flattening-lemma-descent-data-pushout)
        ( flattening-lemma-pushout Q f g c up-c)
```

### Proof of the flattening lemma for pushouts, using equifibered diagrams

The proof is carried out by constructing a commuting cube with the standard
flattening square for `Q` as bottom face and the equifibered flattening square
for `P` as top face. The vertical faces are induced by the equivalence `e`.

```agda
module _
  { l1 l2 l3 l4 l5 l6 l7 l8 : Level}
  { S : UU l1} {A : UU l2} {B : UU l3} {X : UU l4}
  ( f : S â†’ A) (g : S â†’ B) (c : cocone f g X)
  ( P : equifibered-span-diagram (make-span-diagram f g) l5 l6 l7)
  ( Q : X â†’ UU l8)
  ( e :
    equiv-equifibered-span-diagram
      ( P)
      ( equifibered-span-diagram-family-cocone-span-diagram c Q))
  where

  equiv-source-cube-flattening-equifibered-span-diagram :
    Î£ S (spanning-type-family-equifibered-span-diagram P) â‰ƒ
    Î£ S (Q âˆ˜ horizontal-map-cocone f g c âˆ˜ f)
  equiv-source-cube-flattening-equifibered-span-diagram =
    equiv-tot
      ( Î» x â†’
        left-equiv-equiv-equifibered-span-diagram
          ( P)
          ( equifibered-span-diagram-family-cocone-span-diagram c Q)
          ( e)
          ( f x) âˆ˜e
        ( equiv-left-family-equifibered-span-diagram P x))

  map-source-cube-flattening-equifibered-span-diagram :
    Î£ S (spanning-type-family-equifibered-span-diagram P) â†’
    Î£ S (Q âˆ˜ horizontal-map-cocone f g c âˆ˜ f)
  map-source-cube-flattening-equifibered-span-diagram =
    map-equiv equiv-source-cube-flattening-equifibered-span-diagram

  back-right-coherence-cube-flattening-lemma-equifibered-span-diagram :
    coherence-square-maps
      ( horizontal-map-span-flattening-equifibered-span-diagram P)
      ( map-source-cube-flattening-equifibered-span-diagram)
      ( tot
        ( right-map-equiv-equifibered-span-diagram
          ( P)
          ( equifibered-span-diagram-family-cocone-span-diagram c Q)
          ( e)))
      ( horizontal-map-span-flattening-pushout Q f g c)
  back-right-coherence-cube-flattening-lemma-equifibered-span-diagram =
    htpy-map-Î£
      ( Q âˆ˜ vertical-map-cocone f g c)
      ( refl-htpy)
      ( Î» s â†’
        ( tr Q (coherence-square-cocone f g c s)) âˆ˜
        ( map-left-family-flattening-equifibered-span-diagram f g c P Q e s))
      ( coherence-map-flattening-equifibered-span-diagram f g c P Q e)

  coherence-cube-flattening-lemma-equifibered-span-diagram :
    coherence-cube-maps
      ( vertical-map-span-flattening-pushout Q f g c)
      ( horizontal-map-span-flattening-pushout Q f g c)
      ( horizontal-map-cocone-flattening-pushout Q f g c)
      ( vertical-map-cocone-flattening-pushout Q f g c)
      ( vertical-map-span-flattening-equifibered-span-diagram P)
      ( horizontal-map-span-flattening-equifibered-span-diagram P)
      ( horizontal-map-cocone-flattening-equifibered-span-diagram f g c P Q e)
      ( vertical-map-cocone-flattening-equifibered-span-diagram f g c P Q e)
      ( map-source-cube-flattening-equifibered-span-diagram)
      ( tot
        ( left-map-equiv-equifibered-span-diagram
          ( P)
          ( equifibered-span-diagram-family-cocone-span-diagram c Q)
          ( e)))
      ( tot
        ( right-map-equiv-equifibered-span-diagram
          ( P)
          ( equifibered-span-diagram-family-cocone-span-diagram c Q)
          ( e)))
      ( id)
      ( coherence-square-cocone-flattening-equifibered-span-diagram
        f g c P Q e)
      ( refl-htpy)
      ( back-right-coherence-cube-flattening-lemma-equifibered-span-diagram)
      ( refl-htpy)
      ( refl-htpy)
      ( coherence-square-cocone-flattening-pushout Q f g c)
  coherence-cube-flattening-lemma-equifibered-span-diagram (s , t) =
    ( ap-id
      ( coherence-square-cocone-flattening-equifibered-span-diagram f g c P Q e
        ( s , t))) âˆ™
    ( triangle-eq-pair-Î£ Q
      ( coherence-square-cocone f g c s)
      ( coherence-map-flattening-equifibered-span-diagram f g c P Q e s t)) âˆ™
    ( ap
      ( eq-pair-Î£ (coherence-square-cocone f g c s) refl âˆ™_)
      ( inv
        ( ( right-unit) âˆ™
          ( compute-ap-map-Î£-map-base-eq-pair-Î£
            ( vertical-map-cocone f g c)
            ( Q)
            ( refl)
            ( coherence-map-flattening-equifibered-span-diagram
              f g c P Q e s t)))))

  abstract
    flattening-lemma-equifibered-span-diagram :
      flattening-lemma-equifibered-span-diagram-statement f g c P Q e
    flattening-lemma-equifibered-span-diagram up-c =
      universal-property-pushout-top-universal-property-pushout-bottom-cube-equiv
        ( vertical-map-span-flattening-pushout Q f g c)
        ( horizontal-map-span-flattening-pushout Q f g c)
        ( horizontal-map-cocone-flattening-pushout Q f g c)
        ( vertical-map-cocone-flattening-pushout Q f g c)
        ( vertical-map-span-flattening-equifibered-span-diagram P)
        ( horizontal-map-span-flattening-equifibered-span-diagram P)
        ( horizontal-map-cocone-flattening-equifibered-span-diagram f g c P Q e)
        ( vertical-map-cocone-flattening-equifibered-span-diagram f g c P Q e)
        ( equiv-source-cube-flattening-equifibered-span-diagram)
        ( equiv-tot
          ( left-equiv-equiv-equifibered-span-diagram
            ( P)
            ( equifibered-span-diagram-family-cocone-span-diagram c Q)
            ( e)))
        ( equiv-tot
          ( right-equiv-equiv-equifibered-span-diagram
            ( P)
            ( equifibered-span-diagram-family-cocone-span-diagram c Q)
            ( e)))
        ( id-equiv)
        ( coherence-square-cocone-flattening-equifibered-span-diagram
          f g c P Q e)
        ( refl-htpy)
        ( back-right-coherence-cube-flattening-lemma-equifibered-span-diagram)
        ( refl-htpy)
        ( refl-htpy)
        ( coherence-square-cocone-flattening-pushout Q f g c)
        ( coherence-cube-flattening-lemma-equifibered-span-diagram)
        ( flattening-lemma-pushout Q f g c up-c)
```

## See also

- [Mather's second cube theorem](synthetic-homotopy-theory.mathers-second-cube-theorem.md)
  is the [unstraightened](foundation.type-duality.md) version of the flattening
  lemma for pushouts.
