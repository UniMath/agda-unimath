# The circle

```agda
{-# OPTIONS --without-K --exact-split #-}

module synthetic-homotopy-theory.circle where

open import foundation.connected-types
open import foundation.contractible-maps
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.fibers-of-maps
open import foundation.function-extensionality
open import foundation.functions
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.mere-equality
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.sections
open import foundation.structure-identity-principle
open import foundation.universal-property-propositional-truncation
open import foundation.universe-levels

open import structured-types.pointed-homotopies
open import structured-types.pointed-maps
open import structured-types.pointed-types
```

```agda
free-loop : {l1 : Level} (X : UU l1) â†’ UU l1
free-loop X = Î£ X (Î» x â†’ Id x x)

module _
  {l1 : Level} {X : UU l1}
  where
    
  base-free-loop : free-loop X â†’ X
  base-free-loop = pr1
  
  loop-free-loop : (Î± : free-loop X) â†’ Id (base-free-loop Î±) (base-free-loop Î±)
  loop-free-loop = pr2

-- Now we characterize the identity types of free loops

module _
  {l1 : Level} {X : UU l1}
  where

  Eq-free-loop : (Î± Î±' : free-loop X) â†’ UU l1
  Eq-free-loop (pair x Î±) Î±' =
    Î£ (Id x (pr1 Î±')) (Î» p â†’ Id (Î± âˆ™ p) (p âˆ™ (pr2 Î±')))

  refl-Eq-free-loop : (Î± : free-loop X) â†’ Eq-free-loop Î± Î±
  pr1 (refl-Eq-free-loop (pair x Î±)) = refl
  pr2 (refl-Eq-free-loop (pair x Î±)) = right-unit

  Eq-eq-free-loop : (Î± Î±' : free-loop X) â†’ Id Î± Î±' â†’ Eq-free-loop Î± Î±'
  Eq-eq-free-loop Î± .Î± refl = refl-Eq-free-loop Î±

  abstract
    is-contr-total-Eq-free-loop :
      (Î± : free-loop X) â†’ is-contr (Î£ (free-loop X) (Eq-free-loop Î±))
    is-contr-total-Eq-free-loop (pair x Î±) =
      is-contr-total-Eq-structure
        ( Î» x Î±' p â†’ Id (Î± âˆ™ p) (p âˆ™ Î±'))
        ( is-contr-total-path x)
        ( pair x refl)
        ( is-contr-is-equiv'
          ( Î£ (Id x x) (Î» Î±' â†’ Id Î± Î±'))
          ( tot (Î» Î±' Î± â†’ right-unit âˆ™ Î±))
          ( is-equiv-tot-is-fiberwise-equiv
            ( Î» Î±' â†’ is-equiv-concat right-unit Î±'))
          ( is-contr-total-path Î±))

  abstract
    is-equiv-Eq-eq-free-loop :
      (Î± Î±' : free-loop X) â†’ is-equiv (Eq-eq-free-loop Î± Î±')
    is-equiv-Eq-eq-free-loop Î± =
      fundamental-theorem-id Î±
        ( refl-Eq-free-loop Î±)
        ( is-contr-total-Eq-free-loop Î±)
        ( Eq-eq-free-loop Î±) 

{- We introduce dependent free loops, which are used in the induction principle
   of the circle. -}

module _
  {l1 l2 : Level} {X : UU l1} (Î± : free-loop X) (P : X â†’ UU l2)
  where
    
  dependent-free-loop : UU l2
  dependent-free-loop =
    Î£ ( P (base-free-loop Î±))
      ( Î» pâ‚€ â†’ Id (tr P (loop-free-loop Î±) pâ‚€) pâ‚€)

  Eq-dependent-free-loop : (p p' : dependent-free-loop) â†’ UU l2
  Eq-dependent-free-loop (pair y p) p' =
    Î£ ( Id y (pr1 p'))
      ( Î» q â†’ Id (p âˆ™ q) ((ap (tr P (loop-free-loop Î±)) q) âˆ™ (pr2 p')))

  refl-Eq-dependent-free-loop :
    (p : dependent-free-loop) â†’ Eq-dependent-free-loop p p
  pr1 (refl-Eq-dependent-free-loop (pair y p)) = refl
  pr2 (refl-Eq-dependent-free-loop (pair y p)) = right-unit

  Eq-dependent-free-loop-eq :
    ( p p' : dependent-free-loop) â†’ Id p p' â†’ Eq-dependent-free-loop p p'
  Eq-dependent-free-loop-eq p .p refl = refl-Eq-dependent-free-loop p

  abstract
    is-contr-total-Eq-dependent-free-loop :
      ( p : dependent-free-loop) â†’
      is-contr (Î£ dependent-free-loop (Eq-dependent-free-loop p))
    is-contr-total-Eq-dependent-free-loop (pair y p) =
      is-contr-total-Eq-structure
        ( Î» y' p' q â†’ Id (p âˆ™ q) ((ap (tr P (loop-free-loop Î±)) q) âˆ™ p'))
        ( is-contr-total-path y)
        ( pair y refl)
        ( is-contr-is-equiv'
          ( Î£ (Id (tr P (loop-free-loop Î±) y) y) (Î» p' â†’ Id p p'))
          ( tot (Î» p' Î± â†’ right-unit âˆ™ Î±))
          ( is-equiv-tot-is-fiberwise-equiv
            ( Î» p' â†’ is-equiv-concat right-unit p'))
          ( is-contr-total-path p))

  abstract
    is-equiv-Eq-dependent-free-loop-eq :
      (p p' : dependent-free-loop) â†’
      is-equiv (Eq-dependent-free-loop-eq p p')
    is-equiv-Eq-dependent-free-loop-eq p =
      fundamental-theorem-id p
        ( refl-Eq-dependent-free-loop p)
        ( is-contr-total-Eq-dependent-free-loop p)
        ( Eq-dependent-free-loop-eq p)

  eq-Eq-dependent-free-loop :
    (p p' : dependent-free-loop) â†’
    Eq-dependent-free-loop p p' â†’ Id p p'
  eq-Eq-dependent-free-loop p p' =
    map-inv-is-equiv (is-equiv-Eq-dependent-free-loop-eq p p')

{- We now define the induction principle of the circle. -}

module _
  {l1 l2 : Level} {X : UU l1} (Î± : free-loop X) (P : X â†’ UU l2)
  where

  ev-free-loop' : ((x : X) â†’ P x) â†’ dependent-free-loop Î± P
  pr1 (ev-free-loop' f) = f (base-free-loop Î±)
  pr2 (ev-free-loop' f) = apd f (loop-free-loop Î±)

module _
  {l1 : Level} (l2 : Level) {X : UU l1} (Î± : free-loop X)
  where

  induction-principle-circle : UU ((lsuc l2) âŠ” l1)
  induction-principle-circle = (P : X â†’ UU l2) â†’ sec (ev-free-loop' Î± P)

{- Section 11.2 The universal property of the circle -}

{- We first state the universal property of the circle -}

module _
  {l1 l2 : Level} {X : UU l1} (Î± : free-loop X) (Y : UU l2)
  where

  ev-free-loop : (X â†’ Y) â†’ free-loop Y
  ev-free-loop f = pair (f (base-free-loop Î±)) (ap f (loop-free-loop Î±))

module _
  {l1 : Level} (l2 : Level) {X : UU l1} (Î± : free-loop X)
  where
  
  universal-property-circle : UU (l1 âŠ” lsuc l2)
  universal-property-circle = (Y : UU l2) â†’ is-equiv (ev-free-loop Î± Y)

{- A fairly straightforward proof of the universal property of the circle
   factors through the dependent universal property of the circle. -}

module _
  {l1 : Level} (l2 : Level) {X : UU l1} (Î± : free-loop X)
  where

  dependent-universal-property-circle : UU ((lsuc l2) âŠ” l1)
  dependent-universal-property-circle =
    (P : X â†’ UU l2) â†’ is-equiv (ev-free-loop' Î± P)

{- We first prove that the dependent universal property of the circle follows
   from the induction principle of the circle. To show this, we have to show
   that the section of ev-free-loop' is also a retraction. This construction
   is also by the induction principle of the circle, but it requires (a minimal
   amount of) preparations. -}

module _
  {l1 l2 : Level} {X : UU l1} {P : X â†’ UU l2} (f g : (x : X) â†’ P x)
  where

  Eq-subst : X â†’ UU _
  Eq-subst x = Id (f x) (g x)

  tr-Eq-subst :
    { x y : X} (p : Id x y) (q : Eq-subst x) (r : Eq-subst y)â†’
    ( Id ((apd f p) âˆ™ r) ((ap (tr P p) q) âˆ™ (apd g p))) â†’
    ( Id (tr Eq-subst p q) r)
  tr-Eq-subst refl q .((ap id q) âˆ™ refl) refl = inv (right-unit âˆ™ (ap-id q))

module _
  {l1 : Level} {X : UU l1} (Î± : free-loop X)
  where

  dependent-free-loop-htpy :
    {l2 : Level} {P : X â†’ UU l2} {f g : (x : X) â†’ P x} â†’
    ( Eq-dependent-free-loop Î± P (ev-free-loop' Î± P f) (ev-free-loop' Î± P g)) â†’
    ( dependent-free-loop Î± (Î» x â†’ Id (f x) (g x)))
  dependent-free-loop-htpy {l2} {P} {f} {g} (pair p q) =
    pair p (tr-Eq-subst f g (loop-free-loop Î±) p p q)

  isretr-ind-circle :
    ( ind-circle : {l : Level} â†’ induction-principle-circle l Î±)
    { l2 : Level} (P : X â†’ UU l2) â†’
    ( (pr1 (ind-circle P)) âˆ˜ (ev-free-loop' Î± P)) ~ id
  isretr-ind-circle ind-circle P f =
    eq-htpy
      ( pr1
        ( ind-circle
          ( Î» t â†’ Id (pr1 (ind-circle P) (ev-free-loop' Î± P f) t) (f t)))
        ( dependent-free-loop-htpy
          ( Eq-dependent-free-loop-eq Î± P _ _
            ( pr2 (ind-circle P) (ev-free-loop' Î± P f)))))

  abstract
    dependent-universal-property-induction-principle-circle :
      ({l : Level} â†’ induction-principle-circle l Î±) â†’
      ({l : Level} â†’ dependent-universal-property-circle l Î±)
    dependent-universal-property-induction-principle-circle ind-circle P =
      is-equiv-has-inverse
        ( pr1 (ind-circle P))
        ( pr2 (ind-circle P))
        ( isretr-ind-circle ind-circle P)

  {- We use the dependent universal property to derive a uniqeness property of
     dependent functions on the circle. -}

  uniqueness-dependent-universal-property-circle :
    ({l : Level} â†’ dependent-universal-property-circle l Î±) â†’
    {l2 : Level} {P : X â†’ UU l2} (k : dependent-free-loop Î± P) â†’
    is-contr
      ( Î£ ( (x : X) â†’ P x)
          ( Î» h â†’ Eq-dependent-free-loop Î± P (ev-free-loop' Î± P h) k))
  uniqueness-dependent-universal-property-circle dup-circle {l2} {P} k =
    is-contr-is-equiv'
      ( fib (ev-free-loop' Î± P) k)
      ( tot (Î» h â†’ Eq-dependent-free-loop-eq Î± P (ev-free-loop' Î± P h) k))
      ( is-equiv-tot-is-fiberwise-equiv
        (Î» h â†’ is-equiv-Eq-dependent-free-loop-eq Î± P (ev-free-loop' Î± P h) k))
      ( is-contr-map-is-equiv (dup-circle P) k)

{- Now that we have established the dependent universal property, we can
   reduce the (non-dependent) universal property to the dependent case. We do
   so by constructing a commuting triangle relating ev-free-loop to 
   ev-free-loop' via a comparison equivalence. -}

tr-const :
  {i j : Level} {A : UU i} {B : UU j} {x y : A} (p : Id x y) (b : B) â†’
  Id (tr (Î» (a : A) â†’ B) p b) b
tr-const refl b = refl

apd-const :
  {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) {x y : A}
  (p : Id x y) â†’ Id (apd f p) ((tr-const p (f x)) âˆ™ (ap f p))
apd-const f refl = refl

module _
  {l1 l2 : Level} {X : UU l1} (Î± : free-loop X) (Y : UU l2)
  where

  compute-dependent-free-loop-const :
    free-loop Y â‰ƒ dependent-free-loop Î± (Î» x â†’ Y)
  compute-dependent-free-loop-const =
    equiv-tot (Î» y â†’ equiv-concat (tr-const (loop-free-loop Î±) y) y)

  map-compute-dependent-free-loop-const :
    free-loop Y â†’ dependent-free-loop Î± (Î» x â†’ Y)
  map-compute-dependent-free-loop-const =
    map-equiv compute-dependent-free-loop-const

  triangle-comparison-free-loop :
    ( map-compute-dependent-free-loop-const âˆ˜ (ev-free-loop Î± Y)) ~
    ( ev-free-loop' Î± (Î» x â†’ Y))
  triangle-comparison-free-loop f =
    eq-Eq-dependent-free-loop Î±
      ( Î» x â†’ Y)
      ( map-compute-dependent-free-loop-const
        ( ev-free-loop Î± Y f))
      ( ev-free-loop' Î± (Î» x â†’ Y) f)
      ( pair refl (right-unit âˆ™ (inv (apd-const f (loop-free-loop Î±)))))

module _
  {l1 : Level} {X : UU l1} (Î± : free-loop X)
  where

  abstract
    universal-property-dependent-universal-property-circle :
      ({l : Level} â†’ dependent-universal-property-circle l Î±) â†’
      ({l : Level} â†’ universal-property-circle l Î±)
    universal-property-dependent-universal-property-circle dup-circle Y =
      is-equiv-right-factor
        ( ev-free-loop' Î± (Î» x â†’ Y))
        ( map-compute-dependent-free-loop-const Î± Y)
        ( ev-free-loop Î± Y)
        ( inv-htpy (triangle-comparison-free-loop Î± Y))
        ( is-equiv-map-equiv (compute-dependent-free-loop-const Î± Y))
        ( dup-circle (Î» x â†’ Y))

  {- Now we get the universal property of the circle from the induction
     principle of the circle by composing the earlier two proofs. -}

  abstract
    universal-property-induction-principle-circle :
      ({l : Level} â†’ induction-principle-circle l Î±) â†’
      ({l : Level} â†’ universal-property-circle l Î±)
    universal-property-induction-principle-circle X =
      universal-property-dependent-universal-property-circle
        ( dependent-universal-property-induction-principle-circle Î± X)

  abstract
    uniqueness-universal-property-circle :
      ({l : Level} â†’ universal-property-circle l Î±) â†’
      {l2 : Level} (Y : UU l2) (Î±' : free-loop Y) â†’
      is-contr (Î£ (X â†’ Y) (Î» f â†’ Eq-free-loop (ev-free-loop Î± Y f) Î±'))
    uniqueness-universal-property-circle up-circle Y Î±' =
      is-contr-is-equiv'
        ( fib (ev-free-loop Î± Y) Î±')
        ( tot (Î» f â†’ Eq-eq-free-loop (ev-free-loop Î± Y f) Î±'))
        ( is-equiv-tot-is-fiberwise-equiv
          ( Î» f â†’ is-equiv-Eq-eq-free-loop (ev-free-loop Î± Y f) Î±'))
        ( is-contr-map-is-equiv (up-circle Y) Î±')

{- We assume that we have a circle. -}

postulate ğ•ŠÂ¹ : UU lzero

postulate base-ğ•ŠÂ¹ : ğ•ŠÂ¹

postulate loop-ğ•ŠÂ¹ : Id base-ğ•ŠÂ¹ base-ğ•ŠÂ¹

free-loop-ğ•ŠÂ¹ : free-loop ğ•ŠÂ¹
pr1 free-loop-ğ•ŠÂ¹ = base-ğ•ŠÂ¹
pr2 free-loop-ğ•ŠÂ¹ = loop-ğ•ŠÂ¹

ğ•ŠÂ¹-Pointed-Type : Pointed-Type lzero
pr1 ğ•ŠÂ¹-Pointed-Type = ğ•ŠÂ¹
pr2 ğ•ŠÂ¹-Pointed-Type = base-ğ•ŠÂ¹

postulate ind-ğ•ŠÂ¹ : {l : Level} â†’ induction-principle-circle l free-loop-ğ•ŠÂ¹

module _
  where
  
  dependent-universal-property-ğ•ŠÂ¹ :
    {l : Level} â†’ dependent-universal-property-circle l free-loop-ğ•ŠÂ¹
  dependent-universal-property-ğ•ŠÂ¹ =
    dependent-universal-property-induction-principle-circle free-loop-ğ•ŠÂ¹ ind-ğ•ŠÂ¹

  uniqueness-dependent-universal-property-ğ•ŠÂ¹ :
    {l : Level} {P : ğ•ŠÂ¹ â†’ UU l} (k : dependent-free-loop free-loop-ğ•ŠÂ¹ P) â†’
    is-contr
      ( Î£ ( (x : ğ•ŠÂ¹) â†’ P x)
          ( Î» h â†’
            Eq-dependent-free-loop free-loop-ğ•ŠÂ¹ P
              ( ev-free-loop' free-loop-ğ•ŠÂ¹ P h) k))
  uniqueness-dependent-universal-property-ğ•ŠÂ¹ {l} {P} =
    uniqueness-dependent-universal-property-circle
      free-loop-ğ•ŠÂ¹
      dependent-universal-property-ğ•ŠÂ¹

  module _
    {l : Level} (P : ğ•ŠÂ¹ â†’ UU l) (p0 : P base-ğ•ŠÂ¹) (Î± : Id (tr P loop-ğ•ŠÂ¹ p0) p0)
    where

    Î -ğ•ŠÂ¹ : UU l
    Î -ğ•ŠÂ¹ =
      Î£ ( (x : ğ•ŠÂ¹) â†’ P x)
        ( Î» h â†’
          Eq-dependent-free-loop free-loop-ğ•ŠÂ¹ P
            ( ev-free-loop' free-loop-ğ•ŠÂ¹ P h) (pair p0 Î±))

    apply-dependent-universal-property-ğ•ŠÂ¹ : Î -ğ•ŠÂ¹
    apply-dependent-universal-property-ğ•ŠÂ¹ =
      center (uniqueness-dependent-universal-property-ğ•ŠÂ¹ (pair p0 Î±))
  
    function-apply-dependent-universal-property-ğ•ŠÂ¹ : (x : ğ•ŠÂ¹) â†’ P x
    function-apply-dependent-universal-property-ğ•ŠÂ¹ =
      pr1 apply-dependent-universal-property-ğ•ŠÂ¹

    base-dependent-universal-property-ğ•ŠÂ¹ :
      Id (function-apply-dependent-universal-property-ğ•ŠÂ¹ base-ğ•ŠÂ¹) p0
    base-dependent-universal-property-ğ•ŠÂ¹ =
      pr1 (pr2 apply-dependent-universal-property-ğ•ŠÂ¹)

    loop-dependent-universal-property-ğ•ŠÂ¹ :
      Id ( apd function-apply-dependent-universal-property-ğ•ŠÂ¹ loop-ğ•ŠÂ¹ âˆ™
           base-dependent-universal-property-ğ•ŠÂ¹)
         ( ap (tr P loop-ğ•ŠÂ¹) base-dependent-universal-property-ğ•ŠÂ¹ âˆ™ Î±)
    loop-dependent-universal-property-ğ•ŠÂ¹ =
      pr2 (pr2 apply-dependent-universal-property-ğ•ŠÂ¹)

  universal-property-ğ•ŠÂ¹ :
    {l : Level} â†’ universal-property-circle l free-loop-ğ•ŠÂ¹
  universal-property-ğ•ŠÂ¹ =
    universal-property-dependent-universal-property-circle
      free-loop-ğ•ŠÂ¹
      dependent-universal-property-ğ•ŠÂ¹

  uniqueness-universal-property-ğ•ŠÂ¹ :
    {l : Level} {X : UU l} (Î± : free-loop X) â†’
    is-contr
      ( Î£ ( ğ•ŠÂ¹ â†’ X)
          ( Î» h â†’ Eq-free-loop (ev-free-loop free-loop-ğ•ŠÂ¹ X h) Î±))
  uniqueness-universal-property-ğ•ŠÂ¹ {l} {X} =
    uniqueness-universal-property-circle free-loop-ğ•ŠÂ¹ universal-property-ğ•ŠÂ¹ X

  module _
    {l : Level} {X : UU l} (x : X) (Î± : Id x x)
    where

    Map-ğ•ŠÂ¹ : UU l
    Map-ğ•ŠÂ¹ =
      Î£ ( ğ•ŠÂ¹ â†’ X)
        ( Î» h â†’ Eq-free-loop (ev-free-loop free-loop-ğ•ŠÂ¹ X h) (pair x Î±))

    apply-universal-property-ğ•ŠÂ¹ : Map-ğ•ŠÂ¹
    apply-universal-property-ğ•ŠÂ¹ =
      center (uniqueness-universal-property-ğ•ŠÂ¹ (pair x Î±))
      
    map-apply-universal-property-ğ•ŠÂ¹ : ğ•ŠÂ¹ â†’ X
    map-apply-universal-property-ğ•ŠÂ¹ =
      pr1 apply-universal-property-ğ•ŠÂ¹

    base-universal-property-ğ•ŠÂ¹ :
      Id (map-apply-universal-property-ğ•ŠÂ¹ base-ğ•ŠÂ¹) x
    base-universal-property-ğ•ŠÂ¹ =
      pr1 (pr2 apply-universal-property-ğ•ŠÂ¹)

    loop-universal-property-ğ•ŠÂ¹ :
      Id ( ap map-apply-universal-property-ğ•ŠÂ¹ loop-ğ•ŠÂ¹ âˆ™
           base-universal-property-ğ•ŠÂ¹)
         ( base-universal-property-ğ•ŠÂ¹ âˆ™ Î±)
    loop-universal-property-ğ•ŠÂ¹ =
      pr2 (pr2 apply-universal-property-ğ•ŠÂ¹)

{- Exercises -}

-- Exercise 11.1

{- The dependent universal property of the circle (and hence also the induction
   principle of the circle, implies that the circle is connected in the sense
   that for any family of propositions parametrized by the circle, if the
   proposition at the base holds, then it holds for any x : circle. -}

abstract
  is-connected-circle' :
    { l1 l2 : Level} {X : UU l1} (l : free-loop X) â†’
    ( dup-circle : dependent-universal-property-circle l2 l)
    ( P : X â†’ UU l2) (is-prop-P : (x : X) â†’ is-prop (P x)) â†’
    P (base-free-loop l) â†’ (x : X) â†’ P x
  is-connected-circle' l dup-circle P is-prop-P p =
    map-inv-is-equiv
      ( dup-circle P)
      ( pair p (center (is-prop-P _ (tr P (pr2 l) p) p)))

--------------------------------------------------------------------------------

-- The circle is path connected

mere-eq-ğ•ŠÂ¹ : (x y : ğ•ŠÂ¹) â†’ mere-eq x y
mere-eq-ğ•ŠÂ¹ =
  function-apply-dependent-universal-property-ğ•ŠÂ¹
    ( Î» x â†’ (y : ğ•ŠÂ¹) â†’ mere-eq x y)
    ( function-apply-dependent-universal-property-ğ•ŠÂ¹
      ( mere-eq base-ğ•ŠÂ¹)
      ( refl-mere-eq)
      ( eq-is-prop is-prop-type-trunc-Prop))
    ( eq-is-prop (is-prop-Î  (Î» y â†’ is-prop-type-trunc-Prop)))

is-path-connected-ğ•ŠÂ¹ : is-path-connected ğ•ŠÂ¹
is-path-connected-ğ•ŠÂ¹ = is-path-connected-mere-eq base-ğ•ŠÂ¹ (mere-eq-ğ•ŠÂ¹ base-ğ•ŠÂ¹)
```
