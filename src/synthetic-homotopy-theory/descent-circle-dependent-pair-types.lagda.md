# Descent data for families of dependent pair types over the circle

```agda
module synthetic-homotopy-theory.descent-circle-dependent-pair-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.equality-dependent-pair-types
open import foundation.functoriality-dependent-pair-types
open import foundation.identity-types
open import foundation.universe-levels

open import synthetic-homotopy-theory.dependent-descent-circle
open import synthetic-homotopy-theory.descent-circle
open import synthetic-homotopy-theory.free-loops
```

</details>

## Idea

Given a family `A : ğ•ŠÂ¹ â†’ ğ’°` over the
[circle](synthetic-homotopy-theory.circle.md) and a family
`B : (t : ğ•ŠÂ¹) â†’ (A t) â†’ ğ’°` over `A`, the
[descent data](synthetic-homotopy-theory.descent-circle.md) for the family of
[dependent pair types](foundation.dependent-pair-types.md) `Î» t â†’ Î£ (A t) (B t)`
is `(Î£ X R, map-Î£ e k)`, where `(X, e)` is descent data for `A` and `(R, k)` is
dependent descent data for `B`.

## Definitions

### Descent data for families of dependent pair types over the circle

```agda
module _
  { l1 l2 l3 : Level} {S : UU l1} (l : free-loop S)
  ( A : family-with-descent-data-circle l l2)
  ( B : double-family-with-dependent-descent-data-circle l A l3)
  where

  descent-data-circle-dependent-pair-type : descent-data-circle (l2 âŠ” l3)
  pr1 descent-data-circle-dependent-pair-type =
    Î£ ( type-family-with-descent-data-circle A)
      ( type-double-family-with-dependent-descent-data-circle A B)
  pr2 descent-data-circle-dependent-pair-type =
    equiv-Î£
      ( type-double-family-with-dependent-descent-data-circle A B)
      ( aut-family-with-descent-data-circle A)
      ( dependent-automorphism-double-family-with-dependent-descent-data-circle
        ( A)
        ( B))

  family-descent-data-circle-dependent-pair-type : S â†’ UU (l2 âŠ” l3)
  family-descent-data-circle-dependent-pair-type x =
    Î£ ( family-family-with-descent-data-circle A x)
      ( double-family-double-family-with-dependent-descent-data-circle A B x)
```

## Properties

### Characterization of descent data for families of dependent pair types over the circle

```agda
module _
  { l1 l2 l3 : Level} {S : UU l1} (l : free-loop S)
  ( A : family-with-descent-data-circle l l2)
  ( B : double-family-with-dependent-descent-data-circle l A l3)
  where

  eq-descent-data-circle-dependent-pair-type :
    equiv-descent-data-circle
      ( descent-data-circle-dependent-pair-type l A B)
      ( descent-data-family-circle l
        ( family-descent-data-circle-dependent-pair-type l A B))
  pr1 eq-descent-data-circle-dependent-pair-type =
    equiv-Î£
      ( double-family-double-family-with-dependent-descent-data-circle A B
        ( base-free-loop l))
      ( equiv-family-with-descent-data-circle A)
      ( equiv-double-family-with-dependent-descent-data-circle A B)
  pr2 eq-descent-data-circle-dependent-pair-type u =
    inv
      ( tr-Î£
          ( double-family-double-family-with-dependent-descent-data-circle A B)
          ( loop-free-loop l)
          ( map-Î£
            ( double-family-double-family-with-dependent-descent-data-circle
              ( A)
              ( B)
              ( base-free-loop l))
            ( map-equiv-family-with-descent-data-circle A)
            ( map-equiv-double-family-with-dependent-descent-data-circle A B)
            ( u)) âˆ™
        eq-pair-Î£
          ( inv (coherence-square-family-with-descent-data-circle A (pr1 u)))
          ( inv
            ( coherence-square-double-family-with-dependent-descent-data-circle
              ( A)
              ( B)
              ( pr1 u)
              ( pr2 u) âˆ™
              tr-eq-pair-Î£
                ( ind-Î£
                  ( double-family-double-family-with-dependent-descent-data-circle
                    ( A)
                    ( B)))
                ( loop-free-loop l)
                ( inv
                  ( coherence-square-family-with-descent-data-circle A (pr1 u)))
                ( map-equiv-double-family-with-dependent-descent-data-circle A B
                  ( pr1 u)
                  ( pr2 u)))))

  family-with-descent-data-circle-dependent-pair-type :
    family-with-descent-data-circle l (l2 âŠ” l3)
  pr1 family-with-descent-data-circle-dependent-pair-type =
    family-descent-data-circle-dependent-pair-type l A B
  pr1 (pr2 family-with-descent-data-circle-dependent-pair-type) =
    descent-data-circle-dependent-pair-type l A B
  pr2 (pr2 family-with-descent-data-circle-dependent-pair-type) =
    eq-descent-data-circle-dependent-pair-type
```
