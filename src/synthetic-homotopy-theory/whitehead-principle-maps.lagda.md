# The Whitehead principle for maps

```agda
module synthetic-homotopy-theory.whitehead-principle-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.connected-maps
open import foundation.connected-types
open import foundation.dependent-pair-types
open import foundation.fibers-of-maps
open import foundation.truncation-levels
open import foundation.unit-type
open import foundation.universe-levels

open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation-core.equivalences
open import foundation-core.identity-types
open import foundation-core.propositions

open import synthetic-homotopy-theory.whitehead-principle-types
```

</details>

## Idea

A map `f : X â†’ Y` is said to be **âˆ-connected** if it is `k`-connected for all
`k : ğ•‹`.

By the equivalence between equivalences and contractible maps, equivalences are
âˆ-connected.

The **Whitehead principle for maps** asserts the converse, that âˆ-connected maps
are equivalences.

## Definition

### âˆ-connected maps

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  is-âˆ-connected-map-Prop : Prop (l1 âŠ” l2)
  is-âˆ-connected-map-Prop = Î -Prop ğ•‹ (Î» k â†’ is-connected-map-Prop k f)

  is-âˆ-connected-map : UU (l1 âŠ” l2)
  is-âˆ-connected-map = type-Prop is-âˆ-connected-map-Prop

  is-prop-is-âˆ-connected-map : is-prop is-âˆ-connected-map
  is-prop-is-âˆ-connected-map = is-prop-type-Prop is-âˆ-connected-map-Prop
```

### A map is âˆ-connected iff its fibers are [âˆ-connected](synthetic-homotopy-theory.whitehead-principle-types.md)

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  is-âˆ-connected-map-fibers-are-âˆ-connected :
    is-âˆ-connected-map f â†’ (y : Y) â†’ is-âˆ-connected (fiber f y)
  is-âˆ-connected-map-fibers-are-âˆ-connected f-âˆ-conn y k = f-âˆ-conn k y

  fibers-are-âˆ-connected-is-âˆ-connected-map :
    ((y : Y) â†’ is-âˆ-connected (fiber f y)) â†’ is-âˆ-connected-map f
  fibers-are-âˆ-connected-is-âˆ-connected-map fib-âˆ-conn k y = fib-âˆ-conn y k
```

### Equivalences are âˆ-connected

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  is-equiv-is-âˆ-connected : is-equiv f â†’ is-âˆ-connected-map f
  is-equiv-is-âˆ-connected f-equiv k = is-connected-map-is-equiv f-equiv
```

### The Whitehead principle for maps

```agda
Whitehead-Principle-Map-Level : (l1 l2 : Level) â†’ UU (lsuc l1 âŠ” lsuc l2)
Whitehead-Principle-Map-Level l1 l2 =
  ( X : UU l1) â†’ (Y : UU l2) â†’ (f : X â†’ Y) â†’ is-âˆ-connected-map f â†’ is-equiv f

Whitehead-Principle-Map : UUÏ‰
Whitehead-Principle-Map = {l1 l2 : Level} â†’ Whitehead-Principle-Map-Level l1 l2
```

## Properties

### The Whitehead principle for maps implies the Whitehead principle for types

```agda
Whitehead-Principle-Maps-implies-Types :
  Whitehead-Principle-Map â†’ Whitehead-Principle
Whitehead-Principle-Maps-implies-Types WP X X-âˆ-conn =
  is-contr-equiv-unit eq where
  eq : X â‰ƒ unit
  pr1 eq = Î» _ â†’ star
  pr2 eq =
    WP X unit (Î» _ â†’ star) (fibers-are-âˆ-connected-is-âˆ-connected-map
    ( Î» _ â†’ star)
    Î» y â†’ is-âˆ-connected-equiv (equiv-fiber-terminal-map star) X-âˆ-conn)
```

### The Whitehead principle for types implies the Whitehead principle for maps

```agda
Whitehead-Principle-Types-implies-Maps :
  Whitehead-Principle â†’ Whitehead-Principle-Map
Whitehead-Principle-Types-implies-Maps WP X Y f f-âˆ-conn =
  is-equiv-is-contr-map f-ctr where
  f-ctr : is-contr-map f
  f-ctr y = WP (fiber f y) (Î» x â†’ f-âˆ-conn x y)
```

## External links

For the equivalent concept in the âˆ-categorical semantics of homotopy type
theory, cf.
[Â§ 6.5.2 of Lurie's _Higher Topos Theory_](https://www.math.ias.edu/~lurie/papers/highertopoi.pdf)
