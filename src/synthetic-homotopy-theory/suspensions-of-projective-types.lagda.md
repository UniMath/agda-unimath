# Suspensions of projective types

```agda
module synthetic-homotopy-theory.suspensions-of-projective-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.0-connected-types
open import foundation.connected-maps
open import foundation.connected-types
open import foundation.dependent-identifications
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.fibers-of-maps
open import foundation.function-types
open import foundation.functoriality-propositional-truncation
open import foundation.identity-types
open import foundation.inhabited-types
open import foundation.postcomposition-functions
open import foundation.projective-types
open import foundation.propositional-truncations
open import foundation.transport-along-identifications
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.universe-levels

open import synthetic-homotopy-theory.dependent-suspension-structures
open import synthetic-homotopy-theory.suspension-structures
open import synthetic-homotopy-theory.suspensions-of-types
```

</details>

## Idea

The [suspension](synthetic-homotopy-theory.suspensions-of-types.md) of a
`k`-[projective](foundation.projective-types.md)
`k`-[type](foundation.truncated-types.md) is `k+1`-projective.

## Definitions

### Postcomposition at suspensions

```agda
module _
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ B) (g : suspension X â†’ B)
  where

  fam-fiber-postcomp-suspension : suspension X â†’ UU (l2 âŠ” l3)
  fam-fiber-postcomp-suspension z = fiber f (g z)

  dependent-suspension-structure-fam-fiber-postcomp-suspension :
    UU (l1 âŠ” l2 âŠ” l3)
  dependent-suspension-structure-fam-fiber-postcomp-suspension =
    dependent-suspension-structure
      ( fam-fiber-postcomp-suspension)
      ( suspension-structure-suspension X)

  is-inhabited-fiber-postcomp-suspension :
    is-inhabited
      ( dependent-suspension-structure
        ( fam-fiber-postcomp-suspension)
        ( suspension-structure-suspension X)) â†’
    is-inhabited (fiber (postcomp (suspension X) f) g)
  is-inhabited-fiber-postcomp-suspension =
    map-trunc-Prop
      ( map-equiv (compute-Î -fiber-postcomp (suspension X) f g) âˆ˜
        map-inv-equiv (equiv-dup-suspension (fam-fiber-postcomp-suspension)))

  dependent-identification-merid-fam-fiber-postcomp-suspension :
    (N : fam-fiber-postcomp-suspension north-suspension)
    (S : fam-fiber-postcomp-suspension south-suspension) â†’
    X â†’ UU (l2 âŠ” l3)
  dependent-identification-merid-fam-fiber-postcomp-suspension N S x =
    dependent-identification
      ( fam-fiber-postcomp-suspension)
      ( meridian-suspension x)
      ( N)
      ( S)

  is-connected-dependent-identification-merid-fam-fiber-postcomp-suspension :
    (k : ğ•‹) â†’ is-connected-map (succ-ğ•‹ k) f â†’
    (N : fam-fiber-postcomp-suspension north-suspension)
    (S : fam-fiber-postcomp-suspension south-suspension) â†’
    (x : X) â†’
    is-connected k
      ( dependent-identification-merid-fam-fiber-postcomp-suspension N S x)
  is-connected-dependent-identification-merid-fam-fiber-postcomp-suspension
    k F N S x =
    is-connected-eq-is-connected (F (g south-suspension))

  is-connected-map-pr1-Î£-dependent-identification-merid-fam-fiber-postcomp-suspension :
    (k : ğ•‹) â†’ is-connected-map (succ-ğ•‹ k) f â†’
    (N : fam-fiber-postcomp-suspension north-suspension)
    (S : fam-fiber-postcomp-suspension south-suspension) â†’
    is-connected-map k
      ( pr1
        { B = dependent-identification-merid-fam-fiber-postcomp-suspension N S})
  is-connected-map-pr1-Î£-dependent-identification-merid-fam-fiber-postcomp-suspension
    k F N S x =
      is-connected-equiv
        ( equiv-fiber-pr1
          ( dependent-identification-merid-fam-fiber-postcomp-suspension N S)
          ( x))
        ( is-connected-dependent-identification-merid-fam-fiber-postcomp-suspension
          k F N S x)
```

## Properties

### Suspensions of `k`-projective `k`-types are `(k+1)`-projective

```agda
module _
  {l1 l2 l3 : Level} {X : UU l1} (k : â„•)
  (is-k-trunc-X : is-trunc (truncation-level-â„• k) X)
  (is-k-projective-X : is-trunc-projective-Level (l1 âŠ” l2 âŠ” l3) l1 k X)
  where

  is-inhabited-Î -dependent-identification-merid-fam-fiber-postcomp-suspension-is-trunc-projective :
    {A : UU l2} {B : UU l3}
    (f : A â†’ B) (g : suspension X â†’ B) â†’
    is-connected-map (truncation-level-â„• k) f â†’
    (N : fam-fiber-postcomp-suspension f g north-suspension)
    (S : fam-fiber-postcomp-suspension f g south-suspension) â†’
    is-inhabited
      ( (x : X) â†’
        dependent-identification-merid-fam-fiber-postcomp-suspension f g N S x)
  is-inhabited-Î -dependent-identification-merid-fam-fiber-postcomp-suspension-is-trunc-projective
    f g F N S =
    map-trunc-Prop
      ( map-inv-equiv
        ( compute-fiber-postcomp-pr1
          ( dependent-identification-merid-fam-fiber-postcomp-suspension f g
            ( N)
            ( S))
          ( id)))
      ( is-k-projective-X
        ( Î£ ( X)
            ( dependent-identification-merid-fam-fiber-postcomp-suspension f g
              ( N)
              ( S)))
        ( X , is-k-trunc-X)
        ( pr1 ,
          is-connected-map-pr1-Î£-dependent-identification-merid-fam-fiber-postcomp-suspension
            ( f)
            ( g)
            ( truncation-level-minus-one-â„• k)
            ( F)
            ( N)
            ( S))
        ( id))

  is-inhabited-dependent-suspension-structure-fam-fiber-postcomp-suspension-is-trunc-projective :
    {A : UU l2} {B : UU l3}
    (f : A â†’ B) (g : suspension X â†’ B) â†’
    is-connected-map (truncation-level-â„• k) f â†’
    is-inhabited
      ( dependent-suspension-structure-fam-fiber-postcomp-suspension f g)
  is-inhabited-dependent-suspension-structure-fam-fiber-postcomp-suspension-is-trunc-projective
    f g F =
    let
        open
          do-syntax-trunc-Prop
            ( trunc-Prop
              ( dependent-suspension-structure-fam-fiber-postcomp-suspension
                ( f)
                ( g)))
      in do
        N â† is-inhabited-is-connected (F (g north-suspension))
        S â† is-inhabited-is-connected (F (g south-suspension))
        merid â†
          is-inhabited-Î -dependent-identification-merid-fam-fiber-postcomp-suspension-is-trunc-projective
            f g F N S
        unit-trunc-Prop (N , S , merid)

  is-trunc-projective-level-suspension :
    is-trunc-projective-Level l2 l3 (succ-â„• k) (suspension X)
  is-trunc-projective-level-suspension A B (f , F) g =
    is-inhabited-fiber-postcomp-suspension f g
      ( is-inhabited-dependent-suspension-structure-fam-fiber-postcomp-suspension-is-trunc-projective
          f g F)
```

### Suspensions of projective types in the alternative sense are 1-projective

For the alternative phrasing of set-projectivity using distributivity of
propositional truncation, the argument goes through without assuming `X` is
set-truncated.

```agda
module _
  {l1 l2 l3 : Level} {X : UU l1}
  where

  is-inhabited-dependent-suspension-structure-fam-fiber-postcomp-suspension-is-projective' :
    is-projective-Level' (l2 âŠ” l3) X â†’
    {A : UU l2} {B : UU l3}
    (f : connected-map zero-ğ•‹ A B) (g : suspension X â†’ B) â†’
    is-inhabited
      ( dependent-suspension-structure-fam-fiber-postcomp-suspension
        ( map-connected-map f)
        ( g))
  is-inhabited-dependent-suspension-structure-fam-fiber-postcomp-suspension-is-projective'
    is-projective-X (f , F) g =
    let
      open
        do-syntax-trunc-Prop
          ( trunc-Prop
            ( dependent-suspension-structure-fam-fiber-postcomp-suspension
              ( f)
              ( g)))
    in do
      N â† is-inhabited-is-0-connected (F (g north-suspension))
      S â† is-inhabited-is-0-connected (F (g south-suspension))
      merid â†
        is-projective-X
          ( Î» x â†’
            dependent-identification
              ( fam-fiber-postcomp-suspension f g)
              ( meridian-suspension x)
              ( N)
              ( S))
          ( Î» x â†’
            mere-eq-is-0-connected
              ( F (g south-suspension))
              ( tr
                ( fam-fiber-postcomp-suspension f g)
                ( meridian-suspension x)
                ( N))
              ( S))
      unit-trunc-Prop (N , S , merid)

  is-0-projective-suspension' :
    is-projective-Level' (l2 âŠ” l3) X â†’
    is-trunc-projective-Level l2 l3 1 (suspension X)
  is-0-projective-suspension' is-projective-X A B (f , F) g =
    is-inhabited-fiber-postcomp-suspension f g
      ( is-inhabited-dependent-suspension-structure-fam-fiber-postcomp-suspension-is-projective'
        ( is-projective-X)
        ( f , F)
        ( g))
```
