# Equifibered sequential diagrams

```agda
module synthetic-homotopy-theory.equifibered-sequential-diagrams where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.universe-levels

open import synthetic-homotopy-theory.dependent-sequential-diagrams
open import synthetic-homotopy-theory.sequential-diagrams
```

</details>

## Idea

An
{{#concept "equifibered sequential diagram" Disambiguation="over a sequential diagram" Agda=equifibered-sequential-diagram}}
over a [sequential diagram](synthetic-homotopy-theory.sequential-diagrams.md)
`(A, a)` consists of a type family `B : (n : ‚Ñï) ‚Üí A‚Çô ‚Üí ùí∞`
[equipped](foundation.structure.md) with a family of fiberwise equivalences

```text
b‚Çô : (a : A‚Çô) ‚Üí B‚Çô a ‚âÉ B‚Çô‚Çä‚ÇÅ (a‚Çô a) .
```

The term "equifibered" comes from the equivalent definition as
[dependent sequential diagrams](synthetic-homotopy-theory.dependent-sequential-diagrams.md)
over `(A, a)`

```text
     b‚ÇÄ      b‚ÇÅ      b‚ÇÇ
 B‚ÇÄ ---> B‚ÇÅ ---> B‚ÇÇ ---> ‚ãØ
 |       |       |
 |       |       |
 ‚Ü°       ‚Ü°       ‚Ü°
 A‚ÇÄ ---> A‚ÇÅ ---> A‚ÇÇ ---> ‚ãØ ,
     a‚ÇÄ      a‚ÇÅ      a‚ÇÇ
```

which are said to be "fibered over `A`", whose maps `b‚Çô` are equivalences. This
terminology was originally introduced by Charles Rezk in _Toposes and Homotopy
Toposes_ {{#cite Rezk10HomotopyToposes}}.

## Definitions

### Equifibered sequential diagrams

```agda
module _
  {l1 : Level} (A : sequential-diagram l1)
  where

  equifibered-sequential-diagram : (l : Level) ‚Üí UU (l1 ‚äî lsuc l)
  equifibered-sequential-diagram l =
    Œ£ ( (n : ‚Ñï) ‚Üí family-sequential-diagram A n ‚Üí UU l)
      ( Œª B ‚Üí
        (n : ‚Ñï) (a : family-sequential-diagram A n) ‚Üí
        B n a ‚âÉ B (succ-‚Ñï n) (map-sequential-diagram A n a))
```

### Components of an equifibered sequential diagram

```agda
module _
  {l1 l2 : Level} {A : sequential-diagram l1}
  (B : equifibered-sequential-diagram A l2)
  where

  family-equifibered-sequential-diagram :
    (n : ‚Ñï) ‚Üí family-sequential-diagram A n ‚Üí UU l2
  family-equifibered-sequential-diagram = pr1 B

  equiv-equifibered-sequential-diagram :
    (n : ‚Ñï) (a : family-sequential-diagram A n) ‚Üí
    family-equifibered-sequential-diagram n a ‚âÉ
    family-equifibered-sequential-diagram
      ( succ-‚Ñï n)
      ( map-sequential-diagram A n a)
  equiv-equifibered-sequential-diagram = pr2 B

  map-equifibered-sequential-diagram :
    (n : ‚Ñï) (a : family-sequential-diagram A n) ‚Üí
    family-equifibered-sequential-diagram n a ‚Üí
    family-equifibered-sequential-diagram
      ( succ-‚Ñï n)
      ( map-sequential-diagram A n a)
  map-equifibered-sequential-diagram n a =
    map-equiv (equiv-equifibered-sequential-diagram n a)

  is-equiv-map-equifibered-sequential-diagram :
    (n : ‚Ñï) (a : family-sequential-diagram A n) ‚Üí
    is-equiv (map-equifibered-sequential-diagram n a)
  is-equiv-map-equifibered-sequential-diagram n a =
    is-equiv-map-equiv (equiv-equifibered-sequential-diagram n a)

  dependent-sequential-diagram-equifibered-sequential-diagram :
    dependent-sequential-diagram A l2
  pr1 dependent-sequential-diagram-equifibered-sequential-diagram =
    family-equifibered-sequential-diagram
  pr2 dependent-sequential-diagram-equifibered-sequential-diagram =
    map-equifibered-sequential-diagram
```

### The predicate on dependent sequential diagrams of being equifibered

```agda
module _
  {l1 l2 : Level} {A : sequential-diagram l1}
  where

  is-equifibered-dependent-sequential-diagram :
    (B : dependent-sequential-diagram A l2) ‚Üí UU (l1 ‚äî l2)
  is-equifibered-dependent-sequential-diagram B =
    (n : ‚Ñï) (a : family-sequential-diagram A n) ‚Üí
    is-equiv (map-dependent-sequential-diagram B n a)

  is-equifibered-dependent-sequential-diagram-equifibered-sequential-diagram :
    (B : equifibered-sequential-diagram A l2) ‚Üí
    is-equifibered-dependent-sequential-diagram
      ( dependent-sequential-diagram-equifibered-sequential-diagram B)
  is-equifibered-dependent-sequential-diagram-equifibered-sequential-diagram
    B =
    is-equiv-map-equifibered-sequential-diagram B
```

## Properties

### Dependent sequential diagrams which are equifibered are equifibered sequential diagrams

To construct an equifibered sequential diagram over `A`, it suffices to
construct a dependent sequential diagram `(B, b)` over `A`, and show that the
maps `b‚Çô` are equivalences.

```agda
module _
  {l1 l2 : Level} {A : sequential-diagram l1}
  (B : dependent-sequential-diagram A l2)
  where

  equifibered-sequential-diagram-dependent-sequential-diagram :
    is-equifibered-dependent-sequential-diagram B ‚Üí
    equifibered-sequential-diagram A l2
  pr1
    ( equifibered-sequential-diagram-dependent-sequential-diagram _) =
    family-dependent-sequential-diagram B
  pr2
    ( equifibered-sequential-diagram-dependent-sequential-diagram
      is-equiv-map)
    n a =
    ( map-dependent-sequential-diagram B n a , is-equiv-map n a)
```

## References

{{#bibliography}}
