# Multivariable loop spaces

```agda
module synthetic-homotopy-theory.multivariable-loop-spaces where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cartesian-product-types
open import foundation.commuting-triangles-of-identifications
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.constant-maps
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.path-algebra
open import foundation.retractions
open import foundation.sections
open import foundation.structure-identity-principle
open import foundation.torsorial-type-families
open import foundation.type-arithmetic-cartesian-product-types
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unit-type
open import foundation.universal-property-maybe
open import foundation.universe-levels

open import structured-types.h-spaces
open import structured-types.left-invertible-magmas
open import structured-types.magmas
open import structured-types.pointed-types
open import structured-types.pointed-homotopies
open import structured-types.pointed-maps
open import structured-types.pointed-equivalences

open import synthetic-homotopy-theory.loop-spaces
open import synthetic-homotopy-theory.suspension-structures
open import synthetic-homotopy-theory.suspensions-of-pointed-types
open import synthetic-homotopy-theory.suspensions-of-types
open import synthetic-homotopy-theory.cavallos-trick
```

</details>

## Idea

Given a type `I` and a [pointed type](structured-types.pointed-types.md)
`aâˆ— : A`, we can form the `I`-ary loop space in `A` as the type
`Î£ (a : A), (I â†’ (a ï¼ aâˆ—))`. We recover the
[standard loops](synthetic-homotopy-theory.loop-spaces.md) as the binary loops,
`A` as the `âˆ…`-ary loops, and there is a unique unary loop. The `ğ•ŠÂ¹`-ary loops
correspond to [loops of loops](synthetic-homotopy-theory.double-loop-spaces.md).

For every point of `I` there is a coherent, associative, and invertible H-space
structure on `I`-ary loops.

## Table of files directly related to loop spaces

{{#include tables/loop-spaces-concepts.md}}

## Definitions

### The `I`-ary loop space

```agda
module _
  {l1 l2 : Level} (I : UU l1) (Aâˆ— : Pointed-Type l2)
  (let A = type-Pointed-Type Aâˆ—) (let aâˆ— = point-Pointed-Type Aâˆ—)
  where

  type-multivar-Î© : UU (l1 âŠ” l2)
  type-multivar-Î© = Î£ A (Î» a â†’ I â†’ a ï¼ aâˆ—)

  refl-multivar-Î© : type-multivar-Î©
  refl-multivar-Î© = (aâˆ— , refl-htpy)

  multivar-Î© : Pointed-Type (l1 âŠ” l2)
  multivar-Î© = (type-multivar-Î© , refl-multivar-Î©)
```

## Properties

### Characterizing equality

```agda
module _
  {l1 l2 : Level} {I : UU l1} {Aâˆ— : Pointed-Type l2}
  where

  Eq-multivar-Î© : (x y : type-multivar-Î© I Aâˆ—) â†’ UU (l1 âŠ” l2)
  Eq-multivar-Î© (a , p) (b , q) =
    Î£ ( a ï¼ b)
      ( Î» r â†’ (i : I) â†’ coherence-triangle-identifications (p i) (q i) r)

  refl-Eq-multivar-Î© : (x : type-multivar-Î© I Aâˆ—) â†’ Eq-multivar-Î© x x
  refl-Eq-multivar-Î© p = (refl , refl-htpy)

  Eq-eq-multivar-Î© :
    (x y : type-multivar-Î© I Aâˆ—) â†’ x ï¼ y â†’ Eq-multivar-Î© x y
  Eq-eq-multivar-Î© x .x refl = refl-Eq-multivar-Î© x

  abstract
    is-torsorial-Eq-multivar-Î© :
      (x : type-multivar-Î© I Aâˆ—) â†’ is-torsorial (Eq-multivar-Î© x)
    is-torsorial-Eq-multivar-Î© (a , p) =
      is-torsorial-Eq-structure
        ( is-torsorial-Id a)
        ( a , refl)
        ( is-torsorial-htpy p)

  is-equiv-Eq-eq-multivar-Î© :
    (x y : type-multivar-Î© I Aâˆ—) â†’ is-equiv (Eq-eq-multivar-Î© x y)
  is-equiv-Eq-eq-multivar-Î© x =
    fundamental-theorem-id (is-torsorial-Eq-multivar-Î© x) (Eq-eq-multivar-Î© x)

  extensionality-multivar-Î© :
    (x y : type-multivar-Î© I Aâˆ—) â†’ (x ï¼ y) â‰ƒ Eq-multivar-Î© x y
  extensionality-multivar-Î© x y =
    ( Eq-eq-multivar-Î© x y , is-equiv-Eq-eq-multivar-Î© x y)

  eq-Eq-multivar-Î© :
    (x y : type-multivar-Î© I Aâˆ—) â†’ Eq-multivar-Î© x y â†’ x ï¼ y
  eq-Eq-multivar-Î© x y =
    map-inv-equiv (extensionality-multivar-Î© x y)
```

### Characterizing equality of equality

```agda
module _
  {l1 l2 : Level} {I : UU l1} {Aâˆ— : Pointed-Type l2}
  (x y : type-multivar-Î© I Aâˆ—)
  where

  EqÂ²-multivar-Î© :
    (p q : Eq-multivar-Î© x y) â†’ UU (l1 âŠ” l2)
  EqÂ²-multivar-Î© (p , H) (q , K) =
    Î£ (p ï¼ q) (Î» r â†’ (i : I) â†’ H i âˆ™ ap (_âˆ™ pr2 y i) r ï¼ K i)

  refl-EqÂ²-multivar-Î© :
    (p : Eq-multivar-Î© x y) â†’ EqÂ²-multivar-Î© p p
  refl-EqÂ²-multivar-Î© p = (refl , right-unit-htpy)

  EqÂ²-eq-multivar-Î© :
    (p q : Eq-multivar-Î© x y) â†’ p ï¼ q â†’ EqÂ²-multivar-Î© p q
  EqÂ²-eq-multivar-Î© p .p refl = refl-EqÂ²-multivar-Î© p

  abstract
    is-torsorial-EqÂ²-multivar-Î© :
      (p : Eq-multivar-Î© x y) â†’ is-torsorial (EqÂ²-multivar-Î© p)
    is-torsorial-EqÂ²-multivar-Î© (a , p) =
      is-torsorial-Eq-structure
        ( is-torsorial-Id a)
        ( a , refl)
        ( is-torsorial-htpy (p âˆ™h refl-htpy))

  is-equiv-EqÂ²-eq-multivar-Î© :
    (p q : Eq-multivar-Î© x y) â†’ is-equiv (EqÂ²-eq-multivar-Î© p q)
  is-equiv-EqÂ²-eq-multivar-Î© p =
    fundamental-theorem-id
      ( is-torsorial-EqÂ²-multivar-Î© p)
      ( EqÂ²-eq-multivar-Î© p)

  extensionalityÂ²-multivar-Î© :
    (p q : Eq-multivar-Î© x y) â†’ (p ï¼ q) â‰ƒ EqÂ²-multivar-Î© p q
  extensionalityÂ²-multivar-Î© p q =
    ( EqÂ²-eq-multivar-Î© p q , is-equiv-EqÂ²-eq-multivar-Î© p q)

  eq-EqÂ²-multivar-Î© :
    (p q : Eq-multivar-Î© x y) â†’ EqÂ²-multivar-Î© p q â†’ p ï¼ q
  eq-EqÂ²-multivar-Î© p q =
    map-inv-equiv (extensionalityÂ²-multivar-Î© p q)
```

### The `I`-ary loops over a pointed type forms a magma

```agda
module _
  {l1 l2 : Level} (Iâˆ— : Pointed-Type l1) (Aâˆ— : Pointed-Type l2)
  (let I = type-Pointed-Type Iâˆ—) (let iâˆ— = point-Pointed-Type Iâˆ—)
  (let A = type-Pointed-Type Aâˆ—) (let aâˆ— = point-Pointed-Type Aâˆ—)
  where

  mul-multivar-Î© :
    type-multivar-Î© I Aâˆ— â†’ type-multivar-Î© I Aâˆ— â†’ type-multivar-Î© I Aâˆ—
  mul-multivar-Î© (a , p) (b , q) = (a , (Î» x â†’ p x âˆ™ inv (q iâˆ—) âˆ™ q x))

  multivar-Î©-Magma : Magma (l1 âŠ” l2)
  multivar-Î©-Magma = (type-multivar-Î© I Aâˆ— , mul-multivar-Î©)
```

### The coherent H-space of `I`-ary loops on a pointed type

```agda
module _
  {l1 l2 : Level}
  (Iâˆ— : Pointed-Type l1) (Aâˆ— : Pointed-Type l2)
  (let I = type-Pointed-Type Iâˆ—) (let iâˆ— = point-Pointed-Type Iâˆ—)
  (let A = type-Pointed-Type Aâˆ—) (let aâˆ— = point-Pointed-Type Aâˆ—)
  where

  Eq-left-unit-law-mul-multivar-Î© :
    (x : type-multivar-Î© I Aâˆ—) â†’
    Eq-multivar-Î© (mul-multivar-Î© Iâˆ— Aâˆ— (refl-multivar-Î© I Aâˆ—) x) x
  Eq-left-unit-law-mul-multivar-Î© (a , p) = (inv (p iâˆ—) , refl-htpy)

  left-unit-law-mul-multivar-Î© :
    (x : type-multivar-Î© I Aâˆ—) â†’
    mul-multivar-Î© Iâˆ— Aâˆ— (refl-multivar-Î© I Aâˆ—) x ï¼ x
  left-unit-law-mul-multivar-Î© x =
    eq-Eq-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— (refl-multivar-Î© I Aâˆ—) x)
      ( x)
      ( Eq-left-unit-law-mul-multivar-Î© x)

  Eq-right-unit-law-mul-multivar-Î© :
    (x : type-multivar-Î© I Aâˆ—) â†’
    Eq-multivar-Î© (mul-multivar-Î© Iâˆ— Aâˆ— x (refl-multivar-Î© I Aâˆ—)) x
  Eq-right-unit-law-mul-multivar-Î© x =
    ( refl , right-unit-htpy âˆ™h right-unit-htpy)

  right-unit-law-mul-multivar-Î© :
    (x : type-multivar-Î© I Aâˆ—) â†’
    mul-multivar-Î© Iâˆ— Aâˆ— x (refl-multivar-Î© I Aâˆ—) ï¼ x
  right-unit-law-mul-multivar-Î© x =
    eq-Eq-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— x (refl-multivar-Î© I Aâˆ—))
      ( x)
      ( Eq-right-unit-law-mul-multivar-Î© x)

  Eq-coherence-unit-laws-mul-multivar-Î© :
    EqÂ²-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— (refl-multivar-Î© I Aâˆ—) (refl-multivar-Î© I Aâˆ—))
      ( refl-multivar-Î© I Aâˆ—)
      ( Eq-left-unit-law-mul-multivar-Î© (refl-multivar-Î© I Aâˆ—))
      ( Eq-right-unit-law-mul-multivar-Î© (refl-multivar-Î© I Aâˆ—))
  Eq-coherence-unit-laws-mul-multivar-Î© =
    ( refl , refl-htpy)

  coherence-unit-laws-mul-multivar-Î© :
    left-unit-law-mul-multivar-Î© (refl-multivar-Î© I Aâˆ—) ï¼
    right-unit-law-mul-multivar-Î© (refl-multivar-Î© I Aâˆ—)
  coherence-unit-laws-mul-multivar-Î© = refl

  multivar-Î©-H-Space : H-Space (l1 âŠ” l2)
  multivar-Î©-H-Space =
    ( multivar-Î© I Aâˆ— ,
      mul-multivar-Î© Iâˆ— Aâˆ— ,
      left-unit-law-mul-multivar-Î© ,
      right-unit-law-mul-multivar-Î© ,
      coherence-unit-laws-mul-multivar-Î©)
```

### Associativity

```agda
module _
  {l1 l2 : Level}
  (Iâˆ— : Pointed-Type l1) (Aâˆ— : Pointed-Type l2)
  (let I = type-Pointed-Type Iâˆ—) (let iâˆ— = point-Pointed-Type Iâˆ—)
  (let A = type-Pointed-Type Aâˆ—) (let aâˆ— = point-Pointed-Type Aâˆ—)
  where

  Eq-associative-mul-multivar-Î© :
    (x y z : type-multivar-Î© I Aâˆ—) â†’
    Eq-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— (mul-multivar-Î© Iâˆ— Aâˆ— x y) z)
      ( mul-multivar-Î© Iâˆ— Aâˆ— x (mul-multivar-Î© Iâˆ— Aâˆ— y z))
  Eq-associative-mul-multivar-Î© (a , p) (b , q) (c , r) =
    ( refl ,
      Î» x â†’
      equational-reasoning
      (((p x âˆ™ inv (q iâˆ—)) âˆ™ q x) âˆ™ inv (r iâˆ—)) âˆ™ r x
      ï¼ (p x âˆ™ inv (q iâˆ—)) âˆ™ ((q x âˆ™ inv (r iâˆ—)) âˆ™ r x)
        by
        assocÂ²-1 (p x âˆ™ inv (q iâˆ—)) (q x) (inv (r iâˆ—)) (r x)
      ï¼ (p x âˆ™ inv ((q iâˆ— âˆ™ inv (r iâˆ—)) âˆ™ r iâˆ—)) âˆ™ ((q x âˆ™ inv (r iâˆ—)) âˆ™ r x)
        by
        ap
          ( Î» u â†’ (p x âˆ™ inv u) âˆ™ (q x âˆ™ inv (r iâˆ—) âˆ™ r x))
          ( inv (is-section-inv-concat' (r iâˆ—) (q iâˆ—))))

  associative-mul-multivar-Î© :
    (x y z : type-multivar-Î© I Aâˆ—) â†’
    mul-multivar-Î© Iâˆ— Aâˆ— (mul-multivar-Î© Iâˆ— Aâˆ— x y) z ï¼
    mul-multivar-Î© Iâˆ— Aâˆ— x (mul-multivar-Î© Iâˆ— Aâˆ— y z)
  associative-mul-multivar-Î© x y z =
    eq-Eq-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— (mul-multivar-Î© Iâˆ— Aâˆ— x y) z)
      ( mul-multivar-Î© Iâˆ— Aâˆ— x (mul-multivar-Î© Iâˆ— Aâˆ— y z))
      ( Eq-associative-mul-multivar-Î© x y z)
```

### The inverse

```agda
module _
  {l1 l2 : Level}
  (Iâˆ— : Pointed-Type l1) (Aâˆ— : Pointed-Type l2)
  (let I = type-Pointed-Type Iâˆ—) (let iâˆ— = point-Pointed-Type Iâˆ—)
  (let A = type-Pointed-Type Aâˆ—) (let aâˆ— = point-Pointed-Type Aâˆ—)
  where

  inv-multivar-Î© : type-multivar-Î© I Aâˆ— â†’ type-multivar-Î© I Aâˆ—
  inv-multivar-Î© (a , p) = (aâˆ— , (Î» i â†’ inv (p i) âˆ™ p iâˆ—))

  Eq-right-inverse-law-mul-multivar-Î© :
    (x : type-multivar-Î© I Aâˆ—) â†’
    Eq-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— x (inv-multivar-Î© x))
      ( refl-multivar-Î© I Aâˆ—)
  Eq-right-inverse-law-mul-multivar-Î© (a , p) =
    ( p iâˆ— ,
      Î» x â†’
      equational-reasoning
        (p x âˆ™ inv (inv (p iâˆ—) âˆ™ p iâˆ—)) âˆ™ (inv (p x) âˆ™ p iâˆ—)
        ï¼ p x âˆ™ (inv (p x) âˆ™ p iâˆ—)
          by
          ap
            ( _âˆ™ (inv (p x) âˆ™ p iâˆ—))
            ( ap (p x âˆ™_) (ap inv (left-inv (p iâˆ—))) âˆ™ right-unit)
        ï¼ p iâˆ—
          by is-section-inv-concat (p x) (p iâˆ—)
        ï¼ p iâˆ— âˆ™ refl
          by inv right-unit)

  right-inverse-law-mul-multivar-Î© :
    (x : type-multivar-Î© I Aâˆ—) â†’
    mul-multivar-Î© Iâˆ— Aâˆ— x (inv-multivar-Î© x) ï¼
    refl-multivar-Î© I Aâˆ—
  right-inverse-law-mul-multivar-Î© x =
    eq-Eq-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— x (inv-multivar-Î© x))
      ( refl-multivar-Î© I Aâˆ—)
      ( Eq-right-inverse-law-mul-multivar-Î© x)

  Eq-left-inverse-law-mul-multivar-Î© :
    (x : type-multivar-Î© I Aâˆ—) â†’
    Eq-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— (inv-multivar-Î© x) x)
      ( refl-multivar-Î© I Aâˆ—)
  Eq-left-inverse-law-mul-multivar-Î© (a , p) =
    ( refl ,
      ( Î» i â†’
        equational-reasoning
        ((inv (p i) âˆ™ p iâˆ—) âˆ™ inv (p iâˆ—)) âˆ™ p i
        ï¼ inv (p i) âˆ™ p i
          by ap (_âˆ™ p i) (is-retraction-inv-concat' (p iâˆ—) (inv (p i)))
        ï¼ refl by left-inv (p i)))

  left-inverse-law-mul-multivar-Î© :
    (x : type-multivar-Î© I Aâˆ—) â†’
    mul-multivar-Î© Iâˆ— Aâˆ— (inv-multivar-Î© x) x ï¼
    refl-multivar-Î© I Aâˆ—
  left-inverse-law-mul-multivar-Î© x =
    eq-Eq-multivar-Î©
      ( mul-multivar-Î© Iâˆ— Aâˆ— (inv-multivar-Î© x) x)
      ( refl-multivar-Î© I Aâˆ—)
      ( Eq-left-inverse-law-mul-multivar-Î© x)
```

### Invertibility of left and right multiplication

```agda
module _
  {l1 l2 : Level}
  (Iâˆ— : Pointed-Type l1) (Aâˆ— : Pointed-Type l2)
  (let I = type-Pointed-Type Iâˆ—)
  (let A = type-Pointed-Type Aâˆ—)
  where

  left-mul-inv-multivar-Î© :
    type-multivar-Î© I Aâˆ— â†’ type-multivar-Î© I Aâˆ— â†’ type-multivar-Î© I Aâˆ—
  left-mul-inv-multivar-Î© a =
    mul-multivar-Î© Iâˆ— Aâˆ— (inv-multivar-Î© Iâˆ— Aâˆ— a)

  is-section-left-mul-inv-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’
    is-section (mul-multivar-Î© Iâˆ— Aâˆ— a) (left-mul-inv-multivar-Î© a)
  is-section-left-mul-inv-multivar-Î© a x =
    equational-reasoning
    mul-multivar-Î© Iâˆ— Aâˆ— a (mul-multivar-Î© Iâˆ— Aâˆ— (inv-multivar-Î© Iâˆ— Aâˆ— a) x)
    ï¼ mul-multivar-Î© Iâˆ— Aâˆ— (mul-multivar-Î© Iâˆ— Aâˆ— a (inv-multivar-Î© Iâˆ— Aâˆ— a)) x
      by inv (associative-mul-multivar-Î© Iâˆ— Aâˆ— a (inv-multivar-Î© Iâˆ— Aâˆ— a) x)
    ï¼ mul-multivar-Î© Iâˆ— Aâˆ— (refl-multivar-Î© I Aâˆ—) x
      by
      ap
        ( Î» u â†’ mul-multivar-Î© Iâˆ— Aâˆ— u x)
        ( right-inverse-law-mul-multivar-Î© Iâˆ— Aâˆ— a)
    ï¼ x
      by left-unit-law-mul-multivar-Î© Iâˆ— Aâˆ— x

  is-retraction-left-mul-inv-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’
    is-retraction (mul-multivar-Î© Iâˆ— Aâˆ— a) (left-mul-inv-multivar-Î© a)
  is-retraction-left-mul-inv-multivar-Î© a x =
    equational-reasoning
    mul-multivar-Î© Iâˆ— Aâˆ—
      ( inv-multivar-Î© Iâˆ— Aâˆ— a)
      ( mul-multivar-Î© Iâˆ— Aâˆ— a x)
    ï¼ mul-multivar-Î© Iâˆ— Aâˆ—
        ( mul-multivar-Î© Iâˆ— Aâˆ— (inv-multivar-Î© Iâˆ— Aâˆ— a) a)
        ( x)
      by
      inv (associative-mul-multivar-Î© Iâˆ— Aâˆ— (inv-multivar-Î© Iâˆ— Aâˆ— a) a x)
    ï¼ mul-multivar-Î© Iâˆ— Aâˆ— (refl-multivar-Î© I Aâˆ—) x
      by
      ap
        ( Î» u â†’ mul-multivar-Î© Iâˆ— Aâˆ— u x)
        ( left-inverse-law-mul-multivar-Î© Iâˆ— Aâˆ— a)
    ï¼ x by left-unit-law-mul-multivar-Î© Iâˆ— Aâˆ— x

  section-left-mul-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’ section (mul-multivar-Î© Iâˆ— Aâˆ— a)
  section-left-mul-multivar-Î© a =
    ( left-mul-inv-multivar-Î© a ,
      is-section-left-mul-inv-multivar-Î© a)

  retraction-left-mul-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’ retraction (mul-multivar-Î© Iâˆ— Aâˆ— a)
  retraction-left-mul-multivar-Î© a =
    ( left-mul-inv-multivar-Î© a ,
      is-retraction-left-mul-inv-multivar-Î© a)

  is-equiv-left-mul-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’ is-equiv (mul-multivar-Î© Iâˆ— Aâˆ— a)
  is-equiv-left-mul-multivar-Î© a =
    is-equiv-is-invertible
      ( left-mul-inv-multivar-Î© a)
      ( is-section-left-mul-inv-multivar-Î© a)
      ( is-retraction-left-mul-inv-multivar-Î© a)

  equiv-left-mul-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’ type-multivar-Î© I Aâˆ— â‰ƒ type-multivar-Î© I Aâˆ—
  equiv-left-mul-multivar-Î© a =
    ( mul-multivar-Î© Iâˆ— Aâˆ— a , is-equiv-left-mul-multivar-Î© a)

  is-left-invertible-mul-multivar-Î© :
    is-left-invertible-Magma (multivar-Î©-Magma Iâˆ— Aâˆ—)
  is-left-invertible-mul-multivar-Î© = is-equiv-left-mul-multivar-Î©
```

```agda
module _
  {l1 l2 : Level}
  (Iâˆ— : Pointed-Type l1) (Aâˆ— : Pointed-Type l2)
  (let I = type-Pointed-Type Iâˆ—)
  (let A = type-Pointed-Type Aâˆ—)
  where

  right-mul-inv-multivar-Î© :
    type-multivar-Î© I Aâˆ— â†’ type-multivar-Î© I Aâˆ— â†’ type-multivar-Î© I Aâˆ—
  right-mul-inv-multivar-Î© a x =
    mul-multivar-Î© Iâˆ— Aâˆ— x (inv-multivar-Î© Iâˆ— Aâˆ— a)

  is-section-right-mul-inv-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’
    is-section
      ( Î» x â†’ mul-multivar-Î© Iâˆ— Aâˆ— x a)
      ( right-mul-inv-multivar-Î© a)
  is-section-right-mul-inv-multivar-Î© a x =
    equational-reasoning
    mul-multivar-Î© Iâˆ— Aâˆ— (mul-multivar-Î© Iâˆ— Aâˆ— x (inv-multivar-Î© Iâˆ— Aâˆ— a)) a
    ï¼ mul-multivar-Î© Iâˆ— Aâˆ— x (mul-multivar-Î© Iâˆ— Aâˆ— (inv-multivar-Î© Iâˆ— Aâˆ— a) a)
      by associative-mul-multivar-Î© Iâˆ— Aâˆ— x (inv-multivar-Î© Iâˆ— Aâˆ— a) a
    ï¼ mul-multivar-Î© Iâˆ— Aâˆ— x (refl-multivar-Î© I Aâˆ—)
      by ap (mul-multivar-Î© Iâˆ— Aâˆ— x) (left-inverse-law-mul-multivar-Î© Iâˆ— Aâˆ— a)
    ï¼ x
      by right-unit-law-mul-multivar-Î© Iâˆ— Aâˆ— x

  is-retraction-right-mul-inv-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’
    is-retraction
      ( Î» x â†’ mul-multivar-Î© Iâˆ— Aâˆ— x a)
      ( right-mul-inv-multivar-Î© a)
  is-retraction-right-mul-inv-multivar-Î© a x =
    equational-reasoning
    mul-multivar-Î© Iâˆ— Aâˆ— (mul-multivar-Î© Iâˆ— Aâˆ— x a) (inv-multivar-Î© Iâˆ— Aâˆ— a)
    ï¼ mul-multivar-Î© Iâˆ— Aâˆ— x (mul-multivar-Î© Iâˆ— Aâˆ— a (inv-multivar-Î© Iâˆ— Aâˆ— a))
      by associative-mul-multivar-Î© Iâˆ— Aâˆ— x a (inv-multivar-Î© Iâˆ— Aâˆ— a)
    ï¼ mul-multivar-Î© Iâˆ— Aâˆ— x (refl-multivar-Î© I Aâˆ—)
      by ap (mul-multivar-Î© Iâˆ— Aâˆ— x) (right-inverse-law-mul-multivar-Î© Iâˆ— Aâˆ— a)
    ï¼ x
      by right-unit-law-mul-multivar-Î© Iâˆ— Aâˆ— x

  section-right-mul-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’
    section (Î» x â†’ mul-multivar-Î© Iâˆ— Aâˆ— x a)
  section-right-mul-multivar-Î© a =
    ( right-mul-inv-multivar-Î© a ,
      is-section-right-mul-inv-multivar-Î© a)

  retraction-right-mul-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’
    retraction (Î» x â†’ mul-multivar-Î© Iâˆ— Aâˆ— x a)
  retraction-right-mul-multivar-Î© a =
    ( right-mul-inv-multivar-Î© a ,
      is-retraction-right-mul-inv-multivar-Î© a)

  is-equiv-right-mul-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’
    is-equiv (Î» x â†’ mul-multivar-Î© Iâˆ— Aâˆ— x a)
  is-equiv-right-mul-multivar-Î© a =
    is-equiv-is-invertible
      ( right-mul-inv-multivar-Î© a)
      ( is-section-right-mul-inv-multivar-Î© a)
      ( is-retraction-right-mul-inv-multivar-Î© a)

  equiv-right-mul-multivar-Î© :
    (a : type-multivar-Î© I Aâˆ—) â†’ type-multivar-Î© I Aâˆ— â‰ƒ type-multivar-Î© I Aâˆ—
  equiv-right-mul-multivar-Î© a =
    ((Î» x â†’ mul-multivar-Î© Iâˆ— Aâˆ— x a) , is-equiv-right-mul-multivar-Î© a)
```

### `I+1`-ary loops

`(I + 1)`-ary loops are equivalent to families `I â†’ Î©A`, where `Î©` is the
standard loop type.

```agda
module _
  {l1 l2 : Level} {I : UU l1} (Aâˆ— : Pointed-Type l1)
  (let A = type-Pointed-Type Aâˆ—) (let aâˆ— = point-Pointed-Type Aâˆ—)
  where

  equiv-type-isolated-point-multivar-Î© :
    type-multivar-Î© (I + unit) Aâˆ— â‰ƒ (I â†’ type-Î© Aâˆ—)
  equiv-type-isolated-point-multivar-Î© =
    equivalence-reasoning
    Î£ A (Î» a â†’ I + unit â†’ a ï¼ aâˆ—)
    â‰ƒ Î£ A (Î» a â†’ (I â†’ a ï¼ aâˆ—) Ã— (a ï¼ aâˆ—))
      by equiv-tot (Î» a â†’ equiv-universal-property-Maybe)
    â‰ƒ Î£ A (Î» a â†’ (a ï¼ aâˆ—) Ã— (I â†’ a ï¼ aâˆ—))
      by equiv-tot (Î» a â†’ commutative-product)
    â‰ƒ Î£ (Î£ A (Î» a â†’ (a ï¼ aâˆ—))) (Î» u â†’ (I â†’ pr1 u ï¼ aâˆ—))
      by inv-associative-Î£
    â‰ƒ (I â†’ type-Î© Aâˆ—)
      by left-unit-law-Î£-is-contr (is-torsorial-Id' aâˆ—) (aâˆ— , refl)
```

### If `I` is pointed then `I`-ary loops are pointed equivalent to pointed maps `I â†’âˆ— Î©A`

First we demonstrate the equivalence directly as a composite of simple
equivalences, then we construct the explicit map and demonstrate that it is also
pointed.

```agda
module _
  {l1 l2 : Level}
  (Iâˆ— : Pointed-Type l1) (Aâˆ— : Pointed-Type l2)
  (let I = type-Pointed-Type Iâˆ—) (let iâˆ— = point-Pointed-Type Iâˆ—)
  (let A = type-Pointed-Type Aâˆ—) (let aâˆ— = point-Pointed-Type Aâˆ—)
  where

  compute-type-multivar-Î©-pointed' :
    type-multivar-Î© I Aâˆ— â‰ƒ (Iâˆ— â†’âˆ— Î© Aâˆ—)
  compute-type-multivar-Î©-pointed' =
    equivalence-reasoning
      Î£ A (Î» a â†’ I â†’ a ï¼ aâˆ—)
      â‰ƒ Î£ A (Î» a â†’ Î£ (I â†’ a ï¼ aâˆ—) (Î» f â†’ Î£ (a ï¼ aâˆ—) (f iâˆ— ï¼_)))
        by
        equiv-tot
          ( Î» a â†’ inv-right-unit-law-Î£-is-contr (Î» f â†’ is-torsorial-Id (f iâˆ—)))
      â‰ƒ Î£ (Î£ A (_ï¼ aâˆ—)) (Î» (a , p) â†’ Î£ (I â†’ a ï¼ aâˆ—) (Î» a â†’ a iâˆ— ï¼ p))
        by inv-associative-Î£ âˆ˜e equiv-tot (Î» a â†’ equiv-left-swap-Î£)
      â‰ƒ Î£ (I â†’ aâˆ— ï¼ aâˆ—) (Î» f â†’ f iâˆ— ï¼ refl)
        by left-unit-law-Î£-is-contr (is-torsorial-Id' aâˆ—) (aâˆ— , refl)

  map-compute-multivar-Î©-pointed :
    type-multivar-Î© I Aâˆ— â†’ (Iâˆ— â†’âˆ— Î© Aâˆ—)
  map-compute-multivar-Î©-pointed (a , p) =
    ( (Î» i â†’ inv (p iâˆ—) âˆ™ p i) , left-inv (p iâˆ—))

  map-inv-compute-multivar-Î©-pointed :
    (Iâˆ— â†’âˆ— Î© Aâˆ—) â†’ type-multivar-Î© I Aâˆ—
  map-inv-compute-multivar-Î©-pointed (f , p) = (aâˆ— , f)

  is-section-map-inv-compute-multivar-Î©-pointed :
    is-section map-compute-multivar-Î©-pointed map-inv-compute-multivar-Î©-pointed
  is-section-map-inv-compute-multivar-Î©-pointed (f , p) =
    eq-pointed-htpy _ _
      ( cavallos-trick-H-Space' Iâˆ— (Î©-H-Space Aâˆ—) _ _
        ( Î» x â†’ ap (Î» u â†’ inv u âˆ™ f x) p))

  is-retraction-map-inv-compute-multivar-Î©-pointed :
    is-retraction
      ( map-compute-multivar-Î©-pointed)
      ( map-inv-compute-multivar-Î©-pointed)
  is-retraction-map-inv-compute-multivar-Î©-pointed (a , p) =
    eq-Eq-multivar-Î© _ _ (inv (p iâˆ—) , refl-htpy)

  preserves-point-map-compute-multivar-Î©-pointed :
    map-compute-multivar-Î©-pointed (refl-multivar-Î© I Aâˆ—) ï¼
    ( const I (refl-Î© Aâˆ—) , refl)
  preserves-point-map-compute-multivar-Î©-pointed = refl

  is-equiv-map-compute-multivar-Î©-pointed :
    is-equiv map-compute-multivar-Î©-pointed
  is-equiv-map-compute-multivar-Î©-pointed =
    is-equiv-is-invertible
      ( map-inv-compute-multivar-Î©-pointed)
      ( is-section-map-inv-compute-multivar-Î©-pointed)
      ( is-retraction-map-inv-compute-multivar-Î©-pointed)

  compute-type-multivar-Î©-pointed :
    type-multivar-Î© I Aâˆ— â‰ƒ (Iâˆ— â†’âˆ— Î© Aâˆ—)
  compute-type-multivar-Î©-pointed =
    ( map-compute-multivar-Î©-pointed ,
      is-equiv-map-compute-multivar-Î©-pointed)

  compute-multivar-Î©-pointed :
    multivar-Î© I Aâˆ— â‰ƒâˆ— (Iâˆ— â†’âˆ— Î© Aâˆ— , (const I (refl-Î© Aâˆ—) , refl))
  compute-multivar-Î©-pointed =
    ( compute-type-multivar-Î©-pointed ,
      preserves-point-map-compute-multivar-Î©-pointed)
```

### `Î£I`-ary loops are `I`-ary loops of loops

For every type $I$ we have the equivalence

$$Î©_{Î£I}(A) â‰ƒ Î©_I(Î©(A)).$$

```agda
module _
  {l1 l2 : Level} (I : UU l1) (Aâˆ— : Pointed-Type l2)
  (let A = type-Pointed-Type Aâˆ—) (let aâˆ— = point-Pointed-Type Aâˆ—)
  where

  compute-type-multivar-Î©-suspension :
    type-multivar-Î© (suspension I) Aâˆ— â‰ƒ type-multivar-Î© I (Î© Aâˆ—)
  compute-type-multivar-Î©-suspension =
    equivalence-reasoning
    Î£ A (Î» a â†’ suspension I â†’ a ï¼ aâˆ—)
    â‰ƒ Î£ A (Î» a â†’ Î£ (a ï¼ aâˆ—) (Î» S â†’ Î£ (a ï¼ aâˆ—) (Î» N â†’ I â†’ N ï¼ S)))
      by equiv-tot (Î» a â†’ equiv-left-swap-Î£ âˆ˜e equiv-up-suspension)
    â‰ƒ Î£ (Î£ A (Î» a â†’ a ï¼ aâˆ—)) (Î» (a , S) â†’ Î£ (a ï¼ aâˆ—) (Î» N â†’ I â†’ N ï¼ S))
      by inv-associative-Î£
    â‰ƒ Î£ (aâˆ— ï¼ aâˆ—) (Î» N â†’ I â†’ N ï¼ refl)
      by left-unit-law-Î£-is-contr (is-torsorial-Id' aâˆ—) (aâˆ— , refl-Î© Aâˆ—)
```
