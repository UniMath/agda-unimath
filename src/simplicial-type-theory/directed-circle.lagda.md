# The directed circle

```agda
module simplicial-type-theory.directed-circle where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.action-on-identifications-dependent-functions
open import foundation.action-on-identifications-functions
open import foundation.booleans
open import foundation.cartesian-product-types
open import foundation.conjunction
open import foundation.coproduct-types
open import foundation.dependent-identifications
open import foundation.dependent-pair-types
open import foundation.disjunction
open import foundation.empty-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.propositions
open import foundation.retractions
open import foundation.sections
open import foundation.sets
open import foundation.subtypes
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unit-type
open import foundation.universe-levels

open import reflection.erasing-equality

open import simplicial-type-theory.directed-interval-type
open import simplicial-type-theory.directed-relation-on-directed-interval-type
open import simplicial-type-theory.free-directed-loops
open import simplicial-type-theory.simplicial-arrows
open import simplicial-type-theory.simplicial-edges
open import simplicial-type-theory.universal-property-directed-circle

open import synthetic-homotopy-theory.cocones-under-spans
open import synthetic-homotopy-theory.dependent-cocones-under-spans
open import synthetic-homotopy-theory.joins-of-types
open import synthetic-homotopy-theory.pushouts
```

</details>

## Idea

The {{#concept "directed circle"}} is the type consisting of a point `*` and a
nontrivial [directed edge](simplicial-type-theory.simplicial-edges.md) `* â†’â‚‚ *`.
The directed circle classifies
[free directed loops](simplicial-type-theory.free-directed-loops.md), meaning
that maps `directed-circle â†’ X` are in correspondence with free directed loops
of `X`.

The directed circle also satisfies the
[universal property of the coequalizer](synthetic-homotopy-theory.universal-property-coequalizers.md)
of the diagram

```text
       0â‚‚
    ------->
  1          ðŸš.
    ------->
       1â‚‚
```

## Postulates

### The standard directed circle type

```agda
postulate
  directed-circle : UU lzero

postulate
  arrow-directed-circle : simplicial-arrow directed-circle

base-directed-circle : directed-circle
base-directed-circle = arrow-directed-circle 0â‚‚

compute-target-arrow-directed-circle :
  arrow-directed-circle 1â‚‚ ï¼ base-directed-circle
compute-target-arrow-directed-circle =
  primEraseEquality compute-target-arrow-directed-circle'
  where postulate
  compute-target-arrow-directed-circle' :
    arrow-directed-circle 1â‚‚ ï¼ base-directed-circle

compute-source-arrow-directed-circle :
  arrow-directed-circle 0â‚‚ ï¼ base-directed-circle
compute-source-arrow-directed-circle = refl

eq-source-target-arrow-directed-circle :
  arrow-directed-circle 1â‚‚ ï¼ arrow-directed-circle 0â‚‚
eq-source-target-arrow-directed-circle =
  compute-target-arrow-directed-circle

eq-target-source-arrow-directed-circle :
  arrow-directed-circle 0â‚‚ ï¼ arrow-directed-circle 1â‚‚
eq-target-source-arrow-directed-circle =
  inv compute-target-arrow-directed-circle

free-loop-directed-circle : free-directed-loop directed-circle
free-loop-directed-circle =
  ( arrow-directed-circle , eq-source-target-arrow-directed-circle)

loop-directed-circle : base-directed-circle â†’â‚‚ base-directed-circle
loop-directed-circle =
  ( arrow-directed-circle ,
    compute-source-arrow-directed-circle ,
    compute-target-arrow-directed-circle)
```

### The induction principle of the standard directed circle

```agda
module _
  {l : Level} (P : directed-circle â†’ UU l)
  where

  postulate
    ind-directed-circle :
      free-dependent-directed-loop free-loop-directed-circle P â†’
      (x : directed-circle) â†’ P x

  compute-arrow-ind-directed-circle :
    (Î± : free-dependent-directed-loop free-loop-directed-circle P) â†’
    (t : ðŸš) â†’
    ind-directed-circle Î± (arrow-directed-circle t) ï¼
    arrow-free-dependent-directed-loop free-loop-directed-circle Î± t
  compute-arrow-ind-directed-circle Î± t =
    primEraseEquality compute-arrow-ind-directed-circle'
    where postulate
      compute-arrow-ind-directed-circle' :
        ind-directed-circle Î± (arrow-directed-circle t) ï¼
        arrow-free-dependent-directed-loop free-loop-directed-circle Î± t

  postulate
    coherence-ind-directed-circle :
      (Î± : free-dependent-directed-loop free-loop-directed-circle P) â†’
      coherence-htpy-free-dependent-directed-loop
        ( free-loop-directed-circle)
        ( P)
        ( ev-free-directed-loop-Î  free-loop-directed-circle P
          ( ind-directed-circle Î±))
        ( Î±)
        ( compute-arrow-ind-directed-circle Î±)

  compute-ind-directed-circle :
    is-section
      ( ev-free-directed-loop-Î  free-loop-directed-circle P)
      ( ind-directed-circle)
  compute-ind-directed-circle Î± =
    eq-htpy-free-dependent-directed-loop free-loop-directed-circle P
      ( ev-free-directed-loop-Î  free-loop-directed-circle P
        ( ind-directed-circle Î±))
      ( Î±)
      ( compute-arrow-ind-directed-circle Î± , coherence-ind-directed-circle Î±)

induction-principle-directed-circle' :
  induction-principle-directed-circle free-loop-directed-circle
induction-principle-directed-circle' P =
  ( ind-directed-circle P , compute-ind-directed-circle P)
```

## Definitions

### The dependent universal property of the directed circle

```agda
dup-directed-circle :
  dependent-universal-property-directed-circle free-loop-directed-circle
dup-directed-circle =
  dependent-universal-property-induction-principle-directed-circle
    ( free-loop-directed-circle)
    ( induction-principle-directed-circle')
```

### The universal property of the directed circle

```agda
up-directed-circle :
  universal-property-directed-circle free-loop-directed-circle
up-directed-circle =
  universal-property-induction-principle-directed-circle
    ( free-loop-directed-circle)
    ( induction-principle-directed-circle')
```

### The recursion principle of the directed circle

```agda
module _
  {l : Level} {X : UU l}
  where

  rec-directed-circle : free-directed-loop X â†’ directed-circle â†’ X
  rec-directed-circle Î± =
    ind-directed-circle
      ( Î» _ â†’ X)
      ( map-compute-free-dependent-directed-loop-constant-type-family
        ( free-loop-directed-circle)
        ( X)
        ( Î±))
```

## Properties

### The directed circle as a coequalizer

The directed circle satisfies the universal property of the coequalizer of the
diagram

```text
       0â‚‚
    ------->
  1          ðŸš.
    ------->
       1â‚‚
```

This remains to be formalized.
