# Simplicial mapping cylinders

```agda
module simplicial-type-theory.simplicial-mapping-cylinders where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.action-on-identifications-functions
open import foundation.booleans
open import foundation.cartesian-product-types
open import foundation.conjunction
open import foundation.coproduct-types
open import foundation.dependent-identifications
open import foundation.dependent-pair-types
open import foundation.disjunction
open import foundation.empty-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.propositions
open import foundation.retractions
open import foundation.sections
open import foundation.sets
open import foundation.subtypes
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unit-type
open import foundation.universe-levels

open import simplicial-type-theory.directed-interval-type
open import simplicial-type-theory.directed-relation-on-directed-interval-type
open import simplicial-type-theory.simplicial-arrows
open import simplicial-type-theory.simplicial-edges

open import synthetic-homotopy-theory.cocones-under-spans
open import synthetic-homotopy-theory.dependent-cocones-under-spans
open import synthetic-homotopy-theory.joins-of-types
open import synthetic-homotopy-theory.pushouts
```

</details>

## Idea

Given a map `f : X â†’ Y`, we define the
{{#concept "simplicial mapping cylinder"}} of `f` as the
[pushout](synthetic-homotopy-theory.pushouts.md)

```text
                  f
            X --------> Y
            |           |
  (id , 1â‚‚) |           |
            âˆ¨         âŒœ âˆ¨
          X Ã— ğŸš ----> cylâ‚‚ f
```

Intuitively, the simplicial mapping cylinder of `f` can be understood as `X`
simplicially glued to `Y` along `f`. I.e., for every `x : X` there is a
[directed edge](simplicial-type-theory.simplicial-edges.md) `x â†’â‚‚ f x`.

## Definitions

### The standard simplicial mapping cylinder of a function

```agda
simplicial-mapping-cylinder :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’ (X â†’ Y) â†’ UU (l1 âŠ” l2)
simplicial-mapping-cylinder {X = X} {Y} f =
  pushout (Î» (x : X) â†’ (x , 1â‚‚)) f

module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  in-domain-interval-simplicial-mapping-cylinder :
    X â†’ ğŸš â†’ simplicial-mapping-cylinder f
  in-domain-interval-simplicial-mapping-cylinder x t =
    inl-pushout (Î» (x : X) â†’ (x , 1â‚‚)) f (x , t)

  in-domain-simplicial-mapping-cylinder : X â†’ simplicial-mapping-cylinder f
  in-domain-simplicial-mapping-cylinder x =
    in-domain-interval-simplicial-mapping-cylinder x 0â‚‚

  in-codomain-simplicial-mapping-cylinder : Y â†’ simplicial-mapping-cylinder f
  in-codomain-simplicial-mapping-cylinder =
    inr-pushout (Î» (x : X) â†’ (x , 1â‚‚)) f

  glue-simplicial-mapping-cylinder :
    (x : X) â†’
    in-domain-interval-simplicial-mapping-cylinder x 1â‚‚ ï¼
    in-codomain-simplicial-mapping-cylinder (f x)
  glue-simplicial-mapping-cylinder =
    glue-pushout (Î» (x : X) â†’ (x , 1â‚‚)) f

  hom-simplicial-mapping-cylinder :
    (x : X) â†’
    in-domain-simplicial-mapping-cylinder x â†’â‚‚
    in-codomain-simplicial-mapping-cylinder (f x)
  hom-simplicial-mapping-cylinder x =
    ( in-domain-interval-simplicial-mapping-cylinder x ,
      refl ,
      glue-simplicial-mapping-cylinder x)

  cocone-simplicial-mapping-cylinder :
    cocone (Î» (x : X) â†’ (x , 1â‚‚)) f (simplicial-mapping-cylinder f)
  cocone-simplicial-mapping-cylinder =
    cocone-pushout (Î» (x : X) â†’ (x , 1â‚‚)) f
```

### The dependent cogap map for the simplicial mapping cylinder

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  dependent-cogap-simplicial-mapping-cylinder' :
    {l : Level} {P : simplicial-mapping-cylinder f â†’ UU l}
    (c :
      dependent-cocone (Î» x â†’ (x , 1â‚‚)) (f)
        ( cocone-simplicial-mapping-cylinder f)
        ( P))
    (x : simplicial-mapping-cylinder f) â†’ P x
  dependent-cogap-simplicial-mapping-cylinder' =
    dependent-cogap (Î» x â†’ (x , 1â‚‚)) f

  dependent-cogap-simplicial-mapping-cylinder :
    { l : Level} {P : simplicial-mapping-cylinder f â†’ UU l}
    ( g :
      (x : X) (t : ğŸš) â†’
      P (in-domain-interval-simplicial-mapping-cylinder f x t)) â†’
    ( h : (y : Y) â†’ P (in-codomain-simplicial-mapping-cylinder f y)) â†’
    ( p :
      (x : X) â†’
      dependent-identification P
        ( glue-simplicial-mapping-cylinder f x)
        ( g x 1â‚‚)
        ( h (f x)))
    ( x : simplicial-mapping-cylinder f) â†’ P x
  dependent-cogap-simplicial-mapping-cylinder g h p =
    dependent-cogap-simplicial-mapping-cylinder'
      ( (Î» (x , t) â†’ g x t) , h , p)
```

### The cogap map for the simplicial mapping cylinder

```agda
module _
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} {f : X â†’ Y} {S : UU l3}
  where

  cogap-simplicial-mapping-cylinder' :
    cocone (Î» x â†’ (x , 1â‚‚)) f S â†’
    simplicial-mapping-cylinder f â†’ S
  cogap-simplicial-mapping-cylinder' =
    cogap (Î» (x : X) â†’ (x , 1â‚‚)) f

  cogap-simplicial-mapping-cylinder :
    (g : X â†’ ğŸš â†’ S) (h : Y â†’ S) (p : (x : X) â†’ g x 1â‚‚ ï¼ h (f x)) â†’
    simplicial-mapping-cylinder f â†’ S
  cogap-simplicial-mapping-cylinder g h p =
    cogap-simplicial-mapping-cylinder' ((Î» (x , t) â†’ g x t) , h , p)

  compute-in-codomain-cogap-simplicial-mapping-cylinder :
    (g : X â†’ ğŸš â†’ S) (h : Y â†’ S) (p : (x : X) â†’ g x 1â‚‚ ï¼ h (f x)) â†’
    (y : Y) â†’
    cogap-simplicial-mapping-cylinder g h p
      ( in-codomain-simplicial-mapping-cylinder f y) ï¼
    ( h y)
  compute-in-codomain-cogap-simplicial-mapping-cylinder g h p =
    compute-inr-cogap (Î» x â†’ (x , 1â‚‚)) f ((Î» (x , t) â†’ g x t) , h , p)

  compute-in-domain-interval-cogap-simplicial-mapping-cylinder :
    (g : X â†’ ğŸš â†’ S) (h : Y â†’ S) (p : (x : X) â†’ g x 1â‚‚ ï¼ h (f x))
    (x : X) (t : ğŸš) â†’
    cogap-simplicial-mapping-cylinder g h p
      ( in-domain-interval-simplicial-mapping-cylinder f x t) ï¼
    ( g x t)
  compute-in-domain-interval-cogap-simplicial-mapping-cylinder g h p x t =
    compute-inl-cogap
      ( Î» x â†’ (x , 1â‚‚))
      ( f)
      ( (Î» (x , t) â†’ g x t) , h , p)
      ( x , t)

  compute-in-domain-cogap-simplicial-mapping-cylinder :
    (g : X â†’ ğŸš â†’ S) (h : Y â†’ S) (p : (x : X) â†’ g x 1â‚‚ ï¼ h (f x)) (x : X) â†’
    cogap-simplicial-mapping-cylinder g h p
      ( in-domain-simplicial-mapping-cylinder f x) ï¼
    g x 0â‚‚
  compute-in-domain-cogap-simplicial-mapping-cylinder g h p x =
    compute-in-domain-interval-cogap-simplicial-mapping-cylinder g h p x 0â‚‚
```
