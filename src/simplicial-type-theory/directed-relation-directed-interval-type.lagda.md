# The directed relation on the directed interval type

```agda
module simplicial-type-theory.directed-relation-directed-interval-type where
```

<details><summary>Imports</summary>

```agda
open import foundation.0-connected-types
open import foundation.cartesian-product-types
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.function-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.injective-maps
open import foundation.mere-equality
open import foundation.negated-equality
open import foundation.negation
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.sets
open import foundation.universe-levels

open import order-theory.posets
open import order-theory.preorders
open import order-theory.total-orders

open import simplicial-type-theory.directed-interval-type

open import synthetic-homotopy-theory.joins-of-types
```

</details>

## Idea

The
{{#concept "directed relation" Disambiguation="on the directed interval type" Agda=_â‰¤-ğŸš_}}
on the
[directed interval type](simplicial-type-theory.directed-interval-type.md) `ğŸš`
is a [total order](order-theory.total-orders.md) with `0â‚‚` as the
[bottom element](order-theory.bottom-elements-posets.md), and `1â‚‚` as the
[top element](order-theory.top-elements-posets.md).

## Postulates

### The directed relation

```agda
postulate
  _â‰¤-ğŸš_ : ğŸš â†’ ğŸš â†’ UU lzero
  is-prop-leq-ğŸš : {x y : ğŸš} â†’ is-prop (x â‰¤-ğŸš y)

infix 30 _â‰¤-ğŸš_

leq-ğŸš : ğŸš â†’ ğŸš â†’ UU lzero
leq-ğŸš = _â‰¤-ğŸš_
```

### The directed relation is a total order

```agda
postulate
  refl-leq-ğŸš : {x : ğŸš} â†’ x â‰¤-ğŸš x

  transitive-leq-ğŸš : {x y z : ğŸš} â†’ y â‰¤-ğŸš z â†’ x â‰¤-ğŸš y â†’ x â‰¤-ğŸš z

  antisymmetric-leq-ğŸš : {x y : ğŸš} â†’ x â‰¤-ğŸš y â†’ y â‰¤-ğŸš x â†’ x ï¼ y

  total-leq-ğŸš : {x y : ğŸš} â†’ (x â‰¤-ğŸš y) * (y â‰¤-ğŸš x)
```

**Note.** We define totality using the
[join operation](synthetic-homotopy-theory.joins-of-types.md) on types as we may
commonly want to eliminate the directed relation into types, and not just
propositions. Moreover, the join operation is defined in terms of the
[standard pushout](synthetic-homotopy-theory.pushouts.md) which we can enable
rewrite rules for by importing the module
[`synthetic-homotopy-theory.rewriting-pushouts`](synthetic-homotopy-theory.rewriting-pushouts.md).

### The source and target of the directed interval are bottom and top elements

```agda
postulate
  min-leq-ğŸš : {x : ğŸš} â†’ 0â‚‚ â‰¤-ğŸš x

  max-leq-ğŸš : {x : ğŸš} â†’ x â‰¤-ğŸš 1â‚‚
```

## Definitions

```agda
leq-ğŸš-Prop : (x y : ğŸš) â†’ Prop lzero
leq-ğŸš-Prop x y = (x â‰¤-ğŸš y , is-prop-leq-ğŸš)

{-# INLINE leq-ğŸš-Prop #-}

min-leq-eq-ğŸš : {x y : ğŸš} â†’ x ï¼ 0â‚‚ â†’ x â‰¤-ğŸš y
min-leq-eq-ğŸš refl = min-leq-ğŸš

max-leq-eq-ğŸš : {x y : ğŸš} â†’ y ï¼ 1â‚‚ â†’ x â‰¤-ğŸš y
max-leq-eq-ğŸš refl = max-leq-ğŸš

leq-eq-ğŸš : {x y : ğŸš} â†’ x ï¼ y â†’ x â‰¤-ğŸš y
leq-eq-ğŸš refl = refl-leq-ğŸš
```

### The poset on the directed interval

```agda
ğŸš-Preorder : Preorder lzero lzero
pr1 ğŸš-Preorder = ğŸš
pr1 (pr2 ğŸš-Preorder) = leq-ğŸš-Prop
pr1 (pr2 (pr2 ğŸš-Preorder)) x = refl-leq-ğŸš {x}
pr2 (pr2 (pr2 ğŸš-Preorder)) x y z = transitive-leq-ğŸš

ğŸš-Poset : Poset lzero lzero
pr1 ğŸš-Poset = ğŸš-Preorder
pr2 ğŸš-Poset x y = antisymmetric-leq-ğŸš
```

### The total order on the directed interval

```agda
is-total-leq-ğŸš : is-total-Poset ğŸš-Poset
is-total-leq-ğŸš x y =
  map-disjunction-join-Prop (leq-ğŸš-Prop x y) (leq-ğŸš-Prop y x) (total-leq-ğŸš)

ğŸš-Total-Order : Total-Order lzero lzero
ğŸš-Total-Order = (ğŸš-Poset , is-total-leq-ğŸš)
```

## Properties

### The directed interval is a set

```agda
is-set-ğŸš : is-set ğŸš
is-set-ğŸš = is-set-type-Poset ğŸš-Poset

ğŸš-Set : Set lzero
ğŸš-Set = (ğŸš , is-set-ğŸš)

Id-ğŸš-Prop : ğŸš â†’ ğŸš â†’ Prop lzero
Id-ğŸš-Prop = Id-Prop ğŸš-Set

{-# INLINE Id-ğŸš-Prop #-}
```

### The maximal element is not less than or equal to the bottom element of the directed interval

```agda
not-leq-target-source-ğŸš : Â¬ (1â‚‚ â‰¤-ğŸš 0â‚‚)
not-leq-target-source-ğŸš leq-1-0 =
  is-nontrivial-ğŸš (antisymmetric-leq-ğŸš min-leq-ğŸš leq-1-0)
```

### The canonical inclusion of the booleans into the directed interval is an embedding

```agda
is-emb-map-directed-interval-bool : is-emb map-directed-interval-bool
is-emb-map-directed-interval-bool =
  is-emb-is-injective is-set-ğŸš is-injective-map-directed-interval-bool
```

### The directed interval is not connected

```agda
is-not-connected-ğŸš : Â¬ (is-0-connected ğŸš)
is-not-connected-ğŸš H =
  is-nontrivial-ğŸš (reflects-mere-eq (ğŸš-Set) id (mere-eq-is-0-connected H 0â‚‚ 1â‚‚))
```
