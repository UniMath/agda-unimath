# Dependent directed edges

```agda
module simplicial-type-theory.dependent-directed-edges where
```

<details><summary>Imports</summary>

```agda
open import foundation.cartesian-product-types
open import foundation.contractible-types
open import foundation.dependent-identifications
open import foundation.dependent-pair-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.structure-identity-principle
open import foundation.torsorial-type-families
open import foundation.universe-levels

open import orthogonal-factorization-systems.extensions-of-maps

open import simplicial-type-theory.action-on-directed-edges-functions
open import simplicial-type-theory.directed-edges
open import simplicial-type-theory.directed-interval-type
open import simplicial-type-theory.simplicial-arrows
```

</details>

## Idea

Given a type family `B : A â†’ ğ’°` and a
[directed edge](simplicial-type-theory.directed-edges.md) `Î± : x â†’â‚‚ y` in `A`, a
{{#concept "dependent directed edge" Disambiguation="simplicial type theory" Agda=dependent-simplicial-hom}}
_over_ `Î±` from `x'` to `y'` is a simplicial arrow `Î²` in `B âˆ˜ Î± : ğŸš â†’ ğ’°`. such
that `Î² 0â‚‚ ï¼ x'` over the identification `Î± 0â‚‚ ï¼ x` and `Î² 1â‚‚ ï¼ y'` over the
identification `Î± 1â‚‚ ï¼ y`.

Assuming for simplicity that the endpoints are strict, i.e., `Î± 0â‚‚ â‰ x` and
`Î± 1â‚‚ â‰ y`, the situation can be drawn as in the following diagram. The
dependent arrow `Î²` lives in the dependent type `B` varying over `A`, pointing
from an element in the fiber `B x` to an element in the fiber `B y`. On each end
of the dependent arrow, there is a correcting identification within the
respective fiber of `B`

```text
       B x           B y
   ~~~~~~~~~~~~~~~~~~~~~~~~~
  â”‚     â”‚             â”‚     â”‚
  â”‚  x' âˆ™ Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·> â”‚     â”‚      B
  â”‚     â”‚      Î²      âˆ™ y'  â”‚
  â”‚     â”‚             â”‚     â”‚
   ~~~~~~~~~~~~~~~~~~~~~~~~~
        â†§             â†§
  â”€â”€â”€â”€â”€ âˆ™ â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€> âˆ™ â”€â”€â”€â”€â”€      A.
        x      Î±      y
```

## Definitions

### Dependent directed edges

```agda
module _
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2)
  {x y : A} (Î± : x â†’â‚‚ y)
  where

  dependent-simplicial-hom : B x â†’ B y â†’ UU l2
  dependent-simplicial-hom x' y' =
    Î£ ( simplicial-arrow' (B âˆ˜ simplicial-arrow-simplicial-hom Î±))
      ( Î» Î² â†’
        ( dependent-identification B (eq-source-simplicial-hom Î±) (Î² 0â‚‚) x') Ã—
        ( dependent-identification B (eq-target-simplicial-hom Î±) (Î² 1â‚‚) y'))

module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  {x y : A} (Î± : x â†’â‚‚ y) {x' : B x} {y' : B y}
  (Î² : dependent-simplicial-hom B Î± x' y')
  where

  simplicial-arrow-dependent-simplicial-hom :
    simplicial-arrow' (B âˆ˜ simplicial-arrow-simplicial-hom Î±)
  simplicial-arrow-dependent-simplicial-hom = pr1 Î²

  eq-source-dependent-simplicial-hom :
    dependent-identification B
      ( eq-source-simplicial-hom Î±)
      ( simplicial-arrow-dependent-simplicial-hom 0â‚‚)
      ( x')
  eq-source-dependent-simplicial-hom = pr1 (pr2 Î²)

  eq-target-dependent-simplicial-hom :
    dependent-identification B
      ( eq-target-simplicial-hom Î±)
      ( simplicial-arrow-dependent-simplicial-hom 1â‚‚)
      ( y')
  eq-target-dependent-simplicial-hom = pr2 (pr2 Î²)
```

### The identity/constant dependent directed edges

```agda
id-dependent-simplicial-hom :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} {x : A} (x' : B x) â†’
  dependent-simplicial-hom B (id-simplicial-hom x) x' x'
id-dependent-simplicial-hom = id-simplicial-hom
```

### Dependent directed edges arising from identifications

```agda
dependent-simplicial-hom-eq :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} {x y : A} (p : x ï¼ y)
  {x' : B x} {y' : B y} â†’ dependent-identification B p x' y' â†’
  dependent-simplicial-hom B (simplicial-hom-eq p) x' y'
dependent-simplicial-hom-eq refl = simplicial-hom-eq
```

### Characterizing equality of dependent directed edges over a fixed edge

Given a directed edge `Î± : x â†’â‚‚ y` in `A` and elements `x' : B x` and
`y' : B y`, we want to characterize identifications of dependent directed edges
from `x'` to `y'` over `Î±`. The situation is as follows

```text
       B x                                     B y
   ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  â”‚     â”‚        â”‚                     â”‚        â”‚     â”‚
  â”‚     â”‚        â”‚          Î²          â”‚        â”‚     â”‚
  â”‚     â”‚ â•â•â•â•â•â• âˆ™ Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·> âˆ™ â•â•â•â•â•â• â”‚     â”‚
  â”‚  x' âˆ™        â”‚                     â”‚        âˆ™ y'  â”‚      B
  â”‚     â”‚ â•â•â•â•â•â• âˆ™ Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·Â·> âˆ™ â•â•â•â•â•â• â”‚     â”‚
  â”‚     â”‚        â”‚          Î²'         â”‚        â”‚     â”‚
  â”‚     â”‚        â”‚                     â”‚        â”‚     â”‚
   ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        â†§        â†§                     â†§        â†§
  â”€â”€â”€â”€â”€ âˆ™ â•â•â•â•â•â• âˆ™ â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€> âˆ™ ====== âˆ™ â”€â”€â”€â”€â”€      A.
        x       Î± 0â‚‚        Î±         Î± 1â‚‚      y
```

We are looking for coherence data to fill the area between `x'` and `y'`, and
the area is naturally subdivided into three sections. To fill the middle, we ask
for a homotopy of the underlying dependent arrows `H : Î² ~ Î²'`. Now, to also
fill the triangles at the end points, we require further coherences of the
dependent triangles depicted below

```text
            â”€â”€â”€â”€ âˆ™ Î² 0â‚‚           Î² 1â‚‚ âˆ™ â”€â”€â”€â”€
          â•±      â”‚                     â”‚      â•²
     x' âˆ™        â”‚ H 0â‚‚           H 1â‚‚ â”‚        âˆ™ y'
          â•²      â”‚                     â”‚      â•±
            â”€â”€â”€â”€ âˆ™ Î²' 0â‚‚         Î²' 1â‚‚ âˆ™ â”€â”€â”€â”€

        â†§        â†§                     â†§        â†§

      x âˆ™ â”€â”€â”€â”€â”€â”€ âˆ™ Î± 0â‚‚           Î± 1â‚‚ âˆ™ â”€â”€â”€â”€â”€â”€ âˆ™ y.
```

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  {x y : A} (Î± : x â†’â‚‚ y) {x' : B x} {y' : B y}
  where

  coherence-htpy-dependent-simplicial-hom-over :
    (Î² Î²' : dependent-simplicial-hom B Î± x' y') â†’
    simplicial-arrow-dependent-simplicial-hom Î± Î² ~
    simplicial-arrow-dependent-simplicial-hom Î± Î²' â†’
    UU l2
  coherence-htpy-dependent-simplicial-hom-over Î² Î²' H =
    ( ( eq-source-dependent-simplicial-hom Î± Î²) ï¼
      ( concat-dependent-identification B refl
        ( eq-source-simplicial-hom Î±)
        ( H 0â‚‚)
        ( eq-source-dependent-simplicial-hom Î± Î²'))) Ã—
    ( ( eq-target-dependent-simplicial-hom Î± Î²) ï¼
      ( concat-dependent-identification B refl
        ( eq-target-simplicial-hom Î±)
        ( H 1â‚‚)
        ( eq-target-dependent-simplicial-hom Î± Î²')))

  htpy-dependent-simplicial-hom-over :
    (Î² Î²' : dependent-simplicial-hom B Î± x' y') â†’ UU l2
  htpy-dependent-simplicial-hom-over Î² Î²' =
    Î£ ( simplicial-arrow-dependent-simplicial-hom Î± Î² ~
        simplicial-arrow-dependent-simplicial-hom Î± Î²')
      ( coherence-htpy-dependent-simplicial-hom-over Î² Î²')

  refl-htpy-dependent-simplicial-hom-over :
    (Î² : dependent-simplicial-hom B Î± x' y') â†’
    htpy-dependent-simplicial-hom-over Î² Î²
  refl-htpy-dependent-simplicial-hom-over Î² = (refl-htpy , refl , refl)

  htpy-eq-dependent-simplicial-hom-over :
    (Î² Î²' : dependent-simplicial-hom B Î± x' y') â†’
    Î² ï¼ Î²' â†’ htpy-dependent-simplicial-hom-over Î² Î²'
  htpy-eq-dependent-simplicial-hom-over Î² .Î² refl =
    refl-htpy-dependent-simplicial-hom-over Î²

  is-torsorial-htpy-dependent-simplicial-hom-over :
    (Î² : dependent-simplicial-hom B Î± x' y') â†’
    is-torsorial (htpy-dependent-simplicial-hom-over Î²)
  is-torsorial-htpy-dependent-simplicial-hom-over Î² =
    is-torsorial-Eq-structure
      ( is-torsorial-htpy (simplicial-arrow-dependent-simplicial-hom Î± Î²))
      ( simplicial-arrow-dependent-simplicial-hom Î± Î² , refl-htpy)
      ( is-torsorial-Eq-structure
        ( is-torsorial-Id (eq-source-dependent-simplicial-hom Î± Î²))
        ( eq-source-dependent-simplicial-hom Î± Î² , refl)
        ( is-torsorial-Id (eq-target-dependent-simplicial-hom Î± Î²)))

  is-equiv-htpy-eq-dependent-simplicial-hom-over :
    (Î² Î²' : dependent-simplicial-hom B Î± x' y') â†’
    is-equiv (htpy-eq-dependent-simplicial-hom-over Î² Î²')
  is-equiv-htpy-eq-dependent-simplicial-hom-over Î² =
    fundamental-theorem-id
      ( is-torsorial-htpy-dependent-simplicial-hom-over Î²)
      ( htpy-eq-dependent-simplicial-hom-over Î²)

  extensionality-dependent-simplicial-hom-over :
    (Î² Î²' : dependent-simplicial-hom B Î± x' y') â†’
    (Î² ï¼ Î²') â‰ƒ htpy-dependent-simplicial-hom-over Î² Î²'
  extensionality-dependent-simplicial-hom-over Î² Î²' =
    ( htpy-eq-dependent-simplicial-hom-over Î² Î²' ,
      is-equiv-htpy-eq-dependent-simplicial-hom-over Î² Î²')

  eq-htpy-dependent-simplicial-hom-over :
    (Î² Î²' : dependent-simplicial-hom B Î± x' y') â†’
    htpy-dependent-simplicial-hom-over Î² Î²' â†’ Î² ï¼ Î²'
  eq-htpy-dependent-simplicial-hom-over Î² Î²' =
    map-inv-equiv (extensionality-dependent-simplicial-hom-over Î² Î²')
```
