# Spines

```agda
open import foundation.universe-levels
open import order-theory.nontrivial-bounded-total-orders

module
  simplicial-type-theory.spines
  {I1 I2 : Level} (I : Nontrivial-Bounded-Total-Order I1 I2)
  where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.action-on-identifications-functions
open import foundation.booleans
open import foundation.cartesian-product-types
open import foundation.conjunction
open import foundation.contractible-types
open import foundation.coproduct-types
open import foundation.dependent-identifications
open import foundation.dependent-pair-types
open import foundation.disjunction
open import foundation.empty-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.propositions
open import foundation.retractions
open import foundation.sections
open import foundation.sets
open import foundation.subtypes
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unit-type
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import reflection.erasing-equality

open import simplicial-type-theory.arrows I
open import simplicial-type-theory.cubes I
open import simplicial-type-theory.directed-edges I
open import simplicial-type-theory.directed-interval I
open import simplicial-type-theory.inequality-directed-interval-type I

open import synthetic-homotopy-theory.cocones-under-spans
open import synthetic-homotopy-theory.dependent-cocones-under-spans
open import synthetic-homotopy-theory.dependent-universal-property-pushouts
open import synthetic-homotopy-theory.induction-principle-pushouts
open import synthetic-homotopy-theory.joins-of-types
open import synthetic-homotopy-theory.pushouts
open import synthetic-homotopy-theory.recursion-principle-pushouts
open import synthetic-homotopy-theory.universal-property-pushouts

open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

The {{#concept "ð‘›-spine" Agda=spine}} is the classifying type of chains of
directed edges of length ð‘›.

```text
  0 ---> 1 ----> ... ----> (n-1) ----> n
```

It has the universal property of the iterated
[pushout](synthetic-homotopy-theory.pushouts.md)

```text
               0â–µ
         1 ---------> Î”Â¹
         |            |
  target |            |
         âˆ¨          âŒœ âˆ¨
     spine n ----> spine (n + 1)
```

where

```text
  spine 0 := 1.
```

## Postulates

### The type of ð‘›-spines

> TODO Reconsider this definition.

```agda
postulate
  spine : â„• â†’ UU I1

  star-spine-0 : spine 0

  contraction-star-spine-0 : (x : spine 0) â†’ star-spine-0 ï¼ x

  inl-spine : {n : â„•} â†’ spine n â†’ spine (succ-â„• n)

  in-arrow-spine : {n : â„•} â†’ Î”Â¹ â†’ spine (succ-â„• n)

is-contr-spine-0 : is-contr (spine 0)
is-contr-spine-0 = (star-spine-0 , contraction-star-spine-0)

initial-point-spine : (n : â„•) â†’ spine n
initial-point-spine zero-â„• = star-spine-0
initial-point-spine (succ-â„• n) = inl-spine (initial-point-spine n)

terminal-point-spine : (n : â„•) â†’ spine n
terminal-point-spine zero-â„• = star-spine-0
terminal-point-spine (succ-â„• n) = in-arrow-spine 1â–µ

postulate
  glue-spine :
    {n : â„•} â†’ inl-spine (terminal-point-spine n) ï¼ in-arrow-spine {n} 0â–µ
```

### The induction principle of the (ð‘›+1)-spine

We postulate that the (ð‘›+1)-spine is the pushout

```text
               0â–µ
         1 ---------> Î”Â¹
         |            |
  target |            |
         âˆ¨          âŒœ âˆ¨
     spine n -----> spine (n + 1)
```

```agda
cocone-spine :
  (n : â„•) â†’
  cocone (point (terminal-point-spine n)) (point 0â–µ) (spine (succ-â„• n))
cocone-spine n = (inl-spine , in-arrow-spine , point glue-spine)

module _
  {l : Level} (n : â„•) (P : spine (succ-â„• n) â†’ UU l)
  (d :
    dependent-cocone
      ( point (terminal-point-spine n))
      ( point 0â–µ)
      ( cocone-spine n)
      ( P))
  where

  postulate
    dependent-cogap-spine : (x : spine (succ-â„• n)) â†’ P x

  compute-inl-dependent-cogap-spine :
    (x : spine n) â†’
    dependent-cogap-spine (inl-spine x) ï¼
    horizontal-map-dependent-cocone
      ( point (terminal-point-spine n))
      ( point 0â–µ)
      ( cocone-spine n)
      ( P)
      ( d)
      ( x)
  compute-inl-dependent-cogap-spine x =
    primEraseEquality compute-inl-dependent-cogap-spine'
    where
    postulate
      compute-inl-dependent-cogap-spine' :
        dependent-cogap-spine (inl-spine x) ï¼
        horizontal-map-dependent-cocone
          ( point (terminal-point-spine n))
          ( point 0â–µ)
          ( cocone-spine n)
          ( P)
          ( d)
          ( x)

  compute-inr-dependent-cogap-spine :
    (t : Î”Â¹) â†’
    dependent-cogap-spine (in-arrow-spine t) ï¼
    vertical-map-dependent-cocone
      ( point (terminal-point-spine n))
      ( point 0â–µ)
      ( cocone-spine n)
      ( P)
      ( d)
      ( t)
  compute-inr-dependent-cogap-spine t =
    primEraseEquality compute-inr-dependent-cogap-spine'
    where
    postulate
      compute-inr-dependent-cogap-spine' :
        dependent-cogap-spine (in-arrow-spine t) ï¼
        vertical-map-dependent-cocone
          ( point (terminal-point-spine n))
          ( point 0â–µ)
          ( cocone-spine n)
          ( P)
          ( d)
          ( t)

  postulate
    compute-glue-dependent-cogap-spine :
      coherence-htpy-dependent-cocone
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)
        ( P)
        ( dependent-cocone-map
          ( point (terminal-point-spine n))
          ( point 0â–µ)
          ( cocone-spine n)
          ( P)
          ( dependent-cogap-spine))
        ( d)
        ( compute-inl-dependent-cogap-spine)
        ( compute-inr-dependent-cogap-spine)
```

## Definitions

### The spines as subtypes of the cubes

We can inductively define the ð‘›-spine as a subtype of the ð‘›-cube via t

```text
                 Â· â”„â”„â”„â”„â”„â”„> âˆ™
                âˆ§âˆ§        âˆ§âˆ§
              â‹°  â”†      /  â”†
            Â· â”„â”„â”„â”„â”„â”„> âˆ™    â”†
  y   x     âˆ§    Â· â”„â”„ âˆ§ â”„> Â·
  âˆ§ âˆ§       â”†   âˆ§     |   âˆ§
  |/        â”† â‹°       | â‹°
  â””-> z     âˆ™ ------> âˆ™
```

```agda
subtype-spine : (n : â„•) â†’ subtype I1 (directed-cube n)
subtype-spine 0 _ = raise-unit-Prop I1
subtype-spine 1 _ = raise-unit-Prop I1
subtype-spine 2 (x , y) = join-Prop (Id-Î”Â¹-Prop x 1â–µ) (Id-Î”Â¹-Prop y 0â–µ)
subtype-spine (succ-â„• (succ-â„• (succ-â„• n))) (x , u) =
  join-Prop
    ( is-terminal-element-directed-cube-Prop (succ-â„• (succ-â„• n)) u)
    ( (Id-Î”Â¹-Prop x 0â–µ) âˆ§ (subtype-spine (succ-â„• (succ-â„• n)) u))
```

Let us work out what this definition unfolds to when `n` is `2`:

```text
  subtype-spine 2 (s , t)
  â‰ is-terminal t âˆ¨ ((s ï¼ 0â–µ) âˆ§ (subtype-spine 1 t))
  â‰ (t ï¼ 1â–µ) âˆ¨ ((s ï¼ 0â–µ) âˆ§ unit)
  â‰ƒ (t ï¼ 1â–µ) âˆ¨ (s ï¼ 0â–µ).
```

Observe again that the coordinates are read in order from right to left.

```agda
spine' : â„• â†’ UU I1
spine' n = type-subtype (subtype-spine n)

is-set-spine' : (n : â„•) â†’ is-set (spine' n)
is-set-spine' n =
  is-set-type-subtype (subtype-spine n) (is-set-directed-cube n)
```

### The point inclusions of the spines

The ð‘›-spine has ð‘›+1 points that we enumerate

```text
  0 ---> 1 ---> 2 ---> â‹¯ ---> n
```

```agda
point-spine : (n : â„•) â†’ Fin (succ-â„• n) â†’ spine n
point-spine zero-â„• _ = star-spine-0
point-spine (succ-â„• n) (inl x) = inl-spine (point-spine n x)
point-spine (succ-â„• n) (inr x) = terminal-point-spine (succ-â„• n)

compute-inr-point-spine :
  (n : â„•) {x : unit} â†’ point-spine n (inr x) ï¼ terminal-point-spine n
compute-inr-point-spine zero-â„• = refl
compute-inr-point-spine (succ-â„• n) = refl
```

### The arrow inclusions of the spine

The ð‘›-spine has ð‘› arrows.

```agda
arrow-spine : (n : â„•) â†’ Fin n â†’ Î”Â¹ â†’ spine n
arrow-spine (succ-â„• n) (inl x) = inl-spine âˆ˜ arrow-spine n x
arrow-spine (succ-â„• n) (inr x) = in-arrow-spine
```

### The hom inclusions of the spine

```agda
source-hom-spine : (n : â„•) (x : Fin n) â†’ spine n
source-hom-spine n x = point-spine n (inl-Fin n x)

target-hom-spine : (n : â„•) (x : Fin n) â†’ spine n
target-hom-spine n x = point-spine n (inr-Fin n x)

inv-eq-source-arrow-spine :
  (n : â„•) (x : Fin n) â†’ source-hom-spine n x ï¼ arrow-spine n x 0â–µ
inv-eq-source-arrow-spine (succ-â„• n) (inl x) =
  ap inl-spine (inv-eq-source-arrow-spine n x)
inv-eq-source-arrow-spine (succ-â„• n) (inr x) =
  ap inl-spine (compute-inr-point-spine n) âˆ™ glue-spine

eq-source-arrow-spine :
  (n : â„•) (x : Fin n) â†’ arrow-spine n x 0â–µ ï¼ source-hom-spine n x
eq-source-arrow-spine n x = inv (inv-eq-source-arrow-spine n x)

eq-target-arrow-spine :
  (n : â„•) (x : Fin n) â†’ arrow-spine n x 1â–µ ï¼ target-hom-spine n x
eq-target-arrow-spine (succ-â„• n) (inl x) =
  ap inl-spine (eq-target-arrow-spine n x)
eq-target-arrow-spine (succ-â„• n) (inr x) = refl

hom-spine : (n : â„•) (x : Fin n) â†’ source-hom-spine n x â†’â–µ target-hom-spine n x
hom-spine n x =
  ( arrow-spine n x , eq-source-arrow-spine n x , eq-target-arrow-spine n x)
```

### The dependent universal property of the spines

```agda
module _
  (n : â„•) {l : Level} (P : spine (succ-â„• n) â†’ UU l)
  where

  htpy-compute-dependent-cogap-spine :
    ( c :
      dependent-cocone
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)
        ( P)) â†’
    htpy-dependent-cocone
      ( point (terminal-point-spine n))
      ( point 0â–µ)
      ( cocone-spine n)
      ( P)
      ( dependent-cocone-map
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)
        ( P)
        ( dependent-cogap-spine n P c))
      ( c)
  htpy-compute-dependent-cogap-spine c =
    ( compute-inl-dependent-cogap-spine n P c ,
      compute-inr-dependent-cogap-spine n P c ,
      compute-glue-dependent-cogap-spine n P c)

  is-section-dependent-cogap-spine :
    is-section
      ( dependent-cocone-map
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)
        ( P))
      ( dependent-cogap-spine n P)
  is-section-dependent-cogap-spine c =
    eq-htpy-dependent-cocone
      ( point (terminal-point-spine n))
      ( point 0â–µ)
      ( cocone-spine n)
      ( P)
      ( dependent-cocone-map
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)
        ( P)
        ( dependent-cogap-spine n P c))
      ( c)
      ( htpy-compute-dependent-cogap-spine c)

induction-principle-spine :
  (n : â„•) â†’
  induction-principle-pushout
    ( point (terminal-point-spine n))
    ( point 0â–µ)
    ( cocone-spine n)
induction-principle-spine n P =
  ( dependent-cogap-spine n P , is-section-dependent-cogap-spine n P)

is-retraction-dependent-cogap-spine :
  (n : â„•) {l : Level} (P : spine (succ-â„• n) â†’ UU l) â†’
  is-retraction
    ( dependent-cocone-map
      ( point (terminal-point-spine n))
      ( point 0â–µ)
      ( cocone-spine n)
      ( P))
    ( dependent-cogap-spine n P)
is-retraction-dependent-cogap-spine n =
  is-retraction-ind-induction-principle-pushout
    ( point (terminal-point-spine n))
    ( point 0â–µ)
    ( cocone-spine n)
    ( induction-principle-spine n)

dependent-universal-property-spine :
  (n : â„•) â†’
  dependent-universal-property-pushout
    ( point (terminal-point-spine n))
    ( point 0â–µ)
    ( cocone-spine n)
dependent-universal-property-spine n P =
  is-equiv-is-invertible
    ( dependent-cogap-spine n P)
    ( is-section-dependent-cogap-spine n P)
    ( is-retraction-dependent-cogap-spine n P)

equiv-dependent-universal-property-spine :
  (n : â„•) {l : Level} (P : spine (succ-â„• n) â†’ UU l) â†’
  ( (x : spine (succ-â„• n)) â†’ P x) â‰ƒ
  ( dependent-cocone
    ( point (terminal-point-spine n))
    ( point 0â–µ)
    ( cocone-spine n)
    ( P))
pr1 (equiv-dependent-universal-property-spine n P) =
  dependent-cocone-map
    ( point (terminal-point-spine n))
    ( point 0â–µ)
    ( cocone-spine n)
    ( P)
pr2 (equiv-dependent-universal-property-spine n P) =
  dependent-universal-property-spine n P
```

### The universal property of the spines

```agda
module _
  (n : â„•) {l : Level} {X : UU l}
  where

  cogap-spine :
    cocone (point (terminal-point-spine n)) (point 0â–µ) X â†’ spine (succ-â„• n) â†’ X
  cogap-spine =
    dependent-cogap-spine n (Î» _ â†’ X) âˆ˜
    dependent-cocone-constant-type-family-cocone
      ( point (terminal-point-spine n))
      ( point 0â–µ)
      ( cocone-spine n)

  is-section-cogap-spine :
    is-section
      ( cocone-map
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n))
      ( cogap-spine)
  is-section-cogap-spine =
    ( ( triangle-dependent-cocone-map-constant-type-family'
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)) Â·r
      ( cogap-spine)) âˆ™h
    ( ( cocone-dependent-cocone-constant-type-family
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)) Â·l
      ( is-section-dependent-cogap-spine n (Î» _ â†’ X)) Â·r
      ( dependent-cocone-constant-type-family-cocone
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n))) âˆ™h
    ( is-retraction-cocone-dependent-cocone-constant-type-family
      ( point (terminal-point-spine n))
      ( point 0â–µ)
      ( cocone-spine n))

  is-retraction-cogap-spine :
    is-retraction
      ( cocone-map
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n))
      ( cogap-spine)
  is-retraction-cogap-spine =
    ( ( cogap-spine) Â·l
      ( triangle-dependent-cocone-map-constant-type-family'
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n))) âˆ™h
    ( ( dependent-cogap-spine n (Î» _ â†’ X)) Â·l
      ( is-section-cocone-dependent-cocone-constant-type-family
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)) Â·r
      ( dependent-cocone-map
        ( point (terminal-point-spine n))
        ( point 0â–µ)
        ( cocone-spine n)
        ( Î» _ â†’ X))) âˆ™h
    ( is-retraction-dependent-cogap-spine n (Î» _ â†’ X))

recursion-principle-spine :
  (n : â„•) â†’
  recursion-principle-pushout
    ( point (terminal-point-spine n))
    ( point 0â–µ)
    ( cocone-spine n)
recursion-principle-spine n = (cogap-spine n , is-section-cogap-spine n)

universal-property-spine :
  (n : â„•) â†’
  universal-property-pushout
    ( point (terminal-point-spine n))
    ( point 0â–µ)
    ( cocone-spine n)
universal-property-spine n Y =
  is-equiv-is-invertible
    ( cogap-spine n)
    ( is-section-cogap-spine n)
    ( is-retraction-cogap-spine n)

equiv-universal-property-spine :
  (n : â„•) {l : Level} {X : UU l} â†’
  (spine (succ-â„• n) â†’ X) â‰ƒ cocone (point (terminal-point-spine n)) (point 0â–µ) X
equiv-universal-property-spine n {X = X} =
  ( cocone-map (point (terminal-point-spine n)) (point 0â–µ) (cocone-spine n) ,
    universal-property-spine n X)
```

### Auxiliary definitions for the ð‘›-spine as a subtype of the ð‘›-cube

> This remains to be formalized.

```text
inl-spine' : (n : â„•) â†’ spine' n â†’ spine' (succ-â„• n)
inl-spine' zero-â„• _ = (0â–µ , star)
inl-spine' (succ-â„• zero-â„•) (t , _) = ((0â–µ , t) , inr-join (refl , star))
inl-spine' (succ-â„• (succ-â„• n)) x = ((0â–µ , {!   !}) , inr-join (refl , {!   !}))

terminal-point-spine' : (n : â„•) â†’ spine' n
terminal-point-spine' zero-â„• = star , star
terminal-point-spine' (succ-â„• zero-â„•) = 1â–µ , star
terminal-point-spine' (succ-â„• (succ-â„• n)) = ({!   !} , {!   !})

cocone-spine' :
  (n : â„•) â†’
  cocone (point (terminal-point-spine' n)) (point 0â–µ) (spine' (succ-â„• n))
cocone-spine' = {!   !}

-- map-spine-spine' :
--   (n : â„•) â†’ spine' n â†’ spine n
-- map-spine-spine' 0 x = star-spine-0
-- map-spine-spine' 1 (x , u) = in-arrow-spine x
-- map-spine-spine' (succ-â„• (succ-â„• n)) ((t , x) , u) =
--   cogap-join
--     ( spine (succ-â„• (succ-â„• n)))
--     ( ( Î» _ â†’ in-arrow-spine t) ,
--       ( Î» rs â†’
--         inl-spine (map-spine-spine' (succ-â„• n) (x , pr2 rs))) ,
--       ( Î» (is-terminal-x , t=0 , s) â†’
--         ( ( ap in-arrow-spine t=0) âˆ™
--           ( inv glue-spine) âˆ™
--           ( ap inl-spine
--             ( compute-inr-map-spine-spine' n (x , s) is-terminal-x)))))
--     ( u)
--   where
--     compute-inr-map-spine-spine' :
--       (n : â„•) (xs : spine' (succ-â„• n)) â†’
--       is-terminal-element-directed-cube (succ-â„• n) (pr1 xs) â†’
--       in-arrow-spine 1â–µ ï¼ map-spine-spine' (succ-â„• n) xs
--     compute-inr-map-spine-spine' zero-â„• xs is-terminal-x =
--       ap in-arrow-spine (inv is-terminal-x)
--     compute-inr-map-spine-spine' (succ-â„• n) xs is-terminal-x =
--       inv (compute-inl-cogap-join {!   !} {!   !}) âˆ™ {!   !}

--   -- where map-spine-spine' (succ-â„• n) (x , s)
```

## Properties

### The 1-spine is the directed interval

```text
      1 ----------> Î”Â¹
      |             |
    ~ |             | ~
      âˆ¨           âŒœ âˆ¨
   spine 0 ----> spine 1
```

> This remains to be formalized.
