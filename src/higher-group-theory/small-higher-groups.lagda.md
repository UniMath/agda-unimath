# Small âˆ-groups

```agda
module higher-group-theory.small-higher-groups where
```

<details><summary>Imports</summary>

```agda
open import foundation.0-connected-types
open import foundation.dependent-pair-types
open import foundation.dependent-products-propositions
open import foundation.equivalences
open import foundation.identity-types
open import foundation.images
open import foundation.locally-small-types
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.replacement
open import foundation.small-types
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unit-type
open import foundation.universe-levels

open import higher-group-theory.equivalences-higher-groups
open import higher-group-theory.higher-groups

open import structured-types.pointed-equivalences
open import structured-types.pointed-types
open import structured-types.small-pointed-types
```

</details>

## Idea

An [âˆ-group](higher-group-theory.higher-groups.md) `G` is said to be
{{#concept "small" Disambiguation="âˆ-group" Agda=is-small-âˆ-Group}} with respect
to a universe `ğ’°` if its underlying type is `ğ’°`-small. By the
[type theoretic replacement principle](foundation.replacement.md) it follows
that `G` is small if and only if its classifying type `BG` is small. This
observation implies that an âˆ-group `G` is small if and only if it is
{{#concept "structurally small" Disambiguation="âˆ-group" Agda=is-structurally-small-âˆ-Group}}
in the sense that it comes equipped with an element of type

```text
  Î£ (H : âˆ-Group), G â‰ƒ H,
```

where the type `G â‰ƒ H` is the type of
[equivalences of âˆ-groups](higher-group-theory.equivalences-higher-groups.md).

Finally, we also introduce the notion of _pointed small âˆ-group_. An âˆ-group `G`
is said to be
{{#concept "pointed small" Disambiguation="âˆ-group" Agda=is-pointed-small-âˆ-Group}}
if its classifying [pointed type](structured-types.pointed-types.md) `BG` is
[pointed small](structured-types.small-pointed-types.md).

## Definitions

### The predicate of being a small âˆ-group

```agda
module _
  {l1 : Level} (l2 : Level) (G : âˆ-Group l1)
  where

  is-small-prop-âˆ-Group : Prop (l1 âŠ” lsuc l2)
  is-small-prop-âˆ-Group = is-small-Prop l2 (type-âˆ-Group G)

  is-small-âˆ-Group : UU (l1 âŠ” lsuc l2)
  is-small-âˆ-Group = is-small l2 (type-âˆ-Group G)

  is-prop-is-small-âˆ-Group : is-prop is-small-âˆ-Group
  is-prop-is-small-âˆ-Group = is-prop-is-small l2 (type-âˆ-Group G)
```

### The predicate of being a structurally small âˆ-group

```agda
module _
  {l1 : Level} (l2 : Level) (G : âˆ-Group l1)
  where

  is-structurally-small-âˆ-Group : UU (l1 âŠ” lsuc l2)
  is-structurally-small-âˆ-Group =
    Î£ (âˆ-Group l2) (equiv-âˆ-Group G)

  abstract
    is-prop-is-structurally-small-âˆ-Group :
      is-prop is-structurally-small-âˆ-Group
    is-prop-is-structurally-small-âˆ-Group =
      is-prop-equiv
        ( equiv-right-swap-Î£)
        ( is-prop-Î£
          ( is-prop-is-pointed-small-Pointed-Type
            ( classifying-pointed-type-âˆ-Group G))
          ( Î» H â†’ is-prop-is-0-connected _))

  is-structurally-small-prop-âˆ-Group : Prop (l1 âŠ” lsuc l2)
  pr1 is-structurally-small-prop-âˆ-Group = is-structurally-small-âˆ-Group
  pr2 is-structurally-small-prop-âˆ-Group = is-prop-is-structurally-small-âˆ-Group

module _
  {l1 l2 : Level} (G : âˆ-Group l1) (H : is-structurally-small-âˆ-Group l2 G)
  where

  âˆ-group-is-structurally-small-âˆ-Group : âˆ-Group l2
  âˆ-group-is-structurally-small-âˆ-Group = pr1 H

  type-âˆ-group-is-structurally-small-âˆ-Group : UU l2
  type-âˆ-group-is-structurally-small-âˆ-Group =
    type-âˆ-Group âˆ-group-is-structurally-small-âˆ-Group

  equiv-âˆ-group-is-structurally-small-âˆ-Group :
    equiv-âˆ-Group G âˆ-group-is-structurally-small-âˆ-Group
  equiv-âˆ-group-is-structurally-small-âˆ-Group = pr2 H

  equiv-is-structurally-small-âˆ-Group :
    type-âˆ-Group G â‰ƒ type-âˆ-group-is-structurally-small-âˆ-Group
  equiv-is-structurally-small-âˆ-Group =
    equiv-equiv-âˆ-Group G
      ( âˆ-group-is-structurally-small-âˆ-Group)
      ( equiv-âˆ-group-is-structurally-small-âˆ-Group)
```

### The predicate of being a pointed small âˆ-group

```agda
module _
  {l1 : Level} (l2 : Level) (G : âˆ-Group l1)
  where

  is-pointed-small-prop-âˆ-Group : Prop (l1 âŠ” lsuc l2)
  is-pointed-small-prop-âˆ-Group =
    is-pointed-small-prop-Pointed-Type l2 (classifying-pointed-type-âˆ-Group G)

  is-pointed-small-âˆ-Group : UU (l1 âŠ” lsuc l2)
  is-pointed-small-âˆ-Group =
    is-pointed-small-Pointed-Type l2 (classifying-pointed-type-âˆ-Group G)

  is-prop-is-pointed-small-âˆ-Group : is-prop is-pointed-small-âˆ-Group
  is-prop-is-pointed-small-âˆ-Group =
    is-prop-is-pointed-small-Pointed-Type (classifying-pointed-type-âˆ-Group G)
```

## Properties

### The classifying type of any small âˆ-group is locally small

```agda
module _
  {l1 l2 : Level} (G : âˆ-Group l1) (H : is-small-âˆ-Group l2 G)
  where

  abstract
    is-locally-small-classifying-type-is-small-âˆ-Group' :
      (x y : classifying-type-âˆ-Group G) â†’
      shape-âˆ-Group G ï¼ x â†’ shape-âˆ-Group G ï¼ y â†’
      is-small l2 (x ï¼ y)
    is-locally-small-classifying-type-is-small-âˆ-Group' ._ ._ refl refl = H

  abstract
    is-locally-small-classifying-type-is-small-âˆ-Group :
      is-locally-small l2 (classifying-type-âˆ-Group G)
    is-locally-small-classifying-type-is-small-âˆ-Group x y =
      apply-twice-universal-property-trunc-Prop
        ( mere-eq-classifying-type-âˆ-Group G (shape-âˆ-Group G) x)
        ( mere-eq-classifying-type-âˆ-Group G (shape-âˆ-Group G) y)
        ( is-small-Prop l2 (x ï¼ y))
        ( is-locally-small-classifying-type-is-small-âˆ-Group' x y)
```

### An âˆ-group is small if and only if its classifying type is small

```agda
module _
  {l1 l2 : Level} (G : âˆ-Group l1)
  where

  abstract
    is-small-classifying-type-is-small-âˆ-Group :
      is-small-âˆ-Group l2 G â†’ is-small l2 (classifying-type-âˆ-Group G)
    is-small-classifying-type-is-small-âˆ-Group H =
      is-small-equiv'
        ( im (point-âˆ-Group G))
        ( compute-im-point-âˆ-Group G)
        ( replacement
          ( point-âˆ-Group G)
          ( is-small-unit)
          ( is-locally-small-classifying-type-is-small-âˆ-Group G H))

  abstract
    is-small-is-small-classifying-type-âˆ-Group :
      is-small l2 (classifying-type-âˆ-Group G) â†’ is-small-âˆ-Group l2 G
    is-small-is-small-classifying-type-âˆ-Group H =
      is-locally-small-is-small H (shape-âˆ-Group G) (shape-âˆ-Group G)
```

### An âˆ-group is small if and only if it is pointed small

```agda
module _
  {l1 l2 : Level} (G : âˆ-Group l1)
  where

  abstract
    is-pointed-small-is-small-âˆ-Group :
      is-small-âˆ-Group l2 G â†’ is-pointed-small-âˆ-Group l2 G
    is-pointed-small-is-small-âˆ-Group H =
      is-pointed-small-is-small-Pointed-Type
        ( classifying-pointed-type-âˆ-Group G)
        ( is-small-classifying-type-is-small-âˆ-Group G H)
```

### An âˆ-group is small if and only if it is structurally small

```agda
module _
  {l1 l2 : Level} (G : âˆ-Group l1)
  where

  classifying-pointed-type-is-small-âˆ-Group :
    is-small-âˆ-Group l2 G â†’ Pointed-Type l2
  classifying-pointed-type-is-small-âˆ-Group H =
    pointed-type-is-pointed-small-Pointed-Type
      ( classifying-pointed-type-âˆ-Group G)
      ( is-pointed-small-is-small-âˆ-Group G H)

  classifying-type-is-small-âˆ-Group : is-small-âˆ-Group l2 G â†’ UU l2
  classifying-type-is-small-âˆ-Group H =
    type-is-pointed-small-Pointed-Type
      ( classifying-pointed-type-âˆ-Group G)
      ( is-pointed-small-is-small-âˆ-Group G H)

  abstract
    is-0-connected-classifying-type-is-small-âˆ-Group :
      (H : is-small-âˆ-Group l2 G) â†’
      is-0-connected (classifying-type-is-small-âˆ-Group H)
    is-0-connected-classifying-type-is-small-âˆ-Group H =
      is-0-connected-equiv'
        ( equiv-is-pointed-small-Pointed-Type
          ( classifying-pointed-type-âˆ-Group G)
          ( is-pointed-small-is-small-âˆ-Group G H))
        ( is-0-connected-classifying-type-âˆ-Group G)

  âˆ-group-is-small-âˆ-Group : is-small-âˆ-Group l2 G â†’ âˆ-Group l2
  pr1 (âˆ-group-is-small-âˆ-Group H) =
    classifying-pointed-type-is-small-âˆ-Group H
  pr2 (âˆ-group-is-small-âˆ-Group H) =
    is-0-connected-classifying-type-is-small-âˆ-Group H

  pointed-type-âˆ-group-is-small-âˆ-Group :
    is-small-âˆ-Group l2 G â†’ Pointed-Type l2
  pointed-type-âˆ-group-is-small-âˆ-Group H =
    pointed-type-âˆ-Group (âˆ-group-is-small-âˆ-Group H)

  type-âˆ-group-is-small-âˆ-Group :
    is-small-âˆ-Group l2 G â†’ UU l2
  type-âˆ-group-is-small-âˆ-Group H =
    type-âˆ-Group (âˆ-group-is-small-âˆ-Group H)

  equiv-âˆ-group-is-small-âˆ-Group :
    (H : is-small-âˆ-Group l2 G) â†’ equiv-âˆ-Group G (âˆ-group-is-small-âˆ-Group H)
  equiv-âˆ-group-is-small-âˆ-Group H =
    pointed-equiv-is-pointed-small-Pointed-Type
      ( classifying-pointed-type-âˆ-Group G)
      ( is-pointed-small-is-small-âˆ-Group G H)

  pointed-equiv-equiv-âˆ-group-is-small-âˆ-Group :
    (H : is-small-âˆ-Group l2 G) â†’
    pointed-type-âˆ-Group G â‰ƒâˆ— pointed-type-âˆ-group-is-small-âˆ-Group H
  pointed-equiv-equiv-âˆ-group-is-small-âˆ-Group H =
    pointed-equiv-equiv-âˆ-Group G
      ( âˆ-group-is-small-âˆ-Group H)
      ( equiv-âˆ-group-is-small-âˆ-Group H)

  equiv-equiv-âˆ-group-is-small-âˆ-Group :
    (H : is-small-âˆ-Group l2 G) â†’
    type-âˆ-Group G â‰ƒ type-âˆ-group-is-small-âˆ-Group H
  equiv-equiv-âˆ-group-is-small-âˆ-Group H =
    equiv-equiv-âˆ-Group G
      ( âˆ-group-is-small-âˆ-Group H)
      ( equiv-âˆ-group-is-small-âˆ-Group H)

  abstract
    is-structurally-small-is-small-âˆ-Group :
      is-small-âˆ-Group l2 G â†’ is-structurally-small-âˆ-Group l2 G
    pr1 (is-structurally-small-is-small-âˆ-Group H) =
      âˆ-group-is-small-âˆ-Group H
    pr2 (is-structurally-small-is-small-âˆ-Group H) =
      equiv-âˆ-group-is-small-âˆ-Group H

  abstract
    is-small-is-structurally-small-âˆ-Group :
      is-structurally-small-âˆ-Group l2 G â†’ is-small-âˆ-Group l2 G
    pr1 (is-small-is-structurally-small-âˆ-Group H) =
      type-âˆ-group-is-structurally-small-âˆ-Group G H
    pr2 (is-small-is-structurally-small-âˆ-Group H) =
      equiv-is-structurally-small-âˆ-Group G H

  abstract
    is-small-âˆ-group-is-small-âˆ-Group :
      (H : is-small-âˆ-Group l2 G) â†’
      is-small-âˆ-Group l1 (âˆ-group-is-small-âˆ-Group H)
    pr1 (is-small-âˆ-group-is-small-âˆ-Group H) = type-âˆ-Group G
    pr2 (is-small-âˆ-group-is-small-âˆ-Group H) =
      inv-equiv (equiv-equiv-âˆ-group-is-small-âˆ-Group H)
```
