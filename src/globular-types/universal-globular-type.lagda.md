# The universal globular type

```agda
{-# OPTIONS --guardedness #-}

open import foundation.function-extensionality-axiom

module
  globular-types.universal-globular-type
  (funext : function-extensionality)
  where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.spans
open import foundation.universe-levels

open import globular-types.dependent-globular-types funext
open import globular-types.exponentials-globular-types funext
open import globular-types.globular-maps funext
open import globular-types.globular-types
```

</details>

## Idea

The {{#concept "universal globular type"}} `ð’¢ l` at
[universe level](foundation.universe-levels.md) `l` has the universe `UU l` as
its type of `0`-cells, and uses iterated binary relations for its globular
structure.

Specifically, the universal globular type is a translation from category theory
into type theory of the Hofmannâ€“Streicher universe {{#cite Awodey22}} of
presheaves on the globular category `Î“`

```text
      sâ‚€       sâ‚       sâ‚‚
    ----->   ----->   ----->
  0 -----> 1 -----> 2 -----> â‹¯.
      tâ‚€       tâ‚       tâ‚‚
```

The Hofmannâ€“Streicher universe of presheaves on a category `ð’ž` is the presheaf

```text
     ð’°_ð’ž I := Presheaf ð’ž/I
  El_ð’ž I A := A *,
```

where `*` is the terminal object of `ð’ž/I`, i.e., the identity morphism on `I`.

We compute a few instances of the slice category `Î“/I`:

- The slice category `Î“/0` is the terminal category.
- The slice category `Î“/1` is the representing cospan

  ```text
         sâ‚€       tâ‚€
    sâ‚€ -----> 1 <----- tâ‚€
  ```

  The functors `sâ‚€ tâ‚€ : Î“/0 â†’ Î“/1` are given by `* â†¦ sâ‚€` and `* â†¦ tâ‚€`,
  respectively.

- The slice category `Î“/2` is the free category on the graph

  ```text
    sâ‚sâ‚€             tâ‚sâ‚€
     |                 |
     |                 |
     âˆ¨                 âˆ¨
    sâ‚ -----> 1 <----- tâ‚
     âˆ§                 âˆ§
     |                 |
     |                 |
    sâ‚tâ‚€             tâ‚tâ‚€
  ```

  and so on. The functors `sâ‚ tâ‚ : Î“/1 â†’ Î“/2` are given by

  ```text
    sâ‚€ â†¦ sâ‚sâ‚€                   sâ‚€ â†¦ tâ‚sâ‚€
     1 â†¦ sâ‚           and        1 â†¦ tâ‚
    tâ‚€ â†¦ sâ‚tâ‚€                   tâ‚€ â†¦ tâ‚tâ‚€
  ```

  respectively.

More specifically, the slice category `Î“/n` is isomorphic to the iterated
suspension `Î£â¿1` of the terminal category.

This means that:

- The type `0`-cells of the universal globular type is the universe of types
  `UU l`.
- The type of `1`-cells from `X` to `Y` of the universal globular type is the
  type of spans from `X` to `Y`.
- The type of `2`-cells between any two spans `R` and `S` from `X` to `Y` is the
  type of families of spans from `R x y` to `S x y` indexed by `x : X` and
  `y : Y`, and so on.

In other words, the universal globular type `ð’°` has the universe of types as its
type of `0`-cells, and for any two types `X` and `Y`, the globular type of
`1`-cells is the double
[exponent](globular-types.exponentials-globular-types.md) `(ð’°^Y)^X` of globular
types.

Unfortunately, the termination checking algorithm isn't able to establish that
this definition is terminating. Nevertheless, when termination checking is
turned off for this definition, the types of the `n`-cells come out correctly
for low values of `n`.

## Definitions

### The universal globular type

```agda
0-cell-universal-Globular-Type : (l1 l2 : Level) â†’ UU (lsuc l1)
0-cell-universal-Globular-Type l1 l2 = UU l1

{-# TERMINATING #-}

universal-Globular-Type :
  (l1 l2 : Level) â†’ Globular-Type (lsuc l1) (l1 âŠ” lsuc l2)
0-cell-Globular-Type (universal-Globular-Type l1 l2) =
  0-cell-universal-Globular-Type l1 l2
1-cell-globular-type-Globular-Type (universal-Globular-Type l1 l2) X Y =
  exponential-Globular-Type X
    ( exponential-Globular-Type Y (universal-Globular-Type l2 l2))

1-cell-universal-Globular-Type :
  {l1 l2 : Level} (X Y : UU l1) â†’ UU (l1 âŠ” lsuc l2)
1-cell-universal-Globular-Type {l1} {l2} =
  1-cell-Globular-Type (universal-Globular-Type l1 l2)

2-cell-universal-Globular-Type :
  {l1 l2 : Level} {X Y : UU l1} (R S : X â†’ Y â†’ UU l2) â†’ UU (l1 âŠ” lsuc l2)
2-cell-universal-Globular-Type {l1} {l2} {X} {Y} =
  2-cell-Globular-Type (universal-Globular-Type l1 l2)

3-cell-universal-Globular-Type :
  {l1 l2 : Level} {X Y : UU l1} {R S : X â†’ Y â†’ UU l2}
  (A B : (x : X) (y : Y) â†’ R x y â†’ S x y â†’ UU l2) â†’ UU (l1 âŠ” lsuc l2)
3-cell-universal-Globular-Type {l1} {l2} =
  3-cell-Globular-Type (universal-Globular-Type l1 l2)
```

### Dependent globular types

#### Morphisms into the universal globular type induce dependent globular types

```agda
0-cell-dependent-globular-type-hom-universal-Globular-Type :
  {l1 l2 l3 l4 : Level} (G : Globular-Type l1 l2)
  (h : globular-map G (universal-Globular-Type l3 l4)) â†’
  0-cell-Globular-Type G â†’ UU l3
0-cell-dependent-globular-type-hom-universal-Globular-Type G h =
  0-cell-globular-map h

dependent-globular-type-hom-universal-Globular-Type :
  {l1 l2 l3 l4 : Level} (G : Globular-Type l1 l2)
  (h : globular-map G (universal-Globular-Type l3 l4)) â†’
  Dependent-Globular-Type l3 l4 G
0-cell-Dependent-Globular-Type
  ( dependent-globular-type-hom-universal-Globular-Type G h) =
  0-cell-dependent-globular-type-hom-universal-Globular-Type G h
1-cell-dependent-globular-type-Dependent-Globular-Type
  ( dependent-globular-type-hom-universal-Globular-Type G h)
  {x} {x'} y y' =
  dependent-globular-type-hom-universal-Globular-Type
    ( 1-cell-globular-type-Globular-Type G x x')
    ( ev-hom-exponential-Globular-Type
      ( ev-hom-exponential-Globular-Type
        ( 1-cell-globular-map-globular-map h {x} {x'})
        ( y))
      ( y'))
```

#### Dependent globular types induce morphisms into the universal globular type

```agda
{-# TERMINATING #-}

characteristic-globular-map-Dependent-Globular-Type :
  {l1 l2 l3 l4 : Level} {G : Globular-Type l1 l2}
  (H : Dependent-Globular-Type l3 l4 G) â†’
  globular-map G (universal-Globular-Type l3 l4)
0-cell-globular-map
  ( characteristic-globular-map-Dependent-Globular-Type {G = G} H) =
  0-cell-Dependent-Globular-Type H
1-cell-globular-map-globular-map
  ( characteristic-globular-map-Dependent-Globular-Type {G = G} H) {x} {x'} =
  bind-family-globular-maps
    ( Î» y â†’
      bind-family-globular-maps
        ( Î» y' â†’
          characteristic-globular-map-Dependent-Globular-Type
            ( 1-cell-dependent-globular-type-Dependent-Globular-Type H y y')))
```

## References

{{#bibliography}}
