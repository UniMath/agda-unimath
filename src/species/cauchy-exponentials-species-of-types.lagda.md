# Cauchy exponentials of species of types

```agda
open import foundation.truncations-exist
open import foundation-core.univalence
open import foundation.function-extensionality-axiom

module species.cauchy-exponentials-species-of-types
  (funext : function-extensionality)
  (univalence : univalence-axiom)
  (truncations : truncations-exist)
  where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.arithmetic-law-coproduct-and-sigma-decompositions funext univalence truncations
open import foundation.cartesian-product-types funext univalence
open import foundation.coproduct-decompositions funext univalence truncations
open import foundation.dependent-binomial-theorem funext univalence truncations
open import foundation.dependent-pair-types
open import foundation.equivalences funext
open import foundation.functoriality-cartesian-product-types funext
open import foundation.functoriality-dependent-function-types funext univalence
open import foundation.functoriality-dependent-pair-types funext
open import foundation.homotopies funext
open import foundation.identity-types funext
open import foundation.relaxed-sigma-decompositions funext univalence
open import foundation.type-arithmetic-cartesian-product-types
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unit-type
open import foundation.univalence funext univalence
open import foundation.universe-levels

open import species.cauchy-composition-species-of-types funext univalence
open import species.cauchy-products-species-of-types funext univalence truncations
open import species.coproducts-species-of-types funext univalence truncations
open import species.equivalences-species-of-types funext univalence
open import species.species-of-types funext univalence
```

</details>

## Idea

The **Cauchy exponential** of a species of types `S` can be thought of as the
Cauchy composite `exp ‚àò S`. Since the exponent species is defined as `X ‚Ü¶ ùüô`,
the coefficients of the Cauchy exponential of `S` are defined as follows:
species of types :

```text
  X ‚Ü¶ Œ£ ((U , V , e) : Relaxed-Œ£-Decomposition X),  Œ† (u : U) ‚Üí S (V u).
```

## Definition

```agda
cauchy-exponential-species-types :
  {l1 l2 : Level} ‚Üí species-types l1 l2 ‚Üí species-types l1 (lsuc l1 ‚äî l2)
cauchy-exponential-species-types {l1} {l2} S X =
  Œ£ ( Relaxed-Œ£-Decomposition l1 l1 X)
    ( Œª D ‚Üí
      ( b : indexing-type-Relaxed-Œ£-Decomposition D) ‚Üí
      ( S (cotype-Relaxed-Œ£-Decomposition D b)))
```

## Propositions

### The Cauchy exponential in term of composition

```agda
module _
  {l1 l2 : Level}
  (S : species-types l1 l2)
  (X : UU l1)
  where

  equiv-cauchy-exponential-composition-unit-species-types :
    cauchy-composition-species-types (Œª _ ‚Üí unit) S X ‚âÉ
    cauchy-exponential-species-types S X
  equiv-cauchy-exponential-composition-unit-species-types =
    equiv-tot Œª _ ‚Üí left-unit-law-product-is-contr is-contr-unit
```

### The Cauchy exponential of the sum of a species is equivalent to the Cauchy product of the exponential of the two species

```agda
module _
  {l1 l2 l3 : Level}
  (S : species-types l1 l2)
  (T : species-types l1 l3)
  where

  private
    reassociate :
      ( X : UU l1) ‚Üí
      Œ£ ( Œ£ ( binary-coproduct-Decomposition l1 l1 X)
            ( Œª d ‚Üí
              ( Relaxed-Œ£-Decomposition l1 l1
                ( left-summand-binary-coproduct-Decomposition d)) √ó
              ( Relaxed-Œ£-Decomposition l1 l1
                ( right-summand-binary-coproduct-Decomposition d))))
        ( Œª D ‚Üí
          ( ( b : indexing-type-Relaxed-Œ£-Decomposition (pr1 (pr2 D))) ‚Üí
            S ( cotype-Relaxed-Œ£-Decomposition (pr1 (pr2 D)) b)) √ó
          ( ( b : indexing-type-Relaxed-Œ£-Decomposition (pr2 (pr2 D))) ‚Üí
            T ( cotype-Relaxed-Œ£-Decomposition (pr2 (pr2 D)) b))) ‚âÉ
      cauchy-product-species-types
        ( cauchy-exponential-species-types S)
        ( cauchy-exponential-species-types T)
        ( X)
    pr1 (reassociate X) ((d , dl , dr) , s , t) =
      ( d , (dl , s) , dr , t)
    pr2 (reassociate X) =
      is-equiv-is-invertible
        ( Œª ( d , (dl , s) , dr , t) ‚Üí ((d , dl , dr) , s , t))
        ( refl-htpy)
        ( refl-htpy)

  equiv-cauchy-exponential-sum-species-types :
    equiv-species-types
      ( cauchy-exponential-species-types (coproduct-species-types S T))
      ( cauchy-product-species-types
        ( cauchy-exponential-species-types S)
        ( cauchy-exponential-species-types T))
  equiv-cauchy-exponential-sum-species-types X =
    ( reassociate X) ‚àòe
    ( ( equiv-Œ£
        ( Œª D ‚Üí
          ( ( b : indexing-type-Relaxed-Œ£-Decomposition (pr1 (pr2 D))) ‚Üí
            S (cotype-Relaxed-Œ£-Decomposition (pr1 (pr2 D)) b)) √ó
          ( ( b : indexing-type-Relaxed-Œ£-Decomposition (pr2 (pr2 D))) ‚Üí
            T (cotype-Relaxed-Œ£-Decomposition (pr2 (pr2 D)) b)))
        ( equiv-binary-coproduct-Decomposition-Œ£-Decomposition)
        ( Œª D ‚Üí
          equiv-product
            ( equiv-Œ†-equiv-family
              ( Œª a' ‚Üí
                equiv-eq
                  ( ap S
                    ( inv
                      ( compute-left-equiv-binary-coproduct-Decomposition-Œ£-Decomposition
                        ( D)
                        ( a'))))))
            ( equiv-Œ†-equiv-family
              ( Œª b' ‚Üí
                equiv-eq
                  ( ap T
                    ( inv
                      ( compute-right-equiv-binary-coproduct-Decomposition-Œ£-Decomposition
                        ( D)
                        ( b')))))))) ‚àòe
      ( ( inv-associative-Œ£
          ( Relaxed-Œ£-Decomposition l1 l1 X)
          ( Œª d ‚Üí
            binary-coproduct-Decomposition l1 l1
              ( indexing-type-Relaxed-Œ£-Decomposition d))
              ( _)) ‚àòe
        ( equiv-tot
          ( Œª d ‚Üí distributive-Œ†-coproduct-binary-coproduct-Decomposition))))
```
