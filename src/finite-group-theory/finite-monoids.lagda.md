# Finite monoids

```agda
module finite-group-theory.finite-monoids where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import finite-group-theory.finite-semigroups

open import foundation.1-types
open import foundation.decidable-propositions
open import foundation.decidable-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.identity-types
open import foundation.mere-equivalences
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.set-truncations
open import foundation.sets
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unital-binary-operations
open import foundation.universe-levels

open import group-theory.monoids
open import group-theory.semigroups

open import univalent-combinatorics.cartesian-product-types
open import univalent-combinatorics.counting
open import univalent-combinatorics.decidable-dependent-function-types
open import univalent-combinatorics.decidable-dependent-pair-types
open import univalent-combinatorics.dependent-function-types
open import univalent-combinatorics.dependent-pair-types
open import univalent-combinatorics.equality-finite-types
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.finitely-many-connected-components
open import univalent-combinatorics.pi-finite-types
open import univalent-combinatorics.standard-finite-types
open import univalent-combinatorics.untruncated-pi-finite-types
```

</details>

## Idea

A {{#concept "finite monoid" Agda=Finite-Monoid}} is a
[monoid](group-theory.monoids.md) of which the underlying type is
[finite](univalent-combinatorics.finite-types.md).

## Definition

### The type of finite monoids

```agda
is-unital-Finite-Semigroup :
  {l : Level} â†’ Finite-Semigroup l â†’ UU l
is-unital-Finite-Semigroup G = is-unital (mul-Finite-Semigroup G)

Finite-Monoid :
  (l : Level) â†’ UU (lsuc l)
Finite-Monoid l = Î£ (Finite-Semigroup l) is-unital-Finite-Semigroup

module _
  {l : Level} (M : Finite-Monoid l)
  where

  finite-semigroup-Finite-Monoid : Finite-Semigroup l
  finite-semigroup-Finite-Monoid = pr1 M

  semigroup-Finite-Monoid : Semigroup l
  semigroup-Finite-Monoid = semigroup-Finite-Semigroup finite-semigroup-Finite-Monoid

  finite-type-Finite-Monoid : ğ”½ l
  finite-type-Finite-Monoid = finite-type-Finite-Semigroup finite-semigroup-Finite-Monoid

  type-Finite-Monoid : UU l
  type-Finite-Monoid = type-Finite-Semigroup finite-semigroup-Finite-Monoid

  is-finite-type-Finite-Monoid : is-finite type-Finite-Monoid
  is-finite-type-Finite-Monoid = is-finite-type-Finite-Semigroup finite-semigroup-Finite-Monoid

  set-Finite-Monoid : Set l
  set-Finite-Monoid = set-Semigroup semigroup-Finite-Monoid

  is-set-type-Finite-Monoid : is-set type-Finite-Monoid
  is-set-type-Finite-Monoid = is-set-type-Semigroup semigroup-Finite-Monoid

  mul-Finite-Monoid : type-Finite-Monoid â†’ type-Finite-Monoid â†’ type-Finite-Monoid
  mul-Finite-Monoid = mul-Semigroup semigroup-Finite-Monoid

  mul-Finite-Monoid' : type-Finite-Monoid â†’ type-Finite-Monoid â†’ type-Finite-Monoid
  mul-Finite-Monoid' y x = mul-Finite-Monoid x y

  ap-mul-Finite-Monoid :
    {x x' y y' : type-Finite-Monoid} â†’
    x ï¼ x' â†’ y ï¼ y' â†’ mul-Finite-Monoid x y ï¼ mul-Finite-Monoid x' y'
  ap-mul-Finite-Monoid = ap-mul-Semigroup semigroup-Finite-Monoid

  associative-mul-Finite-Monoid :
    (x y z : type-Finite-Monoid) â†’
    mul-Finite-Monoid (mul-Finite-Monoid x y) z ï¼ mul-Finite-Monoid x (mul-Finite-Monoid y z)
  associative-mul-Finite-Monoid = associative-mul-Semigroup semigroup-Finite-Monoid

  has-unit-Finite-Monoid : is-unital mul-Finite-Monoid
  has-unit-Finite-Monoid = pr2 M

  monoid-Finite-Monoid : Monoid l
  pr1 monoid-Finite-Monoid = semigroup-Finite-Monoid
  pr2 monoid-Finite-Monoid = has-unit-Finite-Monoid

  unit-Finite-Monoid : type-Finite-Monoid
  unit-Finite-Monoid = unit-Monoid monoid-Finite-Monoid

  left-unit-law-mul-Finite-Monoid :
    (x : type-Finite-Monoid) â†’ mul-Finite-Monoid unit-Finite-Monoid x ï¼ x
  left-unit-law-mul-Finite-Monoid = left-unit-law-mul-Monoid monoid-Finite-Monoid

  right-unit-law-mul-Finite-Monoid :
    (x : type-Finite-Monoid) â†’ mul-Finite-Monoid x unit-Finite-Monoid ï¼ x
  right-unit-law-mul-Finite-Monoid = right-unit-law-mul-Monoid monoid-Finite-Monoid
```

### Monoids of order `n`

```agda
Monoid-of-Order : (l : Level) (n : â„•) â†’ UU (lsuc l)
Monoid-of-Order l n = Î£ (Monoid l) (Î» M â†’ mere-equiv (Fin n) (type-Monoid M))

Monoid-of-Order' : (l : Level) (n : â„•) â†’ UU (lsuc l)
Monoid-of-Order' l n =
    Î£ (Semigroup-of-Order l n) (Î» X â†’ is-unital-Semigroup (pr1 X))

compute-Monoid-of-Order :
  {l : Level} (n : â„•) â†’ Monoid-of-Order l n â‰ƒ Monoid-of-Order' l n
compute-Monoid-of-Order n = equiv-right-swap-Î£
```

## Properties

### For any semigroup of order `n`, the type of multiplicative units is finite

```agda
is-finite-is-unital-Semigroup :
  {l : Level} (n : â„•) (X : Semigroup-of-Order l n) â†’
  is-finite (is-unital-Semigroup (pr1 X))
is-finite-is-unital-Semigroup {l} n X =
  apply-universal-property-trunc-Prop
    ( pr2 X)
    ( is-finite-Prop _)
    ( Î» e â†’
      is-finite-is-decidable-Prop
        ( is-unital-prop-Semigroup (pr1 X))
        ( is-decidable-Î£-count
          ( pair n e)
          ( Î» u â†’
            is-decidable-product
              ( is-decidable-Î -count
                ( pair n e)
                ( Î» x â†’
                  has-decidable-equality-count
                    ( pair n e)
                    ( mul-Semigroup (pr1 X) u x)
                    ( x)))
              ( is-decidable-Î -count
                ( pair n e)
                ( Î» x â†’
                  has-decidable-equality-count
                    ( pair n e)
                    ( mul-Semigroup (pr1 X) x u)
                    ( x))))))
```

### The type of monoids of order `n` is a 1-type

```agda
is-1-type-Monoid-of-Order' :
  {l : Level} (n : â„•) â†’ is-1-type (Monoid-of-Order' l n)
is-1-type-Monoid-of-Order' n =
  is-1-type-Î£
    ( is-1-type-Semigroup-of-Order n)
    ( Î» G â†’
      is-1-type-is-set (is-set-is-finite (is-finite-is-unital-Semigroup n G)))

is-1-type-Monoid-of-Order :
  {l : Level} (n : â„•) â†’ is-1-type (Monoid-of-Order l n)
is-1-type-Monoid-of-Order {l} n =
  is-1-type-equiv
    ( Monoid-of-Order' l n)
    ( compute-Monoid-of-Order n)
    ( is-1-type-Monoid-of-Order' n)
```

### The type of monoids of order `n` is Ï€â‚-finite

```agda
is-untruncated-Ï€-finite-Monoid-of-Order :
  {l : Level} (k n : â„•) â†’ is-untruncated-Ï€-finite k (Monoid-of-Order l n)
is-untruncated-Ï€-finite-Monoid-of-Order {l} k n =
  is-untruncated-Ï€-finite-equiv k
    ( compute-Monoid-of-Order n)
    ( is-untruncated-Ï€-finite-Î£ k
      ( is-untruncated-Ï€-finite-Semigroup-of-Order (succ-â„• k) n)
      ( Î» X â†’
        is-untruncated-Ï€-finite-is-finite k
          ( is-finite-is-unital-Semigroup n X)))

is-Ï€-finite-Monoid-of-Order :
  {l : Level} (n : â„•) â†’ is-Ï€-finite 1 (Monoid-of-Order l n)
is-Ï€-finite-Monoid-of-Order n =
  is-Ï€-finite-is-untruncated-Ï€-finite 1
    ( is-1-type-Monoid-of-Order n)
    ( is-untruncated-Ï€-finite-Monoid-of-Order 1 n)
```

### The number of monoids of a given order up to isomorphism

The number of monoids of order `n` is listed as
[A058129](https://oeis.org/A058129) in the [OEIS](literature.oeis.md)
{{#cite oeis}}.

```agda
number-of-monoids-of-order : â„• â†’ â„•
number-of-monoids-of-order n =
  number-of-connected-components
    ( is-untruncated-Ï€-finite-Monoid-of-Order {lzero} zero-â„• n)

mere-equiv-number-of-monoids-of-order :
  (n : â„•) â†’
  mere-equiv
    ( Fin (number-of-monoids-of-order n))
    ( type-trunc-Set (Monoid-of-Order lzero n))
mere-equiv-number-of-monoids-of-order n =
  mere-equiv-number-of-connected-components
    ( is-untruncated-Ï€-finite-Monoid-of-Order {lzero} zero-â„• n)
```

### For any finite semigroup `G`, being unital is a property

```agda
abstract
  is-prop-is-unital-Finite-Semigroup :
    {l : Level} (G : Finite-Semigroup l) â†’ is-prop (is-unital-Finite-Semigroup G)
  is-prop-is-unital-Finite-Semigroup G =
    is-prop-is-unital-Semigroup (semigroup-Finite-Semigroup G)

is-unital-Finite-Semigroup-Prop : {l : Level} (G : Finite-Semigroup l) â†’ Prop l
pr1 (is-unital-Finite-Semigroup-Prop G) = is-unital-Finite-Semigroup G
pr2 (is-unital-Finite-Semigroup-Prop G) = is-prop-is-unital-Finite-Semigroup G
```

### For any finite semigroup `G`, being unital is finite

```agda
is-finite-is-unital-Finite-Semigroup :
  {l : Level} (G : Finite-Semigroup l) â†’ is-finite (is-unital-Finite-Semigroup G)
is-finite-is-unital-Finite-Semigroup G =
  is-finite-Î£
    ( is-finite-type-Finite-Semigroup G)
    ( Î» e â†’
      is-finite-product
        ( is-finite-Î 
          ( is-finite-type-Finite-Semigroup G)
          ( Î» x â†’ is-finite-eq-ğ”½ (finite-type-Finite-Semigroup G)))
        ( is-finite-Î 
          ( is-finite-type-Finite-Semigroup G)
          ( Î» x â†’ is-finite-eq-ğ”½ (finite-type-Finite-Semigroup G))))
```

### There is a finite number of ways to equip a finite type with the structure of a monoid

```agda
structure-monoid-ğ”½ :
  {l1 : Level} â†’ ğ”½ l1 â†’ UU l1
structure-monoid-ğ”½ X =
  Î£ (structure-semigroup-ğ”½ X) (Î» p â†’ is-unital-Finite-Semigroup (X , p))

finite-monoid-structure-monoid-ğ”½ :
  {l : Level} â†’ (X : ğ”½ l) â†’ structure-monoid-ğ”½ X â†’ Finite-Monoid l
pr1 (finite-monoid-structure-monoid-ğ”½ X (a , u)) = X , a
pr2 (finite-monoid-structure-monoid-ğ”½ X (a , u)) = u

is-finite-structure-monoid-ğ”½ :
  {l : Level} â†’ (X : ğ”½ l) â†’ is-finite (structure-monoid-ğ”½ X)
is-finite-structure-monoid-ğ”½ X =
  is-finite-Î£
    ( is-finite-structure-semigroup-ğ”½ X)
    ( Î» m â†’ is-finite-is-unital-Finite-Semigroup (X , m))
```

## References

{{#bibliography}}
