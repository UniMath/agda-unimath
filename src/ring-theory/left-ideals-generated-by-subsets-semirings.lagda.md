# Left ideals generated by subsets of semirings

```agda
module ring-theory.left-ideals-generated-by-subsets-semirings where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cartesian-product-types
open import foundation.dependent-pair-types
open import foundation.fibers-of-maps
open import foundation.function-types
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.powersets
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.subtypes
open import foundation.transport-along-identifications
open import foundation.unions-subtypes
open import foundation.universe-levels

open import lists.concatenation-lists
open import lists.functoriality-lists
open import lists.lists

open import order-theory.galois-connections-large-posets
open import order-theory.least-upper-bounds-large-posets
open import order-theory.order-preserving-maps-large-posets
open import order-theory.order-preserving-maps-large-preorders
open import order-theory.reflective-galois-connections-large-posets

open import ring-theory.left-ideals-semirings
open import ring-theory.left-linear-combinations-of-elements-semirings
open import ring-theory.monoids-with-left-semiring-actions
open import ring-theory.poset-of-left-ideals-semirings
open import ring-theory.semirings
open import ring-theory.subsets-semirings
```

</details>

## Idea

The
{{#concept "left ideal generated by a subset" Disambiguation="semirings" Agda=left-ideal-subset-Semiring}}
`S` of a [semiring](ring-theory.semirings.md) `R` is the least
[left ideal](ring-theory.left-ideals-semirings.md) in `R` containing the
[subset](ring-theory.subsets-semirings.md) `S`.

## Definitions

### The universal property of left ideals generated by a subset of a semiring

```agda
module _
  {l1 l2 l3 : Level} (R : Semiring l1) (S : subset-Semiring l2 R)
  (I : left-ideal-Semiring l3 R) (H : S ⊆ subset-left-ideal-Semiring R I)
  where

  is-left-ideal-generated-by-subset-Semiring : UUω
  is-left-ideal-generated-by-subset-Semiring =
    {l : Level} (J : left-ideal-Semiring l R) →
    S ⊆ subset-left-ideal-Semiring R J →
    subset-left-ideal-Semiring R I ⊆ subset-left-ideal-Semiring R J
```

### The universal property of left ideals generated by a family of subsets of a semiring

```agda
module _
  {l1 l2 l3 l4 : Level} (R : Semiring l1) {U : UU l2}
  (S : U → subset-Semiring l3 R)
  (I : left-ideal-Semiring l4 R)
  (H : (α : U) → S α ⊆ subset-left-ideal-Semiring R I)
  where

  is-left-ideal-generated-by-family-of-subsets-Semiring : UUω
  is-left-ideal-generated-by-family-of-subsets-Semiring =
    {l : Level} (J : left-ideal-Semiring l R) →
    ((α : U) → S α ⊆ subset-left-ideal-Semiring R J) →
    leq-left-ideal-Semiring R I J
```

### The universal property of left ideals generated by a family of elements in a semiring

```agda
module _
  {l1 l2 l3 : Level} (R : Semiring l1) {U : UU l2} (a : U → type-Semiring R)
  (I : left-ideal-Semiring l3 R)
  (H : (α : U) → is-in-left-ideal-Semiring R I (a α))
  where

  is-left-ideal-generated-by-family-of-elements-Semiring : UUω
  is-left-ideal-generated-by-family-of-elements-Semiring =
    {l : Level} (J : left-ideal-Semiring l R) →
    ((α : U) → is-in-left-ideal-Semiring R J (a α)) →
    leq-left-ideal-Semiring R I J
```

### Construction of the Galois connection of left ideals generated by subsets

#### Left ideals generated by subsets

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) (S : subset-Semiring l2 R)
  where

  subset-left-ideal-subset-Semiring' : type-Semiring R → UU (l1 ⊔ l2)
  subset-left-ideal-subset-Semiring' =
    is-left-linear-combination-subset-Semiring R S

  subset-left-ideal-subset-Semiring : subset-Semiring (l1 ⊔ l2) R
  subset-left-ideal-subset-Semiring =
    is-mere-left-linear-combination-prop-subset-Semiring R S

  is-in-left-ideal-subset-Semiring : type-Semiring R → UU (l1 ⊔ l2)
  is-in-left-ideal-subset-Semiring =
    is-in-subtype subset-left-ideal-subset-Semiring

  contains-zero-left-ideal-subset-Semiring :
    contains-zero-subset-Semiring R subset-left-ideal-subset-Semiring
  contains-zero-left-ideal-subset-Semiring =
    unit-trunc-Prop (nil , refl)

  is-closed-under-addition-left-ideal-subset-Semiring :
    is-closed-under-addition-subset-Semiring R subset-left-ideal-subset-Semiring
  is-closed-under-addition-left-ideal-subset-Semiring H K =
    apply-twice-universal-property-trunc-Prop H K
      ( subset-left-ideal-subset-Semiring _)
      ( λ H' K' →
        unit-trunc-Prop
          ( is-left-linear-combination-mul-monoid-Semiring R
            ( additive-monoid-Semiring R)
            ( λ r (s , p) → mul-Semiring R r s)
            ( _)
            ( _)
            ( H')
            ( K')))

  is-closed-under-left-multiplication-left-ideal-subset-Semiring :
    is-closed-under-left-multiplication-subset-Semiring R
      ( subset-left-ideal-subset-Semiring)
  is-closed-under-left-multiplication-left-ideal-subset-Semiring r x H =
    apply-universal-property-trunc-Prop H
      ( subset-left-ideal-subset-Semiring _)
      ( λ H' →
        unit-trunc-Prop
          ( is-left-linear-combination-action-Semiring
            ( R)
            ( additive-monoid-with-left-semiring-action-Semiring R)
            ( λ t (s , H) → mul-Semiring R t s)
            ( λ s t (a , H) → associative-mul-Semiring R s t a)
            ( r)
            ( x)
            ( H')))

  left-ideal-subset-Semiring :
    left-ideal-Semiring (l1 ⊔ l2) R
  pr1 left-ideal-subset-Semiring =
    subset-left-ideal-subset-Semiring
  pr1 (pr1 (pr2 left-ideal-subset-Semiring)) =
    contains-zero-left-ideal-subset-Semiring
  pr2 (pr1 (pr2 left-ideal-subset-Semiring)) =
    is-closed-under-addition-left-ideal-subset-Semiring
  pr2 (pr2 left-ideal-subset-Semiring) =
    is-closed-under-left-multiplication-left-ideal-subset-Semiring

  contains-subset-left-ideal-subset-Semiring :
    S ⊆ subset-left-ideal-subset-Semiring
  contains-subset-left-ideal-subset-Semiring s H =
    unit-trunc-Prop
      ( ( unit-list (one-Semiring R , (s , H))) ,
        ( right-unit-law-add-Semiring R (mul-Semiring R (one-Semiring R) s)) ∙
        ( left-unit-law-mul-Semiring R s))

  contains-left-linear-combinations-left-ideal-subset-Semiring :
    {l3 : Level} (I : left-ideal-Semiring l3 R) →
    S ⊆ subset-left-ideal-Semiring R I →
    (x : left-linear-combination-subset-Semiring R S) →
    is-in-left-ideal-Semiring R I
      ( ev-left-linear-combination-subset-Semiring R S x)
  contains-left-linear-combinations-left-ideal-subset-Semiring I H nil =
    contains-zero-left-ideal-Semiring R I
  contains-left-linear-combinations-left-ideal-subset-Semiring I H
    ( cons (r , (s , K)) l) =
    is-closed-under-addition-left-ideal-Semiring R I
      ( is-closed-under-left-multiplication-left-ideal-Semiring R I r s (H s K))
      ( contains-left-linear-combinations-left-ideal-subset-Semiring I H l)

  is-left-ideal-generated-by-subset-left-ideal-subset-Semiring :
    is-left-ideal-generated-by-subset-Semiring R S
      ( left-ideal-subset-Semiring)
      ( contains-subset-left-ideal-subset-Semiring)
  is-left-ideal-generated-by-subset-left-ideal-subset-Semiring {l} J K x H =
    apply-universal-property-trunc-Prop H (subset-left-ideal-Semiring R J x) P
    where
    P : subset-left-ideal-subset-Semiring' x → is-in-left-ideal-Semiring R J x
    P (pair c refl) =
      contains-left-linear-combinations-left-ideal-subset-Semiring J K c

  is-closed-under-eq-left-ideal-subset-Semiring :
    {x y : type-Semiring R} → is-in-left-ideal-subset-Semiring x →
    (x ＝ y) → is-in-left-ideal-subset-Semiring y
  is-closed-under-eq-left-ideal-subset-Semiring =
    is-closed-under-eq-left-ideal-Semiring R left-ideal-subset-Semiring

  is-closed-under-eq-left-ideal-subset-Semiring' :
    {x y : type-Semiring R} → is-in-left-ideal-subset-Semiring y →
    (x ＝ y) → is-in-left-ideal-subset-Semiring x
  is-closed-under-eq-left-ideal-subset-Semiring' =
    is-closed-under-eq-left-ideal-Semiring' R left-ideal-subset-Semiring
```

#### The subset relation is preserved by generating left ideals

```agda
module _
  {l1 : Level} (A : Semiring l1)
  where

  preserves-order-left-ideal-subset-Semiring :
    {l2 l3 : Level} (S : subset-Semiring l2 A) (T : subset-Semiring l3 A) →
    S ⊆ T →
    leq-left-ideal-Semiring A
      ( left-ideal-subset-Semiring A S)
      ( left-ideal-subset-Semiring A T)
  preserves-order-left-ideal-subset-Semiring S T H =
    is-left-ideal-generated-by-subset-left-ideal-subset-Semiring A S
      ( left-ideal-subset-Semiring A T)
      ( transitive-leq-subtype S T
        ( subset-left-ideal-subset-Semiring A T)
        ( contains-subset-left-ideal-subset-Semiring A T)
        ( H))

  left-ideal-subset-hom-large-poset-Semiring :
    hom-Large-Poset
      ( λ l2 → l1 ⊔ l2)
      ( powerset-Large-Poset (type-Semiring A))
      ( left-ideal-Semiring-Large-Poset A)
  map-hom-Large-Preorder left-ideal-subset-hom-large-poset-Semiring =
    left-ideal-subset-Semiring A
  preserves-order-hom-Large-Preorder
    left-ideal-subset-hom-large-poset-Semiring =
    preserves-order-left-ideal-subset-Semiring
```

#### The Galois connection `S ↦ (S)`

```agda
module _
  {l1 : Level} (A : Semiring l1)
  where

  adjoint-relation-left-ideal-subset-Semiring :
    {l2 l3 : Level} (S : subset-Semiring l2 A) (I : left-ideal-Semiring l3 A) →
    leq-left-ideal-Semiring A (left-ideal-subset-Semiring A S) I ↔
    (S ⊆ subset-left-ideal-Semiring A I)
  pr1 (adjoint-relation-left-ideal-subset-Semiring S I) H =
    transitive-leq-subtype S
      ( subset-left-ideal-subset-Semiring A S)
      ( subset-left-ideal-Semiring A I)
      ( H)
      ( contains-subset-left-ideal-subset-Semiring A S)
  pr2 (adjoint-relation-left-ideal-subset-Semiring S I) =
    is-left-ideal-generated-by-subset-left-ideal-subset-Semiring A S I

  left-ideal-subset-galois-connection-Semiring :
    galois-connection-Large-Poset
      ( l1 ⊔_) (λ l → l)
      ( powerset-Large-Poset (type-Semiring A))
      ( left-ideal-Semiring-Large-Poset A)
  lower-adjoint-galois-connection-Large-Poset
    left-ideal-subset-galois-connection-Semiring =
    left-ideal-subset-hom-large-poset-Semiring A
  upper-adjoint-galois-connection-Large-Poset
    left-ideal-subset-galois-connection-Semiring =
    subset-left-ideal-hom-large-poset-Semiring A
  adjoint-relation-galois-connection-Large-Poset
    left-ideal-subset-galois-connection-Semiring =
    adjoint-relation-left-ideal-subset-Semiring
```

#### The reflective Galois connection `S ↦ (S)`

```agda
module _
  {l1 : Level} (A : Semiring l1)
  where

  forward-inclusion-is-reflective-left-ideal-subset-galois-connection-Semiring :
    {l2 : Level} (I : left-ideal-Semiring l2 A) →
    leq-left-ideal-Semiring A
      ( left-ideal-subset-Semiring A (subset-left-ideal-Semiring A I))
      ( I)
  forward-inclusion-is-reflective-left-ideal-subset-galois-connection-Semiring
    I =
    is-left-ideal-generated-by-subset-left-ideal-subset-Semiring A
      ( subset-left-ideal-Semiring A I)
      ( I)
      ( refl-leq-left-ideal-Semiring A I)

  backward-inclusion-is-reflective-left-ideal-subset-galois-connection-Semiring :
    {l2 : Level} (I : left-ideal-Semiring l2 A) →
    leq-left-ideal-Semiring A I
      ( left-ideal-subset-Semiring A (subset-left-ideal-Semiring A I))
  backward-inclusion-is-reflective-left-ideal-subset-galois-connection-Semiring
    I =
    contains-subset-left-ideal-subset-Semiring A
      ( subset-left-ideal-Semiring A I)

  is-reflective-left-ideal-subset-galois-connection-Semiring :
    is-reflective-galois-connection-Large-Poset
      ( powerset-Large-Poset (type-Semiring A))
      ( left-ideal-Semiring-Large-Poset A)
      ( left-ideal-subset-galois-connection-Semiring A)
  pr1 (is-reflective-left-ideal-subset-galois-connection-Semiring I) =
    forward-inclusion-is-reflective-left-ideal-subset-galois-connection-Semiring
      I
  pr2 (is-reflective-left-ideal-subset-galois-connection-Semiring I) =
    backward-inclusion-is-reflective-left-ideal-subset-galois-connection-Semiring
      I

  left-ideal-subset-reflective-galois-connection-Semiring :
    reflective-galois-connection-Large-Poset
      ( powerset-Large-Poset (type-Semiring A))
      ( left-ideal-Semiring-Large-Poset A)
  galois-connection-reflective-galois-connection-Large-Poset
    left-ideal-subset-reflective-galois-connection-Semiring =
    left-ideal-subset-galois-connection-Semiring A
  is-reflective-reflective-galois-connection-Large-Poset
    left-ideal-subset-reflective-galois-connection-Semiring =
    is-reflective-left-ideal-subset-galois-connection-Semiring
```

### The left ideal generated by a family of subsets of a semiring

```agda
module _
  {l1 l2 l3 : Level} (R : Semiring l1)
  {U : UU l2} (S : U → subset-Semiring l3 R)
  where

  generating-subset-left-ideal-family-of-subsets-Semiring :
    subset-Semiring (l2 ⊔ l3) R
  generating-subset-left-ideal-family-of-subsets-Semiring x =
    union-family-of-subtypes S x

  left-ideal-family-of-subsets-Semiring : left-ideal-Semiring (l1 ⊔ l2 ⊔ l3) R
  left-ideal-family-of-subsets-Semiring =
    left-ideal-subset-Semiring R
      generating-subset-left-ideal-family-of-subsets-Semiring

  subset-left-ideal-family-of-subsets-Semiring :
    subset-Semiring (l1 ⊔ l2 ⊔ l3) R
  subset-left-ideal-family-of-subsets-Semiring =
    subset-left-ideal-subset-Semiring R
      generating-subset-left-ideal-family-of-subsets-Semiring

  is-in-left-ideal-family-of-subsets-Semiring :
    type-Semiring R → UU (l1 ⊔ l2 ⊔ l3)
  is-in-left-ideal-family-of-subsets-Semiring =
    is-in-left-ideal-subset-Semiring R
      generating-subset-left-ideal-family-of-subsets-Semiring

  contains-subset-left-ideal-family-of-subsets-Semiring :
    {α : U} → (S α) ⊆ subset-left-ideal-family-of-subsets-Semiring
  contains-subset-left-ideal-family-of-subsets-Semiring {α} x H =
    contains-subset-left-ideal-subset-Semiring R
      ( generating-subset-left-ideal-family-of-subsets-Semiring)
      ( x)
      ( unit-trunc-Prop (α , H))

  is-left-ideal-generated-by-family-of-subsets-left-ideal-family-of-subsets-Semiring :
    is-left-ideal-generated-by-family-of-subsets-Semiring R S
      ( left-ideal-family-of-subsets-Semiring)
      ( λ α → contains-subset-left-ideal-family-of-subsets-Semiring)
  is-left-ideal-generated-by-family-of-subsets-left-ideal-family-of-subsets-Semiring
    J H =
    is-left-ideal-generated-by-subset-left-ideal-subset-Semiring R
      ( generating-subset-left-ideal-family-of-subsets-Semiring)
      ( J)
      ( λ y q →
        apply-universal-property-trunc-Prop q
          ( subset-left-ideal-Semiring R J y)
          ( λ (α , K) → H α y K))
```

### The left ideal generated by a family of elements in a semiring

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) {I : UU l1} (a : I → type-Semiring R)
  where

  generating-subset-left-ideal-family-of-elements-Semiring :
    subset-Semiring l1 R
  generating-subset-left-ideal-family-of-elements-Semiring x =
    trunc-Prop (fiber a x)

  left-ideal-family-of-elements-Semiring :
    left-ideal-Semiring l1 R
  left-ideal-family-of-elements-Semiring =
    left-ideal-subset-Semiring R
      generating-subset-left-ideal-family-of-elements-Semiring

  subset-left-ideal-family-of-elements-Semiring :
    subset-Semiring l1 R
  subset-left-ideal-family-of-elements-Semiring =
    subset-left-ideal-subset-Semiring R
      generating-subset-left-ideal-family-of-elements-Semiring

  is-in-left-ideal-family-of-elements-Semiring :
    type-Semiring R → UU l1
  is-in-left-ideal-family-of-elements-Semiring =
    is-in-left-ideal-subset-Semiring R
      generating-subset-left-ideal-family-of-elements-Semiring

  contains-element-left-ideal-family-of-elements-Semiring :
    (i : I) → is-in-left-ideal-family-of-elements-Semiring (a i)
  contains-element-left-ideal-family-of-elements-Semiring i =
    contains-subset-left-ideal-subset-Semiring R
      ( generating-subset-left-ideal-family-of-elements-Semiring)
      ( a i)
      ( unit-trunc-Prop (i , refl))

  abstract
    is-left-ideal-generated-by-family-of-elements-left-ideal-family-of-elements-Semiring :
      is-left-ideal-generated-by-family-of-elements-Semiring R a
        left-ideal-family-of-elements-Semiring
        contains-element-left-ideal-family-of-elements-Semiring
    is-left-ideal-generated-by-family-of-elements-left-ideal-family-of-elements-Semiring
      J H =
      is-left-ideal-generated-by-subset-left-ideal-subset-Semiring R
        ( generating-subset-left-ideal-family-of-elements-Semiring)
        ( J)
        ( λ x p →
          apply-universal-property-trunc-Prop p
            ( subset-left-ideal-Semiring R J x)
            ( λ where (i , refl) → H i))
```

## Properties

### The left ideal generated by the underlying subset of a left ideal `I` is `I` itself

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) (I : left-ideal-Semiring l2 R)
  where

  cases-forward-inclusion-idempotent-left-ideal-subset-Semiring :
    ( l :
      left-linear-combination-subset-Semiring R
        ( subset-left-ideal-Semiring R I)) →
    is-in-left-ideal-Semiring R I
      ( ev-left-linear-combination-subset-Semiring R
        ( subset-left-ideal-Semiring R I)
        ( l))
  cases-forward-inclusion-idempotent-left-ideal-subset-Semiring nil =
    contains-zero-left-ideal-Semiring R I
  cases-forward-inclusion-idempotent-left-ideal-subset-Semiring
    ( cons (x , y , u) l) =
    is-closed-under-addition-left-ideal-Semiring R I
      ( is-closed-under-left-multiplication-left-ideal-Semiring R I x y u)
      ( cases-forward-inclusion-idempotent-left-ideal-subset-Semiring l)

  abstract
    forward-inclusion-idempotent-left-ideal-subset-Semiring :
      leq-left-ideal-Semiring R
        ( left-ideal-subset-Semiring R (subset-left-ideal-Semiring R I))
        ( I)
    forward-inclusion-idempotent-left-ideal-subset-Semiring x H =
      apply-universal-property-trunc-Prop H
        ( subset-left-ideal-Semiring R I x)
        ( λ where
          ( l , refl) →
            cases-forward-inclusion-idempotent-left-ideal-subset-Semiring l)

  backward-inclusion-idempotent-left-ideal-subset-Semiring :
    leq-left-ideal-Semiring R
      ( I)
      ( left-ideal-subset-Semiring R (subset-left-ideal-Semiring R I))
  backward-inclusion-idempotent-left-ideal-subset-Semiring =
    contains-subset-left-ideal-subset-Semiring R
      ( subset-left-ideal-Semiring R I)

  idempotent-left-ideal-subset-Semiring :
    has-same-elements-left-ideal-Semiring R
      ( left-ideal-subset-Semiring R (subset-left-ideal-Semiring R I))
      ( I)
  pr1 (idempotent-left-ideal-subset-Semiring x) =
    forward-inclusion-idempotent-left-ideal-subset-Semiring x
  pr2 (idempotent-left-ideal-subset-Semiring x) =
    backward-inclusion-idempotent-left-ideal-subset-Semiring x
```

### The operation `S ↦ (S)` preserves least upper bounds

In
[`ring-theory.joins-left-ideals-semirings`](ring-theory.joins-left-ideals-semirings.md)
we will convert this fact to the fact that `S ↦ (S)` preserves joins.

```agda
module _
  {l1 l2 l3 : Level} (R : Semiring l1)
  {U : UU l2} (S : U → subset-Semiring l3 R)
  where

  preserves-least-upper-bounds-left-ideal-subset-Semiring :
    {l4 : Level} (T : subset-Semiring l4 R) →
    is-least-upper-bound-family-of-elements-Large-Poset
      ( powerset-Large-Poset (type-Semiring R))
      ( S)
      ( T) →
    is-least-upper-bound-family-of-elements-Large-Poset
      ( left-ideal-Semiring-Large-Poset R)
      ( λ α → left-ideal-subset-Semiring R (S α))
      ( left-ideal-subset-Semiring R T)
  preserves-least-upper-bounds-left-ideal-subset-Semiring =
    preserves-join-lower-adjoint-galois-connection-Large-Poset
      ( powerset-Large-Poset (type-Semiring R))
      ( left-ideal-Semiring-Large-Poset R)
      ( left-ideal-subset-galois-connection-Semiring R)
      ( S)
```
