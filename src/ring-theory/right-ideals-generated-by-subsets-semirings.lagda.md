# Right ideals generated by subsets of semirings

```agda
module ring-theory.right-ideals-generated-by-subsets-semirings where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cartesian-product-types
open import foundation.dependent-pair-types
open import foundation.fibers-of-maps
open import foundation.function-types
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.powersets
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.subtypes
open import foundation.transport-along-identifications
open import foundation.unions-subtypes
open import foundation.universe-levels

open import lists.concatenation-lists
open import lists.functoriality-lists
open import lists.lists

open import order-theory.galois-connections-large-posets
open import order-theory.least-upper-bounds-large-posets
open import order-theory.order-preserving-maps-large-posets
open import order-theory.order-preserving-maps-large-preorders
open import order-theory.reflective-galois-connections-large-posets

open import ring-theory.monoids-with-right-semiring-actions
open import ring-theory.poset-of-right-ideals-semirings
open import ring-theory.right-ideals-semirings
open import ring-theory.right-linear-combinations-of-elements-semirings
open import ring-theory.semirings
open import ring-theory.subsets-semirings
```

</details>

## Idea

The
{{#concept "right ideal generated by a subset" Disambiguation="semirings" Agda=right-ideal-subset-Semiring}}
`S` of a [semiring](ring-theory.semirings.md) `R` is the least
[right ideal](ring-theory.right-ideals-semirings.md) in `R` containing the
[subset](ring-theory.subsets-semirings.md) `S`.

## Definitions

### The universal property of right ideals generated by a subset of a semiring

```agda
module _
  {l1 l2 l3 : Level} (R : Semiring l1) (S : subset-Semiring l2 R)
  (I : right-ideal-Semiring l3 R) (H : S ⊆ subset-right-ideal-Semiring R I)
  where

  is-right-ideal-generated-by-subset-Semiring : UUω
  is-right-ideal-generated-by-subset-Semiring =
    {l : Level} (J : right-ideal-Semiring l R) →
    S ⊆ subset-right-ideal-Semiring R J →
    subset-right-ideal-Semiring R I ⊆ subset-right-ideal-Semiring R J
```

### The universal property of right ideals generated by a family of subsets of a semiring

```agda
module _
  {l1 l2 l3 l4 : Level} (R : Semiring l1) {U : UU l2}
  (S : U → subset-Semiring l3 R)
  (I : right-ideal-Semiring l4 R)
  (H : (α : U) → S α ⊆ subset-right-ideal-Semiring R I)
  where

  is-right-ideal-generated-by-family-of-subsets-Semiring : UUω
  is-right-ideal-generated-by-family-of-subsets-Semiring =
    {l : Level} (J : right-ideal-Semiring l R) →
    ((α : U) → S α ⊆ subset-right-ideal-Semiring R J) →
    leq-right-ideal-Semiring R I J
```

### The universal property of right ideals generated by a family of elements in a semiring

```agda
module _
  {l1 l2 l3 : Level} (R : Semiring l1) {U : UU l2} (a : U → type-Semiring R)
  (I : right-ideal-Semiring l3 R)
  (H : (α : U) → is-in-right-ideal-Semiring R I (a α))
  where

  is-right-ideal-generated-by-family-of-elements-Semiring : UUω
  is-right-ideal-generated-by-family-of-elements-Semiring =
    {l : Level} (J : right-ideal-Semiring l R) →
    ((α : U) → is-in-right-ideal-Semiring R J (a α)) →
    leq-right-ideal-Semiring R I J
```

### Construction of the Galois connection of right ideals generated by subsets

#### Right ideals generated by subsets

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) (S : subset-Semiring l2 R)
  where

  subset-right-ideal-subset-Semiring' : type-Semiring R → UU (l1 ⊔ l2)
  subset-right-ideal-subset-Semiring' =
    is-right-linear-combination-subset-Semiring R S

  subset-right-ideal-subset-Semiring : subset-Semiring (l1 ⊔ l2) R
  subset-right-ideal-subset-Semiring =
    is-mere-right-linear-combination-prop-subset-Semiring R S

  is-in-right-ideal-subset-Semiring : type-Semiring R → UU (l1 ⊔ l2)
  is-in-right-ideal-subset-Semiring =
    is-in-subtype subset-right-ideal-subset-Semiring

  contains-zero-right-ideal-subset-Semiring :
    contains-zero-subset-Semiring R subset-right-ideal-subset-Semiring
  contains-zero-right-ideal-subset-Semiring =
    unit-trunc-Prop (nil , refl)

  is-closed-under-addition-right-ideal-subset-Semiring :
    is-closed-under-addition-subset-Semiring R
      ( subset-right-ideal-subset-Semiring)
  is-closed-under-addition-right-ideal-subset-Semiring H K =
    apply-twice-universal-property-trunc-Prop H K
      ( subset-right-ideal-subset-Semiring _)
      ( λ H' K' →
        unit-trunc-Prop
          ( is-right-linear-combination-mul-monoid-Semiring R
            ( additive-monoid-Semiring R)
            ( λ (s , p) r → mul-Semiring R s r)
            ( _)
            ( _)
            ( H')
            ( K')))

  is-closed-under-right-multiplication-right-ideal-subset-Semiring :
    is-closed-under-right-multiplication-subset-Semiring R
      ( subset-right-ideal-subset-Semiring)
  is-closed-under-right-multiplication-right-ideal-subset-Semiring r x H =
    apply-universal-property-trunc-Prop H
      ( subset-right-ideal-subset-Semiring _)
      ( λ H' →
        unit-trunc-Prop
          ( is-right-linear-combination-action-Semiring
            ( R)
            ( additive-monoid-with-right-semiring-action-Semiring R)
            ( λ (s , H) t → mul-Semiring R s t)
            ( λ (a , H) t s → inv (associative-mul-Semiring R a t s))
            ( r)
            ( x)
            ( H')))

  right-ideal-subset-Semiring :
    right-ideal-Semiring (l1 ⊔ l2) R
  pr1 right-ideal-subset-Semiring =
    subset-right-ideal-subset-Semiring
  pr1 (pr1 (pr2 right-ideal-subset-Semiring)) =
    contains-zero-right-ideal-subset-Semiring
  pr2 (pr1 (pr2 right-ideal-subset-Semiring)) =
    is-closed-under-addition-right-ideal-subset-Semiring
  pr2 (pr2 right-ideal-subset-Semiring) =
    is-closed-under-right-multiplication-right-ideal-subset-Semiring

  contains-subset-right-ideal-subset-Semiring :
    S ⊆ subset-right-ideal-subset-Semiring
  contains-subset-right-ideal-subset-Semiring s H =
    unit-trunc-Prop
      ( ( unit-list ((s , H) , one-Semiring R)) ,
        ( right-unit-law-add-Semiring R (mul-Semiring R s (one-Semiring R))) ∙
        ( right-unit-law-mul-Semiring R s))

  contains-right-linear-combinations-right-ideal-subset-Semiring :
    {l3 : Level} (I : right-ideal-Semiring l3 R) →
    S ⊆ subset-right-ideal-Semiring R I →
    (x : right-linear-combination-subset-Semiring R S) →
    is-in-right-ideal-Semiring R I
      ( ev-right-linear-combination-subset-Semiring R S x)
  contains-right-linear-combinations-right-ideal-subset-Semiring I H nil =
    contains-zero-right-ideal-Semiring R I
  contains-right-linear-combinations-right-ideal-subset-Semiring I H
    ( cons ((s , K) , r) l) =
    is-closed-under-addition-right-ideal-Semiring R I
      ( is-closed-under-right-multiplication-right-ideal-Semiring R I s r
        ( H s K))
      ( contains-right-linear-combinations-right-ideal-subset-Semiring I H l)

  is-right-ideal-generated-by-subset-right-ideal-subset-Semiring :
    is-right-ideal-generated-by-subset-Semiring R S
      ( right-ideal-subset-Semiring)
      ( contains-subset-right-ideal-subset-Semiring)
  is-right-ideal-generated-by-subset-right-ideal-subset-Semiring {l} J K x H =
    apply-universal-property-trunc-Prop H (subset-right-ideal-Semiring R J x) P
    where
    P : subset-right-ideal-subset-Semiring' x → is-in-right-ideal-Semiring R J x
    P (pair c refl) =
      contains-right-linear-combinations-right-ideal-subset-Semiring J K c

  is-closed-under-eq-right-ideal-subset-Semiring :
    {x y : type-Semiring R} → is-in-right-ideal-subset-Semiring x →
    (x ＝ y) → is-in-right-ideal-subset-Semiring y
  is-closed-under-eq-right-ideal-subset-Semiring =
    is-closed-under-eq-right-ideal-Semiring R right-ideal-subset-Semiring

  is-closed-under-eq-right-ideal-subset-Semiring' :
    {x y : type-Semiring R} → is-in-right-ideal-subset-Semiring y →
    (x ＝ y) → is-in-right-ideal-subset-Semiring x
  is-closed-under-eq-right-ideal-subset-Semiring' =
    is-closed-under-eq-right-ideal-Semiring' R right-ideal-subset-Semiring
```

#### The subset relation is preserved by generating right ideals

```agda
module _
  {l1 : Level} (A : Semiring l1)
  where

  preserves-order-right-ideal-subset-Semiring :
    {l2 l3 : Level} (S : subset-Semiring l2 A) (T : subset-Semiring l3 A) →
    S ⊆ T →
    leq-right-ideal-Semiring A
      ( right-ideal-subset-Semiring A S)
      ( right-ideal-subset-Semiring A T)
  preserves-order-right-ideal-subset-Semiring S T H =
    is-right-ideal-generated-by-subset-right-ideal-subset-Semiring A S
      ( right-ideal-subset-Semiring A T)
      ( transitive-leq-subtype S T
        ( subset-right-ideal-subset-Semiring A T)
        ( contains-subset-right-ideal-subset-Semiring A T)
        ( H))

  right-ideal-subset-hom-large-poset-Semiring :
    hom-Large-Poset
      ( λ l2 → l1 ⊔ l2)
      ( powerset-Large-Poset (type-Semiring A))
      ( right-ideal-Semiring-Large-Poset A)
  map-hom-Large-Preorder right-ideal-subset-hom-large-poset-Semiring =
    right-ideal-subset-Semiring A
  preserves-order-hom-Large-Preorder
    right-ideal-subset-hom-large-poset-Semiring =
    preserves-order-right-ideal-subset-Semiring
```

#### The Galois connection `S ↦ (S)`

```agda
module _
  {l1 : Level} (A : Semiring l1)
  where

  adjoint-relation-right-ideal-subset-Semiring :
    {l2 l3 : Level} (S : subset-Semiring l2 A) (I : right-ideal-Semiring l3 A) →
    leq-right-ideal-Semiring A (right-ideal-subset-Semiring A S) I ↔
    (S ⊆ subset-right-ideal-Semiring A I)
  pr1 (adjoint-relation-right-ideal-subset-Semiring S I) H =
    transitive-leq-subtype S
      ( subset-right-ideal-subset-Semiring A S)
      ( subset-right-ideal-Semiring A I)
      ( H)
      ( contains-subset-right-ideal-subset-Semiring A S)
  pr2 (adjoint-relation-right-ideal-subset-Semiring S I) =
    is-right-ideal-generated-by-subset-right-ideal-subset-Semiring A S I

  right-ideal-subset-galois-connection-Semiring :
    galois-connection-Large-Poset
      ( l1 ⊔_) (λ l → l)
      ( powerset-Large-Poset (type-Semiring A))
      ( right-ideal-Semiring-Large-Poset A)
  lower-adjoint-galois-connection-Large-Poset
    right-ideal-subset-galois-connection-Semiring =
    right-ideal-subset-hom-large-poset-Semiring A
  upper-adjoint-galois-connection-Large-Poset
    right-ideal-subset-galois-connection-Semiring =
    subset-right-ideal-hom-large-poset-Semiring A
  adjoint-relation-galois-connection-Large-Poset
    right-ideal-subset-galois-connection-Semiring =
    adjoint-relation-right-ideal-subset-Semiring
```

#### The reflective Galois connection `S ↦ (S)`

```agda
module _
  {l1 : Level} (A : Semiring l1)
  where

  forward-inclusion-is-reflective-right-ideal-subset-galois-connection-Semiring :
    {l2 : Level} (I : right-ideal-Semiring l2 A) →
    leq-right-ideal-Semiring A
      ( right-ideal-subset-Semiring A (subset-right-ideal-Semiring A I))
      ( I)
  forward-inclusion-is-reflective-right-ideal-subset-galois-connection-Semiring
    I =
    is-right-ideal-generated-by-subset-right-ideal-subset-Semiring A
      ( subset-right-ideal-Semiring A I)
      ( I)
      ( refl-leq-right-ideal-Semiring A I)

  backward-inclusion-is-reflective-right-ideal-subset-galois-connection-Semiring :
    {l2 : Level} (I : right-ideal-Semiring l2 A) →
    leq-right-ideal-Semiring A I
      ( right-ideal-subset-Semiring A (subset-right-ideal-Semiring A I))
  backward-inclusion-is-reflective-right-ideal-subset-galois-connection-Semiring
    I =
    contains-subset-right-ideal-subset-Semiring A
      ( subset-right-ideal-Semiring A I)

  is-reflective-right-ideal-subset-galois-connection-Semiring :
    is-reflective-galois-connection-Large-Poset
      ( powerset-Large-Poset (type-Semiring A))
      ( right-ideal-Semiring-Large-Poset A)
      ( right-ideal-subset-galois-connection-Semiring A)
  pr1 (is-reflective-right-ideal-subset-galois-connection-Semiring I) =
    forward-inclusion-is-reflective-right-ideal-subset-galois-connection-Semiring
      I
  pr2 (is-reflective-right-ideal-subset-galois-connection-Semiring I) =
    backward-inclusion-is-reflective-right-ideal-subset-galois-connection-Semiring
      I

  right-ideal-subset-reflective-galois-connection-Semiring :
    reflective-galois-connection-Large-Poset
      ( powerset-Large-Poset (type-Semiring A))
      ( right-ideal-Semiring-Large-Poset A)
  galois-connection-reflective-galois-connection-Large-Poset
    right-ideal-subset-reflective-galois-connection-Semiring =
    right-ideal-subset-galois-connection-Semiring A
  is-reflective-reflective-galois-connection-Large-Poset
    right-ideal-subset-reflective-galois-connection-Semiring =
    is-reflective-right-ideal-subset-galois-connection-Semiring
```

### The right ideal generated by a family of subsets of a semiring

```agda
module _
  {l1 l2 l3 : Level} (R : Semiring l1)
  {U : UU l2} (S : U → subset-Semiring l3 R)
  where

  generating-subset-right-ideal-family-of-subsets-Semiring :
    subset-Semiring (l2 ⊔ l3) R
  generating-subset-right-ideal-family-of-subsets-Semiring x =
    union-family-of-subtypes S x

  right-ideal-family-of-subsets-Semiring : right-ideal-Semiring (l1 ⊔ l2 ⊔ l3) R
  right-ideal-family-of-subsets-Semiring =
    right-ideal-subset-Semiring R
      generating-subset-right-ideal-family-of-subsets-Semiring

  subset-right-ideal-family-of-subsets-Semiring :
    subset-Semiring (l1 ⊔ l2 ⊔ l3) R
  subset-right-ideal-family-of-subsets-Semiring =
    subset-right-ideal-subset-Semiring R
      generating-subset-right-ideal-family-of-subsets-Semiring

  is-in-right-ideal-family-of-subsets-Semiring :
    type-Semiring R → UU (l1 ⊔ l2 ⊔ l3)
  is-in-right-ideal-family-of-subsets-Semiring =
    is-in-right-ideal-subset-Semiring R
      generating-subset-right-ideal-family-of-subsets-Semiring

  contains-subset-right-ideal-family-of-subsets-Semiring :
    {α : U} → (S α) ⊆ subset-right-ideal-family-of-subsets-Semiring
  contains-subset-right-ideal-family-of-subsets-Semiring {α} x H =
    contains-subset-right-ideal-subset-Semiring R
      ( generating-subset-right-ideal-family-of-subsets-Semiring)
      ( x)
      ( unit-trunc-Prop (α , H))

  is-right-ideal-generated-by-family-of-subsets-right-ideal-family-of-subsets-Semiring :
    is-right-ideal-generated-by-family-of-subsets-Semiring R S
      ( right-ideal-family-of-subsets-Semiring)
      ( λ α → contains-subset-right-ideal-family-of-subsets-Semiring)
  is-right-ideal-generated-by-family-of-subsets-right-ideal-family-of-subsets-Semiring
    J H =
    is-right-ideal-generated-by-subset-right-ideal-subset-Semiring R
      ( generating-subset-right-ideal-family-of-subsets-Semiring)
      ( J)
      ( λ y q →
        apply-universal-property-trunc-Prop q
          ( subset-right-ideal-Semiring R J y)
          ( λ (α , K) → H α y K))
```

### The right ideal generated by a family of elements in a semiring

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) {I : UU l1} (a : I → type-Semiring R)
  where

  generating-subset-right-ideal-family-of-elements-Semiring :
    subset-Semiring l1 R
  generating-subset-right-ideal-family-of-elements-Semiring x =
    trunc-Prop (fiber a x)

  right-ideal-family-of-elements-Semiring :
    right-ideal-Semiring l1 R
  right-ideal-family-of-elements-Semiring =
    right-ideal-subset-Semiring R
      generating-subset-right-ideal-family-of-elements-Semiring

  subset-right-ideal-family-of-elements-Semiring :
    subset-Semiring l1 R
  subset-right-ideal-family-of-elements-Semiring =
    subset-right-ideal-subset-Semiring R
      generating-subset-right-ideal-family-of-elements-Semiring

  is-in-right-ideal-family-of-elements-Semiring :
    type-Semiring R → UU l1
  is-in-right-ideal-family-of-elements-Semiring =
    is-in-right-ideal-subset-Semiring R
      generating-subset-right-ideal-family-of-elements-Semiring

  contains-element-right-ideal-family-of-elements-Semiring :
    (i : I) → is-in-right-ideal-family-of-elements-Semiring (a i)
  contains-element-right-ideal-family-of-elements-Semiring i =
    contains-subset-right-ideal-subset-Semiring R
      ( generating-subset-right-ideal-family-of-elements-Semiring)
      ( a i)
      ( unit-trunc-Prop (i , refl))

  abstract
    is-right-ideal-generated-by-family-of-elements-right-ideal-family-of-elements-Semiring :
      is-right-ideal-generated-by-family-of-elements-Semiring R a
        right-ideal-family-of-elements-Semiring
        contains-element-right-ideal-family-of-elements-Semiring
    is-right-ideal-generated-by-family-of-elements-right-ideal-family-of-elements-Semiring
      J H =
      is-right-ideal-generated-by-subset-right-ideal-subset-Semiring R
        ( generating-subset-right-ideal-family-of-elements-Semiring)
        ( J)
        ( λ x p →
          apply-universal-property-trunc-Prop p
            ( subset-right-ideal-Semiring R J x)
            ( λ where (i , refl) → H i))
```

## Properties

### The right ideal generated by the underlying subset of a right ideal `I` is `I` itself

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) (I : right-ideal-Semiring l2 R)
  where

  cases-forward-inclusion-idempotent-right-ideal-subset-Semiring :
    ( l :
      right-linear-combination-subset-Semiring R
        ( subset-right-ideal-Semiring R I)) →
    is-in-right-ideal-Semiring R I
      ( ev-right-linear-combination-subset-Semiring R
        ( subset-right-ideal-Semiring R I)
        ( l))
  cases-forward-inclusion-idempotent-right-ideal-subset-Semiring nil =
    contains-zero-right-ideal-Semiring R I
  cases-forward-inclusion-idempotent-right-ideal-subset-Semiring
    ( cons ((y , u) , x) l) =
    is-closed-under-addition-right-ideal-Semiring R I
      ( is-closed-under-right-multiplication-right-ideal-Semiring R I y x u)
      ( cases-forward-inclusion-idempotent-right-ideal-subset-Semiring l)

  abstract
    forward-inclusion-idempotent-right-ideal-subset-Semiring :
      leq-right-ideal-Semiring R
        ( right-ideal-subset-Semiring R (subset-right-ideal-Semiring R I))
        ( I)
    forward-inclusion-idempotent-right-ideal-subset-Semiring x H =
      apply-universal-property-trunc-Prop H
        ( subset-right-ideal-Semiring R I x)
        ( λ where
          ( l , refl) →
            cases-forward-inclusion-idempotent-right-ideal-subset-Semiring l)

  backward-inclusion-idempotent-right-ideal-subset-Semiring :
    leq-right-ideal-Semiring R
      ( I)
      ( right-ideal-subset-Semiring R (subset-right-ideal-Semiring R I))
  backward-inclusion-idempotent-right-ideal-subset-Semiring =
    contains-subset-right-ideal-subset-Semiring R
      ( subset-right-ideal-Semiring R I)

  idempotent-right-ideal-subset-Semiring :
    has-same-elements-right-ideal-Semiring R
      ( right-ideal-subset-Semiring R (subset-right-ideal-Semiring R I))
      ( I)
  pr1 (idempotent-right-ideal-subset-Semiring x) =
    forward-inclusion-idempotent-right-ideal-subset-Semiring x
  pr2 (idempotent-right-ideal-subset-Semiring x) =
    backward-inclusion-idempotent-right-ideal-subset-Semiring x
```

### The operation `S ↦ (S)` preserves least upper bounds

In
[`ring-theory.joins-right-ideals-semirings`](ring-theory.joins-right-ideals-semirings.md)
we will convert this fact to the fact that `S ↦ (S)` preserves joins.

```agda
module _
  {l1 l2 l3 : Level} (R : Semiring l1)
  {U : UU l2} (S : U → subset-Semiring l3 R)
  where

  preserves-least-upper-bounds-right-ideal-subset-Semiring :
    {l4 : Level} (T : subset-Semiring l4 R) →
    is-least-upper-bound-family-of-elements-Large-Poset
      ( powerset-Large-Poset (type-Semiring R))
      ( S)
      ( T) →
    is-least-upper-bound-family-of-elements-Large-Poset
      ( right-ideal-Semiring-Large-Poset R)
      ( λ α → right-ideal-subset-Semiring R (S α))
      ( right-ideal-subset-Semiring R T)
  preserves-least-upper-bounds-right-ideal-subset-Semiring =
    preserves-join-lower-adjoint-galois-connection-Large-Poset
      ( powerset-Large-Poset (type-Semiring R))
      ( right-ideal-Semiring-Large-Poset R)
      ( right-ideal-subset-galois-connection-Semiring R)
      ( S)
```
