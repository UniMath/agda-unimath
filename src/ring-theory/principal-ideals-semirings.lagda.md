# Principal ideals of semirings

```agda
module ring-theory.principal-ideals-semirings where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.existential-quantification
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.sets
open import foundation.universe-levels

open import ring-theory.ideals-semirings
open import ring-theory.semirings
open import ring-theory.subsets-semirings
```

</details>

## Idea

An [ideal](ring-theory.semirings.md) $I$ of a [semiring](ring-theory.semirings.md) $R$ is said to be a {{#concept "principal ideal" Disambiguation="semiring" Agda=is-principal-ideal-Semiring}} if there exists an element $a \in I$ so that every element $I$ is a linear combination of $a$s {{#cite golan1999}}. In other words, the ideal $I$ is principal if there exists an element $a$ such that for every element $b$ the [logical equivalence](foundation.logical-equivalences.md)

$$
  b ∈ I \Leftrightarrow \exists_{(x_1,y_1),\ldots,(x_n,y_n):R^2)} x_1 ay_1+\cdots x_naby_n = b.
$$

holds.

## Definitions

### The predicate of being a principal left ideal of a semiring

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) (I : left-ideal-Semiring l2 R)
  where

  is-principal-left-ideal-Semiring : UU (l1 ⊔ l2)
  is-principal-left-ideal-Semiring =
    exists
      ( type-Semiring R)
      ( λ a →
        Π-Prop
          ( type-Semiring R)
          ( λ b →
            iff-Prop
              ( subset-left-ideal-Semiring R I b)
              ( ∃ ( type-Semiring R)
                  ( λ x →
                    Id-Prop
                      ( set-Semiring R)
                      ( mul-Semiring R x a)
                      ( b)))))
```

### The predicate of being a principal right ideal of a semiring

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) (I : right-ideal-Semiring l2 R)
  where

  is-principal-right-ideal-Semiring : UU (l1 ⊔ l2)
  is-principal-right-ideal-Semiring =
    exists
      ( type-Semiring R)
      ( λ a →
        Π-Prop
          ( type-Semiring R)
          ( λ b →
            iff-Prop
              ( subset-right-ideal-Semiring R I b)
              ( ∃ ( type-Semiring R)
                  ( λ y →
                    Id-Prop
                      ( set-Semiring R)
                      ( mul-Semiring R a y)
                      ( b)))))
```

### The predicate of being a principal ideal of a semiring

```agda
module _
  {l1 l2 : Level} (R : Semiring l1) (I : ideal-Semiring l2 R)
  where

  is-principal-ideal-Semiring : UU (l1 ⊔ l2)
  is-principal-ideal-Semiring =
    exists
      ( type-Semiring R)
      ( λ a →
        Π-Prop
          ( type-Semiring R)
          ( λ b →
            iff-Prop
              ( subset-ideal-Semiring R I b)
              ( ∃ ( type-Semiring R)
                  ( λ x →
                    ∃ ( type-Semiring R)
                      ( λ y → 
                        Id-Prop
                          ( set-Semiring R)
                          ( mul-Semiring R (mul-Semiring R x a) y)
                          ( b))))))
```

### The principal left ideal generated by an element

The principal left ideal generated by an element $a$ consists of all element of the form $xa$.

```agda
module _
  {l1 : Level} (R : Semiring l1) (a : type-Semiring R)
  where

  subset-principal-left-ideal-Semiring : subset-Semiring l1 R
  subset-principal-left-ideal-Semiring b =
    ∃ ( type-Semiring R)
      ( λ x → Id-Prop (set-Semiring R) (mul-Semiring R x a) b)

  is-in-principal-left-ideal-Semiring : type-Semiring R → UU l1
  is-in-principal-left-ideal-Semiring =
    is-in-subset-Semiring R subset-principal-left-ideal-Semiring

  contains-zero-principal-left-ideal-Semiring :
    is-in-principal-left-ideal-Semiring (zero-Semiring R)
  contains-zero-principal-left-ideal-Semiring =
    intro-exists (zero-Semiring R) (left-zero-law-mul-Semiring R a)

  is-closed-under-addition-principal-left-ideal-Semiring :
    is-closed-under-addition-subset-Semiring R
      subset-principal-left-ideal-Semiring
  is-closed-under-addition-principal-left-ideal-Semiring x y H K =
    apply-twice-universal-property-trunc-Prop H K
      ( subset-principal-left-ideal-Semiring (add-Semiring R x y))
      ( λ (u , p) (v , q) →
        intro-exists
          ( add-Semiring R u v)
          ( right-distributive-mul-add-Semiring R u v a ∙
            ap-add-Semiring R p q))

  is-additive-submonoid-principal-left-ideal-Semiring :
    is-additive-submonoid-Semiring R subset-principal-left-ideal-Semiring
  pr1 is-additive-submonoid-principal-left-ideal-Semiring =
    contains-zero-principal-left-ideal-Semiring
  pr2 is-additive-submonoid-principal-left-ideal-Semiring =
    is-closed-under-addition-principal-left-ideal-Semiring

  is-closed-under-left-multiplication-principal-left-ideal-Semiring :
    is-closed-under-left-multiplication-subset-Semiring R
      subset-principal-left-ideal-Semiring
  is-closed-under-left-multiplication-principal-left-ideal-Semiring x y H =
    apply-universal-property-trunc-Prop H
      ( subset-principal-left-ideal-Semiring (mul-Semiring R x y))
      ( λ (u , p) →
        intro-exists
          ( mul-Semiring R x u)
          ( associative-mul-Semiring R x u a ∙ ap (mul-Semiring R x) p))

  is-left-ideal-principal-left-ideal-Semiring :
    is-left-ideal-subset-Semiring R subset-principal-left-ideal-Semiring
  pr1 is-left-ideal-principal-left-ideal-Semiring =
    is-additive-submonoid-principal-left-ideal-Semiring
  pr2 is-left-ideal-principal-left-ideal-Semiring =
    is-closed-under-left-multiplication-principal-left-ideal-Semiring

  principal-left-ideal-Semiring :
    left-ideal-Semiring l1 R
  pr1 principal-left-ideal-Semiring =
    subset-principal-left-ideal-Semiring
  pr2 principal-left-ideal-Semiring =
    is-left-ideal-principal-left-ideal-Semiring

  is-principal-principal-left-ideal-Semiring :
    is-principal-left-ideal-Semiring R principal-left-ideal-Semiring
  is-principal-principal-left-ideal-Semiring =
    intro-exists a (λ b → id-iff)
```

### The principal right ideal generated by an element

```agda
module _
  {l1 : Level} (R : Semiring l1) (a : type-Semiring R)
  where

  subset-principal-right-ideal-Semiring : subset-Semiring l1 R
  subset-principal-right-ideal-Semiring b =
    ∃ ( type-Semiring R)
      ( λ x → Id-Prop (set-Semiring R) (mul-Semiring R a x) b)

  is-in-principal-right-ideal-Semiring : type-Semiring R → UU l1
  is-in-principal-right-ideal-Semiring =
    is-in-subset-Semiring R subset-principal-right-ideal-Semiring

  contains-zero-principal-right-ideal-Semiring :
    is-in-principal-right-ideal-Semiring (zero-Semiring R)
  contains-zero-principal-right-ideal-Semiring =
    intro-exists (zero-Semiring R) (right-zero-law-mul-Semiring R a)

  is-closed-under-addition-principal-right-ideal-Semiring :
    is-closed-under-addition-subset-Semiring R
      subset-principal-right-ideal-Semiring
  is-closed-under-addition-principal-right-ideal-Semiring x y H K =
    apply-twice-universal-property-trunc-Prop H K
      ( subset-principal-right-ideal-Semiring (add-Semiring R x y))
      ( λ (u , p) (v , q) →
        intro-exists
          ( add-Semiring R u v)
          ( left-distributive-mul-add-Semiring R a u v ∙
            ap-add-Semiring R p q))

  is-additive-submonoid-principal-right-ideal-Semiring :
    is-additive-submonoid-Semiring R subset-principal-right-ideal-Semiring
  pr1 is-additive-submonoid-principal-right-ideal-Semiring =
    contains-zero-principal-right-ideal-Semiring
  pr2 is-additive-submonoid-principal-right-ideal-Semiring =
    is-closed-under-addition-principal-right-ideal-Semiring

  is-closed-under-right-multiplication-principal-right-ideal-Semiring :
    is-closed-under-right-multiplication-subset-Semiring R
      subset-principal-right-ideal-Semiring
  is-closed-under-right-multiplication-principal-right-ideal-Semiring x y H =
    apply-universal-property-trunc-Prop H
      ( subset-principal-right-ideal-Semiring (mul-Semiring R x y))
      ( λ (u , p) →
        intro-exists
          ( mul-Semiring R u y)
          ( inv (associative-mul-Semiring R a u y) ∙ ap (mul-Semiring' R y) p))

  is-right-ideal-principal-right-ideal-Semiring :
    is-right-ideal-subset-Semiring R subset-principal-right-ideal-Semiring
  pr1 is-right-ideal-principal-right-ideal-Semiring =
    is-additive-submonoid-principal-right-ideal-Semiring
  pr2 is-right-ideal-principal-right-ideal-Semiring =
    is-closed-under-right-multiplication-principal-right-ideal-Semiring

  principal-right-ideal-Semiring :
    right-ideal-Semiring l1 R
  pr1 principal-right-ideal-Semiring =
    subset-principal-right-ideal-Semiring
  pr2 principal-right-ideal-Semiring =
    is-right-ideal-principal-right-ideal-Semiring

  is-principal-principal-right-ideal-Semiring :
    is-principal-right-ideal-Semiring R principal-right-ideal-Semiring
  is-principal-principal-right-ideal-Semiring =
    intro-exists a (λ b → id-iff)
```

### The principal ideal generated by an element

```agda
module _
  {l1 : Level} (R : Semiring l1) (a : type-Semiring R)
  where

  subset-principal-ideal-Semiring : subset-Semiring l1 R
  subset-principal-ideal-Semiring b =
    ∃ ( type-Semiring R)
      ( λ x →
        ∃ ( type-Semiring R)
          ( λ y →
            Id-Prop (set-Semiring R) (mul-Semiring R (mul-Semiring R x a) y) b))

  is-in-principal-ideal-Semiring : type-Semiring R → UU l1
  is-in-principal-ideal-Semiring =
    is-in-subset-Semiring R subset-principal-ideal-Semiring

  contains-zero-principal-ideal-Semiring :
    is-in-principal-ideal-Semiring (zero-Semiring R)
  contains-zero-principal-ideal-Semiring =
    intro-exists
      ( zero-Semiring R)
      ( intro-exists
        ( zero-Semiring R)
        ( right-zero-law-mul-Semiring R (mul-Semiring R (zero-Semiring R) a)))

  is-closed-under-addition-principal-ideal-Semiring :
    is-closed-under-addition-subset-Semiring R
      subset-principal-ideal-Semiring
  is-closed-under-addition-principal-ideal-Semiring x y H K =
    apply-twice-universal-property-trunc-Prop H K
      ( subset-principal-ideal-Semiring (add-Semiring R x y))
      ( λ (u , H') (v , K') →
        apply-twice-universal-property-trunc-Prop H' K'
          ( subset-principal-ideal-Semiring (add-Semiring R x y))
          ( λ (s , p) (t , q) →
            {!!}))

```

## References

{{#bibliography}}
