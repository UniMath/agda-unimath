---
title: Ring homomorphisms
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module ring-theory.homomorphisms-rings where

open import foundation.contractible-types using (is-contr)
open import foundation.cartesian-product-types using (_√ó_)
open import foundation.dependent-pair-types using (Œ£; pair; pr1; pr2)
open import foundation.equivalences using (is-equiv; _‚âÉ_; map-inv-is-equiv)
open import foundation.fundamental-theorem-of-identity-types using
  ( fundamental-theorem-id)
open import foundation.homotopies using (_~_; refl-htpy)
open import foundation.identity-types using (Id; refl; ap; _‚àô_)
open import foundation.propositions using
  ( is-prop; is-prop-Œ†; is-prop-prod; UU-Prop)
open import foundation.sets using (is-set; UU-Set)
open import foundation.subtype-identity-principle using
  ( is-contr-total-Eq-subtype)
open import foundation.subtypes using (is-trunc-type-subtype)
open import foundation.truncation-levels using (ùïã; neg-one-ùïã)
open import foundation.universe-levels using (Level; UU; _‚äî_)

open import group-theory.homomorphisms-abelian-groups using
  ( type-hom-Ab; map-hom-Ab; preserves-add-Ab; preserves-add-hom-Ab;
    is-contr-total-htpy-hom-Ab; is-set-hom-Ab; id-hom-Ab; comp-hom-Ab;
    eq-htpy-hom-Ab; preserves-zero-Ab; preserves-zero-hom-Ab;
    preserves-negatives-Ab; preserves-negatives-hom-Ab)

open import ring-theory.rings using
  ( Ring; ab-Ring; type-Ring; mul-Ring; is-set-type-Ring; one-Ring)
```

## Idea

Ring homomorphisms are maps between rings that preserve the ring structure

## Definition

```agda
{- Ring homomorphisms -}

preserves-mul-hom-Ab :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  type-hom-Ab (ab-Ring R1) (ab-Ring R2) ‚Üí UU (l1 ‚äî l2)
preserves-mul-hom-Ab R1 R2 f =
  (x y : type-Ring R1) ‚Üí
  Id ( map-hom-Ab (ab-Ring R1) (ab-Ring R2) f (mul-Ring R1 x y))
     ( mul-Ring R2
       ( map-hom-Ab (ab-Ring R1) (ab-Ring R2) f x)
       ( map-hom-Ab (ab-Ring R1) (ab-Ring R2) f y))

is-prop-preserves-mul-hom-Ab :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  ( f : type-hom-Ab (ab-Ring R1) (ab-Ring R2)) ‚Üí
  is-prop (preserves-mul-hom-Ab R1 R2 f)
is-prop-preserves-mul-hom-Ab R1 R2 f =
  is-prop-Œ†
    ( Œª x ‚Üí
      is-prop-Œ†
        ( Œª y ‚Üí
          is-set-type-Ring R2
            ( map-hom-Ab (ab-Ring R1) (ab-Ring R2) f (mul-Ring R1 x y))
            ( mul-Ring R2
              ( map-hom-Ab (ab-Ring R1) (ab-Ring R2) f x)
              ( map-hom-Ab (ab-Ring R1) (ab-Ring R2) f y))))

preserves-one-hom-Ab :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  type-hom-Ab (ab-Ring R1) (ab-Ring R2) ‚Üí UU l2
preserves-one-hom-Ab R1 R2 f =
  Id (map-hom-Ab (ab-Ring R1) (ab-Ring R2) f (one-Ring R1)) (one-Ring R2)

is-prop-preserves-one-hom-Ab :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  ( f : type-hom-Ab (ab-Ring R1) (ab-Ring R2)) ‚Üí
  is-prop (preserves-one-hom-Ab R1 R2 f)
is-prop-preserves-one-hom-Ab R1 R2 f =
  is-set-type-Ring R2
    ( map-hom-Ab (ab-Ring R1) (ab-Ring R2) f (one-Ring R1))
    ( one-Ring R2)

is-ring-homomorphism-hom-Ab :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  ( f : type-hom-Ab (ab-Ring R1) (ab-Ring R2)) ‚Üí UU (l1 ‚äî l2)
is-ring-homomorphism-hom-Ab R1 R2 f =
  preserves-mul-hom-Ab R1 R2 f √ó preserves-one-hom-Ab R1 R2 f

is-prop-is-ring-homomorphism-hom-Ab :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  ( f : type-hom-Ab (ab-Ring R1) (ab-Ring R2)) ‚Üí
  is-prop (is-ring-homomorphism-hom-Ab R1 R2 f)
is-prop-is-ring-homomorphism-hom-Ab R1 R2 f =
  is-prop-prod
    ( is-prop-preserves-mul-hom-Ab R1 R2 f)
    ( is-prop-preserves-one-hom-Ab R1 R2 f)

is-ring-homomorphism-hom-ab-Prop :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f : type-hom-Ab (ab-Ring R1) (ab-Ring R2)) ‚Üí UU-Prop (l1 ‚äî l2)
pr1 (is-ring-homomorphism-hom-ab-Prop R1 R2 f) =
  is-ring-homomorphism-hom-Ab R1 R2 f
pr2 (is-ring-homomorphism-hom-ab-Prop R1 R2 f) =
  is-prop-is-ring-homomorphism-hom-Ab R1 R2 f

type-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R : Ring l2) ‚Üí UU (l1 ‚äî l2)
type-hom-Ring R1 R2 =
  Œ£ (type-hom-Ab (ab-Ring R1) (ab-Ring R2)) (is-ring-homomorphism-hom-Ab R1 R2)

{- Basic infrastructure for ring homomorphisms. -}

hom-ab-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  type-hom-Ring R1 R2 ‚Üí type-hom-Ab (ab-Ring R1) (ab-Ring R2)
hom-ab-hom-Ring R1 R2 = pr1

map-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  type-hom-Ring R1 R2 ‚Üí type-Ring R1 ‚Üí type-Ring R2
map-hom-Ring R1 R2 f =
  map-hom-Ab (ab-Ring R1) (ab-Ring R2) (hom-ab-hom-Ring R1 R2 f)

preserves-add-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f : type-hom-Ring R1 R2) ‚Üí
  preserves-add-Ab (ab-Ring R1) (ab-Ring R2) (map-hom-Ring R1 R2 f)
preserves-add-hom-Ring R1 R2 f =
  preserves-add-hom-Ab (ab-Ring R1) (ab-Ring R2) (hom-ab-hom-Ring R1 R2 f)

preserves-zero-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f : type-hom-Ring R1 R2) ‚Üí
  preserves-zero-Ab (ab-Ring R1) (ab-Ring R2) (map-hom-Ring R1 R2 f)
preserves-zero-hom-Ring R1 R2 f =
  preserves-zero-hom-Ab (ab-Ring R1) (ab-Ring R2) (hom-ab-hom-Ring R1 R2 f)

preserves-neg-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f : type-hom-Ring R1 R2) ‚Üí
  preserves-negatives-Ab (ab-Ring R1) (ab-Ring R2) (map-hom-Ring R1 R2 f)
preserves-neg-hom-Ring R1 R2 f =
  preserves-negatives-hom-Ab (ab-Ring R1) (ab-Ring R2) (hom-ab-hom-Ring R1 R2 f)

preserves-mul-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f : type-hom-Ring R1 R2) ‚Üí preserves-mul-hom-Ab R1 R2 (hom-ab-hom-Ring R1 R2 f)
preserves-mul-hom-Ring R1 R2 f = pr1 (pr2 f)

preserves-one-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f : type-hom-Ring R1 R2) ‚Üí preserves-one-hom-Ab R1 R2 (hom-ab-hom-Ring R1 R2 f)
preserves-one-hom-Ring R1 R2 f = pr2 (pr2 f)

is-ring-homomorphism-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f : type-hom-Ring R1 R2) ‚Üí
  ( preserves-mul-hom-Ab R1 R2 (hom-ab-hom-Ring R1 R2 f)) √ó
  ( preserves-one-hom-Ab R1 R2 (hom-ab-hom-Ring R1 R2 f))
is-ring-homomorphism-hom-Ring R1 R2 f =
  pair ( preserves-mul-hom-Ring R1 R2 f)
       ( preserves-one-hom-Ring R1 R2 f)
```

```agda
{- We characterize the identity type of ring homomorphisms -}

htpy-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  type-hom-Ring R1 R2 ‚Üí type-hom-Ring R1 R2 ‚Üí UU (l1 ‚äî l2)
htpy-hom-Ring R1 R2 f g = map-hom-Ring R1 R2 f ~ map-hom-Ring R1 R2 g

reflexive-htpy-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f : type-hom-Ring R1 R2) ‚Üí htpy-hom-Ring R1 R2 f f
reflexive-htpy-hom-Ring R1 R2 f = refl-htpy

htpy-eq-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí
  (f g : type-hom-Ring R1 R2) ‚Üí Id f g ‚Üí htpy-hom-Ring R1 R2 f g
htpy-eq-hom-Ring R1 R2 f .f refl = reflexive-htpy-hom-Ring R1 R2 f

is-contr-total-htpy-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) (f : type-hom-Ring R1 R2) ‚Üí
  is-contr (Œ£ (type-hom-Ring R1 R2) (htpy-hom-Ring R1 R2 f))
is-contr-total-htpy-hom-Ring R1 R2 f =
  is-contr-total-Eq-subtype
    ( is-contr-total-htpy-hom-Ab
      ( ab-Ring R1)
      ( ab-Ring R2)
      ( hom-ab-hom-Ring R1 R2 f))
    ( is-prop-is-ring-homomorphism-hom-Ab R1 R2)
    ( hom-ab-hom-Ring R1 R2 f)
    ( reflexive-htpy-hom-Ring R1 R2 f)
    ( is-ring-homomorphism-hom-Ring R1 R2 f)

is-equiv-htpy-eq-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) (f g : type-hom-Ring R1 R2) ‚Üí
  is-equiv (htpy-eq-hom-Ring R1 R2 f g)
is-equiv-htpy-eq-hom-Ring R1 R2 f =
  fundamental-theorem-id f
    ( reflexive-htpy-hom-Ring R1 R2 f)
    ( is-contr-total-htpy-hom-Ring R1 R2 f)
    ( htpy-eq-hom-Ring R1 R2 f)

equiv-htpy-eq-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) (f g : type-hom-Ring R1 R2) ‚Üí
  Id f g ‚âÉ htpy-hom-Ring R1 R2 f g
equiv-htpy-eq-hom-Ring R1 R2 f g =
  pair
    ( htpy-eq-hom-Ring R1 R2 f g)
    ( is-equiv-htpy-eq-hom-Ring R1 R2 f g)

eq-htpy-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) (f g : type-hom-Ring R1 R2) ‚Üí
  htpy-hom-Ring R1 R2 f g ‚Üí Id f g
eq-htpy-hom-Ring R1 R2 f g =
  map-inv-is-equiv (is-equiv-htpy-eq-hom-Ring R1 R2 f g)

is-set-type-hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí is-set (type-hom-Ring R1 R2)
is-set-type-hom-Ring R1 R2 =
  is-trunc-type-subtype
    ( neg-one-ùïã)
    ( is-ring-homomorphism-hom-ab-Prop R1 R2)
    ( is-set-hom-Ab (ab-Ring R1) (ab-Ring R2))

hom-Ring :
  {l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) ‚Üí UU-Set (l1 ‚äî l2)
pr1 (hom-Ring R1 R2) = type-hom-Ring R1 R2
pr2 (hom-Ring R1 R2) = is-set-type-hom-Ring R1 R2

{- We define the categorical structure of rings -}

preserves-mul-id-hom-Ring :
  {l : Level} (R : Ring l) ‚Üí preserves-mul-hom-Ab R R (id-hom-Ab (ab-Ring R))
preserves-mul-id-hom-Ring R x y = refl

preserves-one-id-hom-Ring :
  {l : Level} (R : Ring l) ‚Üí preserves-one-hom-Ab R R (id-hom-Ab (ab-Ring R))
preserves-one-id-hom-Ring R = refl

is-ring-homomorphism-id-hom-Ring :
  {l : Level} (R : Ring l) ‚Üí is-ring-homomorphism-hom-Ab R R (id-hom-Ab (ab-Ring R))
is-ring-homomorphism-id-hom-Ring R =
  pair (preserves-mul-id-hom-Ring R) (preserves-one-id-hom-Ring R)

id-hom-Ring :
  {l : Level} (R : Ring l) ‚Üí type-hom-Ring R R
id-hom-Ring R = pair (id-hom-Ab (ab-Ring R)) (is-ring-homomorphism-id-hom-Ring R)

hom-Ab-comp-hom-Ring :
  { l1 l2 l3 : Level} (R1 : Ring l1) (R2 : Ring l2) (R3 : Ring l3) ‚Üí
  ( g : type-hom-Ring R2 R3) (f : type-hom-Ring R1 R2) ‚Üí
  type-hom-Ab (ab-Ring R1) (ab-Ring R3) 
hom-Ab-comp-hom-Ring R1 R2 R3 g f =
  comp-hom-Ab
    ( ab-Ring R1)
    ( ab-Ring R2)
    ( ab-Ring R3)
    ( hom-ab-hom-Ring R2 R3 g)
    ( hom-ab-hom-Ring R1 R2 f)

preserves-mul-comp-hom-Ring :
  { l1 l2 l3 : Level} (R1 : Ring l1) (R2 : Ring l2) (R3 : Ring l3) ‚Üí
  ( g : type-hom-Ring R2 R3) (f : type-hom-Ring R1 R2) ‚Üí
  preserves-mul-hom-Ab R1 R3 (hom-Ab-comp-hom-Ring R1 R2 R3 g f)
preserves-mul-comp-hom-Ring R1 R2 R3 g f x y =
  ( ap (map-hom-Ring R2 R3 g) (preserves-mul-hom-Ring R1 R2 f x y)) ‚àô
  ( preserves-mul-hom-Ring R2 R3 g
    ( map-hom-Ring R1 R2 f x)
    ( map-hom-Ring R1 R2 f y))

preserves-one-comp-hom-Ring :
  { l1 l2 l3 : Level} (R1 : Ring l1) (R2 : Ring l2) (R3 : Ring l3) ‚Üí
  ( g : type-hom-Ring R2 R3) (f : type-hom-Ring R1 R2) ‚Üí
  preserves-one-hom-Ab R1 R3 (hom-Ab-comp-hom-Ring R1 R2 R3 g f)
preserves-one-comp-hom-Ring R1 R2 R3 g f =
  ( ap (map-hom-Ring R2 R3 g) (preserves-one-hom-Ring R1 R2 f)) ‚àô
  ( preserves-one-hom-Ring R2 R3 g)

is-ring-homomorphism-comp-hom-Ring :
  { l1 l2 l3 : Level} (R1 : Ring l1) (R2 : Ring l2) (R3 : Ring l3) ‚Üí
  ( g : type-hom-Ring R2 R3) (f : type-hom-Ring R1 R2) ‚Üí
  is-ring-homomorphism-hom-Ab R1 R3 (hom-Ab-comp-hom-Ring R1 R2 R3 g f)
is-ring-homomorphism-comp-hom-Ring R1 R2 R3 g f =
  pair ( preserves-mul-comp-hom-Ring R1 R2 R3 g f)
       ( preserves-one-comp-hom-Ring R1 R2 R3 g f)

comp-hom-Ring :
  { l1 l2 l3 : Level} (R1 : Ring l1) (R2 : Ring l2) (R3 : Ring l3) ‚Üí
  type-hom-Ring R2 R3 ‚Üí type-hom-Ring R1 R2 ‚Üí type-hom-Ring R1 R3
comp-hom-Ring R1 R2 R3 g f =
  pair ( hom-Ab-comp-hom-Ring R1 R2 R3 g f)
       ( is-ring-homomorphism-comp-hom-Ring R1 R2 R3 g f)

{- We prove the laws of a category for Rings -}

is-associative-comp-hom-Ring :
  { l1 l2 l3 l4 : Level}
  ( R1 : Ring l1) (R2 : Ring l2) (R3 : Ring l3) (R4 : Ring l4) ‚Üí
  ( h : type-hom-Ring R3 R4) (g : type-hom-Ring R2 R3) (f : type-hom-Ring R1 R2) ‚Üí
  Id (comp-hom-Ring R1 R2 R4 (comp-hom-Ring R2 R3 R4 h g) f)
     (comp-hom-Ring R1 R3 R4 h (comp-hom-Ring R1 R2 R3 g f))
is-associative-comp-hom-Ring R1 R2 R3 R4 h g f =
  eq-htpy-hom-Ring R1 R4
    ( comp-hom-Ring R1 R2 R4 (comp-hom-Ring R2 R3 R4 h g) f)
    ( comp-hom-Ring R1 R3 R4 h (comp-hom-Ring R1 R2 R3 g f))
    ( refl-htpy)

left-unit-law-comp-hom-Ring :
  { l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) (f : type-hom-Ring R1 R2) ‚Üí
  Id (comp-hom-Ring R1 R2 R2 (id-hom-Ring R2) f) f
left-unit-law-comp-hom-Ring R1 R2 f =
  eq-htpy-hom-Ring R1 R2
    ( comp-hom-Ring R1 R2 R2 (id-hom-Ring R2) f)
    ( f)
    ( refl-htpy)

right-unit-law-comp-hom-Ring :
  { l1 l2 : Level} (R1 : Ring l1) (R2 : Ring l2) (f : type-hom-Ring R1 R2) ‚Üí
  Id (comp-hom-Ring R1 R1 R2 f (id-hom-Ring R1)) f
right-unit-law-comp-hom-Ring R1 R2 f =
  eq-htpy-hom-Ring R1 R2
    ( comp-hom-Ring R1 R1 R2 f (id-hom-Ring R1))
    ( f)
    ( refl-htpy)
```

```agda
id-law-ab-Ring :
  { l1 : Level} (R1 : Ring l1) ‚Üí
  Id (hom-ab-hom-Ring R1 R1 (id-hom-Ring R1)) (id-hom-Ab (ab-Ring R1))
id-law-ab-Ring R1 =
  eq-htpy-hom-Ab
    ( ab-Ring R1)
    ( ab-Ring R1)
    ( refl-htpy)

comp-law-ab-Ring :
  { l1 l2 l3 : Level} (R1 : Ring l1) (R2 : Ring l2) (R3 : Ring l3) ‚Üí
  ( g : type-hom-Ring R2 R3) (f : type-hom-Ring R1 R2) ‚Üí
  Id ( hom-ab-hom-Ring R1 R3 (comp-hom-Ring R1 R2 R3 g f))
     ( comp-hom-Ab
       ( ab-Ring R1)
       ( ab-Ring R2)
       ( ab-Ring R3)
       ( hom-ab-hom-Ring R2 R3 g)
       ( hom-ab-hom-Ring R1 R2 f))
comp-law-ab-Ring R1 R2 R3 g f =
  eq-htpy-hom-Ab
    ( ab-Ring R1)
    ( ab-Ring R3)
    ( refl-htpy)
```
