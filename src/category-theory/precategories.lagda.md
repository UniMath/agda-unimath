# Precategories

```agda
module category-theory.precategories where
```

<details><summary>Imports</summary>

```agda
open import category-theory.composition-operations-on-binary-families-of-sets
open import category-theory.nonunital-precategories
open import category-theory.set-magmoids

open import foundation.action-on-identifications-functions
open import foundation.cartesian-product-types
open import foundation.dependent-pair-types
open import foundation.function-types
open import foundation.identity-types
open import foundation.propositions
open import foundation.sets
open import foundation.strictly-involutive-identity-types
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.universe-levels
```

</details>

## Idea

A {{#concept "precategory" Agda=Precategory}} `ùíû` in Homotopy Type Theory is the
structure of an associative and unital
[composition operation](category-theory.composition-operations-on-binary-families-of-sets.md)
on a binary familiy of sets.

This means a precategory consists of:

- **Objects.** A type `Ob ùíû` of _objects_.
- **Morphisms.** For each pair of objects `x y : Ob ùíû`, a
  [set](foundation-core.sets.md) of _morphisms_ `hom ùíû x y : Set`.
- **Composition.** For every triple of objects `x y z : Ob ùíû` there is a
  _composition operation_ on morphisms
  ```text
    _‚àò_ : hom ùíû y z ‚Üí hom ùíû x y ‚Üí hom ùíû x z.
  ```
- **Associativity.** For every triple of composable morphisms, we have
  ```text
    (h ‚àò g) ‚àò f Ôºù h ‚àò (g ‚àò f).
  ```
- **Identity morphisms.** For every object `x : Ob ùíû`, there is a distinguished
  _identity_ morphism `id_x : hom ùíû x x`.
- **Unitality.** The identity morphisms are two-sided units for the composition
  operation, meaning that for every `f : hom ùíû x y` we have
  ```text
    id_y ‚àò f Ôºù f   and   f ‚àò id_x Ôºù f.
  ```

**Note.** The reason this is called a *pre*category and not a _category_ in
Homotopy Type Theory is that we reserve that name for precategories where the
[identity types](foundation-core.identity-types.md) of the type of objects are
characterized by the
[isomorphism sets](category-theory.isomorphisms-in-precategories.md).

## Definitions

### The predicate on composition operations on binary families of sets of being a precategory

```agda
module _
  {l1 l2 : Level} {A : UU l1}
  (hom-set : A ‚Üí A ‚Üí Set l2)
  (comp-hom : composition-operation-binary-family-Set hom-set)
  where

  is-precategory-prop-composition-operation-binary-family-Set : Prop (l1 ‚äî l2)
  is-precategory-prop-composition-operation-binary-family-Set =
    product-Prop
      ( is-unital-prop-composition-operation-binary-family-Set hom-set comp-hom)
      ( is-associative-prop-composition-operation-binary-family-Set
        ( hom-set)
        ( comp-hom))

  is-precategory-composition-operation-binary-family-Set : UU (l1 ‚äî l2)
  is-precategory-composition-operation-binary-family-Set =
    type-Prop is-precategory-prop-composition-operation-binary-family-Set

  is-prop-is-precategory-composition-operation-binary-family-Set :
    is-prop is-precategory-composition-operation-binary-family-Set
  is-prop-is-precategory-composition-operation-binary-family-Set =
    is-prop-type-Prop
      is-precategory-prop-composition-operation-binary-family-Set
```

### The type of precategories

```agda
Precategory :
  (l1 l2 : Level) ‚Üí UU (lsuc l1 ‚äî lsuc l2)
Precategory l1 l2 =
  Œ£ ( UU l1)
    ( Œª A ‚Üí
      Œ£ ( A ‚Üí A ‚Üí Set l2)
        ( Œª hom-set ‚Üí
          Œ£ ( associative-composition-operation-binary-family-Set hom-set)
            ( Œª (comp-hom , assoc-comp) ‚Üí
              is-unital-composition-operation-binary-family-Set
                ( hom-set)
                ( comp-hom))))

make-Precategory :
  { l1 l2 : Level}
  ( obj : UU l1)
  ( hom-set : obj ‚Üí obj ‚Üí Set l2)
  ( _‚àò_ : composition-operation-binary-family-Set hom-set)
  ( id : (x : obj) ‚Üí type-Set (hom-set x x))
  ( assoc-comp-hom :
    { x y z w : obj} ‚Üí
    ( h : type-Set (hom-set z w))
    ( g : type-Set (hom-set y z))
    ( f : type-Set (hom-set x y)) ‚Üí
    ( (h ‚àò g) ‚àò f Ôºù h ‚àò (g ‚àò f)))
  ( left-unit-comp-hom :
    { x y : obj} (f : type-Set (hom-set x y)) ‚Üí id y ‚àò f Ôºù f)
  ( right-unit-comp-hom :
    { x y : obj} (f : type-Set (hom-set x y)) ‚Üí f ‚àò id x Ôºù f) ‚Üí
  Precategory l1 l2
make-Precategory
  obj hom-set _‚àò_ id assoc-comp-hom left-unit-comp-hom right-unit-comp-hom =
  ( ( obj) ,
    ( hom-set) ,
    ( _‚àò_ , (Œª h g f ‚Üí involutive-eq-eq (assoc-comp-hom h g f))) ,
    ( id) ,
    ( left-unit-comp-hom) ,
    ( right-unit-comp-hom))

{-# INLINE make-Precategory #-}

module _
  {l1 l2 : Level} (C : Precategory l1 l2)
  where

  obj-Precategory : UU l1
  obj-Precategory = pr1 C

  hom-set-Precategory : (x y : obj-Precategory) ‚Üí Set l2
  hom-set-Precategory = pr1 (pr2 C)

  hom-Precategory : (x y : obj-Precategory) ‚Üí UU l2
  hom-Precategory x y = type-Set (hom-set-Precategory x y)

  is-set-hom-Precategory :
    (x y : obj-Precategory) ‚Üí is-set (hom-Precategory x y)
  is-set-hom-Precategory x y = is-set-type-Set (hom-set-Precategory x y)

  associative-composition-operation-Precategory :
    associative-composition-operation-binary-family-Set hom-set-Precategory
  associative-composition-operation-Precategory = pr1 (pr2 (pr2 C))

  comp-hom-Precategory :
    {x y z : obj-Precategory} ‚Üí
    hom-Precategory y z ‚Üí
    hom-Precategory x y ‚Üí
    hom-Precategory x z
  comp-hom-Precategory =
    comp-hom-associative-composition-operation-binary-family-Set
      ( hom-set-Precategory)
      ( associative-composition-operation-Precategory)

  comp-hom-Precategory' :
    {x y z : obj-Precategory} ‚Üí
    hom-Precategory x y ‚Üí
    hom-Precategory y z ‚Üí
    hom-Precategory x z
  comp-hom-Precategory' f g = comp-hom-Precategory g f

  involutive-eq-associative-comp-hom-Precategory :
    {x y z w : obj-Precategory}
    (h : hom-Precategory z w)
    (g : hom-Precategory y z)
    (f : hom-Precategory x y) ‚Üí
    ( comp-hom-Precategory (comp-hom-Precategory h g) f) Ôºù‚Å±
    ( comp-hom-Precategory h (comp-hom-Precategory g f))
  involutive-eq-associative-comp-hom-Precategory =
    involutive-eq-associative-composition-operation-binary-family-Set
      ( hom-set-Precategory)
      ( associative-composition-operation-Precategory)

  associative-comp-hom-Precategory :
    {x y z w : obj-Precategory}
    (h : hom-Precategory z w)
    (g : hom-Precategory y z)
    (f : hom-Precategory x y) ‚Üí
    ( comp-hom-Precategory (comp-hom-Precategory h g) f) Ôºù
    ( comp-hom-Precategory h (comp-hom-Precategory g f))
  associative-comp-hom-Precategory =
    witness-associative-composition-operation-binary-family-Set
      ( hom-set-Precategory)
      ( associative-composition-operation-Precategory)

  is-unital-composition-operation-Precategory :
    is-unital-composition-operation-binary-family-Set
      ( hom-set-Precategory)
      ( comp-hom-Precategory)
  is-unital-composition-operation-Precategory = pr2 (pr2 (pr2 C))

  id-hom-Precategory : {x : obj-Precategory} ‚Üí hom-Precategory x x
  id-hom-Precategory {x} = pr1 is-unital-composition-operation-Precategory x

  left-unit-law-comp-hom-Precategory :
    {x y : obj-Precategory} (f : hom-Precategory x y) ‚Üí
    comp-hom-Precategory id-hom-Precategory f Ôºù f
  left-unit-law-comp-hom-Precategory =
    pr1 (pr2 is-unital-composition-operation-Precategory)

  right-unit-law-comp-hom-Precategory :
    {x y : obj-Precategory} (f : hom-Precategory x y) ‚Üí
    comp-hom-Precategory f id-hom-Precategory Ôºù f
  right-unit-law-comp-hom-Precategory =
    pr2 (pr2 is-unital-composition-operation-Precategory)
```

### The underlying nonunital precategory of a precategory

```agda
module _
  {l1 l2 : Level} (C : Precategory l1 l2)
  where

  nonunital-precategory-Precategory : Nonunital-Precategory l1 l2
  pr1 nonunital-precategory-Precategory = obj-Precategory C
  pr1 (pr2 nonunital-precategory-Precategory) = hom-set-Precategory C
  pr2 (pr2 nonunital-precategory-Precategory) =
    associative-composition-operation-Precategory C
```

### The underlying set-magmoid of a precategory

```agda
module _
  {l1 l2 : Level} (C : Precategory l1 l2)
  where

  set-magmoid-Precategory : Set-Magmoid l1 l2
  set-magmoid-Precategory =
    set-magmoid-Nonunital-Precategory (nonunital-precategory-Precategory C)
```

### The total hom-type of a precategory

```agda
total-hom-Precategory :
  {l1 l2 : Level} (C : Precategory l1 l2) ‚Üí UU (l1 ‚äî l2)
total-hom-Precategory C =
  total-hom-Nonunital-Precategory (nonunital-precategory-Precategory C)

obj-total-hom-Precategory :
  {l1 l2 : Level} (C : Precategory l1 l2) ‚Üí
  total-hom-Precategory C ‚Üí obj-Precategory C √ó obj-Precategory C
obj-total-hom-Precategory C =
  obj-total-hom-Nonunital-Precategory (nonunital-precategory-Precategory C)
```

### Equalities induce morphisms

```agda
module _
  {l1 l2 : Level} (C : Precategory l1 l2)
  where

  hom-eq-Precategory :
    (x y : obj-Precategory C) ‚Üí x Ôºù y ‚Üí hom-Precategory C x y
  hom-eq-Precategory x .x refl = id-hom-Precategory C

  hom-inv-eq-Precategory :
    (x y : obj-Precategory C) ‚Üí x Ôºù y ‚Üí hom-Precategory C y x
  hom-inv-eq-Precategory x y = hom-eq-Precategory y x ‚àò inv
```

### Pre- and postcomposition by a morphism

```agda
module _
  {l1 l2 : Level} (C : Precategory l1 l2)
  {x y : obj-Precategory C}
  (f : hom-Precategory C x y)
  (z : obj-Precategory C)
  where

  precomp-hom-Precategory : hom-Precategory C y z ‚Üí hom-Precategory C x z
  precomp-hom-Precategory g = comp-hom-Precategory C g f

  postcomp-hom-Precategory : hom-Precategory C z x ‚Üí hom-Precategory C z y
  postcomp-hom-Precategory = comp-hom-Precategory C f
```

## Properties

### If the objects of a precategory are `k`-truncated for nonnegative `k`, the total hom-type is `k`-truncated

```agda
module _
  {l1 l2 : Level} {k : ùïã} (C : Precategory l1 l2)
  where

  is-trunc-total-hom-is-trunc-obj-Precategory :
    is-trunc (succ-ùïã (succ-ùïã k)) (obj-Precategory C) ‚Üí
    is-trunc (succ-ùïã (succ-ùïã k)) (total-hom-Precategory C)
  is-trunc-total-hom-is-trunc-obj-Precategory =
    is-trunc-total-hom-is-trunc-obj-Nonunital-Precategory
      ( nonunital-precategory-Precategory C)

  total-hom-truncated-type-is-trunc-obj-Precategory :
    is-trunc (succ-ùïã (succ-ùïã k)) (obj-Precategory C) ‚Üí
    Truncated-Type (l1 ‚äî l2) (succ-ùïã (succ-ùïã k))
  total-hom-truncated-type-is-trunc-obj-Precategory =
    total-hom-truncated-type-is-trunc-obj-Nonunital-Precategory
      ( nonunital-precategory-Precategory C)
```

### Coherence between the left and right unit law of a precategory

```agda
module _
  {l1 l2 : Level} (C : Precategory l1 l2)
  where

  coh-unit-laws-comp-hom-Precategory :
    {x : obj-Precategory C} ‚Üí
    left-unit-law-comp-hom-Precategory C (id-hom-Precategory C {x = x}) Ôºù
    right-unit-law-comp-hom-Precategory C (id-hom-Precategory C {x = x})
  coh-unit-laws-comp-hom-Precategory {x} =
    eq-is-prop
      ( is-set-hom-Precategory C x x
        ( comp-hom-Precategory C (id-hom-Precategory C) (id-hom-Precategory C))
        ( id-hom-Precategory C))
```

### Coherence between the associativity law and the unit laws of a precategory

```agda
module _
  {l1 l2 : Level} (C : Precategory l1 l2)
  where

  coh-associative-left-unit-law-comp-hom-Precategory :
    {x y z : obj-Precategory C}
    {f : hom-Precategory C x y} {g : hom-Precategory C y z} ‚Üí
    ( associative-comp-hom-Precategory C (id-hom-Precategory C) g f) ‚àô
    ( left-unit-law-comp-hom-Precategory C (comp-hom-Precategory C g f)) Ôºù
    ( ap
      ( comp-hom-Precategory' C f)
      ( left-unit-law-comp-hom-Precategory C g))
  coh-associative-left-unit-law-comp-hom-Precategory {x} {y} {z} {f} {g} =
      eq-is-prop
      ( is-set-hom-Precategory C x z
        ( comp-hom-Precategory C
          ( comp-hom-Precategory C (id-hom-Precategory C) g)
          ( f))
        ( comp-hom-Precategory C g f))

  coh-associative-left-unit-law-comp-hom-Precategory'' :
    {x y : obj-Precategory C} {f : hom-Precategory C x y} ‚Üí
    ( associative-comp-hom-Precategory C
      ( id-hom-Precategory C)
      ( id-hom-Precategory C)
      ( f)) ‚àô
    left-unit-law-comp-hom-Precategory C
      ( comp-hom-Precategory C (id-hom-Precategory C) f) ‚àô
    left-unit-law-comp-hom-Precategory C f Ôºù
    ( ap
      ( comp-hom-Precategory' C f)
      ( left-unit-law-comp-hom-Precategory C (id-hom-Precategory C))) ‚àô
    ( left-unit-law-comp-hom-Precategory C f)
  coh-associative-left-unit-law-comp-hom-Precategory'' {x} {y} {f} =
    eq-is-prop
      ( is-set-hom-Precategory C x y
        ( comp-hom-Precategory C
          ( comp-hom-Precategory C
            ( id-hom-Precategory C)
            ( id-hom-Precategory C))
          ( f))
        ( f))
```

## See also

- [Categories](category-theory.categories.md) are univalent precategories.
- [Functors between precategories](category-theory.functors-precategories.md)
  are [structure](foundation.structure.md)-preserving maps of precategories.
- [Large precategories](category-theory.large-precategories.md) are
  precategories whose collections of objects and morphisms form large types.

## External links

- [Precategories](https://1lab.dev/Cat.Base.html) at 1lab
- [precategory](https://ncatlab.org/nlab/show/precategory) at $n$Lab
