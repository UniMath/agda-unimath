# Truncated addition of truncation levels

```agda
module foundation.truncated-addition-truncation-levels where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.addition-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.action-on-identifications-binary-functions
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.identity-types
open import foundation.inequality-truncation-levels
open import foundation.iterated-successors-truncation-levels
open import foundation.negated-equality
open import foundation.negation
open import foundation.truncation-levels
open import foundation.unit-type

open import foundation-core.cartesian-product-types
open import foundation-core.function-types
```

</details>

## Idea

We define the
{{#concept "truncated addition" Disambiguation="of truncation levels" Agda=truncadd-ğ•‹}}
binary operation on [truncation levels](foundation-core.truncation-levels.md).
Truncated addition is given by interpreting truncation levels as
[integers](elementary-number-theory.integers.md),
[adding](elementary-number-theory.addition-integers.md) them together, and
interpreting the result as a truncation level again, mapping any result less
than -2 to -2.

**Note.** This operation, while conceptually clear, has many flaws that makes it
ill-equipped for use in formalization, and hence is not recommended. For
instance, the operation is not associative and does not reflect inequality. See
instead
[`iterated-successors-truncation-levels`](foundation.iterated-successors-truncation-levels.md)
for a more natural alternative.

## Definitions

### Truncated addition of truncation levels

```agda
truncadd-ğ•‹' : ğ•‹ â†’ ğ•‹ â†’ ğ•‹
truncadd-ğ•‹' neg-two-ğ•‹ neg-two-ğ•‹ = neg-two-ğ•‹
truncadd-ğ•‹' neg-two-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) = neg-two-ğ•‹
truncadd-ğ•‹' neg-two-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ l)) = l
truncadd-ğ•‹' (succ-ğ•‹ neg-two-ğ•‹) neg-two-ğ•‹ = neg-two-ğ•‹
truncadd-ğ•‹' (succ-ğ•‹ neg-two-ğ•‹) (succ-ğ•‹ l) = l
truncadd-ğ•‹' (succ-ğ•‹ (succ-ğ•‹ k)) neg-two-ğ•‹ = k
truncadd-ğ•‹' (succ-ğ•‹ (succ-ğ•‹ k)) (succ-ğ•‹ l) =
  succ-ğ•‹ (truncadd-ğ•‹' (succ-ğ•‹ k) (succ-ğ•‹ l))

truncadd-ğ•‹ : ğ•‹ â†’ ğ•‹ â†’ ğ•‹
truncadd-ğ•‹ k r = truncadd-ğ•‹' r k
```

Agda is not happy with the following definition due to the `--exact-split` flag.

```text
truncadd-ğ•‹ : ğ•‹ â†’ ğ•‹ â†’ ğ•‹
truncadd-ğ•‹ neg-two-ğ•‹ neg-two-ğ•‹ = neg-two-ğ•‹
truncadd-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) neg-two-ğ•‹ = neg-two-ğ•‹
truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ k)) neg-two-ğ•‹ = k
truncadd-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) = neg-two-ğ•‹
truncadd-ğ•‹ (succ-ğ•‹ k) (succ-ğ•‹ neg-two-ğ•‹) = k
truncadd-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ r)) = r
truncadd-ğ•‹ (succ-ğ•‹ k) (succ-ğ•‹ (succ-ğ•‹ r)) =
  succ-ğ•‹ (truncadd-ğ•‹ (succ-ğ•‹ k) (succ-ğ•‹ r))
```

### The binary action of truncated addition on identifications

```agda
ap-binary-truncadd-ğ•‹ :
  {m n m' n' : ğ•‹} â†’ m ï¼ m' â†’ n ï¼ n' â†’ truncadd-ğ•‹ m n ï¼ truncadd-ğ•‹ m' n'
ap-binary-truncadd-ğ•‹ p q = ap-binary truncadd-ğ•‹ p q
```

## Properties

### Unit laws for addition of truncation levels

```agda
left-unit-law-truncadd-ğ•‹ : (k : ğ•‹) â†’ truncadd-ğ•‹ zero-ğ•‹ k ï¼ k
left-unit-law-truncadd-ğ•‹ neg-two-ğ•‹ = refl
left-unit-law-truncadd-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) = refl
left-unit-law-truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ k)) =
  ap succ-ğ•‹ (left-unit-law-truncadd-ğ•‹ (succ-ğ•‹ k))

right-unit-law-truncadd-ğ•‹ : (k : ğ•‹) â†’ truncadd-ğ•‹ k zero-ğ•‹ ï¼ k
right-unit-law-truncadd-ğ•‹ neg-two-ğ•‹ = refl
right-unit-law-truncadd-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) = refl
right-unit-law-truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹)) = refl
right-unit-law-truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ k))) = refl
```

### Successor laws for addition of truncation levels

```agda
right-successor-law-truncadd-ğ•‹ :
  (n k : ğ•‹) â†’
  truncadd-ğ•‹ k (iterate-succ-ğ•‹ 3 n) ï¼ succ-ğ•‹ (truncadd-ğ•‹ k (iterate-succ-ğ•‹ 2 n))
right-successor-law-truncadd-ğ•‹ n neg-two-ğ•‹ = refl
right-successor-law-truncadd-ğ•‹ n (succ-ğ•‹ k) = refl

left-successor-law-truncadd-ğ•‹ :
  (k n : ğ•‹) â†’
  truncadd-ğ•‹ (iterate-succ-ğ•‹ 3 n) k ï¼ succ-ğ•‹ (truncadd-ğ•‹ (iterate-succ-ğ•‹ 2 n) k)
left-successor-law-truncadd-ğ•‹ neg-two-ğ•‹ n = refl
left-successor-law-truncadd-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) n = refl
left-successor-law-truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ k)) n =
  ap succ-ğ•‹ (left-successor-law-truncadd-ğ•‹ (succ-ğ•‹ k) n)
```

### The balancing law of the successor function over addition

```agda
balance-succ-truncadd-ğ•‹ :
  (k r : ğ•‹) â†’ truncadd-ğ•‹ (succ-ğ•‹ k) r ï¼ truncadd-ğ•‹ k (succ-ğ•‹ r)
balance-succ-truncadd-ğ•‹ neg-two-ğ•‹ neg-two-ğ•‹ = refl
balance-succ-truncadd-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) = refl
balance-succ-truncadd-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ r)) =
  ap succ-ğ•‹ (balance-succ-truncadd-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ r))
balance-succ-truncadd-ğ•‹ (succ-ğ•‹ k) neg-two-ğ•‹ = refl
balance-succ-truncadd-ğ•‹ (succ-ğ•‹ k) (succ-ğ•‹ neg-two-ğ•‹) = refl
balance-succ-truncadd-ğ•‹ (succ-ğ•‹ k) (succ-ğ•‹ (succ-ğ•‹ r)) =
  ap succ-ğ•‹ (balance-succ-truncadd-ğ•‹ (succ-ğ•‹ k) (succ-ğ•‹ r))

abstract
  balance-iterated-succ-truncadd-ğ•‹ :
    (n : â„•) (k r : ğ•‹) â†’
    truncadd-ğ•‹ (iterate-succ-ğ•‹ n k) r ï¼ truncadd-ğ•‹ k (iterate-succ-ğ•‹ n r)
  balance-iterated-succ-truncadd-ğ•‹ zero-â„• k r = refl
  balance-iterated-succ-truncadd-ğ•‹ (succ-â„• n) k r =
    ( balance-iterated-succ-truncadd-ğ•‹ n (succ-ğ•‹ k) r) âˆ™
    ( balance-succ-truncadd-ğ•‹ k (iterate-succ-ğ•‹ n r)) âˆ™
    ( ap (truncadd-ğ•‹ k) (inv (reassociate-iterate-succ-ğ•‹ n r)))
```

### The double successor of addition is the double successor of addition

```agda
abstract
  compute-succ-succ-truncadd-ğ•‹ :
    (k r : ğ•‹) â†’ add+2-ğ•‹ k r ï¼ truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ k)) r
  compute-succ-succ-truncadd-ğ•‹ k neg-two-ğ•‹ = refl
  compute-succ-succ-truncadd-ğ•‹ k (succ-ğ•‹ neg-two-ğ•‹) = refl
  compute-succ-succ-truncadd-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ r)) =
    left-unit-law-add+2-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ r)) âˆ™
    inv (left-unit-law-truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ r)))
  compute-succ-succ-truncadd-ğ•‹ (succ-ğ•‹ k) (succ-ğ•‹ (succ-ğ•‹ r)) =
    ap succ-ğ•‹ (compute-succ-succ-truncadd-ğ•‹ (succ-ğ•‹ k) (succ-ğ•‹ r))

abstract
  compute-succ-succ-truncadd-ğ•‹' :
    (k r : ğ•‹) â†’ add+2-ğ•‹ k r ï¼ truncadd-ğ•‹ k (succ-ğ•‹ (succ-ğ•‹ r))
  compute-succ-succ-truncadd-ğ•‹' k r =
    compute-succ-succ-truncadd-ğ•‹ k r âˆ™ balance-iterated-succ-truncadd-ğ•‹ 2 k r
```

### Addition is not associative

```agda
example-not-associative-truncadd-ğ•‹ :
  truncadd-ğ•‹ (truncadd-ğ•‹ neg-two-ğ•‹ neg-two-ğ•‹) one-ğ•‹ â‰ 
  truncadd-ğ•‹ neg-two-ğ•‹ (truncadd-ğ•‹ neg-two-ğ•‹ one-ğ•‹)
example-not-associative-truncadd-ğ•‹ ()

not-associative-truncadd-ğ•‹ :
  Â¬ ( (x y z : ğ•‹) â†’
      truncadd-ğ•‹ (truncadd-ğ•‹ x y) z ï¼ truncadd-ğ•‹ x (truncadd-ğ•‹ y z))
not-associative-truncadd-ğ•‹ Î± =
  example-not-associative-truncadd-ğ•‹ (Î± neg-two-ğ•‹ neg-two-ğ•‹ one-ğ•‹)
```

### Addition is commutative

```agda
abstract
  commutative-truncadd-ğ•‹ : (x y : ğ•‹) â†’ truncadd-ğ•‹ x y ï¼ truncadd-ğ•‹ y x
  commutative-truncadd-ğ•‹ neg-two-ğ•‹ neg-two-ğ•‹ = refl
  commutative-truncadd-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) = refl
  commutative-truncadd-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ y)) = refl
  commutative-truncadd-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) neg-two-ğ•‹ = refl
  commutative-truncadd-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) (succ-ğ•‹ neg-two-ğ•‹) = refl
  commutative-truncadd-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) (succ-ğ•‹ (succ-ğ•‹ y)) =
    ap succ-ğ•‹ (commutative-truncadd-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) (succ-ğ•‹ y))
  commutative-truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ x)) neg-two-ğ•‹ = refl
  commutative-truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ x)) (succ-ğ•‹ neg-two-ğ•‹) =
    ap succ-ğ•‹ (commutative-truncadd-ğ•‹ (succ-ğ•‹ x) (succ-ğ•‹ neg-two-ğ•‹))
  commutative-truncadd-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ x)) (succ-ğ•‹ (succ-ğ•‹ y)) =
    ap
      ( succ-ğ•‹)
      ( balance-succ-truncadd-ğ•‹ (succ-ğ•‹ x) (succ-ğ•‹ y) âˆ™
        commutative-truncadd-ğ•‹ (succ-ğ•‹ x) (succ-ğ•‹ (succ-ğ•‹ y)))
```

### Addition does not reflect inequality of truncation levels

```agda
example-not-reflects-leq-left-truncadd-ğ•‹ :
  (truncadd-ğ•‹ neg-one-ğ•‹ neg-two-ğ•‹) â‰¤-ğ•‹ (truncadd-ğ•‹ neg-two-ğ•‹ neg-two-ğ•‹) Ã—
  Â¬ (neg-one-ğ•‹ â‰¤-ğ•‹ neg-two-ğ•‹)
example-not-reflects-leq-left-truncadd-ğ•‹ = (star , id)

not-reflects-leq-left-truncadd-ğ•‹ :
  Â¬ ((k m n : ğ•‹) â†’ (truncadd-ğ•‹ m k) â‰¤-ğ•‹ (truncadd-ğ•‹ n k) â†’ m â‰¤-ğ•‹ n)
not-reflects-leq-left-truncadd-ğ•‹ Î± = Î± neg-two-ğ•‹ neg-one-ğ•‹ neg-two-ğ•‹ star
```

### Coherence with addition on natural numbers

```agda
add+2-truncation-level-minus-one-â„• :
  (k n : â„•) â†’
  truncation-level-minus-one-â„• (k +â„• n) ï¼
  add+2-ğ•‹
    ( truncation-level-minus-one-â„• k)
    ( truncation-level-minus-two-â„• n)
add+2-truncation-level-minus-one-â„• k zero-â„• = refl
add+2-truncation-level-minus-one-â„• k (succ-â„• n) =
  ap succ-ğ•‹ (add+2-truncation-level-minus-one-â„• k n)
```

## See also

- [`iterated-successors-truncation-levels`](foundation.iterated-successors-truncation-levels.md)
