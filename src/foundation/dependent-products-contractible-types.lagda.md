# Dependent products of contractible types

```agda
open import foundation.function-extensionality-axiom

module foundation.dependent-products-contractible-types
  (funext : function-extensionality)
  where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.function-extensionality funext
open import foundation.implicit-function-types
open import foundation.universe-levels

open import foundation-core.contractible-types
open import foundation-core.equivalences
open import foundation-core.identity-types
```

</details>

## Idea

Given a family of [contractible types](foundation-core.contractible-types.md)
`B : A ‚Üí ùí∞`, then the dependent product `Œ† A B` is again contractible.

## Properties

### Products of families of contractible types are contractible

```agda
abstract
  is-contr-Œ† :
    {l1 l2 : Level} {A : UU l1} {B : A ‚Üí UU l2} ‚Üí
    ((x : A) ‚Üí is-contr (B x)) ‚Üí is-contr ((x : A) ‚Üí B x)
  pr1 (is-contr-Œ† {A = A} {B = B} H) x = center (H x)
  pr2 (is-contr-Œ† {A = A} {B = B} H) f =
    eq-htpy (Œª x ‚Üí contraction (H x) (f x))

abstract
  is-contr-implicit-Œ† :
    {l1 l2 : Level} {A : UU l1} {B : A ‚Üí UU l2} ‚Üí
    ((x : A) ‚Üí is-contr (B x)) ‚Üí is-contr ({x : A} ‚Üí B x)
  is-contr-implicit-Œ† H =
    is-contr-equiv _ equiv-explicit-implicit-Œ† (is-contr-Œ† H)
```

### The type of functions into a contractible type is contractible

```agda
is-contr-function-type :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} ‚Üí
  is-contr B ‚Üí is-contr (A ‚Üí B)
is-contr-function-type is-contr-B = is-contr-Œ† (Œª _ ‚Üí is-contr-B)
```

### The type of equivalences between contractible types is contractible

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-contr-equiv-is-contr :
    is-contr A ‚Üí is-contr B ‚Üí is-contr (A ‚âÉ B)
  is-contr-equiv-is-contr (pair a Œ±) (pair b Œ≤) =
    is-contr-Œ£
      ( is-contr-function-type (pair b Œ≤))
      ( Œª x ‚Üí b)
      ( is-contr-product
        ( is-contr-Œ£
          ( is-contr-function-type (pair a Œ±))
          ( Œª y ‚Üí a)
          ( is-contr-Œ† (is-prop-is-contr (pair b Œ≤) b)))
        ( is-contr-Œ£
          ( is-contr-function-type (pair a Œ±))
          ( Œª y ‚Üí a)
          ( is-contr-Œ† (is-prop-is-contr (pair a Œ±) a))))
```

### Being contractible is a proposition

```agda
module _
  {l : Level} {A : UU l}
  where

  abstract
    is-contr-is-contr : is-contr A ‚Üí is-contr (is-contr A)
    is-contr-is-contr (pair a Œ±) =
      is-contr-Œ£
        ( pair a Œ±)
        ( a)
        ( is-contr-Œ† (is-prop-is-contr (pair a Œ±) a))

  abstract
    is-property-is-contr : (H K : is-contr A) ‚Üí is-contr (H Ôºù K)
    is-property-is-contr H = is-prop-is-contr (is-contr-is-contr H) H
```
