# Parametric types

```agda
module foundation.parametric-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.coproduct-types
open import foundation.decidable-equality
open import foundation.decidable-propositions
open import foundation.decidable-subtypes
open import foundation.decidable-type-families
open import foundation.decidable-types
open import foundation.dependent-pair-types
open import foundation.discrete-types
open import foundation.double-negation
open import foundation.double-negation-stable-propositions
open import foundation.embeddings
open import foundation.empty-types
open import foundation.evaluation-functions
open import foundation.fibers-of-maps
open import foundation.function-extensionality
open import foundation.functoriality-coproduct-types
open import foundation.negated-equality
open import foundation.propositional-maps
open import foundation.retracts-of-types
open import foundation.type-arithmetic-cartesian-product-types
open import foundation.type-arithmetic-empty-type
open import foundation.types-with-decidable-dependent-product-types
open import foundation.types-with-decidable-universal-quantifications
open import foundation.unit-type
open import foundation.univalence
open import foundation.universal-property-equivalences
open import foundation.universe-levels
open import foundation.weak-limited-principle-of-omniscience

open import foundation-core.cartesian-product-types
open import foundation-core.contractible-types
open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.negation
open import foundation-core.propositions

open import orthogonal-factorization-systems.null-types
```

</details>

## Idea

A type `X : ğ’°` is
{{#concept "parametric" Disambiguation="type" Agda=is-parametric Agda=Parametric-Type}}
if the [constant map](foundation.constant-maps.md) `X â†’ (ğ’° â†’ X)` is an
[equivalence](foundation-core.equivalences.md). In other words, if `X` is
`ğ’°`-[null](orthogonal-factorization-systems.null-types.md).

Parametricity can be understood as a "classical taboo" since it contradicts the
law of excluded middle. We demonstrate this in
[`foundation.parametricity-booleans`](foundation.parametricity-booleans.md).

## Definitions

### The predicate on a type of being parametric

```agda
module _
  {l1 : Level} (l2 : Level) (X : UU l1)
  where

  is-parametric : UU (l1 âŠ” lsuc l2)
  is-parametric = is-null (UU l2) X

  is-prop-is-parametric : is-prop is-parametric
  is-prop-is-parametric = is-prop-is-null (UU l2) X

  is-parametric-Prop : Prop (l1 âŠ” lsuc l2)
  is-parametric-Prop = is-null-Prop (UU l2) X
```

### The universe of parametric types

```agda
Parametric-Type : (l1 l2 : Level) â†’ UU (lsuc l1 âŠ” lsuc l2)
Parametric-Type l1 l2 = Î£ (UU l1) (is-parametric l2)

module _
  {l1 l2 : Level} (X : Parametric-Type l1 l2)
  where

  type-Parametric-Type : UU l1
  type-Parametric-Type = pr1 X

  is-parametric-type-Parametric-Type :
    is-parametric l2 type-Parametric-Type
  is-parametric-type-Parametric-Type = pr2 X
```

## Properties

### Contractible types are parametric

```agda
abstract
  is-parametric-is-contr :
    {l1 l2 : Level} {X : UU l1} â†’
    is-contr X â†’ is-parametric l2 X
  is-parametric-is-contr {l2 = l2} =
    is-null-is-contr (UU l2)
```

### The unit type is parametric

```agda
abstract
  is-parametric-unit :
    {l : Level} â†’ is-parametric l unit
  is-parametric-unit {l} =
    is-parametric-is-contr is-contr-unit
```

### The empty type is parametric

```agda
abstract
  is-parametric-empty :
    {l : Level} â†’ is-parametric l empty
  is-parametric-empty {l} =
    is-equiv-is-empty _ (ev (raise-empty l))
```

### Propositions are parametric

```agda
abstract
  is-parametric-is-prop :
    {l1 l2 : Level} {X : UU l1} â†’
    is-prop X â†’ is-parametric l2 X
  is-parametric-is-prop {l1} {l2} H =
    is-null-is-prop H (ev (raise-empty l2))
```

### Parametric types are closed under equivalences

```agda
module _
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2}
  where abstract

  is-parametric-equiv :
    X â‰ƒ Y â†’ is-parametric l3 Y â†’ is-parametric l3 X
  is-parametric-equiv = is-null-equiv-base

  is-parametric-equiv' :
    X â‰ƒ Y â†’ is-parametric l3 X â†’ is-parametric l3 Y
  is-parametric-equiv' = is-null-equiv-base âˆ˜ inv-equiv
```

### Parametric types are closed under retracts

```agda
module _
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2}
  where abstract

  is-parametric-retract :
    X retract-of Y â†’ is-parametric l3 Y â†’ is-parametric l3 X
  is-parametric-retract = is-null-retract-base
```

### Parametric types are closed under embeddings

```agda
module _
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2}
  where abstract

  is-parametric-emb :
    Y â†ª X â†’ is-parametric l3 X â†’ is-parametric l3 Y
  is-parametric-emb e is-parametric-X =
    is-parametric-equiv'
      ( equiv-total-fiber (map-emb e))
      ( is-null-Î£
        ( is-parametric-X)
        ( Î» x â†’ is-parametric-is-prop (is-prop-type-Prop (fiber-emb-Prop e x))))
```

### If there is a type that refutes decidable universal quantifications, then discrete types are parametric

Given a discrete type `B`, if there exists a function `f : I â†’ B` together with
elements `X` and `Y` of `I` such that `f X â‰  f Y`, and moreover there is some
assignment of double negation stable propositions to `I`, `Ï‡ : PropÂ¬Â¬ â†’ I` that
sends true propositions to `X` and false propositions to `Y`, then every type
has decidable universal quantifications. For `I` equal to a universe, there is
such a map given by splitting at any given double negation stable proposition:

```text
  Ï‡ P â‰” (P Ã— X) + ((Â¬ P) Ã— Y)
```

Therefore, if some type does not have decidable universal quantifications, then
`f` must send `X` and `Y` to the same element in `B`.

```agda
module _
  {l1 l2 l3 : Level} {I : UU l1}
  (B : Discrete-Type l2)
  (f : I â†’ type-Discrete-Type B)
  {X Y : I}
  (fXâ‰ fY : f X â‰  f Y)
  (Ï‡ : Double-Negation-Stable-Prop l3 â†’ I)
  (eq-Ï‡-true :
    (P : Double-Negation-Stable-Prop l3) â†’
    type-Double-Negation-Stable-Prop P â†’
    Ï‡ P ï¼ X)
  (eq-Ï‡-false :
    (P : Double-Negation-Stable-Prop l3) â†’
    Â¬ type-Double-Negation-Stable-Prop P â†’
    Ï‡ P ï¼ Y)
  where abstract

  is-decidable-split-on-double-negation-stable-prop-Discrete-Type :
    (P : Double-Negation-Stable-Prop l3) â†’
    is-decidable (type-Double-Negation-Stable-Prop P)
  is-decidable-split-on-double-negation-stable-prop-Discrete-Type P =
    map-coproduct
      ( Î» p â†’
        has-double-negation-elim-type-Double-Negation-Stable-Prop P
          ( Î» np â†’ fXâ‰ fY (inv p âˆ™ ap f (eq-Ï‡-false P np))))
      ( Î» np p â†’ np (ap f (eq-Ï‡-true P p)))
      ( has-decidable-equality-type-Discrete-Type B (f (Ï‡ P)) (f X))

  has-decidable-âˆ€-has-separating-map-split-Discrete-Type :
    (A : UU l3) â†’ has-decidable-âˆ€-Level l3 A
  has-decidable-âˆ€-has-separating-map-split-Discrete-Type A P =
    is-decidable-split-on-double-negation-stable-prop-Discrete-Type
      ( is-full-decidable-subtype P ,
        ( is-prop-type-Prop (is-full-decidable-subtype-Prop P) ,
          Î» nn-all-P a â†’
          double-negation-elim-is-decidable
            ( is-decidable-decidable-subtype P a)
            ( Î» nn-Pa â†’ nn-all-P (Î» all-P â†’ nn-Pa (all-P a)))))

abstract
  separate-map-split-on-propÂ¬Â¬-not-has-decidable-âˆ€-Level-Discrete-Type :
    {l1 l2 l3 : Level} {A : UU l1} {I : UU l2} â†’
    Â¬ has-decidable-âˆ€-Level l1 A â†’
    (B : Discrete-Type l3)
    (f : I â†’ type-Discrete-Type B)
    {X Y : I}
    (Ï‡ : (f X â‰  f Y) â†’ Double-Negation-Stable-Prop l1 â†’ I) â†’
    ( (fXâ‰ fY : f X â‰  f Y) (P : Double-Negation-Stable-Prop l1) â†’
      type-Double-Negation-Stable-Prop P â†’ Ï‡ fXâ‰ fY P ï¼ X) â†’
    ( (fXâ‰ fY : f X â‰  f Y) (P : Double-Negation-Stable-Prop l1) â†’
      Â¬ type-Double-Negation-Stable-Prop P â†’ Ï‡ fXâ‰ fY P ï¼ Y) â†’
    f X ï¼ f Y
  separate-map-split-on-propÂ¬Â¬-not-has-decidable-âˆ€-Level-Discrete-Type
    {A = A} Â¬dâˆ€A B f {X} {Y} Ï‡ eq-Ï‡-true eq-Ï‡-false =
    rec-coproduct
      ( id)
      ( Î» fXâ‰ fY â†’
        ex-falso
          ( Â¬dâˆ€A
            ( has-decidable-âˆ€-has-separating-map-split-Discrete-Type
              ( B)
              ( f)
              ( fXâ‰ fY)
              ( Ï‡ fXâ‰ fY)
              ( eq-Ï‡-true fXâ‰ fY)
              ( eq-Ï‡-false fXâ‰ fY)
              ( A))))
      ( has-decidable-equality-type-Discrete-Type B (f X) (f Y))

module _
  {l1 l2 l3 : Level} (P'@(P , is-prop-P) : Prop l1) {X : UU l2} {Y : UU l3}
  where

  compute-split-on-propÂ¬Â¬-true-parametric-types :
    P â†’ ((P Ã— X) + ((Â¬ P) Ã— Y)) â‰ƒ X
  compute-split-on-propÂ¬Â¬-true-parametric-types p =
    ( left-unit-law-product-is-contr
      ( is-proof-irrelevant-is-prop is-prop-P p)) âˆ˜e
    ( right-unit-law-coproduct-is-empty (P Ã— X) ((Â¬ P) Ã— Y) (Î» z â†’ (pr1 z) p))

  compute-split-on-propÂ¬Â¬-false-parametric-types :
    Â¬ P â†’ ((P Ã— X) + ((Â¬ P) Ã— Y)) â‰ƒ Y
  compute-split-on-propÂ¬Â¬-false-parametric-types np =
    ( left-unit-law-product-is-contr
      ( is-proof-irrelevant-is-prop is-property-is-empty np)) âˆ˜e
    ( left-unit-law-coproduct-is-empty (P Ã— X) ((Â¬ P) Ã— Y) (np âˆ˜ pr1))

split-on-double-negation-stable-prop-Discrete-Type :
  {l1 l2 : Level} (B : Discrete-Type l2) â†’
  (f : UU l1 â†’ type-Discrete-Type B) {X Y : UU l1} â†’
  Double-Negation-Stable-Prop l1 â†’ UU l1
split-on-double-negation-stable-prop-Discrete-Type B f {X} {Y} P =
  ( type-Double-Negation-Stable-Prop P Ã— X) +
  ((Â¬ type-Double-Negation-Stable-Prop P) Ã— Y)

module _
  {l1 l2 : Level} {A : UU l1}
  (Â¬dâˆ€A : Â¬ has-decidable-âˆ€-Level l1 A)
  (B : Discrete-Type l2)
  where abstract

  rice-contrapositive-not-has-decidable-âˆ€-Discrete-Type :
    (f : UU l1 â†’ type-Discrete-Type B) â†’
    (X Y : UU l1) â†’ f X ï¼ f Y
  rice-contrapositive-not-has-decidable-âˆ€-Discrete-Type f X Y =
    separate-map-split-on-propÂ¬Â¬-not-has-decidable-âˆ€-Level-Discrete-Type
      ( Â¬dâˆ€A)
      ( B)
      ( f)
      ( Î» _ â†’ split-on-double-negation-stable-prop-Discrete-Type B f)
      ( Î» _ P p â†’
        eq-equiv
          ( compute-split-on-propÂ¬Â¬-true-parametric-types
            ( prop-Double-Negation-Stable-Prop P)
            ( p)))
      ( Î» _ P np â†’
        eq-equiv
          ( compute-split-on-propÂ¬Â¬-false-parametric-types
            ( prop-Double-Negation-Stable-Prop P)
            ( np)))

  is-parametric-not-has-decidable-âˆ€-Discrete-Type :
    is-parametric l1 (type-Discrete-Type B)
  is-parametric-not-has-decidable-âˆ€-Discrete-Type =
    is-equiv-is-invertible
      ( ev (raise-empty l1))
      ( Î» f â†’
        eq-htpy
          ( rice-contrapositive-not-has-decidable-âˆ€-Discrete-Type f
            ( raise-empty l1)))
      ( refl-htpy)
```

### If the weak limited principle of omniscience is refuted, then discrete types are parametric

```agda
is-parametric-lzero-not-WLPO-Discrete-Type :
  {l : Level} â†’
  Â¬ level-WLPO lzero â†’
  (B : Discrete-Type l) â†’
  is-parametric lzero (type-Discrete-Type B)
is-parametric-lzero-not-WLPO-Discrete-Type =
  is-parametric-not-has-decidable-âˆ€-Discrete-Type
```

This result could be made fully universe polymorphic with some extra universe
raising infrastructure.

## See also

- [Subuniverse-parametric types](foundation.subuniverse-parametric-types.md)
- [Parametricity of the booleans](foundation.parametricity-booleans.md)
- [The parametricity axiom](foundation.parametricity-axiom.md)
