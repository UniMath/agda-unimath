---
title: Embeddings
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation.embeddings where

open import foundation-core.embeddings public

open import foundation-core.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation-core.functions using (_âˆ˜_)
open import foundation-core.fundamental-theorem-of-identity-types using
  ( fundamental-theorem-id-sec)
open import foundation-core.homotopies using
  ( _~_; nat-htpy; inv-htpy; refl-htpy)
open import foundation-core.propositional-maps using
  ( is-emb-is-prop-map; is-prop-map-is-emb)
open import foundation-core.sections using (sec)
open import foundation-core.truncation-levels using (neg-one-ğ•‹)
open import foundation-core.universe-levels using (Level; UU; _âŠ”_)

open import foundation.equivalences using
  ( is-equiv-top-is-equiv-left-square; is-equiv-comp; is-equiv-right-factor;
    is-equiv; is-emb-is-equiv; map-inv-is-equiv; triangle-section;
    issec-map-inv-is-equiv; is-equiv-map-inv-is-equiv; is-property-is-equiv)
open import foundation.identity-types using
  ( ap; concat'; concat; is-equiv-concat; is-equiv-concat'; ap-comp)
open import foundation.propositions using (is-prop; is-prop-Î ; UU-Prop)
open import foundation.pullbacks using (is-pullback)
open import foundation.truncated-maps using
  ( is-trunc-map-is-trunc-domain-codomain; is-trunc-is-pullback)
open import foundation.universal-property-pullbacks using (cone)
```

## Properties

### Being an embedding is a property

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where
  
  is-prop-is-emb : (f : A â†’ B) â†’ is-prop (is-emb f)
  is-prop-is-emb f =
    is-prop-Î  (Î» x â†’ is-prop-Î  (Î» y â†’ is-property-is-equiv (ap f)))

  is-emb-Prop : (A â†’ B) â†’ UU-Prop (l1 âŠ” l2)
  pr1 (is-emb-Prop f) = is-emb f
  pr2 (is-emb-Prop f) = is-prop-is-emb f
```

### Embeddings are closed under homotopies

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f g : A â†’ B) (H : f ~ g)
  where

  abstract
    is-emb-htpy : is-emb g â†’ is-emb f
    is-emb-htpy is-emb-g x y =
      is-equiv-top-is-equiv-left-square
        ( ap g)
        ( concat' (f x) (H y))
        ( ap f)
        ( concat (H x) (g y))
        ( nat-htpy H)
        ( is-equiv-concat (H x) (g y))
        ( is-emb-g x y)
        ( is-equiv-concat' (f x) (H y))

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f g : A â†’ B) (H : f ~ g)
  where
  
  abstract
    is-emb-htpy' : is-emb f â†’ is-emb g
    is-emb-htpy' is-emb-f =
      is-emb-htpy g f (inv-htpy H) is-emb-f
```

### Any map between propositions is an embedding

```
is-emb-is-prop :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-prop A â†’ is-prop B â†’ is-emb f
is-emb-is-prop H K =
  is-emb-is-prop-map (is-trunc-map-is-trunc-domain-codomain neg-one-ğ•‹ H K)
```

### Embeddings are closed under composition

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  where

  abstract
    is-emb-comp :
      (f : A â†’ C) (g : B â†’ C) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’ is-emb g â†’
      is-emb h â†’ is-emb f
    is-emb-comp f g h H is-emb-g is-emb-h =
      is-emb-htpy f (g âˆ˜ h) H
        ( Î» x y â†’ is-equiv-comp (ap (g âˆ˜ h)) (ap g) (ap h) (ap-comp g h)
          ( is-emb-h x y)
          ( is-emb-g (h x) (h y)))

  abstract
    is-emb-comp' :
      (g : B â†’ C) (h : A â†’ B) â†’ is-emb g â†’ is-emb h â†’ is-emb (g âˆ˜ h)
    is-emb-comp' g h = is-emb-comp (g âˆ˜ h) g h refl-htpy

    comp-emb :
      (B â†ª C) â†’ (A â†ª B) â†’ (A â†ª C)
    pr1 (comp-emb (pair g H) (pair f K)) = g âˆ˜ f
    pr2 (comp-emb (pair g H) (pair f K)) = is-emb-comp' g f H K
```

### The right factor of a composed embedding is an embedding

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  where

  abstract
    is-emb-right-factor :
      (f : A â†’ C) (g : B â†’ C) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’ is-emb g â†’
      is-emb f â†’ is-emb h
    is-emb-right-factor f g h H is-emb-g is-emb-f x y =
      is-equiv-right-factor
        ( ap (g âˆ˜ h))
        ( ap g)
        ( ap h)
        ( ap-comp g h)
        ( is-emb-g (h x) (h y))
        ( is-emb-htpy (g âˆ˜ h) f (inv-htpy H) is-emb-f x y)

  abstract
    is-emb-triangle-is-equiv :
      (f : A â†’ C) (g : B â†’ C) (e : A â†’ B) (H : f ~ (g âˆ˜ e)) â†’
      is-equiv e â†’ is-emb g â†’ is-emb f
    is-emb-triangle-is-equiv f g e H is-equiv-e is-emb-g =
      is-emb-comp f g e H is-emb-g (is-emb-is-equiv is-equiv-e)

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  where

  abstract
    is-emb-triangle-is-equiv' :
      (f : A â†’ C) (g : B â†’ C) (e : A â†’ B) (H : f ~ (g âˆ˜ e)) â†’
      is-equiv e â†’ is-emb f â†’ is-emb g
    is-emb-triangle-is-equiv' f g e H is-equiv-e is-emb-f =
      is-emb-triangle-is-equiv g f
        ( map-inv-is-equiv is-equiv-e)
        ( triangle-section f g e H
          ( pair
            ( map-inv-is-equiv is-equiv-e)
            ( issec-map-inv-is-equiv is-equiv-e)))
        ( is-equiv-map-inv-is-equiv is-equiv-e)
        ( is-emb-f)
```

### If the action on identifications has a section, then f is an embedding

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where
  
  abstract
    is-emb-sec-ap :
      ((x y : A) â†’ sec (ap f {x = x} {y = y})) â†’ is-emb f
    is-emb-sec-ap sec-ap-f x y =
      fundamental-theorem-id-sec x (Î» y â†’ ap f {y = y}) (sec-ap-f x) y
```

### Embeddings are closed under pullback

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  {X : UU l4} (f : A â†’ X) (g : B â†’ X) (c : cone f g C)
  where
  
  abstract
    is-emb-is-pullback : is-pullback f g c â†’ is-emb g â†’ is-emb (pr1 c)
    is-emb-is-pullback pb is-emb-g =
      is-emb-is-prop-map
        ( is-trunc-is-pullback neg-one-ğ•‹ f g c pb (is-prop-map-is-emb is-emb-g))
```
