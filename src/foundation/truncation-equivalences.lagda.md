# `k`-Equivalences

```agda
module foundation.truncation-equivalences where
```

<details><summary>Imports</summary>

```agda
open import foundation.commuting-squares-of-maps
open import foundation.dependent-pair-types
open import foundation.functoriality-dependent-pair-types
open import foundation.functoriality-truncation
open import foundation.truncations
open import foundation.universal-property-dependent-pair-types
open import foundation.universal-property-truncation
open import foundation.universe-levels

open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.functoriality-function-types
open import foundation-core.homotopies
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Idea

A map `f : A â†’ B` is said to be a `k`-equivalence if the map
`map-trunc k f : trunc k A â†’ trunc k B` is an equivalence.

## Definition

```agda
is-truncation-equivalence :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU (l1 âŠ” l2)
is-truncation-equivalence k f = is-equiv (map-trunc k f)

truncation-equivalence :
  {l1 l2 : Level} (k : ğ•‹) â†’ UU l1 â†’ UU l2 â†’ UU (l1 âŠ” l2)
truncation-equivalence k A B = Î£ (A â†’ B) (is-truncation-equivalence k)

module _
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2}
  (f : truncation-equivalence k A B)
  where

  map-truncation-equivalence : A â†’ B
  map-truncation-equivalence = pr1 f

  is-truncation-equivalence-truncation-equivalence :
    is-truncation-equivalence k map-truncation-equivalence
  is-truncation-equivalence-truncation-equivalence = pr2 f
```

## Properties

### A map `f : A â†’ B` is a `k`-equivalence if and only if `- âˆ˜ f : (B â†’ X) â†’ (A â†’ X)` is an equivalence for every `k`-truncated type `X`

```agda
is-equiv-precomp-is-truncation-equivalence :
  {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-truncation-equivalence k f â†’
  (X : Truncated-Type l3 k) â†’ is-equiv (precomp f (type-Truncated-Type X))
is-equiv-precomp-is-truncation-equivalence k f H X =
  is-equiv-bottom-is-equiv-top-square
    ( precomp unit-trunc (type-Truncated-Type X))
    ( precomp unit-trunc (type-Truncated-Type X))
    ( precomp (map-trunc k f) (type-Truncated-Type X))
    ( precomp f (type-Truncated-Type X))
    ( precomp-coherence-square-maps
      ( unit-trunc)
      ( f)
      ( map-trunc k f)
      ( unit-trunc)
      ( inv-htpy (coherence-square-map-trunc k f))
      ( type-Truncated-Type X))
    ( is-truncation-trunc X)
    ( is-truncation-trunc X)
    ( is-equiv-precomp-is-equiv (map-trunc k f) H (type-Truncated-Type X))

is-truncation-equivalence-is-equiv-precomp :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  ( (l : Level) (X : Truncated-Type l k) â†’
    is-equiv (precomp f (type-Truncated-Type X))) â†’
  is-truncation-equivalence k f
is-truncation-equivalence-is-equiv-precomp k {A} {B} f H =
  is-equiv-is-equiv-precomp-Truncated-Type k
    ( trunc k A)
    ( trunc k B)
    ( map-trunc k f)
    ( Î» X â†’
      is-equiv-top-is-equiv-bottom-square
        ( precomp unit-trunc (type-Truncated-Type X))
        ( precomp unit-trunc (type-Truncated-Type X))
        ( precomp (map-trunc k f) (type-Truncated-Type X))
        ( precomp f (type-Truncated-Type X))
        ( precomp-coherence-square-maps
          ( unit-trunc)
          ( f)
          ( map-trunc k f)
          ( unit-trunc)
          ( inv-htpy (coherence-square-map-trunc k f))
          ( type-Truncated-Type X))
        ( is-truncation-trunc X)
        ( is-truncation-trunc X)
        ( H _ X))
```

### The map on dependent pair types induced by the unit of the `(k+1)`-truncation is a `k`-equivalence

This is Lemma 2.27 of Christensen, Opie, Rijke & Scoccola listed below.

```agda
module _
  {l1 l2 : Level} {k : ğ•‹}
  {X : UU l1} (P : (type-trunc (succ-ğ•‹ k) X) â†’ UU l2)
  where

  map-Î£-map-base-unit-trunc :
    Î£ X (P âˆ˜ unit-trunc) â†’ Î£ (type-trunc (succ-ğ•‹ k) X) P
  map-Î£-map-base-unit-trunc = map-Î£-map-base unit-trunc P

  is-truncation-equivalence-map-Î£-map-base-unit-trunc :
    is-truncation-equivalence k map-Î£-map-base-unit-trunc
  is-truncation-equivalence-map-Î£-map-base-unit-trunc =
    is-truncation-equivalence-is-equiv-precomp k
      ( map-Î£-map-base-unit-trunc)
      ( Î» l X â†’
        is-equiv-equiv
          ( equiv-ev-pair)
          ( equiv-ev-pair)
          ( refl-htpy)
          ( dependent-universal-property-trunc
            ( Î» t â†’
              ( ( P t â†’ type-Truncated-Type X) ,
                ( is-trunc-succ-is-trunc k
                  ( is-trunc-function-type k
                    ( is-trunc-type-Truncated-Type X)))))))
```

## References

The notion of `k`-equivalence is a special case of the notion of
`L`-equivalence, where `L` is a reflective subuniverse. They were studied in the
paper

- J. D. Christensen, M. Opie, E. Rijke, and L. Scoccola. Localization in
  Homotopy Type Theory. Higher Structures, 2020.

The class of `k`-equivalences is left orthogonal to the class of `k`-Ã©tale maps.
This was shown in

- F. Cherubini, and E. Rijke. Modal descent. Mathematical Structures in Computer
  Science, 2021.
