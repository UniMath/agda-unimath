# Locally small types

```agda
module foundation.locally-small-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.inhabited-subtypes
open import foundation.subuniverses
open import foundation.univalence
open import foundation.universe-levels

open import foundation-core.embeddings
open import foundation-core.equality-dependent-pair-types
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.propositions
open import foundation-core.small-types
open import foundation-core.subtypes
open import foundation-core.transport-along-identifications
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Idea

A type is said to be
{{#concept "locally small" Disambiguation="type" Agda=is-locally-small}} with
respect to a [universe](foundation.universe-levels.md) `UU l` if its
[identity types](foundation-core.identity-types.md) are
[small](foundation-core.small-types.md) with respect to that universe.

## Definition

### Locally small types

```agda
is-locally-small :
  (l : Level) {l1 : Level} (A : UU l1) ‚Üí UU (lsuc l ‚äî l1)
is-locally-small l A = (x y : A) ‚Üí is-small l (x Ôºù y)

module _
  {l l1 : Level} {A : UU l1} (H : is-locally-small l A) (x y : A)
  where

  type-is-locally-small : UU l
  type-is-locally-small = pr1 (H x y)

  equiv-is-locally-small : (x Ôºù y) ‚âÉ type-is-locally-small
  equiv-is-locally-small = pr2 (H x y)

  inv-equiv-is-locally-small : type-is-locally-small ‚âÉ (x Ôºù y)
  inv-equiv-is-locally-small = inv-equiv equiv-is-locally-small

  map-equiv-is-locally-small : (x Ôºù y) ‚Üí type-is-locally-small
  map-equiv-is-locally-small = map-equiv equiv-is-locally-small

  map-inv-equiv-is-locally-small : type-is-locally-small ‚Üí (x Ôºù y)
  map-inv-equiv-is-locally-small = map-inv-equiv equiv-is-locally-small
```

### The subuniverse of `UU l1`-locally small types in `UU l2`

```agda
Locally-Small-Type : (l1 l2 : Level) ‚Üí UU (lsuc l1 ‚äî lsuc l2)
Locally-Small-Type l1 l2 = Œ£ (UU l2) (is-locally-small l1)

module _
  {l1 l2 : Level} (A : Locally-Small-Type l1 l2)
  where

  type-Locally-Small-Type : UU l2
  type-Locally-Small-Type = pr1 A

  is-locally-small-type-Locally-Small-Type :
    is-locally-small l1 type-Locally-Small-Type
  is-locally-small-type-Locally-Small-Type = pr2 A

  small-identity-type-Locally-Small-Type :
    (x y : type-Locally-Small-Type) ‚Üí UU l1
  small-identity-type-Locally-Small-Type =
    type-is-locally-small is-locally-small-type-Locally-Small-Type

  equiv-is-locally-small-type-Locally-Small-Type :
    (x y : type-Locally-Small-Type) ‚Üí
    (x Ôºù y) ‚âÉ small-identity-type-Locally-Small-Type x y
  equiv-is-locally-small-type-Locally-Small-Type =
    equiv-is-locally-small is-locally-small-type-Locally-Small-Type
```

## Properties

### Being locally small is a property

```agda
is-prop-is-locally-small :
  (l : Level) {l1 : Level} (A : UU l1) ‚Üí is-prop (is-locally-small l A)
is-prop-is-locally-small l A =
  is-prop-Œ† (Œª x ‚Üí is-prop-Œ† (Œª y ‚Üí is-prop-is-small l (x Ôºù y)))

is-locally-small-Prop :
  (l : Level) {l1 : Level} (A : UU l1) ‚Üí Prop (lsuc l ‚äî l1)
pr1 (is-locally-small-Prop l A) = is-locally-small l A
pr2 (is-locally-small-Prop l A) = is-prop-is-locally-small l A
```

### Any type in `UU l` is `l`-locally small

```agda
is-locally-small' : {l : Level} {A : UU l} ‚Üí is-locally-small l A
is-locally-small' x y = is-small'
```

### Locally small types are closed under embeddings

```agda
is-locally-small-emb :
  {l1 l2 l : Level} {A : UU l1} {B : UU l2} ‚Üí
  A ‚Ü™ B ‚Üí is-locally-small l B ‚Üí is-locally-small l A
is-locally-small-emb f H x y =
  is-small-equiv
    ( map-emb f x Ôºù map-emb f y)
    ( equiv-ap-emb f)
    ( H (map-emb f x) (map-emb f y))
```

### Locally small types are closed under equivalences

```agda
is-locally-small-equiv :
  {l1 l2 l : Level} {A : UU l1} {B : UU l2} ‚Üí
  A ‚âÉ B ‚Üí is-locally-small l B ‚Üí is-locally-small l A
is-locally-small-equiv e = is-locally-small-emb (emb-equiv e)
```

### Any small type is locally small

```agda
is-locally-small-is-small :
  {l l1 : Level} {A : UU l1} ‚Üí is-small l A ‚Üí is-locally-small l A
pr1 (is-locally-small-is-small (X , e) x y) =
  map-equiv e x Ôºù map-equiv e y
pr2 (is-locally-small-is-small (X , e) x y) = equiv-ap e x y
```

### Any proposition is locally small

```agda
is-locally-small-is-prop :
  (l : Level) {l1 : Level} {A : UU l1} ‚Üí is-prop A ‚Üí is-locally-small l A
is-locally-small-is-prop l H x y = is-small-is-contr l (H x y)
```

### Any univalent universe is locally small

```agda
is-locally-small-UU :
  {l : Level} ‚Üí is-locally-small l (UU l)
pr1 (is-locally-small-UU X Y) = X ‚âÉ Y
pr2 (is-locally-small-UU X Y) = equiv-univalence
```

### Any Œ£-type of locally small types is locally small

```agda
is-locally-small-Œ£ :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : A ‚Üí UU l2} ‚Üí
  is-locally-small l3 A ‚Üí ((x : A) ‚Üí is-locally-small l4 (B x)) ‚Üí
  is-locally-small (l3 ‚äî l4) (Œ£ A B)
is-locally-small-Œ£ {B = B} H K x y =
  is-small-equiv
    ( Eq-Œ£ x y)
    ( equiv-pair-eq-Œ£ x y)
    ( is-small-Œ£
      ( H (pr1 x) (pr1 y))
      ( Œª p ‚Üí K (pr1 y) (tr B p (pr2 x)) (pr2 y)))

Œ£-Locally-Small-Type :
  {l1 l2 l3 l4 : Level} (A : Locally-Small-Type l1 l2) ‚Üí
  (type-Locally-Small-Type A ‚Üí Locally-Small-Type l3 l4) ‚Üí
  Locally-Small-Type (l1 ‚äî l3) (l2 ‚äî l4)
pr1 (Œ£-Locally-Small-Type A B) =
  Œ£ (type-Locally-Small-Type A) (type-Locally-Small-Type ‚àò B)
pr2 (Œ£-Locally-Small-Type A B) =
  is-locally-small-Œ£
    ( is-locally-small-type-Locally-Small-Type A)
    ( is-locally-small-type-Locally-Small-Type ‚àò B)
```

### The underlying type of a subtype of a locally small type is locally small

```agda
is-locally-small-type-subtype :
  {l1 l2 l3 : Level} {A : UU l1} (P : subtype l2 A) ‚Üí
  is-locally-small l3 A ‚Üí is-locally-small l3 (type-subtype P)
is-locally-small-type-subtype {l3 = l3} P H =
  is-locally-small-Œ£ H
    ( Œª a ‚Üí is-locally-small-is-prop l3 (is-prop-is-in-subtype P a))

type-subtype-Locally-Small-Type :
  {l1 l2 l3 : Level} (A : Locally-Small-Type l1 l2) ‚Üí
  subtype l3 (type-Locally-Small-Type A) ‚Üí Locally-Small-Type l1 (l2 ‚äî l3)
pr1 (type-subtype-Locally-Small-Type A P) = type-subtype P
pr2 (type-subtype-Locally-Small-Type A P) =
  is-locally-small-type-subtype P (is-locally-small-type-Locally-Small-Type A)
```

### Any product of locally small types indexed by a small type is small

```agda
is-locally-small-Œ† :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : A ‚Üí UU l2} ‚Üí
  is-small l3 A ‚Üí ((x : A) ‚Üí is-locally-small l4 (B x)) ‚Üí
  is-locally-small (l3 ‚äî l4) ((x : A) ‚Üí B x)
is-locally-small-Œ† H K f g =
  is-small-equiv
    ( f ~ g)
    ( equiv-funext)
    ( is-small-Œ† H (Œª x ‚Üí K x (f x) (g x)))

Œ†-Locally-Small-Type :
  {l1 l2 l3 l4 : Level} (A : Small-Type l1 l2) ‚Üí
  (type-Small-Type A ‚Üí Locally-Small-Type l3 l4) ‚Üí
  Locally-Small-Type (l1 ‚äî l3) (l2 ‚äî l4)
pr1 (Œ†-Locally-Small-Type A B) =
  (a : type-Small-Type A) ‚Üí type-Locally-Small-Type (B a)
pr2 (Œ†-Locally-Small-Type A B) =
  is-locally-small-Œ†
    ( is-small-type-Small-Type A)
    ( is-locally-small-type-Locally-Small-Type ‚àò B)
```

### The type of types in any given subuniverse is locally small

```agda
is-locally-small-type-subuniverse :
  {l1 l2 : Level} (P : subuniverse l1 l2) ‚Üí
  is-locally-small l1 (type-subuniverse P)
is-locally-small-type-subuniverse P =
  is-locally-small-type-subtype P is-locally-small-UU
```

### The type of locally small types is locally small

```agda
is-locally-small-Locally-Small-Type :
  {l1 l2 : Level} ‚Üí is-locally-small l2 (Locally-Small-Type l1 l2)
is-locally-small-Locally-Small-Type {l1} =
  is-locally-small-type-subuniverse (is-locally-small-Prop l1)
```

### The type of truncated types is locally small

```agda
is-locally-small-Truncated-Type :
  {l : Level} (k : ùïã) ‚Üí is-locally-small l (Truncated-Type l k)
is-locally-small-Truncated-Type k =
  is-locally-small-type-subuniverse (is-trunc-Prop k)
```

### The type of propositions is locally small

```agda
is-locally-small-type-Prop :
  {l : Level} ‚Üí is-locally-small l (Prop l)
is-locally-small-type-Prop = is-locally-small-Truncated-Type neg-one-ùïã
```

### The type of subtypes of a small type is locally small

```agda
is-locally-small-subtype :
  {l1 l2 l3 : Level} {A : UU l1} ‚Üí
  is-small l2 A ‚Üí is-locally-small (l2 ‚äî l3) (subtype l3 A)
is-locally-small-subtype H =
  is-locally-small-Œ† H (Œª _ ‚Üí is-locally-small-type-Prop)
```

### The type of inhabited subtypes of a small type is locally small

```agda
is-locally-small-inhabited-subtype :
  {l1 l2 l3 : Level} {A : UU l1} ‚Üí
  is-small l2 A ‚Üí is-locally-small (l2 ‚äî l3) (inhabited-subtype l3 A)
is-locally-small-inhabited-subtype H =
  is-locally-small-type-subtype
    ( is-inhabited-subtype-Prop)
    ( is-locally-small-subtype H)
```

## See also

- [The replacement axiom](foundation.replacement.md)

## References

- Theorem 4.6 in {{#cite Rij17}}.
- Section 2.19 in {{#cite SymmetryBook}}.

{{#bibliography}}
