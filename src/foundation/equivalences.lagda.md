# Equivalences

```agda
module foundation.equivalences where

open import foundation-core.equivalences public
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cones-over-cospans
open import foundation.dependent-pair-types
open import foundation.equivalence-extensionality
open import foundation.function-extensionality
open import foundation.functoriality-fibers-of-maps
open import foundation.identity-systems
open import foundation.identity-types
open import foundation.truncated-maps
open import foundation.type-theoretic-principle-of-choice
open import foundation.universe-levels

open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation-core.embeddings
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.propositions
open import foundation-core.pullbacks
open import foundation-core.retractions
open import foundation-core.sections
open import foundation-core.sets
open import foundation-core.subtypes
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Properties

### Any equivalence is an embedding

We already proved in `foundation-core.equivalences` that equivalences are
embeddings. Here we have `_â†ª_` available, so we record the map from equivalences
to embeddings.

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  emb-equiv : (A â‰ƒ B) â†’ (A â†ª B)
  pr1 (emb-equiv e) = map-equiv e
  pr2 (emb-equiv e) = is-emb-is-equiv (is-equiv-map-equiv e)
```

### Transposing equalities along equivalences

The fact that equivalences are embeddings has many important consequences, we
will use some of these consequences in order to derive basic properties of
embeddings.

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B)
  where

  eq-transpose-equiv :
    (x : A) (y : B) â†’ (map-equiv e x ï¼ y) â‰ƒ (x ï¼ map-inv-equiv e y)
  eq-transpose-equiv x y =
    ( inv-equiv (equiv-ap e x (map-inv-equiv e y))) âˆ˜e
    ( equiv-concat'
      ( map-equiv e x)
      ( inv (is-section-map-inv-equiv e y)))

  map-eq-transpose-equiv :
    {x : A} {y : B} â†’ map-equiv e x ï¼ y â†’ x ï¼ map-inv-equiv e y
  map-eq-transpose-equiv {x} {y} = map-equiv (eq-transpose-equiv x y)

  inv-map-eq-transpose-equiv :
    {x : A} {y : B} â†’ x ï¼ map-inv-equiv e y â†’ map-equiv e x ï¼ y
  inv-map-eq-transpose-equiv {x} {y} = map-inv-equiv (eq-transpose-equiv x y)

  triangle-eq-transpose-equiv :
    {x : A} {y : B} (p : map-equiv e x ï¼ y) â†’
    ( ( ap (map-equiv e) (map-eq-transpose-equiv p)) âˆ™
      ( is-section-map-inv-equiv e y)) ï¼
    ( p)
  triangle-eq-transpose-equiv {x} {y} p =
    ( ap
      ( concat' (map-equiv e x) (is-section-map-inv-equiv e y))
      ( is-section-map-inv-equiv
        ( equiv-ap e x (map-inv-equiv e y))
        ( p âˆ™ inv (is-section-map-inv-equiv e y)))) âˆ™
    ( ( assoc
        ( p)
        ( inv (is-section-map-inv-equiv e y))
        ( is-section-map-inv-equiv e y)) âˆ™
      ( ( ap (concat p y) (left-inv (is-section-map-inv-equiv e y))) âˆ™
        ( right-unit)))

  map-eq-transpose-equiv' :
    {a : A} {b : B} â†’ b ï¼ map-equiv e a â†’ map-inv-equiv e b ï¼ a
  map-eq-transpose-equiv' p = inv (map-eq-transpose-equiv (inv p))

  inv-map-eq-transpose-equiv' :
    {a : A} {b : B} â†’ map-inv-equiv e b ï¼ a â†’ b ï¼ map-equiv e a
  inv-map-eq-transpose-equiv' p =
    inv (inv-map-eq-transpose-equiv (inv p))

  triangle-eq-transpose-equiv' :
    {x : A} {y : B} (p : y ï¼ map-equiv e x) â†’
    ( (is-section-map-inv-equiv e y) âˆ™ p) ï¼
    ( ap (map-equiv e) (map-eq-transpose-equiv' p))
  triangle-eq-transpose-equiv' {x} {y} p =
    map-inv-equiv
      ( equiv-ap
        ( equiv-inv (map-equiv e (map-inv-equiv e y)) (map-equiv e x))
        ( (is-section-map-inv-equiv e y) âˆ™ p)
        ( ap (map-equiv e) (map-eq-transpose-equiv' p)))
      ( ( distributive-inv-concat (is-section-map-inv-equiv e y) p) âˆ™
        ( ( inv
            ( con-inv
              ( ap (map-equiv e) (inv (map-eq-transpose-equiv' p)))
              ( is-section-map-inv-equiv e y)
              ( inv p)
              ( ( ap
                  ( concat' (map-equiv e x) (is-section-map-inv-equiv e y))
                  ( ap
                    ( ap (map-equiv e))
                    ( inv-inv
                      ( map-inv-equiv
                        ( equiv-ap e x (map-inv-equiv e y))
                        ( ( inv p) âˆ™
                          ( inv (is-section-map-inv-equiv e y))))))) âˆ™
                ( triangle-eq-transpose-equiv (inv p))))) âˆ™
          ( ap-inv (map-equiv e) (map-eq-transpose-equiv' p))))
```

## If dependent precomposition by `f` is an equivalence, then precomposition by `f` is an equivalence

```agda
abstract
  is-equiv-precomp-is-equiv-precomp-Î  :
    {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    ((C : B â†’ UU l3) â†’ is-equiv (precomp-Î  f C)) â†’
    ((C : UU l3) â†’ is-equiv (precomp f C))
  is-equiv-precomp-is-equiv-precomp-Î  f is-equiv-precomp-Î -f C =
    is-equiv-precomp-Î -f (Î» y â†’ C)
```

### If `f` is an equivalence, then precomposition by `f` is an equivalence

```agda
abstract
  is-equiv-precomp-is-equiv :
    {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’ is-equiv f â†’
    (C : UU l3) â†’ is-equiv (precomp f C)
  is-equiv-precomp-is-equiv f is-equiv-f =
    is-equiv-precomp-is-equiv-precomp-Î  f
      ( is-equiv-precomp-Î -is-equiv f is-equiv-f)

equiv-precomp :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) (C : UU l3) â†’
  (B â†’ C) â‰ƒ (A â†’ C)
pr1 (equiv-precomp e C) = precomp (map-equiv e) C
pr2 (equiv-precomp e C) =
  is-equiv-precomp-is-equiv (map-equiv e) (is-equiv-map-equiv e) C
```

### If precomposing by `f` is an equivalence, then `f` is an equivalence

First, we prove this relative to a subuniverse, such that `f` is a map between
two types in that subuniverse.

```agda
module _
  { l1 l2 : Level}
  ( Î± : Level â†’ Level) (P : (l : Level) â†’ UU l â†’ UU (Î± l))
  ( A : Î£ (UU l1) (P l1)) (B : Î£ (UU l2) (P l2)) (f : pr1 A â†’ pr1 B)
  ( H : (l : Level) (C : Î£ (UU l) (P l)) â†’ is-equiv (precomp f (pr1 C)))
  where

  map-inv-is-equiv-precomp-subuniverse : pr1 B â†’ pr1 A
  map-inv-is-equiv-precomp-subuniverse =
    pr1 (center (is-contr-map-is-equiv (H _ A) id))

  is-section-map-inv-is-equiv-precomp-subuniverse :
    ( f âˆ˜ map-inv-is-equiv-precomp-subuniverse) ~ id
  is-section-map-inv-is-equiv-precomp-subuniverse =
    htpy-eq
      ( ap
        ( pr1)
        ( eq-is-contr'
          ( is-contr-map-is-equiv (H _ B) f)
          ( ( f âˆ˜ (pr1 (center (is-contr-map-is-equiv (H _ A) id)))) ,
            ( ap
              ( Î» (g : pr1 A â†’ pr1 A) â†’ f âˆ˜ g)
              ( pr2 (center (is-contr-map-is-equiv (H _ A) id)))))
          ( id , refl)))

  is-retraction-map-inv-is-equiv-precomp-subuniverse :
    ( map-inv-is-equiv-precomp-subuniverse âˆ˜ f) ~ id
  is-retraction-map-inv-is-equiv-precomp-subuniverse =
    htpy-eq (pr2 (center (is-contr-map-is-equiv (H _ A) id)))

  abstract
    is-equiv-is-equiv-precomp-subuniverse :
      is-equiv f
    is-equiv-is-equiv-precomp-subuniverse =
      is-equiv-has-inverse
        ( map-inv-is-equiv-precomp-subuniverse)
        ( is-section-map-inv-is-equiv-precomp-subuniverse)
        ( is-retraction-map-inv-is-equiv-precomp-subuniverse)
```

Now we prove the usual statement, without the subuniverse

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-equiv-is-equiv-precomp :
      (f : A â†’ B) â†’ ((l : Level) (C : UU l) â†’ is-equiv (precomp f C)) â†’
      is-equiv f
    is-equiv-is-equiv-precomp f is-equiv-precomp-f =
      is-equiv-is-equiv-precomp-subuniverse
        ( Î» l â†’ l1 âŠ” l2)
        ( Î» l X â†’ A â†’ B)
        ( pair A f)
        ( pair B f)
        ( f)
        ( Î» l C â†’ is-equiv-precomp-f l (pr1 C))
```

```agda
is-equiv-is-equiv-precomp-Prop :
  {l1 l2 : Level} (P : Prop l1) (Q : Prop l2)
  (f : type-Prop P â†’ type-Prop Q) â†’
  ({l : Level} (R : Prop l) â†’ is-equiv (precomp f (type-Prop R))) â†’
  is-equiv f
is-equiv-is-equiv-precomp-Prop P Q f H =
  is-equiv-is-equiv-precomp-subuniverse id (Î» l â†’ is-prop) P Q f (Î» l â†’ H {l})

is-equiv-is-equiv-precomp-Set :
  {l1 l2 : Level} (A : Set l1) (B : Set l2)
  (f : type-Set A â†’ type-Set B) â†’
  ({l : Level} (C : Set l) â†’ is-equiv (precomp f (type-Set C))) â†’
  is-equiv f
is-equiv-is-equiv-precomp-Set A B f H =
  is-equiv-is-equiv-precomp-subuniverse id (Î» l â†’ is-set) A B f (Î» l â†’ H {l})

is-equiv-is-equiv-precomp-Truncated-Type :
  {l1 l2 : Level} (k : ğ•‹)
  (A : Truncated-Type l1 k) (B : Truncated-Type l2 k)
  (f : type-Truncated-Type A â†’ type-Truncated-Type B) â†’
  ({l : Level} (C : Truncated-Type l k) â†’ is-equiv (precomp f (pr1 C))) â†’
  is-equiv f
is-equiv-is-equiv-precomp-Truncated-Type k A B f H =
    is-equiv-is-equiv-precomp-subuniverse id (Î» l â†’ is-trunc k) A B f
      ( Î» l â†’ H {l})
```

### Equivalences have a contractible type of sections

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-contr-section-is-equiv : {f : A â†’ B} â†’ is-equiv f â†’ is-contr (section f)
  is-contr-section-is-equiv {f} is-equiv-f =
    is-contr-equiv'
      ( (b : B) â†’ fib f b)
      ( distributive-Î -Î£)
      ( is-contr-Î  (is-contr-map-is-equiv is-equiv-f))
```

### Equivalences have a contractible type of retractions

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-contr-retraction-is-equiv :
    {f : A â†’ B} â†’ is-equiv f â†’ is-contr (retraction f)
  is-contr-retraction-is-equiv {f} is-equiv-f =
    is-contr-is-equiv'
      ( Î£ (B â†’ A) (Î» h â†’ (h âˆ˜ f) ï¼ id))
      ( tot (Î» h â†’ htpy-eq))
      ( is-equiv-tot-is-fiberwise-equiv
        ( Î» h â†’ funext (h âˆ˜ f) id))
      ( is-contr-map-is-equiv (is-equiv-precomp-is-equiv f is-equiv-f A) id)
```

### Being an equivalence is a property

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-contr-is-equiv-is-equiv : {f : A â†’ B} â†’ is-equiv f â†’ is-contr (is-equiv f)
  is-contr-is-equiv-is-equiv is-equiv-f =
    is-contr-prod
      ( is-contr-section-is-equiv is-equiv-f)
      ( is-contr-retraction-is-equiv is-equiv-f)

  abstract
    is-property-is-equiv : (f : A â†’ B) â†’ (H K : is-equiv f) â†’ is-contr (H ï¼ K)
    is-property-is-equiv f H =
      is-prop-is-contr (is-contr-is-equiv-is-equiv H) H

  is-equiv-Prop : (f : A â†’ B) â†’ Prop (l1 âŠ” l2)
  pr1 (is-equiv-Prop f) = is-equiv f
  pr2 (is-equiv-Prop f) = is-property-is-equiv f

  eq-equiv-eq-map-equiv :
    {e e' : A â‰ƒ B} â†’ (map-equiv e) ï¼ (map-equiv e') â†’ e ï¼ e'
  eq-equiv-eq-map-equiv = eq-type-subtype is-equiv-Prop

  abstract
    is-emb-map-equiv :
      is-emb (map-equiv {A = A} {B = B})
    is-emb-map-equiv = is-emb-inclusion-subtype is-equiv-Prop

  emb-map-equiv : (A â‰ƒ B) â†ª (A â†’ B)
  pr1 emb-map-equiv = map-equiv
  pr2 emb-map-equiv = is-emb-map-equiv
```

### Homotopy induction for homotopies between equivalences

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    Ind-htpy-equiv :
      {l3 : Level} (e : A â‰ƒ B)
      (P : (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ UU l3) â†’
      section
        ( Î» (h : (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ P e' H) â†’
          h e (refl-htpy-equiv e))
    Ind-htpy-equiv e =
      Ind-identity-system e
        ( refl-htpy-equiv e)
        ( is-contr-total-htpy-equiv e)

  ind-htpy-equiv :
    {l3 : Level} (e : A â‰ƒ B) (P : (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ UU l3) â†’
    P e (refl-htpy-equiv e) â†’ (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ P e' H
  ind-htpy-equiv e P = pr1 (Ind-htpy-equiv e P)

  compute-ind-htpy-equiv :
    {l3 : Level} (e : A â‰ƒ B) (P : (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ UU l3)
    (p : P e (refl-htpy-equiv e)) â†’
    ind-htpy-equiv e P p e (refl-htpy-equiv e) ï¼ p
  compute-ind-htpy-equiv e P = pr2 (Ind-htpy-equiv e P)
```

### The groupoid laws for equivalences

```agda
associative-comp-equiv :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {D : UU l4} â†’
  (e : A â‰ƒ B) (f : B â‰ƒ C) (g : C â‰ƒ D) â†’
  ((g âˆ˜e f) âˆ˜e e) ï¼ (g âˆ˜e (f âˆ˜e e))
associative-comp-equiv e f g = eq-equiv-eq-map-equiv refl

module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2}
  where

  left-unit-law-equiv : (e : X â‰ƒ Y) â†’ (id-equiv âˆ˜e e) ï¼ e
  left-unit-law-equiv e = eq-equiv-eq-map-equiv refl

  right-unit-law-equiv : (e : X â‰ƒ Y) â†’ (e âˆ˜e id-equiv) ï¼ e
  right-unit-law-equiv e = eq-equiv-eq-map-equiv refl

  left-inverse-law-equiv : (e : X â‰ƒ Y) â†’ ((inv-equiv e) âˆ˜e e) ï¼ id-equiv
  left-inverse-law-equiv e =
    eq-htpy-equiv (is-retraction-map-inv-is-equiv (is-equiv-map-equiv e))

  right-inverse-law-equiv : (e : X â‰ƒ Y) â†’ (e âˆ˜e (inv-equiv e)) ï¼ id-equiv
  right-inverse-law-equiv e =
    eq-htpy-equiv (is-section-map-inv-is-equiv (is-equiv-map-equiv e))

  inv-inv-equiv : (e : X â‰ƒ Y) â†’ (inv-equiv (inv-equiv e)) ï¼ e
  inv-inv-equiv e = eq-equiv-eq-map-equiv refl

  inv-inv-equiv' : (e : Y â‰ƒ X) â†’ (inv-equiv (inv-equiv e)) ï¼ e
  inv-inv-equiv' e = eq-equiv-eq-map-equiv refl

  is-equiv-inv-equiv : is-equiv (inv-equiv {A = X} {B = Y})
  is-equiv-inv-equiv =
    is-equiv-has-inverse
      ( inv-equiv)
      ( inv-inv-equiv')
      ( inv-inv-equiv)

  equiv-inv-equiv : (X â‰ƒ Y) â‰ƒ (Y â‰ƒ X)
  pr1 equiv-inv-equiv = inv-equiv
  pr2 equiv-inv-equiv = is-equiv-inv-equiv

coh-unit-laws-equiv :
  {l : Level} {X : UU l} â†’
  left-unit-law-equiv (id-equiv {A = X}) ï¼
  right-unit-law-equiv (id-equiv {A = X})
coh-unit-laws-equiv {l} {X} = ap eq-equiv-eq-map-equiv refl

module _
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} {Z : UU l3}
  where

  distributive-inv-comp-equiv :
    (e : X â‰ƒ Y) (f : Y â‰ƒ Z) â†’
    (inv-equiv (f âˆ˜e e)) ï¼ ((inv-equiv e) âˆ˜e (inv-equiv f))
  distributive-inv-comp-equiv e f =
    eq-htpy-equiv
      ( Î» x â†’
        map-eq-transpose-equiv'
          ( f âˆ˜e e)
          ( ( ap (Î» g â†’ map-equiv g x) (inv (right-inverse-law-equiv f))) âˆ™
            ( ap
              ( Î» g â†’ map-equiv (f âˆ˜e (g âˆ˜e (inv-equiv f))) x)
              ( inv (right-inverse-law-equiv e)))))

comp-inv-equiv-comp-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  (f : B â‰ƒ C) (e : A â‰ƒ B) â†’ (inv-equiv f âˆ˜e (f âˆ˜e e)) ï¼ e
comp-inv-equiv-comp-equiv f e =
  eq-htpy-equiv (Î» x â†’ is-retraction-map-inv-equiv f (map-equiv e x))

comp-equiv-comp-inv-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  (f : B â‰ƒ C) (e : A â‰ƒ C) â†’
  (f âˆ˜e (inv-equiv f âˆ˜e e)) ï¼ e
comp-equiv-comp-inv-equiv f e =
  eq-htpy-equiv (Î» x â†’ is-section-map-inv-equiv f (map-equiv e x))

is-equiv-comp-equiv :
  {l1 l2 l3 : Level} {B : UU l2} {C : UU l3}
  (f : B â‰ƒ C) (A : UU l1) â†’ is-equiv (Î» (e : A â‰ƒ B) â†’ f âˆ˜e e)
is-equiv-comp-equiv f A =
  is-equiv-has-inverse
    ( Î» e â†’ inv-equiv f âˆ˜e e)
    ( comp-equiv-comp-inv-equiv f)
    ( comp-inv-equiv-comp-equiv f)

equiv-postcomp-equiv :
  {l1 l2 l3 : Level} {B : UU l2} {C : UU l3} â†’
  (f : B â‰ƒ C) â†’ (A : UU l1) â†’ (A â‰ƒ B) â‰ƒ (A â‰ƒ C)
pr1 (equiv-postcomp-equiv f A) e = f âˆ˜e e
pr2 (equiv-postcomp-equiv f A) = is-equiv-comp-equiv f A
```

```agda
equiv-precomp-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} â†’
  (A â‰ƒ B) â†’ (C : UU l3) â†’ (B â‰ƒ C) â‰ƒ (A â‰ƒ C)
equiv-precomp-equiv e C =
  equiv-subtype-equiv
    ( equiv-precomp e C)
    ( is-equiv-Prop)
    ( is-equiv-Prop)
    ( Î» g â†’
      pair
        ( is-equiv-comp g (map-equiv e) (is-equiv-map-equiv e))
        ( Î» is-equiv-eg â†’
          is-equiv-left-factor
            g (map-equiv e) is-equiv-eg (is-equiv-map-equiv e)))
```

### A cospan in which one of the legs is an equivalence is a pullback if and only if the corresponding map on the cone is an equivalence

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  {X : UU l4} (f : A â†’ X) (g : B â†’ X) (c : cone f g C)
  where

  abstract
    is-equiv-is-pullback : is-equiv g â†’ is-pullback f g c â†’ is-equiv (pr1 c)
    is-equiv-is-pullback is-equiv-g pb =
      is-equiv-is-contr-map
        ( is-trunc-is-pullback neg-two-ğ•‹ f g c pb
          ( is-contr-map-is-equiv is-equiv-g))

  abstract
    is-pullback-is-equiv : is-equiv g â†’ is-equiv (pr1 c) â†’ is-pullback f g c
    is-pullback-is-equiv is-equiv-g is-equiv-p =
      is-pullback-is-fiberwise-equiv-map-fib-cone f g c
        ( Î» a â†’ is-equiv-is-contr
          ( map-fib-cone f g c a)
          ( is-contr-map-is-equiv is-equiv-p a)
          ( is-contr-map-is-equiv is-equiv-g (f a)))
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  {X : UU l4} (f : A â†’ X) (g : B â†’ X) (c : cone f g C)
  where

  abstract
    is-equiv-is-pullback' :
      is-equiv f â†’ is-pullback f g c â†’ is-equiv (pr1 (pr2 c))
    is-equiv-is-pullback' is-equiv-f pb =
      is-equiv-is-contr-map
        ( is-trunc-is-pullback' neg-two-ğ•‹ f g c pb
          ( is-contr-map-is-equiv is-equiv-f))

  abstract
    is-pullback-is-equiv' :
      is-equiv f â†’ is-equiv (pr1 (pr2 c)) â†’ is-pullback f g c
    is-pullback-is-equiv' is-equiv-f is-equiv-q =
      is-pullback-swap-cone' f g c
        ( is-pullback-is-equiv g f
          ( swap-cone f g c)
          is-equiv-f
          is-equiv-q)
```

### Families of equivalences are equivalent to fiberwise equivalences

```agda
equiv-fiberwise-equiv-fam-equiv :
  {l1 l2 l3 : Level} {A : UU l1} (B : A â†’ UU l2) (C : A â†’ UU l3) â†’
  fam-equiv B C â‰ƒ fiberwise-equiv B C
equiv-fiberwise-equiv-fam-equiv B C = distributive-Î -Î£
```

## See also

- For the notions of inverses and coherently invertible maps, also known as
  half-adjoint equivalences, see
  [`foundation.coherently-invertible-maps`](foundation.coherently-invertible-maps.md).
- For the notion of maps with contractible fibers see
  [`foundation.contractible-maps`](foundation.contractible-maps.md).
- For the notion of path-split maps see
  [`foundation.path-split-maps`](foundation.path-split-maps.md).
