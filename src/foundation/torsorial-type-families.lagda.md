# Torsorial type families

```agda
open import foundation.function-extensionality-axiom

module
  foundation.torsorial-type-families
  (funext : function-extensionality)
  where

open import foundation-core.torsorial-type-families public
```

<details><summary>Imports</summary>

```agda
open import foundation.contractible-types funext
open import foundation.dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.logical-equivalences funext
open import foundation.universal-property-identity-types funext
open import foundation.universe-levels

open import foundation-core.equivalences
open import foundation-core.identity-types
open import foundation-core.propositions
open import foundation-core.type-theoretic-principle-of-choice
```

</details>

## Idea

A type family `E` over `B` is said to be **torsorial** if its
[total space](foundation.dependent-pair-types.md) is
[contractible](foundation.contractible-types.md). By the
[fundamental theorem of identity types](foundation.fundamental-theorem-of-identity-types.md)
it follows that a type family `E` is torsorial if and only if it is in the
[image](foundation.images.md) of `Id : B ‚Üí (B ‚Üí ùí∞)`.

## Definitions

### The predicate of being a torsorial type family over `B`

```agda
is-torsorial-Prop :
  {l1 l2 : Level} {B : UU l1} ‚Üí (B ‚Üí UU l2) ‚Üí Prop (l1 ‚äî l2)
is-torsorial-Prop E = is-contr-Prop (Œ£ _ E)

is-prop-is-torsorial :
  {l1 l2 : Level} {B : UU l1} (E : B ‚Üí UU l2) ‚Üí is-prop (is-torsorial E)
is-prop-is-torsorial E = is-prop-type-Prop (is-torsorial-Prop E)
```

### The type of torsorial type families over `B`

```agda
torsorial-family-of-types :
  {l1 : Level} (l2 : Level) ‚Üí UU l1 ‚Üí UU (l1 ‚äî lsuc l2)
torsorial-family-of-types l2 B = Œ£ (B ‚Üí UU l2) is-torsorial

module _
  {l1 l2 : Level} {B : UU l1} (T : torsorial-family-of-types l2 B)
  where

  type-torsorial-family-of-types : B ‚Üí UU l2
  type-torsorial-family-of-types = pr1 T

  is-torsorial-torsorial-family-of-types :
    is-torsorial type-torsorial-family-of-types
  is-torsorial-torsorial-family-of-types = pr2 T
```

## Properties

### `fiber Id B ‚âÉ is-torsorial B` for any type family `B` over `A`

In other words, a type family `B` over `A` is in the
[image](foundation.images.md) of `Id : A ‚Üí (A ‚Üí ùí∞)` if and only if `B` is
torsorial. Since being contractible is a
[proposition](foundation.propositions.md), this observation leads to an
alternative proof of the above claim that `Id : A ‚Üí (A ‚Üí ùí∞)` is an
[embedding](foundation.embeddings.md). Our previous proof of the fact that
`Id : A ‚Üí (A ‚Üí ùí∞)` is an embedding can be found in
[`foundation.universal-property-identity-types`](foundation.universal-property-identity-types.md).

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A ‚Üí UU l2}
  where

  is-torsorial-fiber-Id :
    {a : A} ‚Üí ((x : A) ‚Üí (a Ôºù x) ‚âÉ B x) ‚Üí is-torsorial B
  is-torsorial-fiber-Id H =
    fundamental-theorem-id'
      ( Œª x ‚Üí map-equiv (H x))
      ( Œª x ‚Üí is-equiv-map-equiv (H x))

  fiber-Id-is-torsorial :
    is-torsorial B ‚Üí Œ£ A (Œª a ‚Üí (x : A) ‚Üí (a Ôºù x) ‚âÉ B x)
  pr1 (fiber-Id-is-torsorial ((a , b) , H)) = a
  pr2 (fiber-Id-is-torsorial ((a , b) , H)) =
    map-inv-distributive-Œ†-Œ£ (f , fundamental-theorem-id ((a , b) , H) f)
    where
    f : (x : A) ‚Üí (a Ôºù x) ‚Üí B x
    f x refl = b

  compute-fiber-Id :
    (Œ£ A (Œª a ‚Üí (x : A) ‚Üí (a Ôºù x) ‚âÉ B x)) ‚âÉ is-torsorial B
  compute-fiber-Id =
    equiv-iff
      ( Œ£ A (Œª a ‚Üí (x : A) ‚Üí (a Ôºù x) ‚âÉ B x) ,
        is-prop-total-family-of-equivalences-Id)
      ( is-contr-Prop (Œ£ A B))
      ( Œª u ‚Üí is-torsorial-fiber-Id (pr2 u))
      ( fiber-Id-is-torsorial)
```

### See also

- [Discrete reflexive relations](foundation.discrete-reflexive-relations.md) are
  binary torsorial type families.
