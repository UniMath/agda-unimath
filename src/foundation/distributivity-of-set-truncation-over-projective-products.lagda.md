# Distributivity of set truncation over projective products

```agda
module foundation.distributivity-of-set-truncation-over-projective-products where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.equivalences
open import foundation.fibers-of-maps
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-truncation
open import foundation.homotopies
open import foundation.identity-types
open import foundation.inhabited-types
open import foundation.mere-equality
open import foundation.projective-types
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.reflecting-maps-equivalence-relations
open import foundation.set-truncations
open import foundation.sets
open import foundation.surjective-maps
open import foundation.truncation-levels
open import foundation.universal-property-set-quotients
open import foundation.universe-levels
```

</details>

## Idea

[Set truncation](foundation.set-truncations.md) distributes over dependent
products on [projective](foundation.projective-types.md) types $X$. The
distributive map

$$
  â•‘ (x : X) â†’ A x â•‘â‚€ â†’ ((x : X) â†’ â•‘ A x â•‘â‚€)
$$

is an [equivalence](foundation-core.equivalences.md)

## Properties

### Distributivity of set truncation over projective types

```agda
module _
  {l1 l2 : Level} (X : UU l1) (Y : X â†’ UU l2)
  where

  set-map-Î -unit-trunc : Set (l1 âŠ” l2)
  set-map-Î -unit-trunc =
    ( ( (x : X) â†’ â•‘ Y x â•‘â‚€) ,
      ( is-set-Î  (Î» x â†’ is-set-type-trunc-Set {A = Y x})))

  mere-eq-map-Î -unit-trunc-is-projective-Level :
    (H : is-projective-Level l2 X)
    (f g : (x : X) â†’ Y x) â†’
    map-Î  (Î» x â†’ unit-trunc-Set) f ï¼ map-Î  (Î» x â†’ unit-trunc-Set) g â†’
    mere-eq f g
  mere-eq-map-Î -unit-trunc-is-projective-Level H f g p =
    map-is-inhabited
      ( eq-htpy)
      ( H ( Î» x â†’ f x ï¼ g x)
          ( Î» x â†’ apply-effectiveness-unit-trunc-Set (htpy-eq p x)))

  reflecting-map-map-Î -unit-trunc :
    reflecting-map-equivalence-relation
      ( mere-eq-equivalence-relation ((x : X) â†’ Y x))
      ( type-Set set-map-Î -unit-trunc)
  pr1 reflecting-map-map-Î -unit-trunc =
    map-Î  (Î» x â†’ unit-trunc-Set)
  pr2 reflecting-map-map-Î -unit-trunc {f} {g} r =
    apply-universal-property-trunc-Prop
      ( r)
      ( Id-Prop
        ( set-map-Î -unit-trunc)
        ( map-Î  (Î» x â†’ unit-trunc-Set) f)
        ( map-Î  (Î» x â†’ unit-trunc-Set) g))
      ( ap (map-Î  (Î» x â†’ unit-trunc-Set)))

  map-map-Î -unit-trunc-trunc-is-projective-Level :
    (H : is-projective-Level l2 X) â†’
    â•‘ ((x : X) â†’ Y x) â•‘â‚€ â†’
    (x : X) â†’ â•‘ Y x â•‘â‚€
  map-map-Î -unit-trunc-trunc-is-projective-Level H =
    map-universal-property-set-quotient-is-set-quotient
      ( mere-eq-equivalence-relation ((x : X) â†’ Y x))
      ( trunc-Set ((x : X) â†’ Y x))
      ( reflecting-map-mere-eq-unit-trunc-Set ((x : X) â†’ Y x))
      ( is-set-quotient-trunc-Set ((x : X) â†’ Y x))
      ( set-map-Î -unit-trunc)
      ( reflecting-map-map-Î -unit-trunc)

  triangle-map-map-Î -unit-trunc-trunc-is-projective-Level :
    (H : is-projective-Level l2 X) â†’
    map-map-Î -unit-trunc-trunc-is-projective-Level H âˆ˜ unit-trunc-Set ~
    map-Î  (Î» x â†’ unit-trunc-Set)
  triangle-map-map-Î -unit-trunc-trunc-is-projective-Level H =
    triangle-universal-property-set-quotient-is-set-quotient
      ( mere-eq-equivalence-relation ((x : X) â†’ Y x))
      ( trunc-Set ((x : X) â†’ Y x))
      ( reflecting-map-mere-eq-unit-trunc-Set ((x : X) â†’ Y x))
      ( is-set-quotient-trunc-Set ((x : X) â†’ Y x))
      ( set-map-Î -unit-trunc)
      ( reflecting-map-map-Î -unit-trunc)

  htpy-map-distributive-trunc-Î -map-map-Î -unit-trunc-trunc-is-projective-Level :
    (H : is-projective-Level l2 X) â†’
    map-distributive-trunc-Î  zero-ğ•‹ Y ~
    map-map-Î -unit-trunc-trunc-is-projective-Level H
  htpy-map-distributive-trunc-Î -map-map-Î -unit-trunc-trunc-is-projective-Level
    H t =
    apply-universal-property-trunc-Prop
      ( is-surjective-unit-trunc-Set ((x : X) â†’ Y x) t)
      ( Id-Prop
        ( set-map-Î -unit-trunc)
        ( map-distributive-trunc-Î  zero-ğ•‹ Y t)
        ( map-map-Î -unit-trunc-trunc-is-projective-Level H t))
      ( Î» (f , qf) â†’
        ( inv
          ( ap
            ( map-distributive-trunc-Î  zero-ğ•‹ Y)
            ( qf))) âˆ™
        ( eq-htpy (compute-distributive-trunc-Î  zero-ğ•‹ f)) âˆ™
        ( inv (triangle-map-map-Î -unit-trunc-trunc-is-projective-Level H f)) âˆ™
        ( ap (map-map-Î -unit-trunc-trunc-is-projective-Level H) (qf)))

  is-surjective-map-distributive-trunc-Î -is-projective-Level :
    is-projective-Level l2 X â†’
    is-surjective (map-distributive-trunc-Î  zero-ğ•‹ Y)
  is-surjective-map-distributive-trunc-Î -is-projective-Level H t =
    map-is-inhabited
      ( Î» s â†’
        ( unit-trunc-Set (Î» x â†’ pr1 (s x)) ,
          eq-htpy
            ( Î» x â†’
              ( compute-distributive-trunc-Î  zero-ğ•‹ (Î» y â†’ pr1 (s y)) x) âˆ™
              ( pr2 (s x)))))
      ( H ( Î» x â†’ fiber (unit-trunc-Set {A = Y x}) (t x))
          ( Î» x â†’ is-surjective-unit-trunc-Set (Y x) (t x)))

  is-emb-map-distributive-trunc-Î -is-projective-Level :
    is-projective-Level l2 X â†’
    is-emb (map-distributive-trunc-Î  zero-ğ•‹ Y)
  is-emb-map-distributive-trunc-Î -is-projective-Level H =
    is-emb-htpy
      ( htpy-map-distributive-trunc-Î -map-map-Î -unit-trunc-trunc-is-projective-Level
        ( H))
      ( is-emb-map-universal-property-set-quotient-is-set-quotient
        ( mere-eq-equivalence-relation ((x : X) â†’ Y x))
        ( trunc-Set ((x : X) â†’ Y x))
        ( reflecting-map-mere-eq-unit-trunc-Set ((x : X) â†’ Y x))
        ( is-set-quotient-trunc-Set ((x : X) â†’ Y x))
        ( set-map-Î -unit-trunc)
        ( reflecting-map-map-Î -unit-trunc)
        ( mere-eq-map-Î -unit-trunc-is-projective-Level H))

  is-equiv-map-distributive-trunc-Î -is-projective-Level :
    is-projective-Level l2 X â†’
    is-equiv (map-distributive-trunc-Î  zero-ğ•‹ Y)
  is-equiv-map-distributive-trunc-Î -is-projective-Level H =
    is-equiv-is-emb-is-surjective
      ( is-surjective-map-distributive-trunc-Î -is-projective-Level H)
      ( is-emb-map-distributive-trunc-Î -is-projective-Level H)

  distributive-trunc-Î -is-projective-Level :
    is-projective-Level l2 X â†’
    is-contr
      ( Î£ ( â•‘ ((x : X) â†’ Y x) â•‘â‚€ â‰ƒ ( (x : X) â†’ â•‘ Y x â•‘â‚€))
          ( Î» e â†’ map-equiv e âˆ˜ unit-trunc-Set ~ map-Î  (Î» x â†’ unit-trunc-Set)))
  distributive-trunc-Î -is-projective-Level H =
    uniqueness-trunc-Set
      ( set-map-Î -unit-trunc)
      ( map-Î  (Î» x â†’ unit-trunc-Set))
      ( is-set-truncation-is-equiv
        ( set-map-Î -unit-trunc)
        ( map-Î  (Î» x â†’ unit-trunc-Set))
        ( Î» f â†’ eq-htpy (compute-distributive-trunc-Î  zero-ğ•‹ f))
        ( is-equiv-map-distributive-trunc-Î -is-projective-Level H))
```

### Set truncation distributes over dependent products over set-projective sets

```agda
module _
  {l1 l2 : Level} (X : UU l1) (Y : X â†’ UU l2)
  where

  is-equiv-map-distributive-trunc-Î -is-set-projective :
    is-set X â†’
    is-set-projective X â†’
    is-equiv (map-distributive-trunc-Î  zero-ğ•‹ Y)
  is-equiv-map-distributive-trunc-Î -is-set-projective K H =
    is-equiv-map-distributive-trunc-Î -is-projective-Level X Y
      ( is-projective-is-set-projective K H {l2})

  distributive-trunc-Î -is-set-projective :
    is-set X â†’
    is-set-projective X â†’
    is-contr
      ( Î£ ( â•‘ ((x : X) â†’ Y x) â•‘â‚€ â‰ƒ ((x : X) â†’ â•‘ Y x â•‘â‚€))
          ( Î» e â†’ map-equiv e âˆ˜ unit-trunc-Set ~ map-Î  (Î» x â†’ unit-trunc-Set)))
  distributive-trunc-Î -is-set-projective K H =
    distributive-trunc-Î -is-projective-Level X Y
      ( is-projective-is-set-projective K H {l2})
```

## See also

- [Distributivity of truncation over truncation-projective products](foundation.distributivity-of-truncation-over-truncation-projective-products.md)
- [Distributivity of set truncation over finite products](univalent-combinatorics.distributivity-of-set-truncation-over-finite-products.md)
