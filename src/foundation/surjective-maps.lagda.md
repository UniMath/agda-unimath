# Surjective maps

```agda
module foundation.surjective-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.connected-maps
open import foundation.contractible-types
open import foundation.embeddings
open import foundation.homotopies
open import foundation.identity-types
open import foundation.monomorphisms
open import foundation.propositional-truncations
open import foundation.structure-identity-principle
open import foundation.truncated-types
open import foundation.type-theoretic-principle-of-choice
open import foundation.univalence
open import foundation.universal-property-propositional-truncation

open import foundation-core.constant-maps
open import foundation-core.contractible-maps
open import foundation-core.dependent-pair-types
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.functions
open import foundation-core.functoriality-dependent-function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.fundamental-theorem-of-identity-types
open import foundation-core.injective-maps
open import foundation-core.propositional-maps
open import foundation-core.propositions
open import foundation-core.sections
open import foundation-core.sets
open import foundation-core.subtype-identity-principle
open import foundation-core.truncated-maps
open import foundation-core.truncation-levels
open import foundation-core.universe-levels

open import orthogonal-factorization-systems.extensions-of-maps
```

</details>

## Idea

A map `f : A â†’ B` is surjective if all of its fibers are inhabited.

## Definition

### Surjective maps

```agda
is-surjective-Prop :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ Prop (l1 âŠ” l2)
is-surjective-Prop {B = B} f =
  Î -Prop B (Î» b â†’ trunc-Prop (fib f b))

is-surjective :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU (l1 âŠ” l2)
is-surjective f = type-Prop (is-surjective-Prop f)

is-prop-is-surjective :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-prop (is-surjective f)
is-prop-is-surjective f = is-prop-type-Prop (is-surjective-Prop f)

_â† _ :
  {l1 l2 : Level} â†’ UU l1 â†’ UU l2 â†’ UU (l1 âŠ” l2)
A â†  B = Î£ (A â†’ B) is-surjective

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†  B)
  where

  map-surjection : A â†’ B
  map-surjection = pr1 f

  is-surjective-map-surjection : is-surjective map-surjection
  is-surjective-map-surjection = pr2 f
```

### The type of all surjective maps out of a type

```agda
Surjection : {l1 : Level} (l2 : Level) â†’ UU l1 â†’ UU (l1 âŠ” lsuc l2)
Surjection l2 A = Î£ (UU l2) (Î» X â†’ A â†  X)

module _
  {l1 l2 : Level} {A : UU l1} (f : Surjection l2 A)
  where

  type-Surjection : UU l2
  type-Surjection = pr1 f

  surjection-Surjection : A â†  type-Surjection
  surjection-Surjection = pr2 f

  map-Surjection : A â†’ type-Surjection
  map-Surjection = map-surjection surjection-Surjection

  is-surjective-map-Surjection : is-surjective map-Surjection
  is-surjective-map-Surjection =
    is-surjective-map-surjection surjection-Surjection
```

### The type of all surjective maps into `k`-truncated types

```agda
Surjection-Into-Truncated-Type :
  {l1 : Level} (l2 : Level) (k : ğ•‹) â†’ UU l1 â†’ UU (l1 âŠ” lsuc l2)
Surjection-Into-Truncated-Type l2 k A =
  Î£ (Truncated-Type l2 k) (Î» X â†’ A â†  type-Truncated-Type X)

emb-inclusion-Surjection-Into-Truncated-Type :
  {l1 : Level} (l2 : Level) (k : ğ•‹) (A : UU l1) â†’
  Surjection-Into-Truncated-Type l2 k A â†ª Surjection l2 A
emb-inclusion-Surjection-Into-Truncated-Type l2 k A =
  emb-Î£ (Î» X â†’ A â†  X) (emb-type-Truncated-Type l2 k) (Î» X â†’ id-emb)

inclusion-Surjection-Into-Truncated-Type :
  {l1 l2 : Level} {k : ğ•‹} {A : UU l1} â†’
  Surjection-Into-Truncated-Type l2 k A â†’ Surjection l2 A
inclusion-Surjection-Into-Truncated-Type {l1} {l2} {k} {A} =
  map-emb (emb-inclusion-Surjection-Into-Truncated-Type l2 k A)

module _
  {l1 l2 : Level} {k : ğ•‹} {A : UU l1}
  (f : Surjection-Into-Truncated-Type l2 k A)
  where

  truncated-type-Surjection-Into-Truncated-Type : Truncated-Type l2 k
  truncated-type-Surjection-Into-Truncated-Type = pr1 f

  type-Surjection-Into-Truncated-Type : UU l2
  type-Surjection-Into-Truncated-Type =
    type-Truncated-Type truncated-type-Surjection-Into-Truncated-Type

  is-trunc-type-Surjection-Into-Truncated-Type :
    is-trunc k type-Surjection-Into-Truncated-Type
  is-trunc-type-Surjection-Into-Truncated-Type =
    is-trunc-type-Truncated-Type
      truncated-type-Surjection-Into-Truncated-Type

  surjection-Surjection-Into-Truncated-Type :
    A â†  type-Surjection-Into-Truncated-Type
  surjection-Surjection-Into-Truncated-Type = pr2 f

  map-Surjection-Into-Truncated-Type :
    A â†’ type-Surjection-Into-Truncated-Type
  map-Surjection-Into-Truncated-Type =
    map-surjection surjection-Surjection-Into-Truncated-Type

  is-surjective-Surjection-Into-Truncated-Type :
    is-surjective map-Surjection-Into-Truncated-Type
  is-surjective-Surjection-Into-Truncated-Type =
    is-surjective-map-surjection surjection-Surjection-Into-Truncated-Type
```

### The type of all surjective maps into sets

```agda
Surjection-Into-Set :
  {l1 : Level} (l2 : Level) â†’ UU l1 â†’ UU (l1 âŠ” lsuc l2)
Surjection-Into-Set l2 A = Surjection-Into-Truncated-Type l2 zero-ğ•‹ A

emb-inclusion-Surjection-Into-Set :
  {l1 : Level} (l2 : Level) (A : UU l1) â†’
  Surjection-Into-Set l2 A â†ª Surjection l2 A
emb-inclusion-Surjection-Into-Set l2 A =
  emb-inclusion-Surjection-Into-Truncated-Type l2 zero-ğ•‹ A

inclusion-Surjection-Into-Set :
  {l1 l2 : Level} {A : UU l1} â†’
  Surjection-Into-Set l2 A â†’ Surjection l2 A
inclusion-Surjection-Into-Set {l1} {l2} {A} =
  inclusion-Surjection-Into-Truncated-Type

module _
  {l1 l2 : Level} {A : UU l1} (f : Surjection-Into-Set l2 A)
  where

  set-Surjection-Into-Set : Set l2
  set-Surjection-Into-Set = truncated-type-Surjection-Into-Truncated-Type f

  type-Surjection-Into-Set : UU l2
  type-Surjection-Into-Set = type-Surjection-Into-Truncated-Type f

  is-set-type-Surjection-Into-Set : is-set type-Surjection-Into-Set
  is-set-type-Surjection-Into-Set =
    is-trunc-type-Surjection-Into-Truncated-Type f

  surjection-Surjection-Into-Set : A â†  type-Surjection-Into-Set
  surjection-Surjection-Into-Set = surjection-Surjection-Into-Truncated-Type f

  map-Surjection-Into-Set : A â†’ type-Surjection-Into-Set
  map-Surjection-Into-Set = map-Surjection-Into-Truncated-Type f

  is-surjective-Surjection-Into-Set : is-surjective map-Surjection-Into-Set
  is-surjective-Surjection-Into-Set =
    is-surjective-Surjection-Into-Truncated-Type f
```

## Properties

### Any map that has a section is surjective

```agda
abstract
  is-surjective-has-section :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    sec f â†’ is-surjective f
  is-surjective-has-section (pair g G) b = unit-trunc-Prop (pair (g b) (G b))
```

### Any equivalence is surjective

```agda
is-surjective-is-equiv :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-equiv f â†’ is-surjective f
is-surjective-is-equiv H = is-surjective-has-section (pr1 H)

is-surjective-map-equiv :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
  is-surjective (map-equiv e)
is-surjective-map-equiv e = is-surjective-is-equiv (is-equiv-map-equiv e)
```

### The dependent universal property of surjective maps

```
dependent-universal-property-surj :
  (l : Level) {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  UU ((lsuc l) âŠ” l1 âŠ” l2)
dependent-universal-property-surj l {B = B} f =
  (P : B â†’ Prop l) â†’
    is-equiv (Î» (h : (b : B) â†’ type-Prop (P b)) x â†’ h (f x))

abstract
  is-surjective-dependent-universal-property-surj :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    ({l : Level} â†’ dependent-universal-property-surj l f) â†’
    is-surjective f
  is-surjective-dependent-universal-property-surj f dup-surj-f =
    map-inv-is-equiv
      ( dup-surj-f (Î» b â†’ trunc-Prop (fib f b)))
      ( Î» x â†’ unit-trunc-Prop (pair x refl))

abstract
  square-dependent-universal-property-surj :
    {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    (P : B â†’ Prop l3) â†’
    ( Î» (h : (y : B) â†’ type-Prop (P y)) x â†’ h (f x)) ~
    ( ( Î» h x â†’ h (f x) (pair x refl)) âˆ˜
      ( ( Î» h y â†’ (h y) âˆ˜ unit-trunc-Prop) âˆ˜
        ( Î» h y â†’ const (type-trunc-Prop (fib f y)) (type-Prop (P y)) (h y))))
  square-dependent-universal-property-surj f P = refl-htpy

  dependent-universal-property-surj-is-surjective :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    is-surjective f â†’
    ({l : Level} â†’ dependent-universal-property-surj l f)
  dependent-universal-property-surj-is-surjective f is-surj-f P =
    is-equiv-comp
      ( Î» h x â†’ h (f x) (pair x refl))
      ( ( Î» h y â†’ (h y) âˆ˜ unit-trunc-Prop) âˆ˜
        ( Î» h y â†’ const (type-trunc-Prop (fib f y)) (type-Prop (P y)) (h y)))
      ( is-equiv-comp
        ( Î» h y â†’ (h y) âˆ˜ unit-trunc-Prop)
        ( Î» h y â†’ const (type-trunc-Prop (fib f y)) (type-Prop (P y)) (h y))
        ( is-equiv-map-Î 
          ( Î» y p z â†’ p)
          ( Î» y â†’
            is-equiv-diagonal-is-contr
              ( is-proof-irrelevant-is-prop
                ( is-prop-type-trunc-Prop)
                ( is-surj-f y))
              ( type-Prop (P y))))
        ( is-equiv-map-Î 
          ( Î» b g â†’ g âˆ˜ unit-trunc-Prop)
          ( Î» b â†’ is-propositional-truncation-trunc-Prop (fib f b) (P b))))
      ( is-equiv-map-reduce-Î -fib f ( Î» y z â†’ type-Prop (P y)))

equiv-dependent-universal-property-surj-is-surjective :
  {l l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-surjective f â†’ (C : B â†’ Prop l) â†’
  ((b : B) â†’ type-Prop (C b)) â‰ƒ ((a : A) â†’ type-Prop (C (f a)))
pr1 (equiv-dependent-universal-property-surj-is-surjective f H C) h x = h (f x)
pr2 (equiv-dependent-universal-property-surj-is-surjective f H C) =
  dependent-universal-property-surj-is-surjective f H C

apply-dependent-universal-property-surj-is-surjective :
  {l l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-surjective f â†’ (C : B â†’ Prop l) â†’
  ((a : A) â†’ type-Prop (C (f a))) â†’ ((y : B) â†’ type-Prop (C y))
apply-dependent-universal-property-surj-is-surjective f H C =
  map-inv-equiv (equiv-dependent-universal-property-surj-is-surjective f H C)
```

### A map into a proposition is a propositional truncation if and only if it is surjective

```agda
abstract
  is-surjective-is-propositional-truncation :
    {l1 l2 : Level} {A : UU l1} {P : Prop l2} (f : A â†’ type-Prop P) â†’
    ( {l : Level} â†’
      dependent-universal-property-propositional-truncation l P f) â†’
    is-surjective f
  is-surjective-is-propositional-truncation f duppt-f =
    is-surjective-dependent-universal-property-surj f duppt-f

abstract
  is-propsitional-truncation-is-surjective :
    {l1 l2 : Level} {A : UU l1} {P : Prop l2} (f : A â†’ type-Prop P) â†’
    is-surjective f â†’
    {l : Level} â†’ dependent-universal-property-propositional-truncation l P f
  is-propsitional-truncation-is-surjective f is-surj-f =
    dependent-universal-property-surj-is-surjective f is-surj-f
```

### A map that is both surjective and an embedding is an equivalence

```agda
abstract
  is-equiv-is-emb-is-surjective :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-surjective f â†’ is-emb f â†’ is-equiv f
  is-equiv-is-emb-is-surjective {f = f} H K =
    is-equiv-is-contr-map
      ( Î» y â†’
        is-proof-irrelevant-is-prop
          ( is-prop-map-is-emb K y)
          ( apply-universal-property-trunc-Prop
            ( H y)
            ( fib-emb-Prop (pair f K) y)
            ( id)))
```

### The composite of surjective maps is surjective

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  where

  abstract
    is-surjective-comp-htpy :
      (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
      is-surjective g â†’ is-surjective h â†’ is-surjective f
    is-surjective-comp-htpy f g h H is-surj-g is-surj-h x =
      apply-universal-property-trunc-Prop
        ( is-surj-g x)
        ( trunc-Prop (fib f x))
        ( Î» { (pair b refl) â†’
          apply-universal-property-trunc-Prop
            ( is-surj-h b)
            ( trunc-Prop (fib f (g b)))
            ( Î» { (pair a refl) â†’
              unit-trunc-Prop (pair a (H a))})})

  is-surjective-comp :
    {g : B â†’ X} {h : A â†’ B} â†’
    is-surjective g â†’ is-surjective h â†’ is-surjective (g âˆ˜ h)
  is-surjective-comp {g} {h} =
    is-surjective-comp-htpy (g âˆ˜ h) g h refl-htpy
```

### The composite of a surjective map with an equivalence is surjective

```agda
is-surjective-comp-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  (e : B â‰ƒ C) â†’ {f : A â†’ B} â†’ is-surjective f â†’ is-surjective (map-equiv e âˆ˜ f)
is-surjective-comp-equiv e =
  is-surjective-comp (is-surjective-map-equiv e)
```

### The precomposite of a surjective map with an equivalence is surjective

```agda
is-surjective-precomp-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {f : B â†’ C} â†’
  is-surjective f â†’ (e : A â‰ƒ B) â†’ is-surjective (f âˆ˜ map-equiv e)
is-surjective-precomp-equiv H e =
  is-surjective-comp H (is-surjective-map-equiv e)
```

### If a composite is surjective, then so is its left factor

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  where

  abstract
    is-surjective-left-factor-htpy :
      (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
      is-surjective f â†’ is-surjective g
    is-surjective-left-factor-htpy f g h H is-surj-f x =
      apply-universal-property-trunc-Prop
        ( is-surj-f x)
        ( trunc-Prop (fib g x))
        ( Î» { (pair a refl) â†’
          unit-trunc-Prop (pair (h a) (inv (H a)))})

  is-surjective-left-factor :
    {g : B â†’ X} (h : A â†’ B) â†’ is-surjective (g âˆ˜ h) â†’ is-surjective g
  is-surjective-left-factor {g} h =
    is-surjective-left-factor-htpy (g âˆ˜ h) g h refl-htpy
```

### Surjective maps are -1-connected

```agda
is-neg-one-connected-map-is-surjective :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-surjective f â†’ is-connected-map neg-one-ğ•‹ f
is-neg-one-connected-map-is-surjective H b =
  is-proof-irrelevant-is-prop is-prop-type-trunc-Prop (H b)
```

### Precomposing functions into a family of (k+1)-types by a surjective map is a k-truncated map

```agda
is-trunc-map-precomp-Î -is-surjective :
  {l1 l2 l3 : Level} (k : ğ•‹) â†’
  {A : UU l1} {B : UU l2} {f : A â†’ B} â†’ is-surjective f â†’
  (P : B â†’ Truncated-Type l3 (succ-ğ•‹ k)) â†’
  is-trunc-map k (precomp-Î  f (Î» b â†’ type-Truncated-Type (P b)))
is-trunc-map-precomp-Î -is-surjective k H =
  is-trunc-map-precomp-Î -is-connected-map
    ( neg-one-ğ•‹)
    ( succ-ğ•‹ k)
    ( k)
    ( refl)
    ( is-neg-one-connected-map-is-surjective H)
```

### Characterization of the identity type of `A â†  B`

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†  B)
  where

  htpy-surjection : (A â†  B) â†’ UU (l1 âŠ” l2)
  htpy-surjection g = map-surjection f ~ map-surjection g

  refl-htpy-surjection : htpy-surjection f
  refl-htpy-surjection = refl-htpy

  is-contr-total-htpy-surjection : is-contr (Î£ (A â†  B) htpy-surjection)
  is-contr-total-htpy-surjection =
    is-contr-total-Eq-subtype
      ( is-contr-total-htpy (map-surjection f))
      ( is-prop-is-surjective)
      ( map-surjection f)
      ( refl-htpy)
      ( is-surjective-map-surjection f)

  htpy-eq-surjection :
    (g : A â†  B) â†’ (f ï¼ g) â†’ htpy-surjection g
  htpy-eq-surjection .f refl = refl-htpy-surjection

  is-equiv-htpy-eq-surjection :
    (g : A â†  B) â†’ is-equiv (htpy-eq-surjection g)
  is-equiv-htpy-eq-surjection =
    fundamental-theorem-id is-contr-total-htpy-surjection htpy-eq-surjection

  extensionality-surjection :
    (g : A â†  B) â†’ (f ï¼ g) â‰ƒ htpy-surjection g
  pr1 (extensionality-surjection g) = htpy-eq-surjection g
  pr2 (extensionality-surjection g) = is-equiv-htpy-eq-surjection g

  eq-htpy-surjection : (g : A â†  B) â†’ htpy-surjection g â†’ f ï¼ g
  eq-htpy-surjection g =
    map-inv-equiv (extensionality-surjection g)
```

### Characterization of the identity type of `Surjection l2 A`

```agda
equiv-Surjection :
  {l1 l2 l3 : Level} {A : UU l1} â†’
  Surjection l2 A â†’ Surjection l3 A â†’ UU (l1 âŠ” l2 âŠ” l3)
equiv-Surjection f g =
  Î£ ( type-Surjection f â‰ƒ type-Surjection g)
    ( Î» e â†’ (map-equiv e âˆ˜ map-Surjection f) ~ map-Surjection g)

module _
  {l1 l2 : Level} {A : UU l1} (f : Surjection l2 A)
  where

  id-equiv-Surjection : equiv-Surjection f f
  pr1 id-equiv-Surjection = id-equiv
  pr2 id-equiv-Surjection = refl-htpy

  is-contr-total-equiv-Surjection :
    is-contr (Î£ (Surjection l2 A) (equiv-Surjection f))
  is-contr-total-equiv-Surjection =
    is-contr-total-Eq-structure
      ( Î» Y g e â†’ (map-equiv e âˆ˜ map-Surjection f) ~ map-surjection g)
      ( is-contr-total-equiv (type-Surjection f))
      ( pair (type-Surjection f) id-equiv)
      ( is-contr-total-htpy-surjection (surjection-Surjection f))

  equiv-eq-Surjection :
    (g : Surjection l2 A) â†’ (f ï¼ g) â†’ equiv-Surjection f g
  equiv-eq-Surjection .f refl = id-equiv-Surjection

  is-equiv-equiv-eq-Surjection :
    (g : Surjection l2 A) â†’ is-equiv (equiv-eq-Surjection g)
  is-equiv-equiv-eq-Surjection =
    fundamental-theorem-id
      is-contr-total-equiv-Surjection
      equiv-eq-Surjection

  extensionality-Surjection :
    (g : Surjection l2 A) â†’ (f ï¼ g) â‰ƒ equiv-Surjection f g
  pr1 (extensionality-Surjection g) = equiv-eq-Surjection g
  pr2 (extensionality-Surjection g) = is-equiv-equiv-eq-Surjection g

  eq-equiv-Surjection :
    (g : Surjection l2 A) â†’ equiv-Surjection f g â†’ f ï¼ g
  eq-equiv-Surjection g = map-inv-equiv (extensionality-Surjection g)
```

### Characterization of the identity type of `Surjection-Into-Truncated-Type l2 k A`

```agda
equiv-Surjection-Into-Truncated-Type :
  {l1 l2 l3 : Level} {k : ğ•‹} {A : UU l1} â†’
  Surjection-Into-Truncated-Type l2 k A â†’
  Surjection-Into-Truncated-Type l3 k A â†’ UU (l1 âŠ” l2 âŠ” l3)
equiv-Surjection-Into-Truncated-Type f g =
  equiv-Surjection
    ( inclusion-Surjection-Into-Truncated-Type f)
    ( inclusion-Surjection-Into-Truncated-Type g)

module _
  {l1 l2 : Level} {k : ğ•‹} {A : UU l1}
  (f : Surjection-Into-Truncated-Type l2 k A)
  where

  id-equiv-Surjection-Into-Truncated-Type :
    equiv-Surjection-Into-Truncated-Type f f
  id-equiv-Surjection-Into-Truncated-Type =
    id-equiv-Surjection (inclusion-Surjection-Into-Truncated-Type f)

  extensionality-Surjection-Into-Truncated-Type :
    (g : Surjection-Into-Truncated-Type l2 k A) â†’
    (f ï¼ g) â‰ƒ equiv-Surjection-Into-Truncated-Type f g
  extensionality-Surjection-Into-Truncated-Type g =
    ( extensionality-Surjection
      ( inclusion-Surjection-Into-Truncated-Type f)
      ( inclusion-Surjection-Into-Truncated-Type g)) âˆ˜e
    ( equiv-ap-emb (emb-inclusion-Surjection-Into-Truncated-Type l2 k A))

  equiv-eq-Surjection-Into-Truncated-Type :
    (g : Surjection-Into-Truncated-Type l2 k A) â†’
    (f ï¼ g) â†’ equiv-Surjection-Into-Truncated-Type f g
  equiv-eq-Surjection-Into-Truncated-Type g =
    map-equiv (extensionality-Surjection-Into-Truncated-Type g)

  refl-equiv-eq-Surjection-Into-Truncated-Type :
    equiv-eq-Surjection-Into-Truncated-Type f refl ï¼
    id-equiv-Surjection-Into-Truncated-Type
  refl-equiv-eq-Surjection-Into-Truncated-Type = refl

  eq-equiv-Surjection-Into-Truncated-Type :
    (g : Surjection-Into-Truncated-Type l2 k A) â†’
    equiv-Surjection-Into-Truncated-Type f g â†’ f ï¼ g
  eq-equiv-Surjection-Into-Truncated-Type g =
    map-inv-equiv (extensionality-Surjection-Into-Truncated-Type g)
```

### The type `Surjection-Into-Truncated-Type l2 (succ-ğ•‹ k) A` is `k`-truncated

### Characterization of the identity type of `Surjection-Into-Set l2 A`

```agda
equiv-Surjection-Into-Set :
  {l1 l2 l3 : Level} {A : UU l1} â†’ Surjection-Into-Set l2 A â†’
  Surjection-Into-Set l3 A â†’ UU (l1 âŠ” l2 âŠ” l3)
equiv-Surjection-Into-Set = equiv-Surjection-Into-Truncated-Type

id-equiv-Surjection-Into-Set :
  {l1 l2 : Level} {A : UU l1} (f : Surjection-Into-Set l2 A) â†’
  equiv-Surjection-Into-Set f f
id-equiv-Surjection-Into-Set = id-equiv-Surjection-Into-Truncated-Type

extensionality-Surjection-Into-Set :
  {l1 l2 : Level} {A : UU l1} (f g : Surjection-Into-Set l2 A) â†’
  (f ï¼ g) â‰ƒ equiv-Surjection-Into-Set f g
extensionality-Surjection-Into-Set =
  extensionality-Surjection-Into-Truncated-Type

equiv-eq-Surjection-Into-Set :
  {l1 l2 : Level} {A : UU l1} (f g : Surjection-Into-Set l2 A) â†’
  (f ï¼ g) â†’ equiv-Surjection-Into-Set f g
equiv-eq-Surjection-Into-Set = equiv-eq-Surjection-Into-Truncated-Type

refl-equiv-eq-Surjection-Into-Set :
  {l1 l2 : Level} {A : UU l1} (f : Surjection-Into-Set l2 A) â†’
  equiv-eq-Surjection-Into-Set f f refl ï¼
  id-equiv-Surjection-Into-Set f
refl-equiv-eq-Surjection-Into-Set = refl-equiv-eq-Surjection-Into-Truncated-Type

eq-equiv-Surjection-Into-Set :
  {l1 l2 : Level} {A : UU l1} (f g : Surjection-Into-Set l2 A) â†’
  equiv-Surjection-Into-Set f g â†’ f ï¼ g
eq-equiv-Surjection-Into-Set = eq-equiv-Surjection-Into-Truncated-Type
```

### Postcomposition of extensions along surjective maps by an embedding is an equivalence

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-surjective-postcomp-extension-surjective-map :
    (f : A â†’ B) (i : A â†’ X) (g : X â†’ Y) â†’
    is-surjective f â†’ is-emb g â†’
    is-surjective (postcomp-extension f i g)
  is-surjective-postcomp-extension-surjective-map f i g H K (h , L) =
    unit-trunc-Prop
      ( ( j , N) ,
        ( eq-htpy-extension f
          ( g âˆ˜ i)
          ( postcomp-extension f i g (j , N))
          ( h , L)
          ( M)
          ( Î» a â†’
            ( ap
              ( concat' (g (i a)) (M (f a)))
              ( issec-map-inv-is-equiv
                ( K (i a) ((j (f a))))
                ( L a âˆ™ inv (M (f a))))) âˆ™
            ( issec-inv-concat' (g (i a)) (M (f a)) (L a)))))
    where

    J : (b : B) â†’ fib g (h b)
    J =
      apply-dependent-universal-property-surj-is-surjective f H
        ( Î» b â†’ fib-emb-Prop (g , K) (h b))
        ( Î» a â†’ (i a , L a))

    j : B â†’ X
    j b = pr1 (J b)

    M : (g âˆ˜ j) ~ h
    M b = pr2 (J b)

    N : i ~ (j âˆ˜ f)
    N a = map-inv-is-equiv (K (i a) (j (f a))) (L a âˆ™ inv (M (f a)))

  is-equiv-postcomp-extension-is-surjective :
    (f : A â†’ B) (i : A â†’ X) (g : X â†’ Y) â†’
    is-surjective f â†’ is-emb g â†’
    is-equiv (postcomp-extension f i g)
  is-equiv-postcomp-extension-is-surjective f i g H K =
    is-equiv-is-emb-is-surjective
      ( is-surjective-postcomp-extension-surjective-map f i g H K)
      ( is-emb-postcomp-extension f i g K)

  equiv-postcomp-extension-surjection :
    (f : A â†  B) (i : A â†’ X) (g : X â†ª Y) â†’
    extension (map-surjection f) i â‰ƒ
    extension (map-surjection f) (map-emb g âˆ˜ i)
  pr1 (equiv-postcomp-extension-surjection f i g) =
    postcomp-extension (map-surjection f) i (map-emb g)
  pr2 (equiv-postcomp-extension-surjection f i g) =
    is-equiv-postcomp-extension-is-surjective
      ( map-surjection f)
      ( i)
      ( map-emb g)
      ( is-surjective-map-surjection f)
      ( is-emb-map-emb g)
```
