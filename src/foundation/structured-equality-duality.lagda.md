# Structured equality duality

```agda
module foundation.structured-equality-duality where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.maps-in-subuniverses
open import foundation.separated-types-subuniverses
open import foundation.structure
open import foundation.subuniverses
open import foundation.universe-levels

open import foundation-core.equivalences
```

</details>

## Idea

Given a [structure](foundation.structure.md) `ğ’«` on types that transfers along
[equivalences](foundation-core.equivalences.md) in the sense that `ğ’«` comes
equipped with a family of maps

```text
  {X Y : ğ’°} â†’ (X â‰ƒ Y) â†’ ğ’« X â†’ ğ’« Y,
```

then for every type `A` and type family `B : A â†’ ğ’°` there is a
[mutual correspondence](foundation.logical-equivalences.md) between the
following:

1. `ğ’«`-structured families of maps `f : (y : A) â†’ (x ï¼ y) â†’ B y` for every
   `x : A`.
2. `ğ’«`-structures on the equality of `Î£ A B`.

We refer to this as
{{#concept "structured equality duality" Agda=structured-equality-duality}}.

**Note.** by [the univalence axiom](foundation.univalence.md), every structure
[transfers along equivalences](foundation.transport-along-equivalences.md).
However, we maintain this as an assumption, since for most common notions of
structure this property is independent of the univalence axiom.

One potential but crude justification for using the term "duality" for this
principle is as follows. The principle gives a correspondence between structures
on families of _maps mapping into_ the **type family** `B`, and structures on
the binary family of equality over the _dependent sum_ `Î£ A B`, and so, in a
certain sense, one is trading one straightened dimension for another.

## Duality

### Structured equality duality

```agda
module _
  {l1 l2 l3 : Level} {ğ’« : UU (l1 âŠ” l2) â†’ UU l3}
  (tr-ğ’« : {X Y : UU (l1 âŠ” l2)} â†’ X â‰ƒ Y â†’ ğ’« X â†’ ğ’« Y)
  {A : UU l1} {B : A â†’ UU l2}
  where

  forward-implication-structured-equality-duality :
    ( (x : A) (f : (y : A) â†’ (x ï¼ y) â†’ B y) (y : A) â†’ structure-map ğ’« (f y)) â†’
    structure-equality ğ’« (Î£ A B)
  forward-implication-structured-equality-duality
    K (x , b) (x' , b') =
    tr-ğ’«
      ( compute-fiber-map-out-of-identity-type (ind-Id x (Î» u _ â†’ B u) b) x' b')
      ( K x (ind-Id x (Î» u _ â†’ B u) b) x' b')

  backward-implication-structured-equality-duality :
    structure-equality ğ’« (Î£ A B) â†’
    ( (x : A) (f : (y : A) â†’ (x ï¼ y) â†’ B y) (y : A) â†’ structure-map ğ’« (f y))
  backward-implication-structured-equality-duality K x f y b =
    tr-ğ’«
      ( inv-compute-fiber-map-out-of-identity-type f y b)
      ( K (x , f x refl) (y , b))

  structured-equality-duality :
    ( (x : A) (f : (y : A) â†’ (x ï¼ y) â†’ B y) (y : A) â†’ structure-map ğ’« (f y)) â†”
    ( structure-equality ğ’« (Î£ A B))
  structured-equality-duality =
    ( forward-implication-structured-equality-duality ,
      backward-implication-structured-equality-duality)
```

### Subuniverse equality duality

Given a [subuniverse](foundation.subuniverses.md) `ğ’«` then the following are
logically equivalent:

1. For every `x : A`, every family of maps `f : (y : A) â†’ (x ï¼ y) â†’ B y` is a
   family of `ğ’«`-maps.
2. The dependent sum `Î£ A B` is `ğ’«`-separated.

```agda
module _
  {l1 l2 l3 : Level} (ğ’« : subuniverse (l1 âŠ” l2) l3)
  (tr-ğ’« :
    {X Y : UU (l1 âŠ” l2)} â†’
    X â‰ƒ Y â†’ is-in-subuniverse ğ’« X â†’ is-in-subuniverse ğ’« Y)
  {A : UU l1} {B : A â†’ UU l2}
  where

  forward-implication-subuniverse-equality-duality :
    ( (x : A) (f : (y : A) â†’ (x ï¼ y) â†’ B y)
      (y : A) â†’ is-in-subuniverse-map ğ’« (f y)) â†’
    is-separated ğ’« (Î£ A B)
  forward-implication-subuniverse-equality-duality =
    forward-implication-structured-equality-duality tr-ğ’«

  backward-implication-subuniverse-equality-duality :
    is-separated ğ’« (Î£ A B) â†’
    ( (x : A) (f : (y : A) â†’ (x ï¼ y) â†’ B y)
      (y : A) â†’ is-in-subuniverse-map ğ’« (f y))
  backward-implication-subuniverse-equality-duality =
    backward-implication-structured-equality-duality tr-ğ’«

  subuniverse-equality-duality :
    ( (x : A) (f : (y : A) â†’ (x ï¼ y) â†’ B y)
      (y : A) â†’ is-in-subuniverse-map ğ’« (f y)) â†”
    is-separated ğ’« (Î£ A B)
  subuniverse-equality-duality =
    structured-equality-duality tr-ğ’«
```

## See also

This duality is applied in

- [The Regensburg extension of the fundamental theorem of identity types](foundation.regensburg-extension-fundamental-theorem-of-identity-types.md)
- [The strong preunivalence axiom](foundation.strong-preunivalence.md)
- [Strongly preunivalent categories](category-theory.strongly-preunivalent-categories.md)
