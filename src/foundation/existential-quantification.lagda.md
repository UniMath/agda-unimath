# Existential quantification

```agda
module foundation.existential-quantification where
```

<details><summary>Imports</summary>

```agda
open import foundation.conjunction
open import foundation.dependent-pair-types
open import foundation.functoriality-propositional-truncation
open import foundation.logical-equivalences
open import foundation.propositional-extensionality
open import foundation.propositional-truncations
open import foundation.universe-levels

open import foundation-core.cartesian-product-types
open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.identity-types
open import foundation-core.propositions
```

</details>

## Idea

Given a family of [propositions](foundation-core.propositions.md) `P` over `A`,
the
{{#concept "existential quantification" Disambiguation="on a subtype" WDID=Q773483 WD="existential quantification" Agda=exists}}
of `P` over `A` is the proposition `âˆƒ A P` asserting that there is an element
`a : A` such that `P a` holds. We use the
[propositional truncation](foundation.propositional-truncations.md) to define
the existential quantification,

```text
  âˆƒ (x : A), (P x) := â•‘ Î£ (x : A), (P x) â•‘â‚‹â‚
```

because the
[Curryâ€“Howard interpretation](https://en.wikipedia.org/wiki/Curryâ€“Howard_correspondence)
of the existential quantification as `Î£ A P` does not guarantee that existential
quantifications are interpreted as propositions.

The
{{#concept "universal property" Disambiguation="of existential quantification" Agda=universal-property-exists}}
of existential quantification states that it is the
[least upper bound](order-theory.least-upper-bounds-large-posets.md) on the
family of propositions `P` in the
[locale of propositions](foundation.large-locale-of-propositions.md), by which
we mean that for every proposition `Q` we have the
[logical equivalence](foundation.logical-equivalences.md)

```text
  (âˆ€ (x : A), (P x â‡’ Q)) â‡” ((âˆƒ (x : A), (P x)) â‡’ Q).
```

## Definitions

### Existence of structure

Given a [structure](foundation.structure.md) `B : A â†’ ð’°` on a type `A`, the
propositional truncation

```text
  â•‘ Î£ (x : A), (B x) â•‘â‚‹â‚
```

satisfies the universal property of the existential quantification

```text
  âˆƒ (x : A), â•‘ B x â•‘â‚‹â‚
```

and is thus equivalent to it. Therefore, we may reasonably call this
construction the
{{#concept "existential quantification" Disambiguation="structure" Agda=exists-structure-Prop}}
of structure. It is important to keep in mind that this is not a generalization
of the concept but rather a conflation, and should be read as the statement _the
type of elements `x : A` equipped with `y : B x` is
[inhabited](foundation.inhabited-types.md)_.

Existence of structure is a widely occurring notion in univalent mathematics.
For instance, the condition that an element `y : B` is in the
[image](foundation.images.md) of a map `f : A â†’ B` is formulated using existence
of structure: The element `y` is in the image of `f` if the type of `x : A`
equipped with an identification `f x = y` is inhabited.

Because subtypes are a special case of structure, and Agda can generally infer
structures for us, we will continue to conflate the two in our formalizations
for the benefit that we have to specify the subtype in our code less often. For
instance, even though the introduction rule for existential quantification
`intro-exists` is phrased in terms of existential quantification on structures,
it equally applies to existential quantification on subtypes.

```agda
module _
  {l1 l2 : Level} (A : UU l1) (B : A â†’ UU l2)
  where

  exists-structure-Prop : Prop (l1 âŠ” l2)
  exists-structure-Prop = trunc-Prop (Î£ A B)

  exists-structure : UU (l1 âŠ” l2)
  exists-structure = type-Prop exists-structure-Prop

  is-prop-exists-structure : is-prop exists-structure
  is-prop-exists-structure = is-prop-type-Prop exists-structure-Prop
```

### Existential quantification

```agda
module _
  {l1 l2 : Level} (A : UU l1) (P : A â†’ Prop l2)
  where

  exists-Prop : Prop (l1 âŠ” l2)
  exists-Prop = exists-structure-Prop A (type-Prop âˆ˜ P)

  exists : UU (l1 âŠ” l2)
  exists = type-Prop exists-Prop

  abstract
    is-prop-exists : is-prop exists
    is-prop-exists = is-prop-type-Prop exists-Prop

  âˆƒ : Prop (l1 âŠ” l2)
  âˆƒ = exists-Prop
```

### The introduction rule for existential quantification

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  intro-exists : (a : A) (b : B a) â†’ exists-structure A B
  intro-exists a b = unit-trunc-Prop (a , b)
```

**Note.** Even though the introduction rule is formalized in terms of
existential quantification on structures, it equally applies to existential
quantification on subtypes. This is because subtypes are a special case of
structure. The benefit of this approach is that Agda can infer structures for
us, but not generally subtypes.

### The universal property of existential quantification

```agda
module _
  {l1 l2 l3 : Level} (A : UU l1) (B : A â†’ UU l2) (S : Prop l3)
  where

  universal-property-exists-structure : UUÏ‰
  universal-property-exists-structure =
    {l : Level} (Q : Prop l) â†’
    (type-Prop S â†’ type-Prop Q) â†” ((x : A) â†’ B x â†’ type-Prop Q)

module _
  {l1 l2 l3 : Level} (A : UU l1) (P : A â†’ Prop l2) (S : Prop l3)
  where

  universal-property-exists : UUÏ‰
  universal-property-exists =
    universal-property-exists-structure A (type-Prop âˆ˜ P) S
```

## Properties

### The elimination rule of existential quantification

The
{{#concept "universal property" Disambiguation="of existential quantification"}}
of existential quantification states `âˆƒ A P` is the least upper bound on the
predicate `P` in the locale of propositions.

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  ev-intro-exists :
    {C : UU l3} â†’ (exists-structure A B â†’ C) â†’ (x : A) â†’ B x â†’ C
  ev-intro-exists H x p = H (intro-exists x p)

  elim-exists :
    (Q : Prop l3) â†’
    ((x : A) â†’ B x â†’ type-Prop Q) â†’ (exists-structure A B â†’ type-Prop Q)
  elim-exists Q f = map-universal-property-trunc-Prop Q (ind-Î£ f)

  abstract
    is-equiv-ev-intro-exists :
      (Q : Prop l3) â†’ is-equiv (ev-intro-exists {type-Prop Q})
    is-equiv-ev-intro-exists Q =
      is-equiv-has-converse
        ( function-Prop (exists-structure A B) Q)
        ( Î -Prop A (Î» x â†’ function-Prop (B x) Q))
        ( elim-exists Q)
```

### The existential quantification satisfies the universal property of existential quantification

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  up-exists :
    universal-property-exists-structure A B (exists-structure-Prop A B)
  up-exists Q = (ev-intro-exists , elim-exists Q)
```

### Propositions that satisfy the universal property of a existential quantification are equivalent to the existential quantification

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (Q : Prop l3)
  (up-Q : universal-property-exists-structure A B Q)
  where

  forward-implication-iff-universal-property-exists :
    exists-structure A B â†’ type-Prop Q
  forward-implication-iff-universal-property-exists =
    elim-exists Q (forward-implication (up-Q Q) id)

  backward-implication-iff-universal-property-exists :
    type-Prop Q â†’ exists-structure A B
  backward-implication-iff-universal-property-exists =
    backward-implication (up-Q (exists-structure-Prop A B)) intro-exists

  iff-universal-property-exists :
    exists-structure A B â†” type-Prop Q
  iff-universal-property-exists =
    ( forward-implication-iff-universal-property-exists ,
      backward-implication-iff-universal-property-exists)
```

### Existential quantification of structure is the same as existential quantification over its propositional reflection

We proceed by showing that the latter satisfies the universal property of the
former.

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  universal-property-exists-structure-exists-trunc-Prop :
    universal-property-exists-structure A B (exists-Prop A (trunc-Prop âˆ˜ B))
  universal-property-exists-structure-exists-trunc-Prop Q =
    ( Î» f a b â†’ f (unit-trunc-Prop (a , unit-trunc-Prop b))) ,
    ( Î» f â†’ rec-trunc-Prop Q (Î» (a , |b|) â†’ rec-trunc-Prop Q (f a) |b|))

  compute-exists-trunc-Prop : exists-structure A B â†” exists A (trunc-Prop âˆ˜ B)
  compute-exists-trunc-Prop =
    iff-universal-property-exists
      ( exists-Prop A (trunc-Prop âˆ˜ B))
      ( universal-property-exists-structure-exists-trunc-Prop)
```

### Taking the cartesian product with a proposition distributes over existential quantification of structures

```agda
module _
  {l1 l2 l3 : Level} (P : Prop l1) {A : UU l2} {B : A â†’ UU l3}
  where

  map-distributive-product-exists-structure :
    type-Prop P Ã— exists-structure A B â†’
    exists-structure A (Î» x â†’ type-Prop P Ã— B x)
  map-distributive-product-exists-structure (p , e) =
    elim-exists
      ( exists-structure-Prop A (Î» x â†’ type-Prop P Ã— B x))
      ( Î» x q â†’ intro-exists x (p , q))
      ( e)

  map-inv-distributive-product-exists-structure :
    exists-structure A (Î» x â†’ type-Prop P Ã— B x) â†’
    type-Prop P Ã— exists-structure A B
  map-inv-distributive-product-exists-structure =
    elim-exists
      ( P âˆ§ exists-structure-Prop A B)
      ( Î» x (p , q) â†’ (p , intro-exists x q))

  iff-distributive-product-exists-structure :
    ( type-Prop P Ã— exists-structure A B) â†”
    ( exists-structure A (Î» x â†’ type-Prop P Ã— B x))
  iff-distributive-product-exists-structure =
    ( map-distributive-product-exists-structure ,
      map-inv-distributive-product-exists-structure)

  eq-distributive-product-exists-structure :
    P âˆ§ exists-structure-Prop A B ï¼
    exists-structure-Prop A (Î» x â†’ type-Prop P Ã— B x)
  eq-distributive-product-exists-structure =
    eq-iff'
      ( P âˆ§ exists-structure-Prop A B)
      ( exists-structure-Prop A (Î» x â†’ type-Prop P Ã— B x))
      ( iff-distributive-product-exists-structure)
```

### Conjunction distributes over existential quantification

```agda
module _
  {l1 l2 l3 : Level} (P : Prop l1) {A : UU l2} (Q : A â†’ Prop l3)
  where

  map-distributive-conjunction-exists :
    type-Prop (P âˆ§ (âˆƒ A Q) â‡’ âˆƒ A (Î» x â†’ P âˆ§ Q x))
  map-distributive-conjunction-exists =
    map-distributive-product-exists-structure P

  map-inv-distributive-conjunction-exists :
    type-Prop (âˆƒ A (Î» x â†’ P âˆ§ Q x) â‡’ P âˆ§ (âˆƒ A Q))
  map-inv-distributive-conjunction-exists =
    map-inv-distributive-product-exists-structure P

  iff-distributive-conjunction-exists :
    type-Prop (P âˆ§ âˆƒ A Q â‡” âˆƒ A (Î» x â†’ P âˆ§ Q x))
  iff-distributive-conjunction-exists =
    iff-distributive-product-exists-structure P

  eq-distributive-conjunction-exists :
    P âˆ§ (âˆƒ A Q) ï¼ âˆƒ A (Î» x â†’ P âˆ§ Q x)
  eq-distributive-conjunction-exists =
    eq-distributive-product-exists-structure P
```

## Existential quantification `do` syntax

When eliminating a chain of existential quantifications, which may be
interdependent, Agda's `do` syntax can eliminate many levels of nesting.

```agda
module elim-exists-do
  {l : Level}
  (P : Prop l)
  where

  _>>=_ :
    {l1 l2 : Level}  {A : UU l1} {B : A -> UU l2} â†’
    exists-structure A B â†’
    (Î£ A B -> type-Prop P) -> type-Prop P
  x >>= f = elim-exists P (ev-pair f) x
```

## See also

- Existential quantification is the indexed counterpart to
  [disjunction](foundation.disjunction.md)

## Table of files about propositional logic

The following table gives an overview of basic constructions in propositional
logic and related considerations.

{{#include tables/propositional-logic.md}}

## External links

- [existential quantifier](https://ncatlab.org/nlab/show/existential+quantifier)
  at $n$Lab
