# Mere equivalences

```agda
module foundation.mere-equivalences where
```

<details><summary>Imports</summary>

```agda
open import foundation.binary-relations
open import foundation.decidable-equality
open import foundation.functoriality-propositional-truncation
open import foundation.mere-equality
open import foundation.propositional-truncations
open import foundation.univalence
open import foundation.universe-levels

open import foundation-core.equivalences
open import foundation-core.propositions
open import foundation-core.sets
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Idea

Two types `X` and `Y` are said to be merely equivalent, if there exists an
equivalence from `X` to `Y`. Propositional truncations are used to express mere
equivalence.

## Definition

```agda
mere-equiv-Prop :
  {l1 l2 : Level} â†’ UU l1 â†’ UU l2 â†’ Prop (l1 âŠ” l2)
mere-equiv-Prop X Y = trunc-Prop (X â‰ƒ Y)

mere-equiv :
  {l1 l2 : Level} â†’ UU l1 â†’ UU l2 â†’ UU (l1 âŠ” l2)
mere-equiv X Y = type-Prop (mere-equiv-Prop X Y)

abstract
  is-prop-mere-equiv :
    {l1 l2 : Level} (X : UU l1) (Y : UU l2) â†’ is-prop (mere-equiv X Y)
  is-prop-mere-equiv X Y = is-prop-type-Prop (mere-equiv-Prop X Y)
```

## Properties

### Mere equivalence is reflexive

```agda
abstract
  refl-mere-equiv : {l1 : Level} â†’ is-reflexive (mere-equiv {l1})
  refl-mere-equiv X = unit-trunc-Prop id-equiv
```

### Mere equivalence is symmetric

```agda
abstract
  symmetric-mere-equiv :
    {l1 l2 : Level} (X : UU l1) (Y : UU l2) â†’ mere-equiv X Y â†’ mere-equiv Y X
  symmetric-mere-equiv X Y =
    map-universal-property-trunc-Prop
      ( mere-equiv-Prop Y X)
      ( Î» e â†’ unit-trunc-Prop (inv-equiv e))
```

### Mere equivalence is transitive

```agda
abstract
  transitive-mere-equiv :
    {l1 l2 l3 : Level} (X : UU l1) (Y : UU l2) (Z : UU l3) â†’
    mere-equiv Y Z â†’ mere-equiv X Y â†’ mere-equiv X Z
  transitive-mere-equiv X Y Z =
    map-binary-trunc-Prop (_âˆ˜e_)
```

### Truncated types are closed under mere equivalence

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2}
  where

  is-trunc-mere-equiv : (k : ğ•‹) â†’ mere-equiv X Y â†’ is-trunc k Y â†’ is-trunc k X
  is-trunc-mere-equiv k e H =
    apply-universal-property-trunc-Prop
      ( e)
      ( is-trunc-Prop k X)
      ( Î» f â†’ is-trunc-equiv k Y f H)

  is-trunc-mere-equiv' : (k : ğ•‹) â†’ mere-equiv X Y â†’ is-trunc k X â†’ is-trunc k Y
  is-trunc-mere-equiv' k e H =
    apply-universal-property-trunc-Prop
      ( e)
      ( is-trunc-Prop k Y)
      ( Î» f â†’ is-trunc-equiv' k X f H)
```

### Sets are closed under mere equivalence

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2}
  where

  is-set-mere-equiv : mere-equiv X Y â†’ is-set Y â†’ is-set X
  is-set-mere-equiv = is-trunc-mere-equiv zero-ğ•‹

  is-set-mere-equiv' : mere-equiv X Y â†’ is-set X â†’ is-set Y
  is-set-mere-equiv' = is-trunc-mere-equiv' zero-ğ•‹
```

### Types with decidable equality are closed under mere equivalences

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2}
  where

  has-decidable-equality-mere-equiv :
    mere-equiv X Y â†’ has-decidable-equality Y â†’ has-decidable-equality X
  has-decidable-equality-mere-equiv e d =
    apply-universal-property-trunc-Prop e
      ( has-decidable-equality-Prop X)
      ( Î» f â†’ has-decidable-equality-equiv f d)

  has-decidable-equality-mere-equiv' :
    mere-equiv X Y â†’ has-decidable-equality X â†’ has-decidable-equality Y
  has-decidable-equality-mere-equiv' e d =
    apply-universal-property-trunc-Prop e
      ( has-decidable-equality-Prop Y)
      ( Î» f â†’ has-decidable-equality-equiv' f d)
```

### Mere equivalence implies mere equality

```agda
abstract
  mere-eq-mere-equiv :
    {l : Level} {A B : UU l} â†’ mere-equiv A B â†’ mere-eq A B
  mere-eq-mere-equiv = map-trunc-Prop eq-equiv
```
