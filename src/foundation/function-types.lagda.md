# Function types

```agda
module foundation.function-types where

open import foundation-core.function-types public
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-binary-dependent-functions
open import foundation.action-on-identifications-dependent-functions
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.homotopy-induction
open import foundation.universe-levels

open import foundation-core.commuting-squares-of-maps
open import foundation.commuting-triangles-of-identifications
open import foundation-core.commuting-triangles-of-maps
open import foundation-core.dependent-identifications
open import foundation-core.equality-dependent-pair-types
open import foundation-core.equivalences
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.transport-along-identifications
```

</details>

## Properties

### Transport in a family of function types

Consider two type families `B` and `C` over `A`, an identification `p : x ï¼ y`
in `A` and a function `f : B x â†’ C x`. Then there is an identification

```text
  tr (Î» x â†’ B x â†’ C x) p f ï¼ tr C p âˆ˜ f âˆ˜ tr B (inv p)
```

From this identification we obtain a homotopy witnessing that the square

```text
        tr B p
   B a --------> B b
    |             |
  f |             | tr (Î» x â†’ B x â†’ C x) p f
    âˆ¨             âˆ¨
   C a --------> C b
        tr C p
```

commutes.

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {x y : A} (B : A â†’ UU l2) (C : A â†’ UU l3)
  where

  tr-function-type :
    (p : x ï¼ y) (f : B x â†’ C x) â†’
    tr (Î» a â†’ B a â†’ C a) p f ï¼ tr C p âˆ˜ f âˆ˜ tr B (inv p)
  tr-function-type refl f = refl

  coherence-square-tr-function-type :
    (p : x ï¼ y) (f : B x â†’ C x) â†’
    coherence-square-maps
      ( tr B p)
      ( f)
      ( tr (Î» a â†’ B a â†’ C a) p f)
      ( tr C p)
  coherence-square-tr-function-type refl f = refl-htpy
```

### Dependent identifications in a family of function types

Consider two type families `B` and `C` over `A`, an identification `p : x ï¼ y`
in `A` and two functions

```text
  f : B x â†’ C x
  g : B y â†’ C y.
```

Then the type of dependent identifications from `f` to `g` over `p` can be
computed as

```text
  (tr C p âˆ˜ f ~ g âˆ˜ tr B p) â‰ƒ dependent-identification (x â†¦ B x â†’ C x) f g.
```

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {x y : A} (B : A â†’ UU l2) (C : A â†’ UU l3)
  where

  compute-dependent-identification-function-type :
    (p : x ï¼ y) (f : B x â†’ C x) (g : B y â†’ C y) â†’
    coherence-square-maps (tr B p) f g (tr C p) â‰ƒ
    dependent-identification (Î» a â†’ B a â†’ C a) p f g
  compute-dependent-identification-function-type refl f g =
    inv-equiv equiv-funext

  map-compute-dependent-identification-function-type :
    (p : x ï¼ y) (f : B x â†’ C x) (g : B y â†’ C y) â†’
    coherence-square-maps (tr B p) f g (tr C p) â†’
    dependent-identification (Î» a â†’ B a â†’ C a) p f g
  map-compute-dependent-identification-function-type p f g =
    map-equiv (compute-dependent-identification-function-type p f g)
```

### Transport in a family of function types with fixed codomain

Consider an identification `p : a ï¼ b` in a type `A`, a type family `B : A â†’ ğ’°`, and a function `f : B a â†’ C`. Then the triangle of maps

```text
       tr B p
  B a --------> B b
     \         /
      \       /
     f \     / tr (x â†¦ B x â†’ C) p f
        \   /
         âˆ¨ âˆ¨
          X
```

commutes by a homotopy `H : f ~ tr (x â†¦ B x â†’ C) p f âˆ˜ tr B p`.

Moreover, given a family `f : (x : A) â†’ B x â†’ C` of functions with fixed codomain `C`, we can say more about how the identifications `H y` constructed above fit together: Each identification `H y` fits in a commuting triangle of identifications

```text
                            H y
                   f a y --------> tr (x â†¦ B x â†’ C) p f (tr B p y)
                        \         /
                         \       /
  ap (ind-Î£ f) (p , refl) \     / htpy-eq (apd f p) (tr B p y)
                           \   /
                            âˆ¨ âˆ¨
                      f b (tr B p y).
```

Similarly, given a map `f : Î£ A B â†’ C` we obtain a commuting triangle of identifications

```text
                    H y
       f (a , y) --------> tr (x â†¦ B x â†’ C) (ev-pair f a) (tr B p y)
                \         /
                 \       /
  ap f (p , refl) \     / htpy-eq (apd (ev-pair f) p) (tr B p y)
                   \   /
                    âˆ¨ âˆ¨
               f (b , tr B p y).
```

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {a b : A} (B : A â†’ UU l2) (C : UU l3)
  where

  compute-tr-function-type-fixed-domain :
    (p : a ï¼ b) (f : B a â†’ C) â†’ f ï¼ tr (Î» x â†’ B x â†’ C) p f âˆ˜ tr B p
  compute-tr-function-type-fixed-domain refl f = refl

  htpy-compute-tr-function-type-fixed-domain :
    (p : a ï¼ b) (f : B a â†’ C) â†’
    coherence-triangle-maps f (tr (Î» x â†’ B x â†’ C) p f) (tr B p)
  htpy-compute-tr-function-type-fixed-domain p f =
    htpy-eq (compute-tr-function-type-fixed-domain p f)

  coherence-triangle-compute-tr-function-type-fixed-domain :
    (p : a ï¼ b) (f : (x : A) â†’ B x â†’ C) (y : B a) â†’
    coherence-triangle-identifications
      ( ap (ind-Î£ f) (eq-pair-Î£ p refl))
      ( htpy-eq (apd f p) (tr B p y))
      ( htpy-compute-tr-function-type-fixed-domain p (f a) y)
  coherence-triangle-compute-tr-function-type-fixed-domain refl f y =
    refl

  coherence-triangle-compute-tr-function-type-fixed-domain' :
    (p : a ï¼ b) (f : Î£ A B â†’ C) (y : B a) â†’
    coherence-triangle-identifications
      ( ap f (eq-pair-Î£ p refl))
      ( htpy-eq (apd (ev-pair f) p) (tr B p y))
      ( htpy-compute-tr-function-type-fixed-domain p (ev-pair f a) y)
  coherence-triangle-compute-tr-function-type-fixed-domain' refl f y =
    refl
```

### Dependent identifications in a family of function types with fixed codomain

Consider an identification `p : a ï¼ b` in a type `A`, a type family `B : A â†’ ğ’°`, a function `f : B a â†’ C` and a function `g : B b â†’ C`. Then we have an equivalence

```text
  Î±â»Â¹ p : dependent-identification (x â†¦ B x â†’ C) p f g â‰ƒ (f ~ g âˆ˜ tr B p)
```

with inverse

```text
  Î± p : (f ~ g âˆ˜ tr B p) â‰ƒ dependent-identification (x â†¦ B x â†’ C) p f g.
```

Note that it is conventional in agda-unimath to direct computations of types from the computed value to the type being computed. The family of equivalences `Î±â»Â¹` is constructed by identification elimination. In the case where `p â‰ refl` the equivalence is given by function extensionality.

Given an identification `p : a ï¼ b` and a map `f : Î£ A B â†’ C`, we can compute `Î± p` as follows:

```text
  Î± p (Î» y â†’ ap f (eq-pair-Î£ p refl)) ï¼ apd (ev-pair f) p
```

Furthermore, given an identification `p : a ï¼ b` and a family of maps `f : (x : A) â†’ B x â†’ C` with fixed codomain `C`, we can compute `Î±â»Â¹ p` to the action on identifications of the map `ind-Î£ f : Î£ A B â†’ C`:

```text
  Î±â»Â¹ p (apd f p) y ï¼ ap (ind-Î£ f) (eq-pair-Î£ p refl).
```

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {a b : A} (B : A â†’ UU l2) (C : UU l3)
  where

  inv-compute-dependent-identification-function-type-fixed-codomain :
    (p : a ï¼ b) (f : B a â†’ C) (g : B b â†’ C) â†’
    dependent-identification (Î» x â†’ B x â†’ C) p f g â‰ƒ
    ((y : B a) â†’ f y ï¼ g (tr B p y))
  inv-compute-dependent-identification-function-type-fixed-codomain refl f g =
    equiv-funext

  map-inv-compute-dependent-identification-function-type-fixed-codomain :
    (p : a ï¼ b) {f : B a â†’ C} {g : B b â†’ C} â†’
    dependent-identification (Î» x â†’ B x â†’ C) p f g â†’
    ((y : B a) â†’ f y ï¼ g (tr B p y))
  map-inv-compute-dependent-identification-function-type-fixed-codomain p =
    map-equiv
      ( inv-compute-dependent-identification-function-type-fixed-codomain p _ _)

  compute-dependent-identification-function-type-fixed-codomain :
    (p : a ï¼ b) (f : B a â†’ C) (g : B b â†’ C) â†’
    ((y : B a) â†’ f y ï¼ g (tr B p y)) â‰ƒ
    dependent-identification (Î» x â†’ B x â†’ C) p f g
  compute-dependent-identification-function-type-fixed-codomain p f g =
    inv-equiv
      ( inv-compute-dependent-identification-function-type-fixed-codomain p f g)

  map-compute-dependent-identification-function-type-fixed-codomain :
    (p : a ï¼ b) {f : B a â†’ C} {g : B b â†’ C} â†’
    ((y : B a) â†’ f y ï¼ g (tr B p y)) â†’
    dependent-identification (Î» x â†’ B x â†’ C) p f g
  map-compute-dependent-identification-function-type-fixed-codomain p {f} {g} =
    map-equiv
      ( compute-dependent-identification-function-type-fixed-codomain p f g)

  compute-inv-compute-dependent-identification-function-type-fixed-codomain :
    (p : a ï¼ b) (f : (x : A) â†’ B x â†’ C) (y : B a) â†’
    map-inv-compute-dependent-identification-function-type-fixed-codomain p
      ( apd f p)
      ( y) ï¼
    ap (ind-Î£ f) (eq-pair-Î£ p refl)
  compute-inv-compute-dependent-identification-function-type-fixed-codomain
    refl f =
    refl-htpy

  compute-compute-dependent-identification-function-type-fixed-codomain :
    (p : a ï¼ b) (f : Î£ A B â†’ C) â†’
    map-compute-dependent-identification-function-type-fixed-codomain p
      (Î» y â†’ ap f (eq-pair-Î£ p refl)) ï¼
    apd (ev-pair f) p
  compute-compute-dependent-identification-function-type-fixed-codomain
    refl f =
    eq-htpy-refl-htpy _
```

### Relation between `compute-dependent-identification-function-type` and `preserves-tr`

```agda
module _
  {l1 l2 l3 : Level} {I : UU l1} {i0 i1 : I} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (f : (i : I) â†’ A i â†’ B i)
  where

  preserves-tr-apd-function :
    (p : i0 ï¼ i1) (a : A i0) â†’
    map-inv-equiv
      ( compute-dependent-identification-function-type A B p (f i0) (f i1))
      ( apd f p) a ï¼
    inv-htpy (preserves-tr f p) a
  preserves-tr-apd-function refl = refl-htpy
```

### Computation of dependent identifications of functions over homotopies

```agda
module _
  { l1 l2 l3 l4 : Level}
  { S : UU l1} {X : UU l2} {P : X â†’ UU l3} (Y : UU l4)
  { i : S â†’ X}
  where

  equiv-htpy-dependent-function-dependent-identification-function-type :
    { j : S â†’ X} (H : i ~ j) â†’
    ( k : (s : S) â†’ P (i s) â†’ Y)
    ( l : (s : S) â†’ P (j s) â†’ Y) â†’
    ( s : S) â†’
    ( k s ~ (l s âˆ˜ tr P (H s))) â‰ƒ
    ( dependent-identification
      ( Î» x â†’ P x â†’ Y)
      ( H s)
      ( k s)
      ( l s))
  equiv-htpy-dependent-function-dependent-identification-function-type =
    ind-htpy i
      ( Î» j H â†’
        ( k : (s : S) â†’ P (i s) â†’ Y) â†’
        ( l : (s : S) â†’ P (j s) â†’ Y) â†’
        ( s : S) â†’
        ( k s ~ (l s âˆ˜ tr P (H s))) â‰ƒ
        ( dependent-identification
          ( Î» x â†’ P x â†’ Y)
          ( H s)
          ( k s)
          ( l s)))
      ( Î» k l s â†’ inv-equiv (equiv-funext))

  compute-equiv-htpy-dependent-function-dependent-identification-function-type :
    { j : S â†’ X} (H : i ~ j) â†’
    ( h : (x : X) â†’ P x â†’ Y) â†’
    ( s : S) â†’
    ( map-equiv
      ( equiv-htpy-dependent-function-dependent-identification-function-type H
        ( h âˆ˜ i)
        ( h âˆ˜ j)
        ( s))
      ( Î» t â†’ ap (ind-Î£ h) (eq-pair-Î£ (H s) refl))) ï¼
    ( apd h (H s))
  compute-equiv-htpy-dependent-function-dependent-identification-function-type =
    ind-htpy i
      ( Î» j H â†’
        ( h : (x : X) â†’ P x â†’ Y) â†’
        ( s : S) â†’
        ( map-equiv
          ( equiv-htpy-dependent-function-dependent-identification-function-type
            ( H)
            ( h âˆ˜ i)
            ( h âˆ˜ j)
            ( s))
          ( Î» t â†’ ap (ind-Î£ h) (eq-pair-Î£ (H s) refl))) ï¼
        ( apd h (H s)))
      ( Î» h s â†’
        ( ap
          ( Î» f â†’ map-equiv (f (h âˆ˜ i) (h âˆ˜ i) s) refl-htpy)
          ( compute-ind-htpy i
            ( Î» j H â†’
              ( k : (s : S) â†’ P (i s) â†’ Y) â†’
              ( l : (s : S) â†’ P (j s) â†’ Y) â†’
              ( s : S) â†’
              ( k s ~ (l s âˆ˜ tr P (H s))) â‰ƒ
              ( dependent-identification
                ( Î» x â†’ P x â†’ Y)
                ( H s)
                ( k s)
                ( l s)))
            ( Î» k l s â†’ inv-equiv (equiv-funext)))) âˆ™
        ( eq-htpy-refl-htpy (h (i s))))
```

## See also

### Table of files about function types, composition, and equivalences

{{#include tables/composition.md}}
