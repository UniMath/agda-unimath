# Sets

```agda
module foundation.sets where

open import foundation-core.sets public
```

<details><summary>Imports</summary>

```agda
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.logical-equivalences
open import foundation.subuniverses
open import foundation.truncated-types
open import foundation.univalent-type-families
open import foundation.universe-levels

open import foundation-core.1-types
open import foundation-core.cartesian-product-types
open import foundation-core.embeddings
open import foundation-core.equivalences
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.precomposition-functions
open import foundation-core.propositional-maps
open import foundation-core.propositions
open import foundation-core.subtypes
open import foundation-core.torsorial-type-families
open import foundation-core.truncation-levels
```

</details>

## Properties

### The type of all sets in a universe is a 1-type

```agda
is-1-type-Set : {l : Level} â†’ is-1-type (Set l)
is-1-type-Set = is-trunc-Truncated-Type zero-ğ•‹

Set-1-Type : (l : Level) â†’ 1-Type (lsuc l)
pr1 (Set-1-Type l) = Set l
pr2 (Set-1-Type l) = is-1-type-Set
```

### Any contractible type is a set

```agda
abstract
  is-set-is-contr :
    {l : Level} {A : UU l} â†’ is-contr A â†’ is-set A
  is-set-is-contr = is-trunc-is-contr zero-ğ•‹
```

### Sets are closed under dependent pair types

```agda
abstract
  is-set-Î£ :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    is-set A â†’ ((x : A) â†’ is-set (B x)) â†’ is-set (Î£ A B)
  is-set-Î£ = is-trunc-Î£ {k = zero-ğ•‹}

Î£-Set :
  {l1 l2 : Level} (A : Set l1) (B : pr1 A â†’ Set l2) â†’ Set (l1 âŠ” l2)
pr1 (Î£-Set A B) = Î£ (type-Set A) (Î» x â†’ (type-Set (B x)))
pr2 (Î£-Set A B) = is-set-Î£ (is-set-type-Set A) (Î» x â†’ is-set-type-Set (B x))
```

### Sets are closed under cartesian product types

```agda
abstract
  is-set-product :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-set A â†’ is-set B â†’ is-set (A Ã— B)
  is-set-product = is-trunc-product zero-ğ•‹

product-Set :
  {l1 l2 : Level} (A : Set l1) (B : Set l2) â†’ Set (l1 âŠ” l2)
product-Set A B = Î£-Set A (Î» x â†’ B)
```

### Being a set is a property

```agda
abstract
  is-prop-is-set :
    {l : Level} (A : UU l) â†’ is-prop (is-set A)
  is-prop-is-set = is-prop-is-trunc zero-ğ•‹

is-set-Prop : {l : Level} â†’ UU l â†’ Prop l
pr1 (is-set-Prop A) = is-set A
pr2 (is-set-Prop A) = is-prop-is-set A
```

### The inclusion of sets into the universe is an embedding

```agda
emb-type-Set : (l : Level) â†’ Set l â†ª UU l
emb-type-Set l = emb-type-Truncated-Type l zero-ğ•‹
```

### Products of families of sets are sets

```agda
abstract
  is-set-Î  :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    ((x : A) â†’ is-set (B x)) â†’ is-set ((x : A) â†’ (B x))
  is-set-Î  = is-trunc-Î  zero-ğ•‹

type-Î -Set' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ Set l2) â†’ UU (l1 âŠ” l2)
type-Î -Set' A B = (x : A) â†’ type-Set (B x)

is-set-type-Î -Set' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ Set l2) â†’ is-set (type-Î -Set' A B)
is-set-type-Î -Set' A B =
  is-set-Î  (Î» x â†’ is-set-type-Set (B x))

Î -Set' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ Set l2) â†’ Set (l1 âŠ” l2)
pr1 (Î -Set' A B) = type-Î -Set' A B
pr2 (Î -Set' A B) = is-set-type-Î -Set' A B

function-Set :
  {l1 l2 : Level} (A : UU l1) (B : Set l2) â†’ Set (l1 âŠ” l2)
function-Set A B = Î -Set' A (Î» x â†’ B)

type-Î -Set :
  {l1 l2 : Level} (A : Set l1) (B : type-Set A â†’ Set l2) â†’ UU (l1 âŠ” l2)
type-Î -Set A B = type-Î -Set' (type-Set A) B

is-set-type-Î -Set :
  {l1 l2 : Level} (A : Set l1) (B : type-Set A â†’ Set l2) â†’
  is-set (type-Î -Set A B)
is-set-type-Î -Set A B =
  is-set-type-Î -Set' (type-Set A) B

Î -Set :
  {l1 l2 : Level} (A : Set l1) â†’
  (type-Set A â†’ Set l2) â†’ Set (l1 âŠ” l2)
pr1 (Î -Set A B) = type-Î -Set A B
pr2 (Î -Set A B) = is-set-type-Î -Set A B
```

### The type of functions into a set is a set

```agda
abstract
  is-set-function-type :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-set B â†’ is-set (A â†’ B)
  is-set-function-type = is-trunc-function-type zero-ğ•‹

hom-Set :
  {l1 l2 : Level} â†’ Set l1 â†’ Set l2 â†’ UU (l1 âŠ” l2)
hom-Set A B = type-Set A â†’ type-Set B

is-set-hom-Set :
  {l1 l2 : Level} (A : Set l1) (B : Set l2) â†’
  is-set (hom-Set A B)
is-set-hom-Set A B = is-set-function-type (is-set-type-Set B)

hom-set-Set :
  {l1 l2 : Level} â†’ Set l1 â†’ Set l2 â†’ Set (l1 âŠ” l2)
pr1 (hom-set-Set A B) = hom-Set A B
pr2 (hom-set-Set A B) = is-set-hom-Set A B

precomp-Set :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (C : Set l3) â†’
  (B â†’ type-Set C) â†’ (A â†’ type-Set C)
precomp-Set f C = precomp f (type-Set C)
```

### Extensionality of sets

```agda
module _
  {l : Level} (X : Set l)
  where

  equiv-eq-Set : (Y : Set l) â†’ X ï¼ Y â†’ equiv-Set X Y
  equiv-eq-Set = equiv-eq-subuniverse is-set-Prop X

  abstract
    is-torsorial-equiv-Set : is-torsorial (Î» (Y : Set l) â†’ equiv-Set X Y)
    is-torsorial-equiv-Set =
      is-torsorial-equiv-subuniverse is-set-Prop X

  abstract
    is-equiv-equiv-eq-Set : (Y : Set l) â†’ is-equiv (equiv-eq-Set Y)
    is-equiv-equiv-eq-Set = is-equiv-equiv-eq-subuniverse is-set-Prop X

  eq-equiv-Set : (Y : Set l) â†’ equiv-Set X Y â†’ X ï¼ Y
  eq-equiv-Set Y = eq-equiv-subuniverse is-set-Prop

  extensionality-Set : (Y : Set l) â†’ (X ï¼ Y) â‰ƒ equiv-Set X Y
  pr1 (extensionality-Set Y) = equiv-eq-Set Y
  pr2 (extensionality-Set Y) = is-equiv-equiv-eq-Set Y
```

### If a type embeds into a set, then it is a set

```agda
abstract
  is-set-is-emb :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    is-emb f â†’ is-set B â†’ is-set A
  is-set-is-emb = is-trunc-is-emb neg-one-ğ•‹

abstract
  is-set-emb :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†ª B) â†’ is-set B â†’ is-set A
  is-set-emb = is-trunc-emb neg-one-ğ•‹
```

### Any function from a proposition into a set is an embedding

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-emb-is-prop-is-set : is-prop A â†’ is-set B â†’ {f : A â†’ B} â†’ is-emb f
  is-emb-is-prop-is-set is-prop-A is-set-B {f} =
    is-emb-is-prop-map (Î» b â†’ is-prop-Î£ is-prop-A (Î» a â†’ is-set-B (f a) b))
```

### Sets are `k+2`-truncated for any `k`

```agda
is-trunc-is-set :
  {l : Level} (k : ğ•‹) {A : UU l} â†’ is-set A â†’ is-trunc (succ-ğ•‹ (succ-ğ•‹ k)) A
is-trunc-is-set neg-two-ğ•‹ is-set-A = is-set-A
is-trunc-is-set (succ-ğ•‹ k) is-set-A =
  is-trunc-succ-is-trunc (succ-ğ•‹ (succ-ğ•‹ k)) (is-trunc-is-set k is-set-A)

set-Truncated-Type :
  {l : Level} (k : ğ•‹) â†’ Set l â†’ Truncated-Type l (succ-ğ•‹ (succ-ğ•‹ k))
pr1 (set-Truncated-Type k A) = type-Set A
pr2 (set-Truncated-Type k A) = is-trunc-is-set k (is-set-type-Set A)
```

### The type of equivalences is a set if the domain or codomain is a set

```agda
abstract
  is-set-equiv-is-set-codomain :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ is-set B â†’ is-set (A â‰ƒ B)
  is-set-equiv-is-set-codomain = is-trunc-equiv-is-trunc-codomain neg-one-ğ•‹

  is-set-equiv-is-set-domain :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ is-set A â†’ is-set (A â‰ƒ B)
  is-set-equiv-is-set-domain = is-trunc-equiv-is-trunc-domain neg-one-ğ•‹
```

### The canonical type family over `Set` is univalent

```agda
is-univalent-type-Set : {l : Level} â†’ is-univalent (type-Set {l})
is-univalent-type-Set = is-univalent-inclusion-subuniverse is-set-Prop
```

### The subtype of of preimages of an element of a Set

```agda
preimage-Set :
  {l1 l2 : Level} {A : UU l1} (B : Set l2) â†’ (A â†’ type-Set B) â†’ type-Set B â†’
  subtype l2 A
preimage-Set B f b a = Id-Prop B (f a) b

is-in-preimage-Set :
  {l1 l2 : Level} {A : UU l1} (B : Set l2) â†’ (A â†’ type-Set B) â†’ type-Set B â†’
  A â†’ UU l2
is-in-preimage-Set B f b a = type-Prop (preimage-Set B f b a)
```
