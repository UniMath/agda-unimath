# Truncated types

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation.truncated-types where

open import foundation.cartesian-product-types using (_Ã—_)
open import foundation.contractible-types using
  ( is-contr; is-contr-is-equiv; is-contr-Î£'; is-contr-left-factor-prod;
    is-contr-right-factor-prod; is-contr-retract-of; eq-is-contr;
    is-subtype-is-contr; is-contr-Î ; is-contr-equiv-is-contr)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.embeddings using
  ( is-emb-is-equiv; is-emb; _â†ª_; map-emb; is-emb-map-emb)
open import foundation.equality-cartesian-product-types using
  ( Eq-prod; equiv-pair-eq)
open import foundation.equality-dependent-pair-types using (equiv-pair-eq-Î£)
open import foundation.equivalences using
  ( is-equiv; _â‰ƒ_; map-inv-is-equiv; is-equiv-map-inv-is-equiv)
open import foundation.equivalences-with-function-extensionality using
  ( is-subtype-is-equiv)
open import foundation.function-extensionality using (htpy-eq; funext)
open import foundation.homotopies using (_~_)
open import foundation.identity-types using (Id; ap; tr; refl; left-inv)
open import foundation.retractions using (_retract-of_; retract-eq)
open import foundation.truncation-levels using
  ( ğ•‹; neg-two-ğ•‹; succ-ğ•‹; one-ğ•‹; neg-one-ğ•‹; zero-ğ•‹)
open import foundation.universe-levels using (Level; UU; lsuc; _âŠ”_)
```

## Idea

The truncatedness of a type is a measure of the complexity of its identity types. The simplest case is a contractible type. This is the base case of the inductive definition of truncatedness for types. A type is (k+1)-truncated if its identity types are k-truncated.

## Definition

### The condition of truncatedness

```agda
is-trunc : {i : Level} (k : ğ•‹) â†’ UU i â†’ UU i
is-trunc neg-two-ğ•‹ A = is-contr A
is-trunc (succ-ğ•‹ k) A = (x y : A) â†’ is-trunc k (Id x y)
```

### The universe of truncated types

```agda
UU-Truncated-Type : ğ•‹ â†’ (l : Level) â†’ UU (lsuc l)
UU-Truncated-Type k l = Î£ (UU l) (is-trunc k)

module _
  {k : ğ•‹} {l : Level}
  where
  
  type-Truncated-Type : UU-Truncated-Type k l â†’ UU l
  type-Truncated-Type = pr1

  abstract
    is-trunc-type-Truncated-Type :
      (A : UU-Truncated-Type k l) â†’ is-trunc k (type-Truncated-Type A)
    is-trunc-type-Truncated-Type = pr2
```

## Properties

### If a type is k-truncated, then it is (k+1)-truncated.

```agda
abstract
  is-trunc-succ-is-trunc :
    (k : ğ•‹) {i : Level} {A : UU i} â†’ is-trunc k A â†’ is-trunc (succ-ğ•‹ k) A
  pr1 (is-trunc-succ-is-trunc neg-two-ğ•‹ H x y) = eq-is-contr H
  pr2 (is-trunc-succ-is-trunc neg-two-ğ•‹ H x .x) refl = left-inv (pr2 H x)
  is-trunc-succ-is-trunc (succ-ğ•‹ k) H x y = is-trunc-succ-is-trunc k (H x y)

truncated-type-succ-Truncated-Type :
  (k : ğ•‹) {l : Level} â†’ UU-Truncated-Type k l â†’ UU-Truncated-Type (succ-ğ•‹ k) l
pr1 (truncated-type-succ-Truncated-Type k A) = type-Truncated-Type A
pr2 (truncated-type-succ-Truncated-Type k A) =
  is-trunc-succ-is-trunc k (is-trunc-type-Truncated-Type A)
```

### Contractible types are k-truncated for any k.

```agda
module _
  {l : Level} {A : UU l}
  where
  
  abstract
    is-trunc-is-contr : (k : ğ•‹) â†’ is-contr A â†’ is-trunc k A
    is-trunc-is-contr neg-two-ğ•‹ is-contr-A = is-contr-A
    is-trunc-is-contr (succ-ğ•‹ k) is-contr-A =
      is-trunc-succ-is-trunc k (is-trunc-is-contr k is-contr-A)

module _
  {l : Level} {A : UU l}
  where
  
  abstract
    [is-trunc-is-prop] : (k : ğ•‹) â†’ is-trunc neg-one-ğ•‹ A â†’ is-trunc (succ-ğ•‹ k) A
    [is-trunc-is-prop] k H x y = is-trunc-is-contr k (H x y)
```

### The identity type of a k-truncated type is k-truncated

```agda
abstract
  is-trunc-Id :
    {l : Level} {k : ğ•‹} {A : UU l} â†’
    is-trunc k A â†’ (x y : A) â†’ is-trunc k (Id x y)
  is-trunc-Id {l} {k}= is-trunc-succ-is-trunc k

Id-Truncated-Type :
  {l : Level} {k : ğ•‹} (A : UU-Truncated-Type (succ-ğ•‹ k) l) â†’
  (x y : type-Truncated-Type A) â†’ UU-Truncated-Type k l
pr1 (Id-Truncated-Type A x y) = Id x y
pr2 (Id-Truncated-Type A x y) = is-trunc-type-Truncated-Type A x y

Id-Truncated-Type' :
  {l : Level} {k : ğ•‹} (A : UU-Truncated-Type k l) â†’
  (x y : type-Truncated-Type A) â†’ UU-Truncated-Type k l
pr1 (Id-Truncated-Type' A x y) = Id x y
pr2 (Id-Truncated-Type' A x y) =
  is-trunc-Id (is-trunc-type-Truncated-Type A) x y
```

### k-truncated types are closed under equivalences

```agda
abstract
  is-trunc-is-equiv :
    {i j : Level} (k : ğ•‹) {A : UU i} (B : UU j) (f : A â†’ B) â†’ is-equiv f â†’
    is-trunc k B â†’ is-trunc k A
  is-trunc-is-equiv neg-two-ğ•‹ B f is-equiv-f H =
    is-contr-is-equiv B f is-equiv-f H
  is-trunc-is-equiv (succ-ğ•‹ k) B f is-equiv-f H x y =
    is-trunc-is-equiv k
      ( Id (f x) (f y))
      ( ap f {x} {y})
      ( is-emb-is-equiv is-equiv-f x y)
      ( H (f x) (f y))

abstract
  is-trunc-equiv :
    {i j : Level} (k : ğ•‹) {A : UU i} (B : UU  j) (e : A â‰ƒ B) â†’
    is-trunc k B â†’ is-trunc k A
  is-trunc-equiv k B (pair f is-equiv-f) =
    is-trunc-is-equiv k B f is-equiv-f

abstract
  is-trunc-is-equiv' :
    {i j : Level} (k : ğ•‹) (A : UU i) {B : UU j} (f : A â†’ B) â†’
    is-equiv f â†’ is-trunc k A â†’ is-trunc k B
  is-trunc-is-equiv' k A  f is-equiv-f is-trunc-A =
    is-trunc-is-equiv k A
      ( map-inv-is-equiv is-equiv-f)
      ( is-equiv-map-inv-is-equiv is-equiv-f)
      ( is-trunc-A)

abstract
  is-trunc-equiv' :
    {i j : Level} (k : ğ•‹) (A : UU i) {B : UU j} (e : A â‰ƒ B) â†’
    is-trunc k A â†’ is-trunc k B
  is-trunc-equiv' k A (pair f is-equiv-f) =
    is-trunc-is-equiv' k A f is-equiv-f

```

### If a type embeds into a (k+1)-truncated type, then it is (k+1)-truncated

```agda
abstract
  is-trunc-is-emb :
    {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} (f : A â†’ B) â†’
    is-emb f â†’ is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-is-emb k f Ef H x y =
    is-trunc-is-equiv k (Id (f x) (f y)) (ap f {x} {y}) (Ef x y) (H (f x) (f y))

abstract
  is-trunc-emb :
    {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} (f : A â†ª B) â†’
    is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-emb k f = is-trunc-is-emb k (map-emb f) (is-emb-map-emb f)
```

### Truncated types are closed under dependent pair types

```agda
abstract
  is-trunc-Î£ :
    {l1 l2 : Level} {k : ğ•‹} {A : UU l1} {B : A â†’ UU l2} â†’
    is-trunc k A â†’ ((x : A) â†’ is-trunc k (B x)) â†’ is-trunc k (Î£ A B)
  is-trunc-Î£ {k = neg-two-ğ•‹} is-trunc-A is-trunc-B =
    is-contr-Î£' is-trunc-A is-trunc-B
  is-trunc-Î£ {k = succ-ğ•‹ k} {B = B} is-trunc-A is-trunc-B s t =
    is-trunc-equiv k
      ( Î£ (Id (pr1 s) (pr1 t)) (Î» p â†’ Id (tr B p (pr2 s)) (pr2 t)))
      ( equiv-pair-eq-Î£ s t)
      ( is-trunc-Î£
        ( is-trunc-A (pr1 s) (pr1 t))
        ( Î» p â†’ is-trunc-B (pr1 t) (tr B p (pr2 s)) (pr2 t)))

Î£-Truncated-Type :
  {l1 l2 : Level} {k : ğ•‹} (A : UU-Truncated-Type k l1)
  (B : type-Truncated-Type A â†’ UU-Truncated-Type k l2) â†’
  UU-Truncated-Type k (l1 âŠ” l2)
pr1 (Î£-Truncated-Type A B) =
  Î£ (type-Truncated-Type A) (Î» a â†’ type-Truncated-Type (B a))
pr2 (Î£-Truncated-Type A B) =
  is-trunc-Î£
    ( is-trunc-type-Truncated-Type A)
    ( Î» a â†’ is-trunc-type-Truncated-Type (B a))

fib-Truncated-Type :
  {l1 l2 : Level} {k : ğ•‹} (A : UU-Truncated-Type k l1)
  (B : UU-Truncated-Type k l2)
  (f : type-Truncated-Type A â†’ type-Truncated-Type B) â†’
  type-Truncated-Type B â†’ UU-Truncated-Type k (l1 âŠ” l2)
fib-Truncated-Type A B f b =
  Î£-Truncated-Type A (Î» a â†’ Id-Truncated-Type' B (f a) b)
```

### Products of truncated types are truncated

```agda
abstract
  is-trunc-prod :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
    is-trunc k A â†’ is-trunc k B â†’ is-trunc k (A Ã— B)
  is-trunc-prod k is-trunc-A is-trunc-B =
    is-trunc-Î£ is-trunc-A (Î» x â†’ is-trunc-B)

is-trunc-prod' :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  (B â†’ is-trunc (succ-ğ•‹ k) A) â†’ (A â†’ is-trunc (succ-ğ•‹ k) B) â†’
  is-trunc (succ-ğ•‹ k) (A Ã— B)
is-trunc-prod' k f g (pair a b) (pair a' b') =
  is-trunc-equiv k
    ( Eq-prod (pair a b) (pair a' b'))
    ( equiv-pair-eq (pair a b) (pair a' b'))
    ( is-trunc-prod k (f b a a') (g a b b'))

is-trunc-left-factor-prod :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc k (A Ã— B) â†’ B â†’ is-trunc k A
is-trunc-left-factor-prod neg-two-ğ•‹ {A} {B} H b =
  is-contr-left-factor-prod A B H
is-trunc-left-factor-prod (succ-ğ•‹ k) H b a a' =
  is-trunc-left-factor-prod k {A = Id a a'} {B = Id b b}
    ( is-trunc-equiv' k
      ( Id (pair a b) (pair a' b))
      ( equiv-pair-eq (pair a b) (pair a' b))
      ( H (pair a b) (pair a' b)))
    ( refl)

is-trunc-right-factor-prod :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc k (A Ã— B) â†’ A â†’ is-trunc k B
is-trunc-right-factor-prod neg-two-ğ•‹ {A} {B} H a =
  is-contr-right-factor-prod A B H
is-trunc-right-factor-prod (succ-ğ•‹ k) {A} {B} H a b b' =
  is-trunc-right-factor-prod k {A = Id a a} {B = Id b b'}
    ( is-trunc-equiv' k
      ( Id (pair a b) (pair a b'))
      ( equiv-pair-eq (pair a b) (pair a b'))
      ( H (pair a b) (pair a b')))
    ( refl)
```

### Retracts of truncated types are truncated

```agda
abstract
  is-trunc-retract-of : {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
    A retract-of B â†’ is-trunc k B â†’ is-trunc k A
  is-trunc-retract-of neg-two-ğ•‹ (pair i (pair r H)) is-trunc-B =
    is-contr-retract-of _ (pair i (pair r H)) is-trunc-B
  is-trunc-retract-of (succ-ğ•‹ k) (pair i retr-i) is-trunc-B x y =
    is-trunc-retract-of k
      ( retract-eq (pair i retr-i) x y)
      ( is-trunc-B (i x) (i y))
```

### Products of families of truncated types are truncated

```agda
abstract
  is-trunc-Î  :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : A â†’ UU l2} â†’
    ((x : A) â†’ is-trunc k (B x)) â†’ is-trunc k ((x : A) â†’ B x)
  is-trunc-Î  neg-two-ğ•‹ is-trunc-B = is-contr-Î  is-trunc-B
  is-trunc-Î  (succ-ğ•‹ k) is-trunc-B f g =
    is-trunc-is-equiv k (f ~ g) htpy-eq
      ( funext f g)
      ( is-trunc-Î  k (Î» x â†’ is-trunc-B x (f x) (g x)))

type-Î -Truncated-Type' :
  (k : ğ•‹) {l1 l2 : Level} (A : UU l1) (B : A â†’ UU-Truncated-Type k l2) â†’
  UU (l1 âŠ” l2)
type-Î -Truncated-Type' k A B = (x : A) â†’ type-Truncated-Type (B x)

is-trunc-type-Î -Truncated-Type' :
  (k : ğ•‹) {l1 l2 : Level} (A : UU l1) (B : A â†’ UU-Truncated-Type k l2) â†’
  is-trunc k (type-Î -Truncated-Type' k A B)
is-trunc-type-Î -Truncated-Type' k A B =
  is-trunc-Î  k (Î» x â†’ is-trunc-type-Truncated-Type (B x))

Î -Truncated-Type' :
  (k : ğ•‹) {l1 l2 : Level} (A : UU l1) (B : A â†’ UU-Truncated-Type k l2) â†’
  UU-Truncated-Type k (l1 âŠ” l2)
pr1 (Î -Truncated-Type' k A B) = type-Î -Truncated-Type' k A B
pr2 (Î -Truncated-Type' k A B) = is-trunc-type-Î -Truncated-Type' k A B

type-Î -Truncated-Type :
  (k : ğ•‹) {l1 l2 : Level} (A : UU-Truncated-Type k l1)
  (B : type-Truncated-Type A â†’ UU-Truncated-Type k l2) â†’
  UU (l1 âŠ” l2)
type-Î -Truncated-Type k A B =
  type-Î -Truncated-Type' k (type-Truncated-Type A) B

is-trunc-type-Î -Truncated-Type :
  (k : ğ•‹) {l1 l2 : Level} (A : UU-Truncated-Type k l1)
  (B : type-Truncated-Type A â†’ UU-Truncated-Type k l2) â†’
  is-trunc k (type-Î -Truncated-Type k A B)
is-trunc-type-Î -Truncated-Type k A B =
  is-trunc-type-Î -Truncated-Type' k (type-Truncated-Type A) B

Î -Truncated-Type :
  (k : ğ•‹) {l1 l2 : Level} (A : UU-Truncated-Type k l1)
  (B : type-Truncated-Type A â†’ UU-Truncated-Type k l2) â†’
  UU-Truncated-Type k (l1 âŠ” l2)
Î -Truncated-Type k A B =
  Î -Truncated-Type' k (type-Truncated-Type A) B
```

### The type of functions into a truncated type is truncated

```agda
abstract
  is-trunc-function-type :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
    is-trunc k B â†’ is-trunc k (A â†’ B)
  is-trunc-function-type k {A} {B} is-trunc-B =
    is-trunc-Î  k {B = Î» (x : A) â†’ B} (Î» x â†’ is-trunc-B)

type-hom-Truncated-Type :
  (k : ğ•‹) {l1 l2 : Level} (A : UU-Truncated-Type k l1)
  (B : UU-Truncated-Type k l2) â†’ UU (l1 âŠ” l2)
type-hom-Truncated-Type k A B =
  type-Truncated-Type A â†’ type-Truncated-Type B

is-trunc-type-hom-Truncated-Type :
  (k : ğ•‹) {l1 l2 : Level} (A : UU-Truncated-Type k l1)
  (B : UU-Truncated-Type k l2) â†’
  is-trunc k (type-hom-Truncated-Type k A B)
is-trunc-type-hom-Truncated-Type k A B =
  is-trunc-function-type k (is-trunc-type-Truncated-Type B)

hom-Truncated-Type :
  (k : ğ•‹) {l1 l2 : Level} (A : UU-Truncated-Type k l1)
  (B : UU-Truncated-Type k l2) â†’ UU-Truncated-Type k (l1 âŠ” l2)
pr1 (hom-Truncated-Type k A B) = type-hom-Truncated-Type k A B
pr2 (hom-Truncated-Type k A B) = is-trunc-type-hom-Truncated-Type k A B
```

### Being truncated is a property

```agda
abstract
  is-prop-is-trunc :
    {l : Level} (k : ğ•‹) (A : UU l) â†’ is-trunc neg-one-ğ•‹ (is-trunc k A)
  is-prop-is-trunc neg-two-ğ•‹ A = is-subtype-is-contr
  is-prop-is-trunc (succ-ğ•‹ k) A =
    is-trunc-Î  neg-one-ğ•‹
      ( Î» x â†’ is-trunc-Î  neg-one-ğ•‹ (Î» y â†’ is-prop-is-trunc k (Id x y)))

is-trunc-Prop : {l : Level} (k : ğ•‹) (A : UU l) â†’ Î£ (UU l) (is-trunc neg-one-ğ•‹)
pr1 (is-trunc-Prop k A) = is-trunc k A
pr2 (is-trunc-Prop k A) = is-prop-is-trunc k A
```

### The type of equivalences between truncated types is truncated

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-trunc-equiv-is-trunc :
    (k : ğ•‹) â†’ is-trunc k A â†’ is-trunc k B â†’ is-trunc k (A â‰ƒ B)
  is-trunc-equiv-is-trunc neg-two-ğ•‹ is-trunc-A is-trunc-B =
    is-contr-equiv-is-contr is-trunc-A is-trunc-B
  is-trunc-equiv-is-trunc (succ-ğ•‹ k) is-trunc-A is-trunc-B = 
    is-trunc-Î£
      ( is-trunc-Î  (succ-ğ•‹ k) (Î» x â†’ is-trunc-B))
      ( Î» x â†’ [is-trunc-is-prop] k (is-subtype-is-equiv x))
```
