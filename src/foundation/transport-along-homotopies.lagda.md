# Transport along homotopies

```agda
module foundation.transport-along-homotopies where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.function-extensionality
open import foundation.homotopy-induction
open import foundation.transport-along-higher-identifications
open import foundation.transport-along-identifications
open import foundation.universe-levels

open import foundation-core.homotopies
open import foundation-core.identity-types
```

</details>

## Idea

If `C : (x : A) â†’ B x â†’ ğ’°` is a type family, and `H : f ~ g` is a homotopy
between functions `f g : (x : A) â†’ B x`, then there is a natural transport
operation that transports an element `z : C x (f x)` along the homotopy `H` to
an element of type `C x (g x)`.

## Definitions

### Transporting along homotopies

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  {f g : (x : A) â†’ B x}
  where

  tr-htpy :
    (f ~ g) â†’ ((x : A) â†’ C x (f x)) â†’ ((x : A) â†’ C x (g x))
  tr-htpy H f' x = tr (C x) (H x) (f' x)

  inv-tr-htpy :
    (f ~ g) â†’ ((x : A) â†’ C x (g x)) â†’ ((x : A) â†’ C x (f x))
  inv-tr-htpy H f' x = inv-tr (C x) (H x) (f' x)

  tr-htpyÂ² :
    {H K : f ~ g} (L : H ~ K) (f' : (x : A) â†’ C x (f x)) â†’
    tr-htpy H f' ~ tr-htpy K f'
  tr-htpyÂ² L f' x = trÂ² (C x) (L x) (f' x)
```

## Properties

### Transport along a homotopy `H` is homotopic to transport along the identification `eq-htpy H`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  where

  statement-compute-tr-htpy :
    {f g : (x : A) â†’ B x} (H : f ~ g) â†’ UU (l1 âŠ” l3)
  statement-compute-tr-htpy H =
    tr (Î» u â†’ (x : A) â†’ C x (u x)) (eq-htpy H) ~ tr-htpy C H

  base-case-compute-tr-htpy :
    {f : (x : A) â†’ B x} â†’ statement-compute-tr-htpy (refl-htpy' f)
  base-case-compute-tr-htpy =
    htpy-eq (ap (tr (Î» u â†’ (x : A) â†’ C x (u x))) (eq-htpy-refl-htpy _))

  abstract
    compute-tr-htpy :
      {f g : (x : A) â†’ B x} (H : f ~ g) â†’ statement-compute-tr-htpy H
    compute-tr-htpy {f} =
      ind-htpy f
        ( Î» _ â†’ statement-compute-tr-htpy)
        ( base-case-compute-tr-htpy)

    compute-tr-htpy-refl-htpy :
      {f : (x : A) â†’ B x} â†’
      compute-tr-htpy (refl-htpy' f) ï¼ base-case-compute-tr-htpy
    compute-tr-htpy-refl-htpy {f} =
      compute-ind-htpy f
        ( Î» _ â†’ statement-compute-tr-htpy)
        ( base-case-compute-tr-htpy)
```

### Inverse transport along a homotopy `H` is homotopic to inverse transport along the identification `eq-htpy H`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  where

  statement-compute-inv-tr-htpy :
    {f g : (x : A) â†’ B x} (H : f ~ g) â†’ UU (l1 âŠ” l3)
  statement-compute-inv-tr-htpy H =
    inv-tr (Î» u â†’ (x : A) â†’ C x (u x)) (eq-htpy H) ~ inv-tr-htpy C H

  base-case-compute-inv-tr-htpy :
    {f : (x : A) â†’ B x} â†’ statement-compute-inv-tr-htpy (refl-htpy' f)
  base-case-compute-inv-tr-htpy =
    htpy-eq (ap (inv-tr (Î» u â†’ (x : A) â†’ C x (u x))) (eq-htpy-refl-htpy _))

  abstract
    compute-inv-tr-htpy :
      {f g : (x : A) â†’ B x} (H : f ~ g) â†’ statement-compute-inv-tr-htpy H
    compute-inv-tr-htpy {f} =
      ind-htpy f
        ( Î» _ â†’ statement-compute-inv-tr-htpy)
        ( base-case-compute-inv-tr-htpy)

    compute-inv-tr-htpy-refl-htpy :
      {f : (x : A) â†’ B x} â†’
      compute-inv-tr-htpy (refl-htpy' f) ï¼ base-case-compute-inv-tr-htpy
    compute-inv-tr-htpy-refl-htpy {f} =
      compute-ind-htpy f
        ( Î» _ â†’ statement-compute-inv-tr-htpy)
        ( base-case-compute-inv-tr-htpy)
```
