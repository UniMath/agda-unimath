# 0-Connected types

```agda
module foundation.0-connected-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.constant-maps
open import foundation.contractible-types
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.empty-types
open import foundation.equality-coproduct-types
open import foundation.evaluation-functions
open import foundation.fiber-inclusions
open import foundation.functoriality-set-truncation
open import foundation.images
open import foundation.inhabited-types
open import foundation.mere-equality
open import foundation.negation
open import foundation.propositional-truncations
open import foundation.set-truncations
open import foundation.sets
open import foundation.surjective-maps
open import foundation.unit-type
open import foundation.universal-property-contractible-types
open import foundation.universal-property-unit-type
open import foundation.universe-levels

open import foundation-core.cartesian-product-types
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.identity-types
open import foundation-core.precomposition-functions
open import foundation-core.propositions
open import foundation-core.truncated-maps
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Idea

A type is said to be
{{#concept "0-connected" Disambiguation="type" Agda=is-0-connected}} if its type
of [connected components](foundation.connected-components.md), i.e., its
[set truncation](foundation.set-truncations.md), is
[contractible](foundation-core.contractible-types.md).

```agda
is-0-connected-Prop : {l : Level} â†’ UU l â†’ Prop l
is-0-connected-Prop A = is-contr-Prop (type-trunc-Set A)

is-0-connected : {l : Level} â†’ UU l â†’ UU l
is-0-connected A = type-Prop (is-0-connected-Prop A)

is-prop-is-0-connected : {l : Level} (A : UU l) â†’ is-prop (is-0-connected A)
is-prop-is-0-connected A = is-prop-type-Prop (is-0-connected-Prop A)
```

## Properties

### 0-connected types are inhabited

```agda
abstract
  is-inhabited-is-0-connected :
    {l : Level} {A : UU l} â†’ is-0-connected A â†’ is-inhabited A
  is-inhabited-is-0-connected {l} {A} C =
    apply-universal-property-trunc-Set'
      ( center C)
      ( set-Prop (trunc-Prop A))
      ( unit-trunc-Prop)
```

### Elements of 0-connected types are all merely equal

```agda
abstract
  mere-eq-is-0-connected :
    {l : Level} {A : UU l} â†’ is-0-connected A â†’ all-elements-merely-equal A
  mere-eq-is-0-connected {A = A} H x y =
    apply-effectiveness-unit-trunc-Set (eq-is-contr H)
```

### A type is 0-connected if it is inhabited and all elements are merely equal

```agda
abstract
  is-0-connected-mere-eq :
    {l : Level} {A : UU l} (a : A) â†’
    ((x : A) â†’ mere-eq a x) â†’ is-0-connected A
  is-0-connected-mere-eq {l} {A} a e =
    pair
      ( unit-trunc-Set a)
      ( apply-dependent-universal-property-trunc-Set'
        ( Î» x â†’ set-Prop (Id-Prop (trunc-Set A) (unit-trunc-Set a) x))
        ( Î» x â†’ apply-effectiveness-unit-trunc-Set' (e x)))

abstract
  is-0-connected-mere-eq-is-inhabited :
    {l : Level} {A : UU l} â†’
    is-inhabited A â†’ all-elements-merely-equal A â†’ is-0-connected A
  is-0-connected-mere-eq-is-inhabited H K =
    apply-universal-property-trunc-Prop H
      ( is-0-connected-Prop _)
      ( Î» a â†’ is-0-connected-mere-eq a (K a))
```

### A type is 0-connected iff any point inclusion is surjective

```agda
abstract
  is-0-connected-is-surjective-point :
    {l1 : Level} {A : UU l1} (a : A) â†’
    is-surjective (point a) â†’ is-0-connected A
  is-0-connected-is-surjective-point a H =
    is-0-connected-mere-eq a
      ( Î» x â†’
        apply-universal-property-trunc-Prop
          ( H x)
          ( mere-eq-Prop a x)
          ( Î» u â†’ unit-trunc-Prop (pr2 u)))

abstract
  is-surjective-point-is-0-connected :
    {l1 : Level} {A : UU l1} (a : A) â†’
    is-0-connected A â†’ is-surjective (point a)
  is-surjective-point-is-0-connected a H x =
    apply-universal-property-trunc-Prop
      ( mere-eq-is-0-connected H a x)
      ( trunc-Prop (fiber (point a) x))
      ( Î» where refl â†’ unit-trunc-Prop (star , refl))
```

### If `A` is 0-connected and `B` is `k+1`-truncated then every evaluation map `(A â†’ B) â†’ B` is `k`-truncated

```agda
is-trunc-map-ev-is-connected :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (a : A) â†’
  is-0-connected A â†’ is-trunc (succ-ğ•‹ k) B â†’
  is-trunc-map k (ev-function B a)
is-trunc-map-ev-is-connected k {A} {B} a H K =
  is-trunc-map-comp k
    ( ev-function B star)
    ( precomp (point a) B)
    ( is-trunc-map-is-equiv k
      ( universal-property-contr-is-contr star is-contr-unit B))
    ( is-trunc-map-precomp-Î -is-surjective k
      ( is-surjective-point-is-0-connected a H)
      ( Î» _ â†’ (B , K)))
```

### The dependent universal property of 0-connected types

```agda
module _
  {l1 : Level} {A : UU l1} (a : A) (H : is-0-connected A)
  {l : Level} (P : A â†’ Prop l)
  where

  equiv-dependent-universal-property-is-0-connected :
    ((x : A) â†’ type-Prop (P x)) â‰ƒ type-Prop (P a)
  equiv-dependent-universal-property-is-0-connected =
    ( equiv-universal-property-unit (type-Prop (P a))) âˆ˜e
    ( equiv-dependent-universal-property-surjection-is-surjective
      ( point a)
      ( is-surjective-point-is-0-connected a H)
      ( P))

  apply-dependent-universal-property-is-0-connected :
    type-Prop (P a) â†’ (x : A) â†’ type-Prop (P x)
  apply-dependent-universal-property-is-0-connected =
    map-inv-equiv equiv-dependent-universal-property-is-0-connected
```

### A type `A` is 0-connected if and only if every fiber inclusion `B a â†’ Î£ A B` is surjective

```agda
abstract
  is-surjective-fiber-inclusion :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    is-0-connected A â†’ (a : A) â†’ is-surjective (fiber-inclusion B a)
  is-surjective-fiber-inclusion {B = B} C a (x , b) =
    apply-universal-property-trunc-Prop
      ( mere-eq-is-0-connected C a x)
      ( trunc-Prop (fiber (fiber-inclusion B a) (x , b)))
      ( Î» where refl â†’ unit-trunc-Prop (b , refl))

abstract
  mere-eq-is-surjective-fiber-inclusion :
    {l1 : Level} {A : UU l1} (a : A) â†’
    ({l : Level} (B : A â†’ UU l) â†’ is-surjective (fiber-inclusion B a)) â†’
    (x : A) â†’ mere-eq a x
  mere-eq-is-surjective-fiber-inclusion a H x =
    apply-universal-property-trunc-Prop
      ( H (Î» x â†’ unit) (x , star))
      ( mere-eq-Prop a x)
      ( Î» u â†’ unit-trunc-Prop (ap pr1 (pr2 u)))

abstract
  is-0-connected-is-surjective-fiber-inclusion :
    {l1 : Level} {A : UU l1} (a : A) â†’
    ({l : Level} (B : A â†’ UU l) â†’ is-surjective (fiber-inclusion B a)) â†’
    is-0-connected A
  is-0-connected-is-surjective-fiber-inclusion a H =
    is-0-connected-mere-eq a (mere-eq-is-surjective-fiber-inclusion a H)
```

### 0-connected types are closed under equivalences

```agda
is-0-connected-equiv :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  (A â‰ƒ B) â†’ is-0-connected B â†’ is-0-connected A
is-0-connected-equiv e = is-contr-equiv _ (equiv-trunc-Set e)

is-0-connected-equiv' :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  (A â‰ƒ B) â†’ is-0-connected A â†’ is-0-connected B
is-0-connected-equiv' e = is-0-connected-equiv (inv-equiv e)
```

### 0-connected types are closed under cartesian products

```agda
module _
  {l1 l2 : Level} (X : UU l1) (Y : UU l2)
  (p1 : is-0-connected X) (p2 : is-0-connected Y)
  where

  is-0-connected-product : is-0-connected (X Ã— Y)
  is-0-connected-product =
    is-contr-equiv
      ( type-trunc-Set X Ã— type-trunc-Set Y)
      ( equiv-distributive-trunc-product-Set X Y)
      ( is-contr-product p1 p2)
```

### The unit type is 0-connected

```agda
abstract
  is-0-connected-unit : is-0-connected unit
  is-0-connected-unit =
    is-contr-equiv' unit equiv-unit-trunc-unit-Set is-contr-unit
```

### Contractible types are 0-connected

```agda
is-0-connected-is-contr :
  {l : Level} (X : UU l) â†’
  is-contr X â†’ is-0-connected X
is-0-connected-is-contr X p =
  is-contr-equiv X (inv-equiv (equiv-unit-trunc-Set (X , is-set-is-contr p))) p
```

### The image of a function with a 0-connected domain is 0-connected

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  abstract
    is-contr-im-map-trunc-Set-is-0-connected-domain' :
      A â†’ all-elements-merely-equal A â†’ is-contr (im (map-trunc-Set f))
    is-contr-im-map-trunc-Set-is-0-connected-domain' a C =
      is-contr-im
        ( trunc-Set B)
        ( unit-trunc-Set a)
        ( apply-dependent-universal-property-trunc-Set'
          ( Î» x â†’
            set-Prop
              ( Id-Prop
                ( trunc-Set B)
                ( map-trunc-Set f x)
                ( map-trunc-Set f (unit-trunc-Set a))))
          ( Î» a' â†’
            apply-universal-property-trunc-Prop
              ( C a' a)
              ( Id-Prop
                ( trunc-Set B)
                ( map-trunc-Set f (unit-trunc-Set a'))
                ( map-trunc-Set f (unit-trunc-Set a)))
              ( Î» where refl â†’ refl)))

  abstract
    is-0-connected-im-is-0-connected-domain' :
      A â†’ all-elements-merely-equal A â†’ is-0-connected (im f)
    is-0-connected-im-is-0-connected-domain' a C =
      is-contr-equiv'
        ( im (map-trunc-Set f))
        ( equiv-trunc-im-Set f)
        ( is-contr-im-map-trunc-Set-is-0-connected-domain' a C)

  abstract
    is-0-connected-im-is-0-connected-domain :
      is-0-connected A â†’ is-0-connected (im f)
    is-0-connected-im-is-0-connected-domain C =
      apply-universal-property-trunc-Prop
        ( is-inhabited-is-0-connected C)
        ( is-contr-Prop _)
        ( Î» a â†’
          is-0-connected-im-is-0-connected-domain' a (mere-eq-is-0-connected C))
```

### The image of a point is 0-connected

```agda
module _
  {l1 : Level} {A : UU l1}
  where

  abstract
    is-0-connected-im-point' : (f : unit â†’ A) â†’ is-0-connected (im f)
    is-0-connected-im-point' f =
      is-0-connected-im-is-0-connected-domain f is-0-connected-unit

  abstract
    is-0-connected-im-point : (a : A) â†’ is-0-connected (im (point a))
    is-0-connected-im-point a = is-0-connected-im-point' (point a)
```

### Coproducts of inhabited types are not 0-connected

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-not-0-connected-coproduct-has-element :
      A â†’ B â†’ Â¬ is-0-connected (A + B)
    is-not-0-connected-coproduct-has-element a b H =
      apply-universal-property-trunc-Prop
        ( mere-eq-is-0-connected H (inl a) (inr b))
        ( empty-Prop)
        ( is-empty-eq-coproduct-inl-inr a b)

  abstract
    is-not-0-connected-coproduct-is-inhabited :
      is-inhabited A â†’ is-inhabited B â†’ Â¬ is-0-connected (A + B)
    is-not-0-connected-coproduct-is-inhabited |a| |b| =
      apply-twice-universal-property-trunc-Prop |a| |b|
        ( neg-type-Prop (is-0-connected (A + B)))
        ( is-not-0-connected-coproduct-has-element)
```
