# The universal property of dependent function types

```agda
module foundation.universal-property-dependent-function-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.identity-types
open import foundation.spans-families-of-types
open import foundation.terminal-spans-families-of-types
open import foundation.universe-levels

open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation-core.equivalences
open import foundation-core.functoriality-dependent-function-types
open import foundation-core.functoriality-dependent-pair-types
```

</details>

## Idea

Consider a family of types `B` over `A`. Then the dependent function type
`(a : A) â†’ B a` naturally has the structure of a
[span](foundation.spans-families-of-types.md) on the family of types `B` over
`A`, where for each `a : A` the map

```text
  ((x : A) â†’ B x) â†’ B a
```

is given by evaluation at `a`.

A span `ğ’® := (S , f)` is said to satisfy the
{{#concept "universal property of dependent function types" Agda=universal-property-dependent-function-types}}
if for any type `T` the map

```text
  (T â†’ S) â†’ ((x : A) â†’ T â†’ B x)
```

given by `h â†¦ Î» x t â†’ f x (h t)` is an
[equivalence](foundation-core.equivalences.md). The dependent function type
`(x : A) â†’ B x` equipped with the span structure defined above satisfies the
universal property of dependent function types.

In
[`foundation.dependent-function-types`](foundation.dependent-function-types.md)
we show that dependent function types satisfy the universal property of
dependent function types. In this file we also show that the universal property
of dependent function types is equivalent to being a
[terminal span](foundation.terminal-spans-families-of-types.md) on the type
family `B`.

## Definitions

### The universal property of dependent function types

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (ğ’® : span-type-family l3 B)
  where

  ev-span-type-family :
    {l : Level} (T : UU l) â†’
    (T â†’ spanning-type-span-type-family ğ’®) â†’ (x : A) â†’ T â†’ B x
  ev-span-type-family T h x t = map-span-type-family ğ’® x (h t)

  universal-property-dependent-function-types : UUÏ‰
  universal-property-dependent-function-types =
    {l : Level} (T : UU l) â†’ is-equiv (ev-span-type-family T)
```

## Properties

### A span on a family of types satisfies the universal property of dependent function types if and only if it is terminal

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (ğ’® : span-type-family l3 B)
  where

  abstract
    is-terminal-universal-property-dependent-function-types :
      universal-property-dependent-function-types ğ’® â†’
      is-terminal-span-type-family ğ’®
    is-terminal-universal-property-dependent-function-types U ğ’¯ =
      is-contr-equiv' _
        ( equiv-tot
          ( Î» h â†’
            ( equiv-Î -equiv-family
              ( Î» a â†’
                ( equiv-Î -equiv-family (Î» t â†’ equiv-inv _ _)) âˆ˜e
                ( equiv-funext))) âˆ˜e
            ( equiv-funext)))
        ( is-contr-map-is-equiv
          ( U (spanning-type-span-type-family ğ’¯))
          ( map-span-type-family ğ’¯))

  abstract
    universal-property-dependent-function-types-is-terminal :
      is-terminal-span-type-family ğ’® â†’
      universal-property-dependent-function-types ğ’®
    universal-property-dependent-function-types-is-terminal U T =
      is-equiv-is-contr-map
        ( Î» g â†’
          is-contr-equiv _
            ( equiv-tot
              ( Î» h â†’
                ( equiv-Î -equiv-family
                  ( Î» a â†’
                    ( equiv-Î -equiv-family (Î» t â†’ equiv-inv _ _)) âˆ˜e
                    ( equiv-funext))) âˆ˜e
                ( equiv-funext)))
            ( U (T , g)))
```

## See also

- [Dependent function types](foundation.dependent-function-types.md)
