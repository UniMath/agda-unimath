# Maps fibered over a map

```agda
module foundation.fibered-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.cones-over-cospan-diagrams
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopy-induction
open import foundation.morphisms-slice
open import foundation.structure-identity-principle
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unit-type
open import foundation.universal-property-empty-type
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.commuting-squares-of-maps
open import foundation-core.contractible-types
open import foundation-core.empty-types
open import foundation-core.equality-dependent-pair-types
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.small-types
open import foundation-core.torsorial-type-families
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Idea

Consider a diagram of the form

```text
    A         B
    |         |
  f |         | g
    âˆ¨         âˆ¨
    X ------> Y
         i
```

A **fibered map** from `f` to `g` over `i` is a map `h : A â†’ B` such that the
square `i âˆ˜ f ~ g âˆ˜ h` [commutes](foundation-core.commuting-squares-of-maps.md).

## Definitions

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y)
  where

  is-map-over : (X â†’ Y) â†’ (A â†’ B) â†’ UU (l1 âŠ” l4)
  is-map-over i h = coherence-square-maps h f g i

  map-over : (X â†’ Y) â†’ UU (l1 âŠ” l2 âŠ” l4)
  map-over i = Î£ (A â†’ B) (is-map-over i)

  fibered-map : UU (l1 âŠ” l3 âŠ” l2 âŠ” l4)
  fibered-map = Î£ (X â†’ Y) (map-over)

  fiberwise-map-over : (X â†’ Y) â†’ UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  fiberwise-map-over i = (x : X) â†’ fiber f x â†’ fiber g (i x)

  cone-fibered-map : (ihH : fibered-map) â†’ cone (pr1 ihH) g A
  pr1 (cone-fibered-map ihH) = f
  pr1 (pr2 (cone-fibered-map (i , h , H))) = h
  pr2 (pr2 (cone-fibered-map (i , h , H))) = H

module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y)
  where

  map-total-map-over : (i : X â†’ Y) â†’ map-over f g i â†’ A â†’ B
  map-total-map-over i = pr1

  is-map-over-map-total-map-over :
    (i : X â†’ Y) (m : map-over f g i) â†’
    is-map-over f g i (map-total-map-over i m)
  is-map-over-map-total-map-over i = pr2

  map-over-fibered-map : (m : fibered-map f g) â†’ map-over f g (pr1 m)
  map-over-fibered-map = pr2

  map-base-fibered-map : (m : fibered-map f g) â†’ X â†’ Y
  map-base-fibered-map = pr1

  map-total-fibered-map : (m : fibered-map f g) â†’ A â†’ B
  map-total-fibered-map = pr1 âˆ˜ pr2

  is-map-over-map-total-fibered-map :
    (m : fibered-map f g) â†’
    is-map-over f g (map-base-fibered-map m) (map-total-fibered-map m)
  is-map-over-map-total-fibered-map = pr2 âˆ˜ pr2
```

## Properties

### Identifications of maps over

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y) (i : X â†’ Y)
  where

  coherence-htpy-map-over :
    (m m' : map-over f g i) â†’
    map-total-map-over f g i m ~ map-total-map-over f g i m' â†’ UU (l1 âŠ” l4)
  coherence-htpy-map-over m m' K =
    ( is-map-over-map-total-map-over f g i m âˆ™h (g Â·l K)) ~
    ( is-map-over-map-total-map-over f g i m')

  htpy-map-over : (m m' : map-over f g i) â†’ UU (l1 âŠ” l2 âŠ” l4)
  htpy-map-over m m' =
    Î£ ( map-total-map-over f g i m ~ map-total-map-over f g i m')
      ( coherence-htpy-map-over m m')

  refl-htpy-map-over : (m : map-over f g i) â†’ htpy-map-over m m
  pr1 (refl-htpy-map-over m) = refl-htpy
  pr2 (refl-htpy-map-over m) = right-unit-htpy

  htpy-eq-map-over : (m m' : map-over f g i) â†’ m ï¼ m' â†’ htpy-map-over m m'
  htpy-eq-map-over m .m refl = refl-htpy-map-over m

  is-torsorial-htpy-map-over :
    (m : map-over f g i) â†’ is-torsorial (htpy-map-over m)
  is-torsorial-htpy-map-over m =
    is-torsorial-Eq-structure
      ( is-torsorial-htpy (map-total-map-over f g i m))
      ( map-total-map-over f g i m , refl-htpy)
      ( is-torsorial-htpy (is-map-over-map-total-map-over f g i m âˆ™h refl-htpy))

  is-equiv-htpy-eq-map-over :
    (m m' : map-over f g i) â†’ is-equiv (htpy-eq-map-over m m')
  is-equiv-htpy-eq-map-over m =
    fundamental-theorem-id (is-torsorial-htpy-map-over m) (htpy-eq-map-over m)

  extensionality-map-over :
    (m m' : map-over f g i) â†’ (m ï¼ m') â‰ƒ (htpy-map-over m m')
  pr1 (extensionality-map-over m m') = htpy-eq-map-over m m'
  pr2 (extensionality-map-over m m') = is-equiv-htpy-eq-map-over m m'

  eq-htpy-map-over : (m m' : map-over f g i) â†’ htpy-map-over m m' â†’ m ï¼ m'
  eq-htpy-map-over m m' = map-inv-equiv (extensionality-map-over m m')
```

### Identifications of fibered maps

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y)
  where

  coherence-htpy-fibered-map :
    (m m' : fibered-map f g) â†’
    map-base-fibered-map f g m ~ map-base-fibered-map f g m' â†’
    map-total-fibered-map f g m ~ map-total-fibered-map f g m' â†’ UU (l1 âŠ” l4)
  coherence-htpy-fibered-map m m' I H =
    ( is-map-over-map-total-fibered-map f g m âˆ™h (g Â·l H)) ~
    ( (I Â·r f) âˆ™h is-map-over-map-total-fibered-map f g m')

  htpy-fibered-map : (m m' : fibered-map f g) â†’ UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  htpy-fibered-map m m' =
    Î£ ( map-base-fibered-map f g m ~ map-base-fibered-map f g m')
      ( Î» I â†’
      Î£ ( map-total-fibered-map f g m ~ map-total-fibered-map f g m')
        ( coherence-htpy-fibered-map m m' I))

  refl-htpy-fibered-map : (m : fibered-map f g) â†’ htpy-fibered-map m m
  pr1 (refl-htpy-fibered-map m) = refl-htpy
  pr1 (pr2 (refl-htpy-fibered-map m)) = refl-htpy
  pr2 (pr2 (refl-htpy-fibered-map m)) =
    inv-htpy-left-unit-htpy âˆ™h right-unit-htpy

  htpy-eq-fibered-map :
    (m m' : fibered-map f g) â†’ m ï¼ m' â†’ htpy-fibered-map m m'
  htpy-eq-fibered-map m .m refl = refl-htpy-fibered-map m

  is-torsorial-htpy-fibered-map :
    (m : fibered-map f g) â†’ is-torsorial (htpy-fibered-map m)
  is-torsorial-htpy-fibered-map m =
    is-torsorial-Eq-structure
      ( is-torsorial-htpy (map-base-fibered-map f g m))
      ( map-base-fibered-map f g m , refl-htpy)
      ( is-torsorial-Eq-structure
        ( is-torsorial-htpy (map-total-fibered-map f g m))
        ( map-total-fibered-map f g m , refl-htpy)
        ( is-torsorial-htpy
          ( is-map-over-map-total-fibered-map f g m âˆ™h refl-htpy)))

  is-equiv-htpy-eq-fibered-map :
    (m m' : fibered-map f g) â†’ is-equiv (htpy-eq-fibered-map m m')
  is-equiv-htpy-eq-fibered-map m =
    fundamental-theorem-id
      ( is-torsorial-htpy-fibered-map m)
      ( htpy-eq-fibered-map m)

  extensionality-fibered-map :
    (m m' : fibered-map f g) â†’ (m ï¼ m') â‰ƒ (htpy-fibered-map m m')
  pr1 (extensionality-fibered-map m m') = htpy-eq-fibered-map m m'
  pr2 (extensionality-fibered-map m m') = is-equiv-htpy-eq-fibered-map m m'

  eq-htpy-fibered-map :
    (m m' : fibered-map f g) â†’ htpy-fibered-map m m' â†’ m ï¼ m'
  eq-htpy-fibered-map m m' = map-inv-equiv (extensionality-fibered-map m m')
```

### Fibered maps and fiberwise maps over are equivalent notions

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y) (i : X â†’ Y)
  where

  fiberwise-map-over-map-over :
    map-over f g i â†’ fiberwise-map-over f g i
  pr1 (fiberwise-map-over-map-over (h , H) .(f a) (a , refl)) = h a
  pr2 (fiberwise-map-over-map-over (h , H) .(f a) (a , refl)) = inv (H a)

  map-over-fiberwise-map-over :
    fiberwise-map-over f g i â†’ map-over f g i
  pr1 (map-over-fiberwise-map-over Î±) a = pr1 (Î± (f a) (pair a refl))
  pr2 (map-over-fiberwise-map-over Î±) a = inv (pr2 (Î± (f a) (pair a refl)))

  is-section-map-over-fiberwise-map-over-eq-htpy :
    (Î± : fiberwise-map-over f g i) (x : X) â†’
    fiberwise-map-over-map-over (map-over-fiberwise-map-over Î±) x ~ Î± x
  is-section-map-over-fiberwise-map-over-eq-htpy Î± .(f a) (pair a refl) =
    eq-pair-eq-fiber (inv-inv (pr2 (Î± (f a) (pair a refl))))

  is-section-map-over-fiberwise-map-over :
    fiberwise-map-over-map-over âˆ˜ map-over-fiberwise-map-over ~ id
  is-section-map-over-fiberwise-map-over Î± =
    eq-htpy (eq-htpy âˆ˜ is-section-map-over-fiberwise-map-over-eq-htpy Î±)

  is-retraction-map-over-fiberwise-map-over :
    map-over-fiberwise-map-over âˆ˜ fiberwise-map-over-map-over ~ id
  is-retraction-map-over-fiberwise-map-over (pair h H) =
    eq-pair-eq-fiber (eq-htpy (inv-inv âˆ˜ H))

  abstract
    is-equiv-fiberwise-map-over-map-over :
      is-equiv (fiberwise-map-over-map-over)
    is-equiv-fiberwise-map-over-map-over =
      is-equiv-is-invertible
        ( map-over-fiberwise-map-over)
        ( is-section-map-over-fiberwise-map-over)
        ( is-retraction-map-over-fiberwise-map-over)

  abstract
    is-equiv-map-over-fiberwise-map-over :
      is-equiv (map-over-fiberwise-map-over)
    is-equiv-map-over-fiberwise-map-over =
      is-equiv-is-invertible
        ( fiberwise-map-over-map-over)
        ( is-retraction-map-over-fiberwise-map-over)
        ( is-section-map-over-fiberwise-map-over)

  equiv-fiberwise-map-over-map-over :
    map-over f g i â‰ƒ fiberwise-map-over f g i
  pr1 equiv-fiberwise-map-over-map-over =
    fiberwise-map-over-map-over
  pr2 equiv-fiberwise-map-over-map-over =
    is-equiv-fiberwise-map-over-map-over

  equiv-map-over-fiberwise-map-over :
    fiberwise-map-over f g i â‰ƒ map-over f g i
  pr1 equiv-map-over-fiberwise-map-over =
    map-over-fiberwise-map-over
  pr2 equiv-map-over-fiberwise-map-over =
    is-equiv-map-over-fiberwise-map-over

  equiv-map-over-fiberwise-hom :
    fiberwise-hom (i âˆ˜ f) g â‰ƒ map-over f g i
  equiv-map-over-fiberwise-hom =
    equiv-hom-slice-fiberwise-hom (i âˆ˜ f) g

  equiv-fiberwise-map-over-fiberwise-hom :
    fiberwise-hom (i âˆ˜ f) g â‰ƒ fiberwise-map-over f g i
  equiv-fiberwise-map-over-fiberwise-hom =
    equiv-fiberwise-map-over-map-over âˆ˜e equiv-map-over-fiberwise-hom

  is-small-fiberwise-map-over :
    is-small (l1 âŠ” l2 âŠ” l4) (fiberwise-map-over f g i)
  pr1 is-small-fiberwise-map-over = map-over f g i
  pr2 is-small-fiberwise-map-over = equiv-map-over-fiberwise-map-over
```

### Slice maps are equal to fibered maps over

```agda
eq-map-over-id-hom-slice :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’ hom-slice f g ï¼ map-over f g id
eq-map-over-id-hom-slice f g = refl

eq-map-over-hom-slice :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y) (i : X â†’ Y) â†’ hom-slice (i âˆ˜ f) g ï¼ map-over f g i
eq-map-over-hom-slice f g i = refl
```

### Horizontal composition for fibered maps

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level}
  {A : UU l1} {B : UU l2} {C : UU l3}
  {X : UU l4} {Y : UU l5} {Z : UU l6}
  {f : A â†’ X} {g : B â†’ Y} {h : C â†’ Z}
  where

  is-map-over-pasting-horizontal :
    {k : X â†’ Y} {l : Y â†’ Z} {i : A â†’ B} {j : B â†’ C} â†’
    is-map-over f g k i â†’ is-map-over g h l j â†’
    is-map-over f h (l âˆ˜ k) (j âˆ˜ i)
  is-map-over-pasting-horizontal {k} {l} {i} {j} =
    pasting-horizontal-coherence-square-maps i j f g h k l

  map-over-pasting-horizontal :
    {k : X â†’ Y} {l : Y â†’ Z} â†’
    map-over f g k â†’ map-over g h l â†’ map-over f h (l âˆ˜ k)
  pr1 (map-over-pasting-horizontal {k} {l} (i , I) (j , J)) = j âˆ˜ i
  pr2 (map-over-pasting-horizontal {k} {l} (i , I) (j , J)) =
    is-map-over-pasting-horizontal {k} {l} I J

  fibered-map-pasting-horizontal :
    fibered-map f g â†’ fibered-map g h â†’ fibered-map f h
  pr1 (fibered-map-pasting-horizontal (k , iI) (l , jJ)) = l âˆ˜ k
  pr2 (fibered-map-pasting-horizontal (k , iI) (l , jJ)) =
    map-over-pasting-horizontal {k} {l} iI jJ
```

### Vertical composition for fibered maps

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level}
  {A : UU l1} {B : UU l2}
  {C : UU l3} {D : UU l4}
  {X : UU l5} {Y : UU l6}
  {i : A â†’ B} {j : C â†’ D} {k : X â†’ Y}
  where

  is-map-over-pasting-vertical :
    {f : A â†’ C} {g : B â†’ D}
    {f' : C â†’ X} {g' : D â†’ Y} â†’
    is-map-over f g j i â†’ is-map-over f' g' k j â†’
    is-map-over (f' âˆ˜ f) (g' âˆ˜ g) k i
  is-map-over-pasting-vertical {f} {g} {f'} {g'} =
    pasting-vertical-coherence-square-maps i f g j f' g' k
```

### The truncation level of the types of fibered maps is bounded by the truncation level of the codomains

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-trunc-is-map-over :
    (k : ð•‹) â†’ is-trunc (succ-ð•‹ k) Y â†’
    (f : A â†’ X) (g : B â†’ Y) (i : X â†’ Y) (h : A â†’ B) â†’
    is-trunc k (is-map-over f g i h)
  is-trunc-is-map-over k is-trunc-Y f g i h =
    is-trunc-Î  k (Î» x â†’ is-trunc-Y (i (f x)) (g (h x)))

  is-trunc-map-over :
    (k : ð•‹) â†’ is-trunc (succ-ð•‹ k) Y â†’ is-trunc k B â†’
    (f : A â†’ X) (g : B â†’ Y) (i : X â†’ Y) â†’ is-trunc k (map-over f g i)
  is-trunc-map-over k is-trunc-Y is-trunc-B f g i =
    is-trunc-Î£
      ( is-trunc-function-type k is-trunc-B)
      ( is-trunc-is-map-over k is-trunc-Y f g i)

  is-trunc-fibered-map :
    (k : ð•‹) â†’ is-trunc k Y â†’ is-trunc k B â†’
    (f : A â†’ X) (g : B â†’ Y) â†’ is-trunc k (fibered-map f g)
  is-trunc-fibered-map k is-trunc-Y is-trunc-B f g =
    is-trunc-Î£
      ( is-trunc-function-type k is-trunc-Y)
      ( is-trunc-map-over
        ( k)
        ( is-trunc-succ-is-trunc k is-trunc-Y)
        ( is-trunc-B)
        ( f)
        ( g))
```

### The transpose of a fibered map

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  transpose-is-map-over :
    (f : A â†’ X) (g : B â†’ Y) (i : X â†’ Y) (h : A â†’ B) â†’
    is-map-over f g i h â†’ is-map-over h i g f
  transpose-is-map-over f g i h = inv-htpy

  transpose-map-over :
    (f : A â†’ X) (g : B â†’ Y) (i : X â†’ Y)
    (hH : map-over f g i) â†’ map-over (pr1 hH) i g
  pr1 (transpose-map-over f g i hH) = f
  pr2 (transpose-map-over f g i (h , H)) =
    transpose-is-map-over f g i h H

  transpose-fibered-map :
    (f : A â†’ X) (g : B â†’ Y)
    (ihH : fibered-map f g) â†’ fibered-map (pr1 (pr2 ihH)) (pr1 ihH)
  pr1 (transpose-fibered-map f g ihH) = g
  pr2 (transpose-fibered-map f g (i , hH)) =
    transpose-map-over f g i hH
```

### If the top left corner is empty, the type of fibered maps is equivalent to maps `X â†’ Y`

```text
         !
  empty ---> B
    |        |
  ! |        | g
    âˆ¨        âˆ¨
    X -----> Y
        i
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y) (is-empty-A : is-empty A)
  where

  inv-compute-fibered-map-is-empty : (fibered-map f g) â‰ƒ (X â†’ Y)
  inv-compute-fibered-map-is-empty =
    right-unit-law-Î£-is-contr
      ( Î» i â†’
        is-contr-Î£
          ( universal-property-empty-is-empty A is-empty-A B)
          ( ex-falso âˆ˜ is-empty-A)
          ( dependent-universal-property-empty-is-empty A is-empty-A
            ( eq-value (i âˆ˜ f) (g âˆ˜ ex-falso âˆ˜ is-empty-A))))

  compute-fibered-map-is-empty : (X â†’ Y) â‰ƒ (fibered-map f g)
  compute-fibered-map-is-empty = inv-equiv inv-compute-fibered-map-is-empty

module _
  { l2 l3 l4 : Level} {B : UU l2} {X : UU l3} {Y : UU l4}
  {f : empty â†’ X} (g : B â†’ Y)
  where

  inv-compute-fibered-map-empty : (fibered-map f g) â‰ƒ (X â†’ Y)
  inv-compute-fibered-map-empty = inv-compute-fibered-map-is-empty f g id

  compute-fibered-map-empty : (X â†’ Y) â‰ƒ (fibered-map f g)
  compute-fibered-map-empty = compute-fibered-map-is-empty f g id
```

### If the bottom right corner is contractible, the type of fibered maps is equivalent to maps `A â†’ B`

```text
    A -----> B
    |        |
  f |        | !
    âˆ¨        âˆ¨
    X ---> unit
       !
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (f : A â†’ X) (g : B â†’ Y) (is-contr-Y : is-contr Y)
  where

  inv-compute-fibered-map-is-contr : (fibered-map f g) â‰ƒ (A â†’ B)
  inv-compute-fibered-map-is-contr =
    ( right-unit-law-Î£-is-contr
      ( Î» j â†’
        is-contr-Î 
          ( Î» x â†’
            is-prop-is-contr is-contr-Y (center is-contr-Y) (g (j x))))) âˆ˜e
    ( left-unit-law-Î£-is-contr
      ( is-contr-function-type is-contr-Y)
      ( Î» _ â†’ center is-contr-Y))

  compute-fibered-map-is-contr : (A â†’ B) â‰ƒ (fibered-map f g)
  compute-fibered-map-is-contr = inv-equiv inv-compute-fibered-map-is-contr

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) {g : B â†’ unit}
  where

  inv-compute-fibered-map-unit : (fibered-map f g) â‰ƒ (A â†’ B)
  inv-compute-fibered-map-unit =
    inv-compute-fibered-map-is-contr f g is-contr-unit

  compute-fibered-map-unit : (A â†’ B) â‰ƒ (fibered-map f g)
  compute-fibered-map-unit = compute-fibered-map-is-contr f g is-contr-unit
```

## Examples

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  (h : A â†’ B)
  where

  is-fibered-over-self : is-map-over id id h h
  is-fibered-over-self = refl-htpy

  self-map-over : map-over id id h
  pr1 self-map-over = h
  pr2 self-map-over = is-fibered-over-self

  self-fibered-map : fibered-map id id
  pr1 self-fibered-map = h
  pr2 self-fibered-map = self-map-over

  is-map-over-id : is-map-over h h id id
  is-map-over-id = refl-htpy

  id-map-over : map-over h h id
  pr1 id-map-over = id
  pr2 id-map-over = is-map-over-id

  id-fibered-map : fibered-map h h
  pr1 id-fibered-map = id
  pr2 id-fibered-map = id-map-over
```

## See also

- [Morphisms of arrows](foundation.morphisms-arrows.md) for the same concept
  under a different name.
- For the pullback property of the type of fibered maps, see
  [the pullback-hom](orthogonal-factorization-systems.pullback-hom.md)
