---
title: Maps fibered over a map
---

```agda
module foundation.fibered-maps where

open import foundation-core.commuting-squares
open import foundation-core.small-types
open import foundation-core.truncation-levels
open import foundation-core.truncated-types
open import foundation-core.dependent-pair-types
open import foundation-core.equality-dependent-pair-types
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.functions
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.universe-levels

open import foundation.function-extensionality
open import foundation.slice
```

## Idea

Consider a diagram of the form

```md
  A         B
  |         |
 f|         |g
  |         |
  V         V
  X ------> Y
       i
```

A fibered map from `f` to `g` over `i` is a map `h : A â†’ B` such that the square commutes (`(i âˆ˜ f) ~ (g âˆ˜ h)`).


## Definition

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-fibered-map : 
    (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) (h : A â†’ B) â†’ UU (l1 âŠ” l4)
  is-fibered-map i f g h = (i âˆ˜ f) ~ (g âˆ˜ h)

  fibered-map :
    (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’ UU (l1 âŠ” l2 âŠ” l4)
  fibered-map i f g = Î£ (A â†’ B) (is-fibered-map i f g)

  hom-over :
    (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’ UU (l1 âŠ” l2 âŠ” l4)
  hom-over i f g = hom-slice (i âˆ˜ f) g

  fiberwise-hom-over :
    (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’ UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  fiberwise-hom-over i f g = (x : X) â†’ fib f x â†’ fib g (i x)

  eq-hom-over-fibered-map : fibered-map ï¼ hom-over
  eq-hom-over-fibered-map = refl

  eq-fibered-map-hom-over : hom-over ï¼ fibered-map
  eq-fibered-map-hom-over = refl
```

## Properties

### Fibered maps and fiberwise maps over are equivalent

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y)
  where
  fiberwise-hom-over-hom-over :
    hom-over i f g â†’ fiberwise-hom-over i f g
  fiberwise-hom-over-hom-over (pair h H) .(f a) (pair a refl) =
    pair (h a) (inv (H a))
  hom-over-fiberwise-hom-over :
    fiberwise-hom-over i f g â†’ hom-over i f g
  pr1 (hom-over-fiberwise-hom-over Î±) a = pr1 (Î± (f a) (pair a refl))
  pr2 (hom-over-fiberwise-hom-over Î±) a = inv (pr2 (Î± (f a) (pair a refl)))
  issec-hom-over-fiberwise-hom-over-eq-htpy :
    (Î± : fiberwise-hom-over i f g) (x : X) â†’
    ( fiberwise-hom-over-hom-over
      ( hom-over-fiberwise-hom-over Î±) x) ~ (Î± x)
  issec-hom-over-fiberwise-hom-over-eq-htpy Î± .(f a) (pair a refl) =
    eq-pair-Î£ refl (inv-inv (pr2 (Î± (f a) (pair a refl))))
  issec-hom-over-fiberwise-hom-over :
    ( ( fiberwise-hom-over-hom-over) âˆ˜
      ( hom-over-fiberwise-hom-over)) ~ id
  issec-hom-over-fiberwise-hom-over Î± =
    eq-htpy
      ( eq-htpy âˆ˜ issec-hom-over-fiberwise-hom-over-eq-htpy Î±)
  isretr-hom-over-fiberwise-hom-over :
    ( ( hom-over-fiberwise-hom-over) âˆ˜
      ( fiberwise-hom-over-hom-over)) ~ id
  isretr-hom-over-fiberwise-hom-over (pair h H) =
    eq-pair-Î£ refl (eq-htpy (inv-inv âˆ˜ H))

  abstract
    is-equiv-fiberwise-hom-over-hom-over :
      is-equiv (fiberwise-hom-over-hom-over)
    is-equiv-fiberwise-hom-over-hom-over =
      is-equiv-has-inverse
        ( hom-over-fiberwise-hom-over)
        ( issec-hom-over-fiberwise-hom-over)
        ( isretr-hom-over-fiberwise-hom-over)

  abstract
    is-equiv-hom-over-fiberwise-hom-over :
      is-equiv (hom-over-fiberwise-hom-over)
    is-equiv-hom-over-fiberwise-hom-over =
      is-equiv-has-inverse
        ( fiberwise-hom-over-hom-over)
        ( isretr-hom-over-fiberwise-hom-over)
        ( issec-hom-over-fiberwise-hom-over)

  equiv-fiberwise-hom-over-hom-over :
    hom-over i f g â‰ƒ fiberwise-hom-over i f g 
  equiv-fiberwise-hom-over-hom-over = 
    pair
      ( fiberwise-hom-over-hom-over)
      ( is-equiv-fiberwise-hom-over-hom-over)

  equiv-hom-over-fiberwise-hom-over :
    fiberwise-hom-over i f g â‰ƒ hom-over i f g
  equiv-hom-over-fiberwise-hom-over = 
    pair
      ( hom-over-fiberwise-hom-over)
      ( is-equiv-hom-over-fiberwise-hom-over)

  equiv-hom-over-fiberwise-hom :
    fiberwise-hom (i âˆ˜ f) g â‰ƒ hom-over i f g
  equiv-hom-over-fiberwise-hom =
    equiv-hom-slice-fiberwise-hom (i âˆ˜ f) g

  equiv-fiberwise-hom-over-fiberwise-hom : 
    fiberwise-hom (i âˆ˜ f) g â‰ƒ fiberwise-hom-over i f g
  equiv-fiberwise-hom-over-fiberwise-hom =
    (equiv-fiberwise-hom-over-hom-over) âˆ˜e (equiv-hom-over-fiberwise-hom)

  is-small-fiberwise-hom-over :
    is-small (l1 âŠ” l2 âŠ” l4) (fiberwise-hom-over i f g)
  is-small-fiberwise-hom-over =
    pair
      ( hom-over i f g)
      ( equiv-hom-over-fiberwise-hom-over)
```

### Fibered maps compose horizontally and vertically

```agda
_âˆ˜fm_ :
  {l1 l2 l3 l4 l5 l6 : Level}
  {A : UU l1} {B : UU l2} {C : UU l3}
  {X : UU l4} {Y : UU l5} {Z : UU l6}
  {i : A â†’ B} {j : B â†’ C}
  {f : A â†’ X} {g : B â†’ Y} {h : C â†’ Z}
  {k : X â†’ Y} {l : Y â†’ Z}
  â†’ is-fibered-map l g h j â†’ is-fibered-map k f g i â†’
  is-fibered-map (l âˆ˜ k) f h (j âˆ˜ i)
_âˆ˜fm_ {i = i} {j} {f} {g} {h} {k} {l} J I =
  coherence-square-comp-horizontal i j f g h k l I J

_âˆ™fm_ :
  {l1 l2 l3 l4 l5 l6 : Level}
  {A : UU l1} {B : UU l2}
  {C : UU l3} {D : UU l4}
  {X : UU l5} {Y : UU l6}
  {i : A â†’ B} {j : C â†’ D} {k : X â†’ Y}
  {f : A â†’ C} {g : B â†’ D}
  {f' : C â†’ X} {g' : D â†’ Y}
  â†’ is-fibered-map j f g i â†’ is-fibered-map k f' g' j â†’
  is-fibered-map k (f' âˆ˜ f) (g' âˆ˜ g) i
_âˆ™fm_ {i = i} {j} {k} {f} {g} {f'} {g'} I J =
  coherence-square-comp-vertical i f g j f' g' k I J
```

### The truncation level of the type of fibered maps is determined by the codomains

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  where

  is-trunc-is-fibered-map-is-trunc-codomain :
    (k : ğ•‹) â†’ is-trunc (succ-ğ•‹ k) Y â†’
    (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) (h : A â†’ B) â†’
    is-trunc k (is-fibered-map i f g h)
  is-trunc-is-fibered-map-is-trunc-codomain k is-trunc-succ-Y i f g h =
    is-trunc-Î  k (Î» x â†’ is-trunc-succ-Y (i (f x)) (g (h x)))

  is-trunc-fibered-map :
    (k : ğ•‹) â†’ is-trunc (succ-ğ•‹ k) Y â†’ is-trunc k B â†’
    (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’
    is-trunc k (fibered-map i f g)
  is-trunc-fibered-map k is-trunc-succ-Y is-trunc-B i f g =
    is-trunc-Î£
      ( is-trunc-function-type k is-trunc-B)
      ( is-trunc-is-fibered-map-is-trunc-codomain k is-trunc-succ-Y i f g)
```

## Examples

```agda
is-fibered-over-self :
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  (h : A â†’ B) â†’ is-fibered-map h id id h
is-fibered-over-self h = refl-htpy

hom-over-self :
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  (h : A â†’ B) â†’ hom-over h id id
hom-over-self h = pair h refl-htpy

is-fibered-id :
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  (h : A â†’ B) â†’ is-fibered-map id h h id
is-fibered-id h = refl-htpy

hom-over-id :
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  (h : A â†’ B) â†’ hom-over id h h
hom-over-id h = pair id refl-htpy

is-fibered-self-endo :
  {l : Level} {A : UU l}
  (h : A â†’ A) â†’ is-fibered-map h h h h
is-fibered-self-endo h = refl-htpy

hom-over-self-endo :
  {l : Level} {A : UU l}
  (h : A â†’ A) â†’ hom-over h h h
hom-over-self-endo h = pair h refl-htpy
```
