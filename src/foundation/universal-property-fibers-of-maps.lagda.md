# The universal property of fibers of maps

```agda
module foundation.universal-property-fibers-of-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation.dependent-pair-types
open import foundation.dependent-universal-property-equivalences
open import foundation-core.equivalences
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.subtype-identity-principle
open import foundation.universal-property-dependent-pair-types
open import foundation.universe-levels

open import foundation.families-of-equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.identity-types
```

</details>

## Idea

Any map `f : A ‚Üí B` induces a type family `fiber f : B ‚Üí ùí∞` of
[fibers](foundation-core.fibers-of-maps.md) of `f`. By
[precomposing](foundation.precomposition-type-families.md) with `f`, we obtain
the type family `(fiber f) ‚àò f : A ‚Üí ùí∞`, which always has a section given by

```text
  Œª a ‚Üí (a , refl) : (a : A) ‚Üí fiber f (f a).
```

We can uniquely characterize the family of fibers `fiber f : B ‚Üí ùí∞` as the
initial type family equipped with such a section. Explicitly, the
{{#concept "universal property of the fiber" Disambiguation="of a map"}}
`fiber f : B ‚Üí ùí∞` of a map `f` is that the precomposition operation

```text
  ((b : B) ‚Üí fiber f b ‚Üí P b) ‚Üí ((a : A) ‚Üí P (f a))
```

is an [equivalence](foundation-core.equivalences.md) for any type family
`P : B ‚Üí ùí∞`.

This universal property is especially useful when `A` or `B` enjoy mapping out
universal properties. This lets us characterize the sections `(a : A) ‚Üí P (f a)`
in terms of the mapping out properties of `A` and the descent data of `B`.

## Definition

### The canonical map used in the universal property and dependent universal property of the fibers of a map

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B)
  (F : B ‚Üí UU l3) (Œ¥ : (a : A) ‚Üí F (f a))
  where

  ev-fiber :
    {l4 : Level} {P : (b : B) ‚Üí F b ‚Üí UU l4} ‚Üí
    ((b : B) (z : F b) ‚Üí P b z) ‚Üí (a : A) ‚Üí P (f a) (Œ¥ a)
  ev-fiber h a = h (f a) (Œ¥ a)
```

### The universal property of the fibers of a map

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  where

  universal-property-fiber :
    (f : A ‚Üí B) (F : B ‚Üí UU l3) (Œ¥ : (a : A) ‚Üí F (f a)) ‚Üí UUœâ
  universal-property-fiber f F Œ¥ =
    {l : Level} (P : B ‚Üí UU l) ‚Üí is-equiv (ev-fiber f F Œ¥ {l} {Œª b _ ‚Üí P b})
```

### The dependent universal property of the fibers of a map

```agda
  dependent-universal-property-fiber :
    (f : A ‚Üí B) (F : B ‚Üí UU l3) (Œ¥ : (a : A) ‚Üí F (f a)) ‚Üí UUœâ
  dependent-universal-property-fiber f F Œ¥ =
    {l : Level} (P : (b : B) ‚Üí F b ‚Üí UU l) ‚Üí is-equiv (ev-fiber f F Œ¥ {l} {P})
```

## Properties

### When a product is taken over all fibers of a map, then we can equivalently take the product over the domain of that map

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B)
  (C : (y : B) (z : fiber f y) ‚Üí UU l3)
  where

  map-reduce-Œ†-fiber :
    ((y : B) (z : fiber f y) ‚Üí C y z) ‚Üí ((x : A) ‚Üí C (f x) (x , refl))
  map-reduce-Œ†-fiber h x = h (f x) (x , refl)

  inv-map-reduce-Œ†-fiber :
    ((x : A) ‚Üí C (f x) (x , refl)) ‚Üí ((y : B) (z : fiber f y) ‚Üí C y z)
  inv-map-reduce-Œ†-fiber h .(f x) (x , refl) = h x

  is-section-inv-map-reduce-Œ†-fiber :
    (map-reduce-Œ†-fiber ‚àò inv-map-reduce-Œ†-fiber) ~ id
  is-section-inv-map-reduce-Œ†-fiber h = refl

  is-retraction-inv-map-reduce-Œ†-fiber' :
    (h : (y : B) (z : fiber f y) ‚Üí C y z) (y : B) ‚Üí
    (inv-map-reduce-Œ†-fiber (map-reduce-Œ†-fiber h) y) ~ (h y)
  is-retraction-inv-map-reduce-Œ†-fiber' h .(f z) (z , refl) = refl

  is-retraction-inv-map-reduce-Œ†-fiber :
    (inv-map-reduce-Œ†-fiber ‚àò map-reduce-Œ†-fiber) ~ id
  is-retraction-inv-map-reduce-Œ†-fiber h =
    eq-htpy (eq-htpy ‚àò is-retraction-inv-map-reduce-Œ†-fiber' h)

  is-equiv-map-reduce-Œ†-fiber : is-equiv map-reduce-Œ†-fiber
  is-equiv-map-reduce-Œ†-fiber =
    is-equiv-is-invertible
      ( inv-map-reduce-Œ†-fiber)
      ( is-section-inv-map-reduce-Œ†-fiber)
      ( is-retraction-inv-map-reduce-Œ†-fiber)

  reduce-Œ†-fiber' :
    ((y : B) (z : fiber f y) ‚Üí C y z) ‚âÉ ((x : A) ‚Üí C (f x) (x , refl))
  pr1 reduce-Œ†-fiber' = map-reduce-Œ†-fiber
  pr2 reduce-Œ†-fiber' = is-equiv-map-reduce-Œ†-fiber

reduce-Œ†-fiber :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B) ‚Üí
  (C : B ‚Üí UU l3) ‚Üí ((y : B) ‚Üí fiber f y ‚Üí C y) ‚âÉ ((x : A) ‚Üí C (f x))
reduce-Œ†-fiber f C = reduce-Œ†-fiber' f (Œª y z ‚Üí C y)
```

### The family of fibers has the universal property of fibers of maps

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B)
  where

  section-family-of-fibers :
    (a : A) ‚Üí fiber f (f a)
  pr1 (section-family-of-fibers a) = a
  pr2 (section-family-of-fibers a) = refl

  equiv-up-family-of-fibers :
    {l : Level} ‚Üí (P : B ‚Üí UU l) ‚Üí
    ((b : B) ‚Üí fiber f b ‚Üí P b) ‚âÉ ((a : A) ‚Üí P (f a))
  equiv-up-family-of-fibers P =
    equivalence-reasoning
      ( (b : B) ‚Üí fiber f b ‚Üí P b)
      ‚âÉ ((w : Œ£ B (Œª b ‚Üí fiber f b)) ‚Üí P (pr1 w))
        by equiv-ind-Œ£
      ‚âÉ ((a : A) ‚Üí P (f a))
        by
          equiv-precomp-Œ†
            ( inv-equiv-total-fiber f)
            ( Œª w ‚Üí P (pr1 w))

  up-family-of-fibers :
    universal-property-fiber f (fiber f) (section-family-of-fibers)
  up-family-of-fibers P =
    is-equiv-map-equiv (equiv-up-family-of-fibers P)
```

### The family of fibers has the dependent universal property of fibers of maps

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B)
  where

  equiv-dependent-up-family-of-fibers :
    {l : Level} (P : (b : B) ‚Üí fiber f b ‚Üí UU l) ‚Üí
    ( ( b : B) (z : fiber f b) ‚Üí P b z) ‚âÉ
    ( ( a : A) ‚Üí P (f a) (section-family-of-fibers f a))
  equiv-dependent-up-family-of-fibers P =
    equivalence-reasoning
      ( ( b : B) (z : fiber f b) ‚Üí P b z)
      ‚âÉ ((w : Œ£ B (Œª b ‚Üí fiber f b)) ‚Üí P (pr1 w) (pr2 w))
        by equiv-ind-Œ£
      ‚âÉ ((a : A) ‚Üí P (f a) (section-family-of-fibers f a))
        by
          equiv-precomp-Œ†
            ( inv-equiv-total-fiber f)
            ( Œª w ‚Üí P (pr1 w) (pr2 w))

  dependent-up-family-of-fibers :
    dependent-universal-property-fiber
      ( f)
      ( fiber f)
      ( section-family-of-fibers f)
  dependent-up-family-of-fibers P =
    is-equiv-map-equiv (equiv-dependent-up-family-of-fibers P)
```

### Fibers are uniquely unique

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {f : A ‚Üí B} (F : B ‚Üí UU l3)
  (Œ¥ : (a : A) ‚Üí F (f a)) (P : B ‚Üí UU l4) (Œ≥ : (a : A) ‚Üí P (f a))
  where

  section-preserving-fiberwise-map-fiber : UU (l1 ‚äî l2 ‚äî l3 ‚äî l4)
  section-preserving-fiberwise-map-fiber =
    Œ£ ((b : B) ‚Üí F b ‚Üí P b) (Œª h ‚Üí ev-fiber f F Œ¥ h ~ Œ≥)

module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {f : A ‚Üí B} {F : B ‚Üí UU l3}
  {Œ¥ : (a : A) ‚Üí F (f a)} {P : B ‚Üí UU l4} {Œ≥ : (a : A) ‚Üí P (f a)}
  where

  fiberwise-map-section-preserving-fiberwise-map-fiber :
    section-preserving-fiberwise-map-fiber F Œ¥ P Œ≥ ‚Üí (b : B) ‚Üí F b ‚Üí P b
  fiberwise-map-section-preserving-fiberwise-map-fiber = pr1

  preserves-section-section-preserving-fiberwise-map-fiber :
    (w : section-preserving-fiberwise-map-fiber F Œ¥ P Œ≥) ‚Üí
    ev-fiber
      ( f)
      ( F)
      ( Œ¥)
      ( fiberwise-map-section-preserving-fiberwise-map-fiber w) ~
    ( Œ≥)
  preserves-section-section-preserving-fiberwise-map-fiber = pr2

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {f : A ‚Üí B} {F : B ‚Üí UU l3}
  {Œ¥ : (a : A) ‚Üí F (f a)}
  where

  id-section-preserving-fiberwise-map-fiber :
    section-preserving-fiberwise-map-fiber F Œ¥ F Œ¥
  pr1 id-section-preserving-fiberwise-map-fiber b = id
  pr2 id-section-preserving-fiberwise-map-fiber = refl-htpy

module _
  {l1 l2 l3 l4 l5 : Level} {A : UU l1} {B : UU l2} {f : A ‚Üí B} {F : B ‚Üí UU l3}
  {Œ¥ : (a : A) ‚Üí F (f a)} {P : B ‚Üí UU l4} {Œ≥ : (a : A) ‚Üí P (f a)}
  {Q : B ‚Üí UU l5} {Œ∑ : (a : A) ‚Üí Q (f a)}
  where

  dependent-comp-section-preserving-fiberwise-map-fiber :
    ( section-preserving-fiberwise-map-fiber P Œ≥ Q Œ∑) ‚Üí
    ( section-preserving-fiberwise-map-fiber F Œ¥ P Œ≥) ‚Üí
    ( section-preserving-fiberwise-map-fiber F Œ¥ Q Œ∑)
  pr1 (dependent-comp-section-preserving-fiberwise-map-fiber g h) =
    dependent-comp
      ( fiberwise-map-section-preserving-fiberwise-map-fiber g)
      ( fiberwise-map-section-preserving-fiberwise-map-fiber h)
  pr2 (dependent-comp-section-preserving-fiberwise-map-fiber g h) a =
    ( ap
      ( (fiberwise-map-section-preserving-fiberwise-map-fiber g) (f a))
      ( preserves-section-section-preserving-fiberwise-map-fiber h a)) ‚àô
    ( preserves-section-section-preserving-fiberwise-map-fiber g a)

module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B) (F : B ‚Üí UU l3)
  (Œ¥ : (a : A) ‚Üí F (f a)) (u : universal-property-fiber f F Œ¥)
  (P : B ‚Üí UU l4) (Œ≥ : (a : A) ‚Üí P (f a))
  where

  uniqueness-fiberwise-map-universal-property-fiber :
    is-contr (section-preserving-fiberwise-map-fiber F Œ¥ P Œ≥)
  uniqueness-fiberwise-map-universal-property-fiber =
    is-contr-equiv
      ( fiber (ev-fiber f F Œ¥) Œ≥)
      ( equiv-tot
        ( Œª h ‚Üí equiv-eq-htpy))
      ( is-contr-map-is-equiv (u P) Œ≥)

  section-preserving-fiberwise-map-universal-property-fiber :
    section-preserving-fiberwise-map-fiber F Œ¥ P Œ≥
  section-preserving-fiberwise-map-universal-property-fiber =
    ( center uniqueness-fiberwise-map-universal-property-fiber)

  fiberwise-map-universal-property-fiber :
    (b : B) ‚Üí F b ‚Üí P b
  fiberwise-map-universal-property-fiber =
    fiberwise-map-section-preserving-fiberwise-map-fiber
      section-preserving-fiberwise-map-universal-property-fiber

  preserves-section-fiberwise-map-universal-property-fiber :
    (ev-fiber f F Œ¥ fiberwise-map-universal-property-fiber) ~ Œ≥
  preserves-section-fiberwise-map-universal-property-fiber =
    preserves-section-section-preserving-fiberwise-map-fiber
      section-preserving-fiberwise-map-universal-property-fiber

module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B) (F : B ‚Üí UU l3)
  (Œ¥ : (a : A) ‚Üí F (f a)) (u : universal-property-fiber f F Œ¥)
  (P : B ‚Üí UU l4) (Œ≥ : (a : A) ‚Üí P (f a))
  (u' : universal-property-fiber f P Œ≥)
  where

  dependent-comp-retraction-fiberwise-map-universal-property-fiber-universal-property-fiber :
    ( dependent-comp-section-preserving-fiberwise-map-fiber
      ( section-preserving-fiberwise-map-universal-property-fiber f P Œ≥ u' F Œ¥)
      ( section-preserving-fiberwise-map-universal-property-fiber
        ( f)
        ( F)
        ( Œ¥)
        ( u)
        ( P)
        ( Œ≥))) Ôºù
    ( id-section-preserving-fiberwise-map-fiber)
  dependent-comp-retraction-fiberwise-map-universal-property-fiber-universal-property-fiber =
    eq-is-contr
      ( uniqueness-fiberwise-map-universal-property-fiber f F Œ¥ u F Œ¥)

  dependent-comp-section-fiberwise-map-universal-property-fiber-universal-property-fiber :
    ( dependent-comp-section-preserving-fiberwise-map-fiber
      ( section-preserving-fiberwise-map-universal-property-fiber f F Œ¥ u P Œ≥))
      ( section-preserving-fiberwise-map-universal-property-fiber
        ( f)
        ( P)
        ( Œ≥)
        ( u')
        ( F)
        ( Œ¥)) Ôºù
    ( id-section-preserving-fiberwise-map-fiber)
  dependent-comp-section-fiberwise-map-universal-property-fiber-universal-property-fiber =
    eq-is-contr
      ( uniqueness-fiberwise-map-universal-property-fiber f P Œ≥ u' P Œ≥)

  is-fiberwise-retraction-fiberwise-map-universal-property-fiber-universal-property-fiber :
    (b : B) ‚Üí
    ( ( fiberwise-map-universal-property-fiber f P Œ≥ u' F Œ¥ b) ‚àò
    ( fiberwise-map-universal-property-fiber f F Œ¥ u P Œ≥ b)) ~
    ( id)
  is-fiberwise-retraction-fiberwise-map-universal-property-fiber-universal-property-fiber
    b =
    htpy-eq
      ( htpy-eq
        ( ap
          ( pr1)
          ( dependent-comp-retraction-fiberwise-map-universal-property-fiber-universal-property-fiber))
        ( b))

  is-fiberwise-section-fiberwise-map-universal-property-fiber-universal-property-fiber :
    (b : B) ‚Üí
    ( ( fiberwise-map-universal-property-fiber f F Œ¥ u P Œ≥ b) ‚àò
    ( fiberwise-map-universal-property-fiber f P Œ≥ u' F Œ¥ b)) ~
    ( id)
  is-fiberwise-section-fiberwise-map-universal-property-fiber-universal-property-fiber
    b =
    htpy-eq
      ( htpy-eq
        ( ap
          ( pr1)
          ( dependent-comp-section-fiberwise-map-universal-property-fiber-universal-property-fiber))
        ( b))

  is-fiberwise-equiv-fiberwise-map-universal-property-fiber-universal-property-fiber :
    is-fiberwise-equiv (fiberwise-map-universal-property-fiber f F Œ¥ u P Œ≥)
  is-fiberwise-equiv-fiberwise-map-universal-property-fiber-universal-property-fiber
    b =
    is-equiv-is-invertible
      ( fiberwise-map-section-preserving-fiberwise-map-fiber
        ( section-preserving-fiberwise-map-universal-property-fiber
          ( f)
          ( P)
          ( Œ≥)
          ( u')
          ( F)
          ( Œ¥))
        ( b))
      ( is-fiberwise-section-fiberwise-map-universal-property-fiber-universal-property-fiber
        ( b))
      ( is-fiberwise-retraction-fiberwise-map-universal-property-fiber-universal-property-fiber
        ( b))

  uniquely-unique-fiberwise-map-universal-property-fiber :
    is-contr
      ( Œ£ (fiberwise-equiv F P)
        ( Œª h ‚Üí (ev-fiber f F Œ¥ (map-fiberwise-equiv h)) ~ Œ≥))
  uniquely-unique-fiberwise-map-universal-property-fiber =
    is-torsorial-Eq-subtype
      ( uniqueness-fiberwise-map-universal-property-fiber f F Œ¥ u P Œ≥)
      ( is-property-is-fiberwise-equiv)
      ( fiberwise-map-universal-property-fiber f F Œ¥ u P Œ≥)
      ( preserves-section-fiberwise-map-universal-property-fiber f F Œ¥ u P Œ≥)
      ( is-fiberwise-equiv-fiberwise-map-universal-property-fiber-universal-property-fiber)

  section-preserving-fiberwise-equiv-unique-fiberwise-map-universal-property-fiber :
    Œ£ (fiberwise-equiv F P)
      ( Œª h ‚Üí (ev-fiber f F Œ¥ (map-fiberwise-equiv h)) ~ Œ≥)
  section-preserving-fiberwise-equiv-unique-fiberwise-map-universal-property-fiber =
    center uniquely-unique-fiberwise-map-universal-property-fiber

  fiberwise-equiv-unique-fiberwise-map-universal-property-fiber :
    fiberwise-equiv F P
  fiberwise-equiv-unique-fiberwise-map-universal-property-fiber =
    pr1
      ( section-preserving-fiberwise-equiv-unique-fiberwise-map-universal-property-fiber)

  preserves-section-fiberwise-equiv-unique-fiberwise-map-universal-property-fiber :
    (ev-fiber
      ( f)
      ( F)
      ( Œ¥)
      ( map-fiberwise-equiv
        ( fiberwise-equiv-unique-fiberwise-map-universal-property-fiber))) ~
    ( Œ≥)
  preserves-section-fiberwise-equiv-unique-fiberwise-map-universal-property-fiber =
    pr2
      ( section-preserving-fiberwise-equiv-unique-fiberwise-map-universal-property-fiber)
```
