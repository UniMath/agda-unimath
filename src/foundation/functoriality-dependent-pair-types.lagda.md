# Functoriality of dependent pair types

```agda
module foundation.functoriality-dependent-pair-types where

open import foundation-core.functoriality-dependent-pair-types public
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cones-over-cospans
open import foundation.contractible-maps
open import foundation.dependent-pair-types
open import foundation.transport-along-identifications
open import foundation.truncation-levels
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universe-levels

open import foundation-core.commuting-squares-of-maps
open import foundation-core.dependent-identifications
open import foundation-core.equality-dependent-pair-types
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.propositional-maps
open import foundation-core.pullbacks
open import foundation-core.truncated-maps
open import foundation-core.truncated-types
```

</details>

## Properties

### The map on total spaces induced by a family of truncated maps is truncated

```agda
module _
  {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  {f : (x : A) â†’ B x â†’ C x}
  where

  abstract
    is-trunc-map-tot : ((x : A) â†’ is-trunc-map k (f x)) â†’ is-trunc-map k (tot f)
    is-trunc-map-tot H y =
      is-trunc-equiv k
        ( fiber (f (pr1 y)) (pr2 y))
        ( compute-fiber-tot f y)
        ( H (pr1 y) (pr2 y))

  abstract
    is-trunc-map-is-trunc-map-tot :
      is-trunc-map k (tot f) â†’ ((x : A) â†’ is-trunc-map k (f x))
    is-trunc-map-is-trunc-map-tot is-trunc-tot-f x z =
      is-trunc-equiv k
        ( fiber (tot f) (pair x z))
        ( inv-compute-fiber-tot f (pair x z))
        ( is-trunc-tot-f (pair x z))

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  {f : (x : A) â†’ B x â†’ C x}
  where

  abstract
    is-contr-map-tot :
      ((x : A) â†’ is-contr-map (f x)) â†’ is-contr-map (tot f)
    is-contr-map-tot =
      is-trunc-map-tot neg-two-ğ•‹

  abstract
    is-prop-map-tot : ((x : A) â†’ is-prop-map (f x)) â†’ is-prop-map (tot f)
    is-prop-map-tot = is-trunc-map-tot neg-one-ğ•‹
```

### The functoriality of dependent pair types preserves truncatedness

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-trunc-map-map-Î£-map-base :
      (k : ğ•‹) {f : A â†’ B} (C : B â†’ UU l3) â†’
      is-trunc-map k f â†’ is-trunc-map k (map-Î£-map-base f C)
    is-trunc-map-map-Î£-map-base k {f} C H y =
      is-trunc-equiv' k
        ( fiber f (pr1 y))
        ( equiv-fiber-map-Î£-map-base-fiber f C y)
        ( H (pr1 y))

  abstract
    is-prop-map-map-Î£-map-base :
      {f : A â†’ B} (C : B â†’ UU l3) â†’
      is-prop-map f â†’ is-prop-map (map-Î£-map-base f C)
    is-prop-map-map-Î£-map-base C = is-trunc-map-map-Î£-map-base neg-one-ğ•‹ C

module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : A â†’ UU l3}
  where

  abstract
    is-trunc-map-map-Î£ :
      (k : ğ•‹) (D : B â†’ UU l4) {f : A â†’ B} {g : (x : A) â†’ C x â†’ D (f x)} â†’
      is-trunc-map k f â†’ ((x : A) â†’ is-trunc-map k (g x)) â†’
      is-trunc-map k (map-Î£ D f g)
    is-trunc-map-map-Î£ k D {f} {g} H K =
      is-trunc-map-left-map-triangle k
        ( map-Î£ D f g)
        ( map-Î£-map-base f D)
        ( tot g)
        ( triangle-map-Î£ D f g)
        ( is-trunc-map-map-Î£-map-base k D H)
        ( is-trunc-map-tot k K)

  module _
    (D : B â†’ UU l4) {f : A â†’ B} {g : (x : A) â†’ C x â†’ D (f x)}
    where

    abstract
      is-contr-map-map-Î£ :
        is-contr-map f â†’ ((x : A) â†’ is-contr-map (g x)) â†’
        is-contr-map (map-Î£ D f g)
      is-contr-map-map-Î£ = is-trunc-map-map-Î£ neg-two-ğ•‹ D

    abstract
      is-prop-map-map-Î£ :
        is-prop-map f â†’ ((x : A) â†’ is-prop-map (g x)) â†’
        is-prop-map (map-Î£ D f g)
      is-prop-map-map-Î£ = is-trunc-map-map-Î£ neg-one-ğ•‹ D
```

### A family of squares over a pullback squares is a family of pullback squares if and only if the induced square of total spaces is a pullback square

```agda
module _
  {l1 l2 l3 l4 l5 l6 l7 l8 : Level}
  {X : UU l1} {A : UU l2} {B : UU l3} {C : UU l4}
  (PX : X â†’ UU l5) {PA : A â†’ UU l6} {PB : B â†’ UU l7} {PC : C â†’ UU l8}
  {f : A â†’ X} {g : B â†’ X}
  (f' : (a : A) â†’ PA a â†’ PX (f a)) (g' : (b : B) â†’ PB b â†’ PX (g b))
  (c : cone f g C) (c' : cone-family PX f' g' c PC)
  where

  tot-cone-cone-family :
    cone (map-Î£ PX f f') (map-Î£ PX g g') (Î£ C PC)
  pr1 tot-cone-cone-family =
    map-Î£ _ (vertical-map-cone f g c) (Î» x â†’ pr1 (c' x))
  pr1 (pr2 tot-cone-cone-family) =
    map-Î£ _ (horizontal-map-cone f g c) (Î» x â†’ (pr1 (pr2 (c' x))))
  pr2 (pr2 tot-cone-cone-family) =
    htpy-map-Î£ PX
      ( coherence-square-cone f g c)
      ( Î» z â†’
        ( f' (vertical-map-cone f g c z)) âˆ˜
        ( vertical-map-cone
          ( ( tr PX (coherence-square-cone f g c z)) âˆ˜
            ( f' (vertical-map-cone f g c z)))
          ( g' (horizontal-map-cone f g c z))
          ( c' z)))
      ( Î» z â†’
        coherence-square-cone
          ( ( tr PX (coherence-square-cone f g c z)) âˆ˜
            ( f' (vertical-map-cone f g c z)))
          ( g' (horizontal-map-cone f g c z))
          ( c' z))

  map-standard-pullback-tot-cone-cone-fam-right-factor :
    Î£ ( standard-pullback f g)
      ( Î» t â†’
        standard-pullback
          ( tr PX (coherence-square-standard-pullback t) âˆ˜
            f' (vertical-map-standard-pullback t))
          ( g' (horizontal-map-standard-pullback t))) â†’
    Î£ ( Î£ A PA)
      ( Î» aa' â†’ Î£ (Î£ B (Î» b â†’ Id (f (pr1 aa')) (g b)))
        ( Î» bÎ± â†’ Î£ (PB (pr1 bÎ±))
          ( Î» b' â†’ Id
            ( tr PX (pr2 bÎ±) (f' (pr1 aa') (pr2 aa')))
            ( g' (pr1 bÎ±) b'))))
  map-standard-pullback-tot-cone-cone-fam-right-factor =
    map-interchange-Î£-Î£
      ( Î» a bÎ± a' â†’ Î£ (PB (pr1 bÎ±))
        ( Î» b' â†’ Id (tr PX (pr2 bÎ±) (f' a a')) (g' (pr1 bÎ±) b')))

  map-standard-pullback-tot-cone-cone-fam-left-factor :
    (aa' : Î£ A PA) â†’
    Î£ (Î£ B (Î» b â†’ Id (f (pr1 aa')) (g b)))
      ( Î» bÎ± â†’ Î£ (PB (pr1 bÎ±))
        ( Î» b' â†’ Id
          ( tr PX (pr2 bÎ±) (f' (pr1 aa') (pr2 aa')))
          ( g' (pr1 bÎ±) b'))) â†’
    Î£ ( Î£ B PB)
      ( Î» bb' â†’ Î£ (Id (f (pr1 aa')) (g (pr1 bb')))
        ( Î» Î± â†’ Id (tr PX Î± (f' (pr1 aa') (pr2 aa'))) (g' (pr1 bb') (pr2 bb'))))
  map-standard-pullback-tot-cone-cone-fam-left-factor aa' =
    ( map-interchange-Î£-Î£
      ( Î» b Î± b' â†’ Id (tr PX Î± (f' (pr1 aa') (pr2 aa'))) (g' b b')))

  map-standard-pullback-tot-cone-cone-family :
    Î£ ( standard-pullback f g)
      ( Î» t â†’
        standard-pullback
          ( tr PX (coherence-square-standard-pullback t) âˆ˜
            f' (vertical-map-standard-pullback t))
          ( g' (horizontal-map-standard-pullback t))) â†’
    standard-pullback (map-Î£ PX f f') (map-Î£ PX g g')
  map-standard-pullback-tot-cone-cone-family =
    ( tot
      (Î» aa' â†’
        ( tot (Î» bb' â†’ eq-pair-Î£')) âˆ˜
        ( map-standard-pullback-tot-cone-cone-fam-left-factor aa'))) âˆ˜
    ( map-standard-pullback-tot-cone-cone-fam-right-factor)

  is-equiv-map-standard-pullback-tot-cone-cone-family :
    is-equiv map-standard-pullback-tot-cone-cone-family
  is-equiv-map-standard-pullback-tot-cone-cone-family =
    is-equiv-comp
      ( tot (Î» aa' â†’
        ( tot (Î» bb' â†’ eq-pair-Î£')) âˆ˜
        ( map-standard-pullback-tot-cone-cone-fam-left-factor aa')))
      ( map-standard-pullback-tot-cone-cone-fam-right-factor)
      ( is-equiv-map-interchange-Î£-Î£
        ( Î» a bÎ± a' â†’ Î£ (PB (pr1 bÎ±))
          ( Î» b' â†’ Id (tr PX (pr2 bÎ±) (f' a a')) (g' (pr1 bÎ±) b'))))
      ( is-equiv-tot-is-fiberwise-equiv (Î» aa' â†’ is-equiv-comp
        ( tot (Î» bb' â†’ eq-pair-Î£'))
        ( map-standard-pullback-tot-cone-cone-fam-left-factor aa')
        ( is-equiv-map-interchange-Î£-Î£ _)
        ( is-equiv-tot-is-fiberwise-equiv (Î» bb' â†’ is-equiv-eq-pair-Î£
          ( pair (f (pr1 aa')) (f' (pr1 aa') (pr2 aa')))
          ( pair (g (pr1 bb')) (g' (pr1 bb') (pr2 bb')))))))

  triangle-standard-pullback-tot-cone-cone-family :
    ( gap (map-Î£ PX f f') (map-Î£ PX g g') tot-cone-cone-family) ~
    ( ( map-standard-pullback-tot-cone-cone-family) âˆ˜
      ( map-Î£ _
        ( gap f g c)
        ( Î» x â†’ gap
          ( (tr PX (pr2 (pr2 c) x)) âˆ˜ (f' (pr1 c x)))
          ( g' (pr1 (pr2 c) x))
          ( c' x))))
  triangle-standard-pullback-tot-cone-cone-family x =
    refl

  is-pullback-family-is-pullback-tot :
    is-pullback f g c â†’
    is-pullback
      (map-Î£ PX f f') (map-Î£ PX g g') tot-cone-cone-family â†’
    (x : C) â†’
    is-pullback
      ( (tr PX (pr2 (pr2 c) x)) âˆ˜ (f' (pr1 c x)))
      ( g' (pr1 (pr2 c) x))
      ( c' x)
  is-pullback-family-is-pullback-tot is-pb-c is-pb-tot =
    is-fiberwise-equiv-is-equiv-map-Î£ _
      ( gap f g c)
      ( Î» x â†’ gap
        ( (tr PX (pr2 (pr2 c) x)) âˆ˜ (f' (pr1 c x)))
        ( g' (pr1 (pr2 c) x))
        ( c' x))
      ( is-pb-c)
      ( is-equiv-top-map-triangle
        ( gap (map-Î£ PX f f') (map-Î£ PX g g') tot-cone-cone-family)
        ( map-standard-pullback-tot-cone-cone-family)
        ( map-Î£ _
          ( gap f g c)
          ( Î» x â†’ gap
            ( (tr PX (pr2 (pr2 c) x)) âˆ˜ (f' (pr1 c x)))
            ( g' (pr1 (pr2 c) x))
            ( c' x)))
        ( triangle-standard-pullback-tot-cone-cone-family)
        ( is-equiv-map-standard-pullback-tot-cone-cone-family)
        ( is-pb-tot))

  is-pullback-tot-is-pullback-family :
    is-pullback f g c â†’
    ( (x : C) â†’
      is-pullback
        ( (tr PX (pr2 (pr2 c) x)) âˆ˜ (f' (pr1 c x)))
        ( g' (pr1 (pr2 c) x))
        ( c' x)) â†’
    is-pullback
      (map-Î£ PX f f') (map-Î£ PX g g') tot-cone-cone-family
  is-pullback-tot-is-pullback-family is-pb-c is-pb-c' =
    is-equiv-left-map-triangle
      ( gap (map-Î£ PX f f') (map-Î£ PX g g') tot-cone-cone-family)
      ( map-standard-pullback-tot-cone-cone-family)
      ( map-Î£ _
        ( gap f g c)
        ( Î» x â†’ gap
          ( (tr PX (pr2 (pr2 c) x)) âˆ˜ (f' (pr1 c x)))
          ( g' (pr1 (pr2 c) x))
          ( c' x)))
      ( triangle-standard-pullback-tot-cone-cone-family)
      ( is-equiv-map-Î£ _
        ( is-pb-c)
        ( is-pb-c'))
      ( is-equiv-map-standard-pullback-tot-cone-cone-family)
```

### Commuting squares of maps on total spaces

#### Functoriality of `Î£` preserves commuting squares of maps

```agda
module _
  {l1 l2 l3 l4 l5 l6 l7 l8 : Level}
  {A : UU l1} {P : A â†’ UU l2}
  {B : UU l3} {Q : B â†’ UU l4}
  {C : UU l5} {R : C â†’ UU l6}
  {D : UU l7} (S : D â†’ UU l8)
  {top' : A â†’ C} {left' : A â†’ B} {right' : C â†’ D} {bottom' : B â†’ D}
  (top : (a : A) â†’ P a â†’ R (top' a))
  (left : (a : A) â†’ P a â†’ Q (left' a))
  (right : (c : C) â†’ R c â†’ S (right' c))
  (bottom : (b : B) â†’ Q b â†’ S (bottom' b))
  where

  coherence-square-maps-Î£ :
    {H' : coherence-square-maps top' left' right' bottom'} â†’
    ( (a : A) (p : P a) â†’
      dependent-identification S
        ( H' a)
        ( bottom _ (left _ p))
        ( right _ (top _ p))) â†’
    coherence-square-maps
      ( map-Î£ R top' top)
      ( map-Î£ Q left' left)
      ( map-Î£ S right' right)
      ( map-Î£ S bottom' bottom)
  coherence-square-maps-Î£ {H'} H (a , p) = eq-pair-Î£ (H' a) (H a p)
```

#### Commuting squares of induced maps on total spaces

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  {A : UU l1} {P : A â†’ UU l2} {Q : A â†’ UU l3} {R : A â†’ UU l4} {S : A â†’ UU l5}
  (top : (a : A) â†’ P a â†’ R a)
  (left : (a : A) â†’ P a â†’ Q a)
  (right : (a : A) â†’ R a â†’ S a)
  (bottom : (a : A) â†’ Q a â†’ S a)
  where

  coherence-square-maps-tot :
    ((a : A) â†’ coherence-square-maps (top a) (left a) (right a) (bottom a)) â†’
    coherence-square-maps (tot top) (tot left) (tot right) (tot bottom)
  coherence-square-maps-tot H (a , p) = eq-pair-Î£ refl (H a p)
```

#### `map-Î£-map-base` preserves commuting squares of maps

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  {A : UU l1} {B : UU l2} {C : UU l3} {D : UU l4} (S : D â†’ UU l5)
  (top : A â†’ C) (left : A â†’ B) (right : C â†’ D) (bottom : B â†’ D)
  where

  coherence-square-maps-map-Î£-map-base :
    (H : coherence-square-maps top left right bottom) â†’
    coherence-square-maps
      ( map-Î£ (S âˆ˜ right) top (Î» a â†’ tr S (H a)))
      ( map-Î£-map-base left (S âˆ˜ bottom))
      ( map-Î£-map-base right S)
      ( map-Î£-map-base bottom S)
  coherence-square-maps-map-Î£-map-base H (a , p) = eq-pair-Î£ (H a) refl
```

### The action of `map-Î£-map-base` on identifications of the form `eq-pair-Î£` is given by the action on the base

```agda
module _
  { l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (C : B â†’ UU l3)
  where

  compute-ap-map-Î£-map-base-eq-pair-Î£ :
    { s s' : A} (p : s ï¼ s') {t : C (f s)} {t' : C (f s')}
    ( q : tr (C âˆ˜ f) p t ï¼ t') â†’
    ap (map-Î£-map-base f C) (eq-pair-Î£ p q) ï¼
    eq-pair-Î£ (ap f p) (substitution-law-tr C f p âˆ™ q)
  compute-ap-map-Î£-map-base-eq-pair-Î£ refl refl = refl
```

#### Computing the inverse of `equiv-tot`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  where

  compute-inv-equiv-tot :
    (e : (x : A) â†’ B x â‰ƒ C x) â†’
    ( map-inv-equiv (equiv-tot e)) ~
    ( map-equiv (equiv-tot (Î» x â†’ inv-equiv (e x))))
  compute-inv-equiv-tot e (a , c) =
    is-injective-map-equiv
      ( equiv-tot e)
      ( ( is-section-map-inv-equiv (equiv-tot e) (a , c)) âˆ™
        ( eq-pair-Î£ refl (inv (is-section-map-inv-equiv (e a) c))))
```

## See also

- Arithmetical laws involving dependent pair types are recorded in
  [`foundation.type-arithmetic-dependent-pair-types`](foundation.type-arithmetic-dependent-pair-types.md).
- Equality proofs in dependent pair types are characterized in
  [`foundation.equality-dependent-pair-types`](foundation.equality-dependent-pair-types.md).
- The universal property of dependent pair types is treated in
  [`foundation.universal-property-dependent-pair-types`](foundation.universal-property-dependent-pair-types.md).

- Functorial properties of cartesian product types are recorded in
  [`foundation.functoriality-cartesian-product-types`](foundation.functoriality-cartesian-product-types.md).
- Functorial properties of dependent product types are recorded in
  [`foundation.functoriality-dependent-function-types`](foundation.functoriality-dependent-function-types.md).
