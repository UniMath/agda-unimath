# Functoriality of dependent pair types

```agda
module foundation.functoriality-dependent-pair-types where

open import foundation-core.functoriality-dependent-pair-types public
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-homotopies
open import foundation.dependent-pair-types
open import foundation.morphisms-arrows
open import foundation.universe-levels

open import foundation-core.commuting-squares-of-maps
open import foundation-core.commuting-triangles-of-maps
open import foundation-core.contractible-maps
open import foundation-core.dependent-identifications
open import foundation-core.equality-dependent-pair-types
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.propositional-maps
open import foundation-core.transport-along-identifications
open import foundation-core.truncated-maps
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Properties

### The map `htpy-map-Î£` preserves homotopies

Given a [homotopy](foundation.homotopies.md) `H : f ~ f'` and a family of
[dependent homotopies](foundation.dependent-homotopies.md) `K a : g a ~ g' a`
over `H`, expressed as
[commuting triangles](foundation.commuting-triangles-of-maps.md)

```text
        g a
   C a -----> D (f a)
      \      /
  g' a \    / tr D (H a)
        âˆ¨  âˆ¨
      D (f' a)         ,
```

we get a homotopy `htpy-map-Î£ H K : map-Î£ f g ~ map-Î£ f' g'`.

This assignment itself preserves homotopies: given `H` and `K` as above,
`H' : f ~ f'` with `K' a : g a ~ g' a` over `H'`, we would like to express
coherences between the pairs `H, H'` and `K, K'` which would ensure
`htpy-map-Î£ H K ~ htpy-map-Î£ H' K'`. Because `H` and `H'` have the same type, we
may require a homotopy `Î± : H ~ H'`, but `K` and `K'` are families of dependent
homotopies over different homotopies, so their coherence is provided as a family
of
[commuting triangles of identifications](foundation.commuting-triangles-of-identifications.md)

```text
                      ap (Î» p â†’ tr D p (g a c)) (Î± a)
  tr D (H a) (g a c) --------------------------------- tr D (H' a) (g a c)
                     \                               /
                        \                         /
                           \                   /
                      K a c   \             /   K' a c
                                 \       /
                                    \ /
                                  g' a c        .
```

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : A â†’ UU l3} (D : B â†’ UU l4)
  {f f' : A â†’ B} {H H' : f ~ f'}
  {g : (a : A) â†’ C a â†’ D (f a)}
  {g' : (a : A) â†’ C a â†’ D (f' a)}
  {K : (a : A) â†’ dependent-homotopy (Î» _ â†’ D) (Î» _ â†’ H a) (g a) (g' a)}
  {K' : (a : A) â†’ dependent-homotopy (Î» _ â†’ D) (Î» _ â†’ H' a) (g a) (g' a)}
  where

  abstract
    htpy-htpy-map-Î£ :
      (Î± : H ~ H') â†’
      (Î² :
        (a : A) (c : C a) â†’
        K a c ï¼ ap (Î» p â†’ tr D p (g a c)) (Î± a) âˆ™ K' a c) â†’
      htpy-map-Î£ D H g K ~ htpy-map-Î£ D H' g K'
    htpy-htpy-map-Î£ Î± Î² (a , c) =
      ap
        ( eq-pair-Î£')
        ( eq-pair-Î£
          ( Î± a)
          ( map-compute-dependent-identification-eq-value-function
            ( Î» p â†’ tr D p (g a c))
            ( Î» _ â†’ g' a c)
            ( Î± a)
            ( K a c)
            ( K' a c)
            ( inv
              ( ( ap
                  ( K a c âˆ™_)
                  ( ap-const (g' a c) (Î± a))) âˆ™
                ( right-unit) âˆ™
                ( Î² a c)))))
```

As a corollary of the above statement, we can provide a condition which
guarantees that `htpy-map-Î£` is homotopic to the trivial homotopy.

```agda
module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : A â†’ UU l3} (D : B â†’ UU l4)
  {f : A â†’ B} {H : f ~ f}
  {g : (a : A) â†’ C a â†’ D (f a)}
  {K : (a : A) â†’ tr D (H a) âˆ˜ g a ~ g a}
  where

  abstract
    htpy-htpy-map-Î£-refl-htpy :
      (Î± : H ~ refl-htpy) â†’
      (Î² : (a : A) (c : C a) â†’ K a c ï¼ ap (Î» p â†’ tr D p (g a c)) (Î± a)) â†’
      htpy-map-Î£ D H g K ~ refl-htpy
    htpy-htpy-map-Î£-refl-htpy Î± Î² =
      htpy-htpy-map-Î£ D Î± (Î» a c â†’ Î² a c âˆ™ inv right-unit)
```

### The map on total spaces induced by a family of truncated maps is truncated

```agda
module _
  {l1 l2 l3 : Level} (k : ğ•‹) {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  {f : (x : A) â†’ B x â†’ C x}
  where

  abstract
    is-trunc-map-tot : ((x : A) â†’ is-trunc-map k (f x)) â†’ is-trunc-map k (tot f)
    is-trunc-map-tot H y =
      is-trunc-equiv k
        ( fiber (f (pr1 y)) (pr2 y))
        ( compute-fiber-tot f y)
        ( H (pr1 y) (pr2 y))

  abstract
    is-trunc-map-is-trunc-map-tot :
      is-trunc-map k (tot f) â†’ ((x : A) â†’ is-trunc-map k (f x))
    is-trunc-map-is-trunc-map-tot is-trunc-tot-f x z =
      is-trunc-equiv k
        ( fiber (tot f) (x , z))
        ( inv-compute-fiber-tot f (x , z))
        ( is-trunc-tot-f (x , z))

module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  {f : (x : A) â†’ B x â†’ C x}
  where

  abstract
    is-contr-map-tot :
      ((x : A) â†’ is-contr-map (f x)) â†’ is-contr-map (tot f)
    is-contr-map-tot =
      is-trunc-map-tot neg-two-ğ•‹

  abstract
    is-prop-map-tot : ((x : A) â†’ is-prop-map (f x)) â†’ is-prop-map (tot f)
    is-prop-map-tot = is-trunc-map-tot neg-one-ğ•‹
```

### The functoriality of dependent pair types preserves truncatedness

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-trunc-map-map-Î£-map-base :
      (k : ğ•‹) {f : A â†’ B} (C : B â†’ UU l3) â†’
      is-trunc-map k f â†’ is-trunc-map k (map-Î£-map-base f C)
    is-trunc-map-map-Î£-map-base k {f} C H y =
      is-trunc-equiv' k
        ( fiber f (pr1 y))
        ( compute-fiber-map-Î£-map-base f C y)
        ( H (pr1 y))

  abstract
    is-prop-map-map-Î£-map-base :
      {f : A â†’ B} (C : B â†’ UU l3) â†’
      is-prop-map f â†’ is-prop-map (map-Î£-map-base f C)
    is-prop-map-map-Î£-map-base C = is-trunc-map-map-Î£-map-base neg-one-ğ•‹ C

module _
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : A â†’ UU l3}
  where

  abstract
    is-trunc-map-map-Î£ :
      (k : ğ•‹) (D : B â†’ UU l4) {f : A â†’ B} {g : (x : A) â†’ C x â†’ D (f x)} â†’
      is-trunc-map k f â†’ ((x : A) â†’ is-trunc-map k (g x)) â†’
      is-trunc-map k (map-Î£ D f g)
    is-trunc-map-map-Î£ k D {f} {g} H K =
      is-trunc-map-left-map-triangle k
        ( map-Î£ D f g)
        ( map-Î£-map-base f D)
        ( tot g)
        ( triangle-map-Î£ D f g)
        ( is-trunc-map-map-Î£-map-base k D H)
        ( is-trunc-map-tot k K)

  module _
    (D : B â†’ UU l4) {f : A â†’ B} {g : (x : A) â†’ C x â†’ D (f x)}
    where

    abstract
      is-contr-map-map-Î£ :
        is-contr-map f â†’ ((x : A) â†’ is-contr-map (g x)) â†’
        is-contr-map (map-Î£ D f g)
      is-contr-map-map-Î£ = is-trunc-map-map-Î£ neg-two-ğ•‹ D

    abstract
      is-prop-map-map-Î£ :
        is-prop-map f â†’ ((x : A) â†’ is-prop-map (g x)) â†’
        is-prop-map (map-Î£ D f g)
      is-prop-map-map-Î£ = is-trunc-map-map-Î£ neg-one-ğ•‹ D
```

### Commuting squares of maps on total spaces

#### Functoriality of `Î£` preserves commuting squares of maps

```agda
module _
  {l1 l2 l3 l4 l5 l6 l7 l8 : Level}
  {A : UU l1} {P : A â†’ UU l2}
  {B : UU l3} {Q : B â†’ UU l4}
  {C : UU l5} {R : C â†’ UU l6}
  {D : UU l7} (S : D â†’ UU l8)
  {top' : A â†’ C} {left' : A â†’ B} {right' : C â†’ D} {bottom' : B â†’ D}
  (top : (a : A) â†’ P a â†’ R (top' a))
  (left : (a : A) â†’ P a â†’ Q (left' a))
  (right : (c : C) â†’ R c â†’ S (right' c))
  (bottom : (b : B) â†’ Q b â†’ S (bottom' b))
  where

  coherence-square-maps-Î£ :
    {H' : coherence-square-maps top' left' right' bottom'} â†’
    ( (a : A) (p : P a) â†’
      dependent-identification S
        ( H' a)
        ( bottom _ (left _ p))
        ( right _ (top _ p))) â†’
    coherence-square-maps
      ( map-Î£ R top' top)
      ( map-Î£ Q left' left)
      ( map-Î£ S right' right)
      ( map-Î£ S bottom' bottom)
  coherence-square-maps-Î£ {H'} H (a , p) = eq-pair-Î£ (H' a) (H a p)
```

#### Commuting squares of induced maps on total spaces

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  {A : UU l1} {P : A â†’ UU l2} {Q : A â†’ UU l3} {R : A â†’ UU l4} {S : A â†’ UU l5}
  (top : (a : A) â†’ P a â†’ R a)
  (left : (a : A) â†’ P a â†’ Q a)
  (right : (a : A) â†’ R a â†’ S a)
  (bottom : (a : A) â†’ Q a â†’ S a)
  where

  coherence-square-maps-tot :
    ((a : A) â†’ coherence-square-maps (top a) (left a) (right a) (bottom a)) â†’
    coherence-square-maps (tot top) (tot left) (tot right) (tot bottom)
  coherence-square-maps-tot H (a , p) = eq-pair-eq-fiber (H a p)
```

#### `map-Î£-map-base` preserves commuting squares of maps

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  {A : UU l1} {B : UU l2} {C : UU l3} {D : UU l4} (S : D â†’ UU l5)
  (top : A â†’ C) (left : A â†’ B) (right : C â†’ D) (bottom : B â†’ D)
  where

  coherence-square-maps-map-Î£-map-base :
    (H : coherence-square-maps top left right bottom) â†’
    coherence-square-maps
      ( map-Î£ (S âˆ˜ right) top (Î» a â†’ tr S (H a)))
      ( map-Î£-map-base left (S âˆ˜ bottom))
      ( map-Î£-map-base right S)
      ( map-Î£-map-base bottom S)
  coherence-square-maps-map-Î£-map-base H (a , p) = eq-pair-Î£ (H a) refl
```

### Commuting triangles of maps on total spaces

#### Functoriality of `Î£` preserves commuting triangles of maps

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level}
  {A : UU l1} {P : A â†’ UU l2}
  {B : UU l3} {Q : B â†’ UU l4}
  {C : UU l5} (R : C â†’ UU l6)
  {left' : A â†’ C} {right' : B â†’ C} {top' : A â†’ B}
  (left : (a : A) â†’ P a â†’ R (left' a))
  (right : (b : B) â†’ Q b â†’ R (right' b))
  (top : (a : A) â†’ P a â†’ Q (top' a))
  where

  coherence-triangle-maps-Î£ :
    {H' : coherence-triangle-maps left' right' top'} â†’
    ( (a : A) (p : P a) â†’
      dependent-identification R (H' a) (left _ p) (right _ (top _ p))) â†’
    coherence-triangle-maps
      ( map-Î£ R left' left)
      ( map-Î£ R right' right)
      ( map-Î£ Q top' top)
  coherence-triangle-maps-Î£ {H'} H (a , p) = eq-pair-Î£ (H' a) (H a p)
```

#### `map-Î£-map-base` preserves commuting triangles of maps

```agda
module _
  {l1 l2 l3 l4 : Level}
  {A : UU l1} {B : UU l2} {X : UU l3} (S : X â†’ UU l4)
  (left : A â†’ X) (right : B â†’ X) (top : A â†’ B)
  where

  coherence-triangle-maps-map-Î£-map-base :
    (H : coherence-triangle-maps left right top) â†’
    coherence-triangle-maps
      ( map-Î£-map-base left S)
      ( map-Î£-map-base right S)
      ( map-Î£ (S âˆ˜ right) top (Î» a â†’ tr S (H a)))
  coherence-triangle-maps-map-Î£-map-base H (a , _) = eq-pair-Î£ (H a) refl
```

### The action of `map-Î£-map-base` on identifications of the form `eq-pair-Î£` is given by the action on the base

```agda
module _
  { l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (C : B â†’ UU l3)
  where

  compute-ap-map-Î£-map-base-eq-pair-Î£ :
    { s s' : A} (p : s ï¼ s') {t : C (f s)} {t' : C (f s')}
    ( q : tr (C âˆ˜ f) p t ï¼ t') â†’
    ap (map-Î£-map-base f C) (eq-pair-Î£ p q) ï¼
    eq-pair-Î£ (ap f p) (substitution-law-tr C f p âˆ™ q)
  compute-ap-map-Î£-map-base-eq-pair-Î£ refl refl = refl
```

### The action of `ind-Î£` on identifications in fibers of dependent pair types is given by the action of the fibers of the function with the first argument fixed

```agda
module _
  {l1 l2 l3 : Level}
  {A : UU l1} {B : A â†’ UU l2} {C : UU l3}
  (f : (a : A) (b : B a) â†’ C)
  where

  compute-ap-ind-Î£-eq-pair-eq-fiber :
    {a : A} {b b' : B a} (p : b ï¼ b') â†’
    ap (ind-Î£ f) (eq-pair-eq-fiber p) ï¼ ap (f a) p
  compute-ap-ind-Î£-eq-pair-eq-fiber refl = refl
```

### Computing the inverse of `equiv-tot`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : A â†’ UU l3}
  where

  compute-inv-equiv-tot :
    (e : (x : A) â†’ B x â‰ƒ C x) â†’
    map-inv-equiv (equiv-tot e) ~
    map-equiv (equiv-tot (Î» x â†’ inv-equiv (e x)))
  compute-inv-equiv-tot e (a , c) =
    is-injective-equiv
      ( equiv-tot e)
      ( ( is-section-map-inv-equiv (equiv-tot e) (a , c)) âˆ™
        ( eq-pair-eq-fiber (inv (is-section-map-inv-equiv (e a) c))))
```

### Dependent sums of morphisms of arrows

```agda
module _
  {l1 l2 l3 l4 l5 : Level}
  {I : UU l5} {A : I â†’ UU l1} {B : I â†’ UU l2} {X : I â†’ UU l3} {Y : I â†’ UU l4}
  (f : (i : I) â†’ A i â†’ B i) (g : (i : I) â†’ X i â†’ Y i)
  (Î± : (i : I) â†’ hom-arrow (f i) (g i))
  where

  tot-hom-arrow : hom-arrow (tot f) (tot g)
  pr1 tot-hom-arrow =
    tot (Î» i â†’ map-domain-hom-arrow (f i) (g i) (Î± i))
  pr1 (pr2 tot-hom-arrow) =
    tot (Î» i â†’ map-codomain-hom-arrow (f i) (g i) (Î± i))
  pr2 (pr2 tot-hom-arrow) =
    tot-htpy (Î» i â†’ coh-hom-arrow (f i) (g i) (Î± i))
```

## See also

- Arithmetical laws involving dependent pair types are recorded in
  [`foundation.type-arithmetic-dependent-pair-types`](foundation.type-arithmetic-dependent-pair-types.md).
- Equality proofs in dependent pair types are characterized in
  [`foundation.equality-dependent-pair-types`](foundation.equality-dependent-pair-types.md).
- The universal property of dependent pair types is treated in
  [`foundation.universal-property-dependent-pair-types`](foundation.universal-property-dependent-pair-types.md).
- Functorial properties of cartesian product types are recorded in
  [`foundation.functoriality-cartesian-product-types`](foundation.functoriality-cartesian-product-types.md).
- Functorial properties of dependent product types are recorded in
  [`foundation.functoriality-dependent-function-types`](foundation.functoriality-dependent-function-types.md).
