# Quasicoherently idempotent maps

```agda
module foundation.quasicoherently-idempotent-maps where
```

<details><summary>Imports</summary>

```agda
open import foundation.1-types
open import foundation.action-on-identifications-functions
open import foundation.commuting-squares-of-homotopies
open import foundation.dependent-pair-types
open import foundation.equality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopy-algebra
open import foundation.homotopy-induction
open import foundation.idempotent-maps
open import foundation.identity-types
open import foundation.negated-equality
open import foundation.negation
open import foundation.structure-identity-principle
open import foundation.torsorial-type-families
open import foundation.transport-along-identifications
open import foundation.universe-levels
open import foundation.whiskering-higher-homotopies-composition
open import foundation.whiskering-homotopies-composition

open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.propositions
open import foundation-core.retractions
open import foundation-core.sets

open import synthetic-homotopy-theory.circle
open import synthetic-homotopy-theory.loop-homotopy-circle
```

</details>

## Idea

A
{{#concept "quasicoherently idempotent map" Disambiguation="on a type" Agda=is-quasicoherently-idempotent}}
is a map `f : A â†’ A` [equipped](foundation.structure.md) with a
[homotopy](foundation-core.homotopies.md) `I : f âˆ˜ f ~ f` witnessing that `f` is
[idempotent](foundation.idempotent-maps.md), and a coherence

```text
  f Â·l I ~ I Â·r f.
```

While this data is not enough to capture a fully coherent idempotent map, it is
sufficient for showing that `f` can be made to be fully coherent. This is in
contrast to [idempotent maps](foundation.idempotent-maps.md), which may in
general fail to be coherent.

**Terminology.** Our definition of a _quasicoherently idempotent map_
corresponds to the definition of a _quasiidempotent map_ in {{#cite Shu17}} and
{{#cite Shu14SplittingIdempotents}}.

## Definitions

### The structure of quasicoherent idempotence on maps

```agda
quasicoherence-is-idempotent :
  {l : Level} {A : UU l} (f : A â†’ A) â†’ f âˆ˜ f ~ f â†’ UU l
quasicoherence-is-idempotent f I = f Â·l I ~ I Â·r f

is-quasicoherently-idempotent : {l : Level} {A : UU l} â†’ (A â†’ A) â†’ UU l
is-quasicoherently-idempotent f =
  Î£ (f âˆ˜ f ~ f) (quasicoherence-is-idempotent f)

module _
  {l : Level} {A : UU l} {f : A â†’ A} (H : is-quasicoherently-idempotent f)
  where

  is-idempotent-is-quasicoherently-idempotent : is-idempotent f
  is-idempotent-is-quasicoherently-idempotent = pr1 H

  coh-is-quasicoherently-idempotent :
    quasicoherence-is-idempotent f
      ( is-idempotent-is-quasicoherently-idempotent)
  coh-is-quasicoherently-idempotent = pr2 H
```

### The type of quasicoherently idempotent maps

```agda
quasicoherently-idempotent-map : {l : Level} (A : UU l) â†’ UU l
quasicoherently-idempotent-map A = Î£ (A â†’ A) (is-quasicoherently-idempotent)

module _
  {l : Level} {A : UU l} (H : quasicoherently-idempotent-map A)
  where

  map-quasicoherently-idempotent-map : A â†’ A
  map-quasicoherently-idempotent-map = pr1 H

  is-quasicoherently-idempotent-quasicoherently-idempotent-map :
    is-quasicoherently-idempotent map-quasicoherently-idempotent-map
  is-quasicoherently-idempotent-quasicoherently-idempotent-map = pr2 H

  is-idempotent-quasicoherently-idempotent-map :
    is-idempotent map-quasicoherently-idempotent-map
  is-idempotent-quasicoherently-idempotent-map =
    is-idempotent-is-quasicoherently-idempotent
      ( is-quasicoherently-idempotent-quasicoherently-idempotent-map)

  coh-quasicoherently-idempotent-map :
    quasicoherence-is-idempotent
      ( map-quasicoherently-idempotent-map)
      ( is-idempotent-quasicoherently-idempotent-map)
  coh-quasicoherently-idempotent-map =
    coh-is-quasicoherently-idempotent
      ( is-quasicoherently-idempotent-quasicoherently-idempotent-map)

  idempotent-map-quasicoherently-idempotent-map : idempotent-map A
  idempotent-map-quasicoherently-idempotent-map =
    ( map-quasicoherently-idempotent-map ,
      is-idempotent-quasicoherently-idempotent-map)
```

## Properties

### The identity function is quasicoherently idempotent

In fact, any idempotence witness of the identity function is quasicoherent.

```agda
module _
  {l : Level} {A : UU l} (H : is-idempotent (id {A = A}))
  where

  quasicoherence-is-idempotent-id :
    quasicoherence-is-idempotent id H
  quasicoherence-is-idempotent-id = left-unit-law-left-whisker-comp H

  is-quasicoherently-idempotent-is-idempotent-id :
    is-quasicoherently-idempotent (id {A = A})
  is-quasicoherently-idempotent-is-idempotent-id =
    ( H , quasicoherence-is-idempotent-id)

module _
  {l : Level} {A : UU l}
  where

  is-quasicoherently-idempotent-id :
    is-quasicoherently-idempotent (id {A = A})
  is-quasicoherently-idempotent-id =
    is-quasicoherently-idempotent-is-idempotent-id refl-htpy
```

### Being quasicoherently idempotent on a set is a property

```agda
module _
  {l : Level} {A : UU l} (is-set-A : is-set A) (f : A â†’ A)
  where

  is-prop-quasicoherence-is-idempotent-is-set :
    (I : f âˆ˜ f ~ f) â†’ is-prop (quasicoherence-is-idempotent f I)
  is-prop-quasicoherence-is-idempotent-is-set I =
    is-prop-Î 
      ( Î» x â†’
        is-set-is-prop
          ( is-set-A ((f âˆ˜ f âˆ˜ f) x) ((f âˆ˜ f) x))
          ( (f Â·l I) x)
          ( (I Â·r f) x))

  is-prop-is-quasicoherently-idempotent-is-set :
    is-prop (is-quasicoherently-idempotent f)
  is-prop-is-quasicoherently-idempotent-is-set =
    is-prop-Î£
      ( is-prop-is-idempotent-is-set is-set-A f)
      ( is-prop-quasicoherence-is-idempotent-is-set)

  is-quasicoherently-idempotent-is-set-Prop : Prop l
  is-quasicoherently-idempotent-is-set-Prop =
    ( is-quasicoherently-idempotent f ,
      is-prop-is-quasicoherently-idempotent-is-set)

module _
  {l : Level} (A : Set l) (f : type-Set A â†’ type-Set A)
  where

  is-prop-is-quasicoherently-idempotent-Set :
    is-prop (is-quasicoherently-idempotent f)
  is-prop-is-quasicoherently-idempotent-Set =
    is-prop-is-quasicoherently-idempotent-is-set (is-set-type-Set A) f

  is-quasicoherently-idempotent-prop-Set : Prop l
  is-quasicoherently-idempotent-prop-Set =
    ( is-quasicoherently-idempotent f ,
      is-prop-is-quasicoherently-idempotent-Set)
```

### Being quasicoherently idempotent is generally not a property

Not even for [1-types](foundation.1-types.md): consider the identity function on
the [circle](synthetic-homotopy-theory.circle.md)

```text
  id : ğ•ŠÂ¹ â†’ ğ•ŠÂ¹.
```

Two distinct witnesses that it is idempotent are given by `t â†¦ refl` and
`t â†¦ loop`. Both of these are quasicoherent, because

```text
  quasicoherence-is-idempotent id I â‰ (id Â·l I ~ I Â·r id) â‰ƒ (I ~ I).
```

```agda
is-not-prop-is-quasicoherently-idempotent-id-ğ•ŠÂ¹ :
  Â¬ (is-prop (is-quasicoherently-idempotent (id {A = ğ•ŠÂ¹})))
is-not-prop-is-quasicoherently-idempotent-id-ğ•ŠÂ¹ H =
  nonequal-Î 
    ( loop-htpy-ğ•ŠÂ¹)
    ( refl-htpy)
    ( base-ğ•ŠÂ¹)
    ( is-not-refl-ev-base-loop-htpy-ğ•ŠÂ¹)
    ( ap pr1
      ( eq-is-prop H
        { is-quasicoherently-idempotent-is-idempotent-id loop-htpy-ğ•ŠÂ¹}
        { is-quasicoherently-idempotent-is-idempotent-id refl-htpy}))
```

### Idempotent maps on sets are quasicoherently idempotent

```agda
module _
  {l : Level} {A : UU l} (is-set-A : is-set A) {f : A â†’ A}
  where

  is-quasicoherently-idempotent-is-idempotent-is-set :
    is-idempotent f â†’ is-quasicoherently-idempotent f
  pr1 (is-quasicoherently-idempotent-is-idempotent-is-set I) = I
  pr2 (is-quasicoherently-idempotent-is-idempotent-is-set I) x =
    eq-is-prop (is-set-A ((f âˆ˜ f âˆ˜ f) x) ((f âˆ˜ f) x))
```

### If `i` and `r` form an inclusion-retraction pair, then `i âˆ˜ r` is quasicoherently idempotent

This statement can be found as part of the proof of Lemma 3.6 in
{{#cite Shu17}}.

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  (i : B â†’ A) (r : A â†’ B) (H : is-retraction i r)
  where

  quasicoherence-is-idempotent-inclusion-retraction :
    quasicoherence-is-idempotent
      ( i âˆ˜ r)
      ( is-idempotent-inclusion-retraction i r H)
  quasicoherence-is-idempotent-inclusion-retraction =
    ( inv-preserves-comp-left-whisker-comp i r (i Â·l H Â·r r)) âˆ™h
    ( double-whisker-compÂ²
      ( i)
      ( preserves-comp-left-whisker-comp r i H âˆ™h inv-coh-htpy-id H)
      ( r))

  is-quasicoherently-idempotent-inclusion-retraction :
    is-quasicoherently-idempotent (i âˆ˜ r)
  is-quasicoherently-idempotent-inclusion-retraction =
    ( is-idempotent-inclusion-retraction i r H ,
      quasicoherence-is-idempotent-inclusion-retraction)
```

### Quasicoherent idempotence is preserved by homotopies

This fact does not explicitly appear in {{#cite Shu17}} although it is
implicitly used in Lemma 3.6.

```agda
module _
  {l : Level} {A : UU l} {f g : A â†’ A} (F : is-quasicoherently-idempotent f)
  where

  abstract
    quasicoherence-is-idempotent-htpy :
      (H : g ~ f) â†’
      quasicoherence-is-idempotent g
        ( is-idempotent-htpy
          ( is-idempotent-is-quasicoherently-idempotent F)
          ( H))
    quasicoherence-is-idempotent-htpy H =
      homotopy-reasoning
      ( g Â·l is-idempotent-htpy I H)
      ~ ( H Â·r (g âˆ˜ g)) âˆ™h
        ( f Â·l (g Â·l H âˆ™h H Â·r f âˆ™h I âˆ™h inv-htpy H)) âˆ™h
        ( inv-htpy (H Â·r g))
      by
      ( right-transpose-htpy-concat
        ( g Â·l is-idempotent-htpy I H)
        ( H Â·r g)
        ( H Â·r (g âˆ˜ g) âˆ™h (f Â·l (g Â·l H âˆ™h H Â·r f âˆ™h I âˆ™h inv-htpy H)))
        ( inv-htpy (nat-htpy H âˆ˜ (g Â·l H âˆ™h H Â·r f âˆ™h I âˆ™h inv-htpy H))))
      ~ g Â·l H Â·r g âˆ™h H Â·r (f âˆ˜ g) âˆ™h I Â·r g âˆ™h inv-htpy (H Â·r g)
      by
      ( ap-concat-htpy'
        ( inv-htpy (H Â·r g))
        ( ( ap-concat-htpy
            ( H Â·r ((g âˆ˜ g)))
            ( ( distributive-left-whisker-comp-concat f
                ( g Â·l H âˆ™h H Â·r f âˆ™h I)
                ( inv-htpy H)) âˆ™h
              ( ap-concat-htpy'
                ( f Â·l inv-htpy H)
                ( ( distributive-left-whisker-comp-concat f
                    ( g Â·l H âˆ™h H Â·r f)
                    ( I)) âˆ™h
                  ( ap-binary-concat-htpy
                    ( distributive-left-whisker-comp-concat f (g Â·l H) (H Â·r f))
                    ( coh-is-quasicoherently-idempotent F)))) âˆ™h
              ( assoc-htpy
                ( f Â·l g Â·l H âˆ™h f Â·l H Â·r f)
                ( I Â·r f)
                ( f Â·l inv-htpy H)) âˆ™h
              ( ap-concat-htpy
                ( f Â·l g Â·l H âˆ™h f Â·l H Â·r f)
                ( nat-htpy I âˆ˜ inv-htpy H)) âˆ™h
              ( inv-htpy
                ( assoc-htpy
                  ( f Â·l g Â·l H âˆ™h f Â·l H Â·r f)
                  ( (f âˆ˜ f) Â·l inv-htpy H)
                  ( I Â·r g))))) âˆ™h
          ( inv-htpy
            ( assoc-htpy
              ( H Â·r (g âˆ˜ g))
              ( f Â·l g Â·l H âˆ™h f Â·l H Â·r f âˆ™h (f âˆ˜ f) Â·l inv-htpy H)
              ( I Â·r g))) âˆ™h
          ( ap-concat-htpy'
            ( I Â·r g)
            ( ( ap-concat-htpy
                ( H Â·r (g âˆ˜ g))
                ( ap-concat-htpy'
                  ( (f âˆ˜ f) Â·l inv-htpy H)
                  ( ( ap-concat-htpy'
                      ( f Â·l H Â·r f)
                      ( preserves-comp-left-whisker-comp f g H)) âˆ™h
                    ( inv-htpy (nat-htpy (f Â·l H) âˆ˜ H))) âˆ™h
                  ( ap-concat-htpy
                    ( f Â·l H Â·r g âˆ™h (f âˆ˜ f) Â·l H)
                    ( left-whisker-inv-htpy (f âˆ˜ f) H)) âˆ™h
                  ( is-retraction-inv-concat-htpy'
                    ( (f âˆ˜ f) Â·l H)
                    ( f Â·l H Â·r g)))) âˆ™h
              ( nat-htpy H âˆ˜ (H Â·r g))))))
      where
        I : is-idempotent f
        I = is-idempotent-is-quasicoherently-idempotent F

  is-quasicoherently-idempotent-htpy :
    g ~ f â†’ is-quasicoherently-idempotent g
  pr1 (is-quasicoherently-idempotent-htpy H) =
    is-idempotent-htpy
      ( is-idempotent-is-quasicoherently-idempotent F)
      ( H)
  pr2 (is-quasicoherently-idempotent-htpy H) =
    quasicoherence-is-idempotent-htpy H

  is-quasicoherently-idempotent-inv-htpy :
    f ~ g â†’ is-quasicoherently-idempotent g
  pr1 (is-quasicoherently-idempotent-inv-htpy H) =
    is-idempotent-htpy
      ( is-idempotent-is-quasicoherently-idempotent F)
      ( inv-htpy H)
  pr2 (is-quasicoherently-idempotent-inv-htpy H) =
    quasicoherence-is-idempotent-htpy (inv-htpy H)
```

### Realigning the coherence of a quasicoherent idempotence proof

Given a quasicoherently idempotent map `f`, any other idempotence homotopy
`I : f âˆ˜ f ~ f` that is homotopic to the coherent one is also coherent.

```agda
module _
  {l : Level} {A : UU l} {f : A â†’ A}
  (F : is-quasicoherently-idempotent f)
  (I : f âˆ˜ f ~ f)
  where

  quasicoherence-is-idempotent-is-idempotent-htpy :
    is-idempotent-is-quasicoherently-idempotent F ~ I â†’
    quasicoherence-is-idempotent f I
  quasicoherence-is-idempotent-is-idempotent-htpy Î± =
    ( left-whisker-compÂ² f (inv-htpy Î±)) âˆ™h
    ( coh-is-quasicoherently-idempotent F) âˆ™h
    ( right-whisker-compÂ² Î± f)

  quasicoherence-is-idempotent-is-idempotent-inv-htpy :
    I ~ is-idempotent-is-quasicoherently-idempotent F â†’
    quasicoherence-is-idempotent f I
  quasicoherence-is-idempotent-is-idempotent-inv-htpy Î± =
    ( left-whisker-compÂ² f Î±) âˆ™h
    ( coh-is-quasicoherently-idempotent F) âˆ™h
    ( right-whisker-compÂ² (inv-htpy Î±) f)

  is-quasicoherently-idempotent-is-idempotent-htpy :
    is-idempotent-is-quasicoherently-idempotent F ~ I â†’
    is-quasicoherently-idempotent f
  is-quasicoherently-idempotent-is-idempotent-htpy Î± =
    ( I , quasicoherence-is-idempotent-is-idempotent-htpy Î±)

  is-quasicoherently-idempotent-is-idempotent-inv-htpy :
    I ~ is-idempotent-is-quasicoherently-idempotent F â†’
    is-quasicoherently-idempotent f
  is-quasicoherently-idempotent-is-idempotent-inv-htpy Î± =
    ( I , quasicoherence-is-idempotent-is-idempotent-inv-htpy Î±)
```

### Not every idempotent map is quasicoherently idempotent

To be clear, what we are asking for is an idempotent map `f`, such that _no_
idempotence homotopy `f âˆ˜ f ~ f` is quasicoherent. A counterexample can be
constructed using the [cantor space](set-theory.cantor-space.md), see Section 4
of {{#cite Shu17}} for more details.

### Characterization of identity of quasicoherently idempotent maps

A homotopy of quasicoherent idempotence witnesses `(I, Q) ~ (J, R)` consists of
a homotopy of the underlying idempotence witnesses `H : I ~ J` and a
[coherence](foundation-core.commuting-squares-of-homotopies.md)

```text
            fH
  f Â·l I -------- f Â·l J
     |              |
   Q |              | R
     |              |
  I Â·r f -------â€“ J Â·r f.
            Hf
```

```agda
module _
  {l : Level} {A : UU l} {f : A â†’ A}
  where

  coherence-htpy-is-quasicoherently-idempotent :
    (p q : is-quasicoherently-idempotent f) â†’
    ( is-idempotent-is-quasicoherently-idempotent p ~
      is-idempotent-is-quasicoherently-idempotent q) â†’
    UU l
  coherence-htpy-is-quasicoherently-idempotent (I , Q) (J , R) H =
    coherence-square-homotopies
      ( left-whisker-compÂ² f H)
      ( Q)
      ( R)
      ( right-whisker-compÂ² H f)

  htpy-is-quasicoherently-idempotent :
    (p q : is-quasicoherently-idempotent f) â†’ UU l
  htpy-is-quasicoherently-idempotent p q =
    Î£ ( is-idempotent-is-quasicoherently-idempotent p ~
        is-idempotent-is-quasicoherently-idempotent q)
      ( coherence-htpy-is-quasicoherently-idempotent p q)

  refl-htpy-is-quasicoherently-idempotent :
    (p : is-quasicoherently-idempotent f) â†’
    htpy-is-quasicoherently-idempotent p p
  refl-htpy-is-quasicoherently-idempotent p = (refl-htpy , right-unit-htpy)

  htpy-eq-is-quasicoherently-idempotent :
    (p q : is-quasicoherently-idempotent f) â†’
    p ï¼ q â†’ htpy-is-quasicoherently-idempotent p q
  htpy-eq-is-quasicoherently-idempotent p .p refl =
    refl-htpy-is-quasicoherently-idempotent p

  is-torsorial-htpy-is-quasicoherently-idempotent :
    (p : is-quasicoherently-idempotent f) â†’
    is-torsorial (htpy-is-quasicoherently-idempotent p)
  is-torsorial-htpy-is-quasicoherently-idempotent p =
    is-torsorial-Eq-structure
      ( is-torsorial-htpy (is-idempotent-is-quasicoherently-idempotent p))
      ( is-idempotent-is-quasicoherently-idempotent p , refl-htpy)
      ( is-torsorial-htpy (coh-is-quasicoherently-idempotent p âˆ™h refl-htpy))

  is-equiv-htpy-eq-is-quasicoherently-idempotent :
    (p q : is-quasicoherently-idempotent f) â†’
    is-equiv (htpy-eq-is-quasicoherently-idempotent p q)
  is-equiv-htpy-eq-is-quasicoherently-idempotent p =
    fundamental-theorem-id
      ( is-torsorial-htpy-is-quasicoherently-idempotent p)
      ( htpy-eq-is-quasicoherently-idempotent p)

  extensionality-is-quasicoherently-idempotent :
    (p q : is-quasicoherently-idempotent f) â†’
    (p ï¼ q) â‰ƒ (htpy-is-quasicoherently-idempotent p q)
  extensionality-is-quasicoherently-idempotent p q =
    ( htpy-eq-is-quasicoherently-idempotent p q ,
      is-equiv-htpy-eq-is-quasicoherently-idempotent p q)

  eq-htpy-is-quasicoherently-idempotent :
    (p q : is-quasicoherently-idempotent f) â†’
    htpy-is-quasicoherently-idempotent p q â†’ p ï¼ q
  eq-htpy-is-quasicoherently-idempotent p q =
    map-inv-is-equiv (is-equiv-htpy-eq-is-quasicoherently-idempotent p q)
```

## See also

- In [`foundation.split-idempotent-maps`](foundation.split-idempotent-maps.md)
  we show that every quasicoherently idempotent map splits. Moreover, it is true
  that split idempotent maps are a retract of quasicoherent idempotent maps.

## References

{{#bibliography}} {{#reference Shu17}} {{#reference Shu14SplittingIdempotents}}
