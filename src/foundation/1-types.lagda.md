# `1`-Types

```agda
module foundation.1-types where

open import foundation-core.1-types public
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.subuniverses
open import foundation.truncated-types
open import foundation.universe-levels

open import foundation-core.cartesian-product-types
open import foundation-core.equivalences
open import foundation-core.identity-types
open import foundation-core.propositions
open import foundation-core.subtypes
open import foundation-core.torsorial-type-families
open import foundation-core.truncation-levels
```

</details>

### Being a 1-type is a property

```agda
abstract
  is-prop-is-1-type :
    {l : Level} (A : UU l) â†’ is-prop (is-1-type A)
  is-prop-is-1-type A = is-prop-is-trunc one-ğ•‹ A

is-1-type-Prop :
  {l : Level} â†’ UU l â†’ Prop l
is-1-type-Prop = is-trunc-Prop one-ğ•‹
```

### The type of all 1-types in a universe is a 2-type

```agda
is-trunc-1-Type : {l : Level} â†’ is-trunc two-ğ•‹ (1-Type l)
is-trunc-1-Type = is-trunc-Truncated-Type one-ğ•‹

1-Type-Truncated-Type : (l : Level) â†’ Truncated-Type (lsuc l) two-ğ•‹
1-Type-Truncated-Type l = Truncated-Type-Truncated-Type l one-ğ•‹
```

### Products of families of 1-types are 1-types

```agda
abstract
  is-1-type-Î  :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    ((x : A) â†’ is-1-type (B x)) â†’ is-1-type ((x : A) â†’ B x)
  is-1-type-Î  = is-trunc-Î  one-ğ•‹

type-Î -1-Type' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ 1-Type l2) â†’ UU (l1 âŠ” l2)
type-Î -1-Type' A B = (x : A) â†’ type-1-Type (B x)

is-1-type-type-Î -1-Type' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ 1-Type l2) â†’
  is-1-type (type-Î -1-Type' A B)
is-1-type-type-Î -1-Type' A B =
  is-1-type-Î  (Î» x â†’ is-1-type-type-1-Type (B x))

Î -1-Type' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ 1-Type l2) â†’ 1-Type (l1 âŠ” l2)
pr1 (Î -1-Type' A B) = type-Î -1-Type' A B
pr2 (Î -1-Type' A B) = is-1-type-type-Î -1-Type' A B

type-Î -1-Type :
  {l1 l2 : Level} (A : 1-Type l1) (B : type-1-Type A â†’ 1-Type l2) â†’
  UU (l1 âŠ” l2)
type-Î -1-Type A = type-Î -1-Type' (type-1-Type A)

is-1-type-type-Î -1-Type :
  {l1 l2 : Level} (A : 1-Type l1) (B : type-1-Type A â†’ 1-Type l2) â†’
  is-1-type (type-Î -1-Type A B)
is-1-type-type-Î -1-Type A = is-1-type-type-Î -1-Type' (type-1-Type A)

Î -1-Type :
  {l1 l2 : Level} (A : 1-Type l1) (B : type-1-Type A â†’ 1-Type l2) â†’
  1-Type (l1 âŠ” l2)
Î -1-Type = Î -Truncated-Type one-ğ•‹
```

### The type of functions into a 1-type is a 1-type

```agda
abstract
  is-1-type-function-type :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-1-type B â†’ is-1-type (A â†’ B)
  is-1-type-function-type = is-trunc-function-type one-ğ•‹

type-hom-1-Type :
  {l1 l2 : Level} (A : 1-Type l1) (B : 1-Type l2) â†’ UU (l1 âŠ” l2)
type-hom-1-Type A B = type-1-Type A â†’ type-1-Type B

is-1-type-type-hom-1-Type :
  {l1 l2 : Level} (A : 1-Type l1) (B : 1-Type l2) â†’
  is-1-type (type-hom-1-Type A B)
is-1-type-type-hom-1-Type A B =
  is-1-type-function-type (is-1-type-type-1-Type B)

hom-1-Type :
  {l1 l2 : Level} (A : 1-Type l1) (B : 1-Type l2) â†’ 1-Type (l1 âŠ” l2)
hom-1-Type = hom-Truncated-Type one-ğ•‹
```

### 1-Types are closed under dependent pair types

```agda
abstract
  is-1-type-Î£ :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    is-1-type A â†’ ((x : A) â†’ is-1-type (B x)) â†’ is-1-type (Î£ A B)
  is-1-type-Î£ = is-trunc-Î£ {k = one-ğ•‹}

Î£-1-Type :
  {l1 l2 : Level} (A : 1-Type l1) (B : pr1 A â†’ 1-Type l2) â†’ 1-Type (l1 âŠ” l2)
Î£-1-Type = Î£-Truncated-Type
```

### 1-Types are closed under cartesian product types

```agda
abstract
  is-1-type-product :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-1-type A â†’ is-1-type B â†’ is-1-type (A Ã— B)
  is-1-type-product = is-trunc-product one-ğ•‹

product-1-Type :
  {l1 l2 : Level} (A : 1-Type l1) (B : 1-Type l2) â†’ 1-Type (l1 âŠ” l2)
product-1-Type A B = Î£-1-Type A (Î» x â†’ B)
```

### Subtypes of 1-types are 1-types

```agda
module _
  {l1 l2 : Level} {A : UU l1} (P : subtype l2 A)
  where

  abstract
    is-1-type-type-subtype : is-1-type A â†’ is-1-type (type-subtype P)
    is-1-type-type-subtype = is-trunc-type-subtype zero-ğ•‹ P
```

```agda
module _
  {l : Level} (X : 1-Type l)
  where

  type-equiv-1-Type : {l2 : Level} (Y : 1-Type l2) â†’ UU (l âŠ” l2)
  type-equiv-1-Type Y = type-1-Type X â‰ƒ type-1-Type Y

  equiv-eq-1-Type : (Y : 1-Type l) â†’ X ï¼ Y â†’ type-equiv-1-Type Y
  equiv-eq-1-Type = equiv-eq-subuniverse is-1-type-Prop X

  abstract
    is-torsorial-equiv-1-Type :
      is-torsorial (Î» (Y : 1-Type l) â†’ type-equiv-1-Type Y)
    is-torsorial-equiv-1-Type =
      is-torsorial-equiv-subuniverse is-1-type-Prop X

  abstract
    is-equiv-equiv-eq-1-Type : (Y : 1-Type l) â†’ is-equiv (equiv-eq-1-Type Y)
    is-equiv-equiv-eq-1-Type = is-equiv-equiv-eq-subuniverse is-1-type-Prop X

  extensionality-1-Type :
    (Y : 1-Type l) â†’ (X ï¼ Y) â‰ƒ type-equiv-1-Type Y
  pr1 (extensionality-1-Type Y) = equiv-eq-1-Type Y
  pr2 (extensionality-1-Type Y) = is-equiv-equiv-eq-1-Type Y

  eq-equiv-1-Type : (Y : 1-Type l) â†’ type-equiv-1-Type Y â†’ X ï¼ Y
  eq-equiv-1-Type Y = eq-equiv-subuniverse is-1-type-Prop
```
