# The double negation image of a map

```agda
module foundation.double-negation-images where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.double-negation
open import foundation.embeddings
open import foundation.fundamental-theorem-of-identity-types
open import foundation.hilbert-epsilon-operators-maps
open import foundation.slice
open import foundation.split-surjective-maps
open import foundation.subtype-identity-principle
open import foundation.universe-levels

open import foundation-core.1-types
open import foundation-core.commuting-triangles-of-maps
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.propositions
open import foundation-core.sets
open import foundation-core.subtypes
open import foundation-core.torsorial-type-families
open import foundation-core.truncated-types
open import foundation-core.truncation-levels

open import logic.double-negation-dense-maps
open import logic.double-negation-eliminating-maps
open import logic.double-negation-stable-embeddings
```

</details>

## Idea

The
{{#concept "double negation image" Disambiguation="of a map" Agda=double-negation-im}}
of `f : A â†’ B` is the essentially unique type that factorizes `f` as a
[double negation dense map](logic.double-negation-dense-maps.md) followed by a
[double negation stable embedding](logic.double-negation-stable-embeddings.md).

## Definitions

```agda
module _
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  where

  subtype-double-negation-im : subtype (l1 âŠ” l2) X
  subtype-double-negation-im x = double-negation-type-Prop (fiber f x)

  is-in-double-negation-im : X â†’ UU (l1 âŠ” l2)
  is-in-double-negation-im = is-in-subtype subtype-double-negation-im

  double-negation-im : UU (l1 âŠ” l2)
  double-negation-im = type-subtype subtype-double-negation-im

  inclusion-double-negation-im : double-negation-im â†’ X
  inclusion-double-negation-im = inclusion-subtype subtype-double-negation-im

  map-unit-double-negation-im : A â†’ double-negation-im
  map-unit-double-negation-im a = (f a , intro-double-negation (a , refl))

  triangle-unit-double-negation-im :
    coherence-triangle-maps
      ( f)
      ( inclusion-double-negation-im)
      ( map-unit-double-negation-im)
  triangle-unit-double-negation-im a = refl

  unit-double-negation-im : hom-slice f inclusion-double-negation-im
  pr1 unit-double-negation-im = map-unit-double-negation-im
  pr2 unit-double-negation-im = triangle-unit-double-negation-im
```

## Properties

### We characterize the identity type of `double-negation-im f`

```agda
module _
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  where

  Eq-double-negation-im : double-negation-im f â†’ double-negation-im f â†’ UU l1
  Eq-double-negation-im x y = (pr1 x ï¼ pr1 y)

  refl-Eq-double-negation-im :
    (x : double-negation-im f) â†’ Eq-double-negation-im x x
  refl-Eq-double-negation-im x = refl

  Eq-eq-double-negation-im :
    (x y : double-negation-im f) â†’ x ï¼ y â†’ Eq-double-negation-im x y
  Eq-eq-double-negation-im x .x refl = refl-Eq-double-negation-im x

  abstract
    is-torsorial-Eq-double-negation-im :
      (x : double-negation-im f) â†’ is-torsorial (Eq-double-negation-im x)
    is-torsorial-Eq-double-negation-im x =
      is-torsorial-Eq-subtype
        ( is-torsorial-Id (pr1 x))
        ( is-prop-is-in-subtype (subtype-double-negation-im f))
        ( pr1 x)
        ( refl)
        ( pr2 x)

  abstract
    is-equiv-Eq-eq-double-negation-im :
      (x y : double-negation-im f) â†’ is-equiv (Eq-eq-double-negation-im x y)
    is-equiv-Eq-eq-double-negation-im x =
      fundamental-theorem-id
        ( is-torsorial-Eq-double-negation-im x)
        ( Eq-eq-double-negation-im x)

  equiv-Eq-eq-double-negation-im :
    (x y : double-negation-im f) â†’ (x ï¼ y) â‰ƒ Eq-double-negation-im x y
  equiv-Eq-eq-double-negation-im x y =
    ( Eq-eq-double-negation-im x y , is-equiv-Eq-eq-double-negation-im x y)

  eq-Eq-double-negation-im :
    (x y : double-negation-im f) â†’ Eq-double-negation-im x y â†’ x ï¼ y
  eq-Eq-double-negation-im x y =
    map-inv-is-equiv (is-equiv-Eq-eq-double-negation-im x y)
```

### The unit map of the double negation image is double negation dense

```agda
abstract
  is-double-negation-dense-unit-double-negation-im :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    is-double-negation-dense-map (map-unit-double-negation-im f)
  is-double-negation-dense-unit-double-negation-im f (y , nnq) np =
    nnq
      ( Î» p â†’
        np
          ( pr1 p ,
            eq-Eq-double-negation-im f
              ( map-unit-double-negation-im f (pr1 p)) (y , nnq) (pr2 p)))
```

### The double negation image inclusion is a double negation stable embedding

```agda
module _
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  where

  is-emb-inclusion-double-negation-im :
    is-emb (inclusion-double-negation-im f)
  is-emb-inclusion-double-negation-im =
    is-emb-inclusion-subtype (subtype-double-negation-im f)

  emb-double-negation-im : double-negation-im f â†ª X
  emb-double-negation-im =
    ( inclusion-double-negation-im f , is-emb-inclusion-double-negation-im)

  is-injective-inclusion-double-negation-im :
    is-injective (inclusion-double-negation-im f)
  is-injective-inclusion-double-negation-im =
    is-injective-is-emb is-emb-inclusion-double-negation-im

  is-double-negation-eliminating-map-inclusion-double-negation-im :
    is-double-negation-eliminating-map (inclusion-double-negation-im f)
  is-double-negation-eliminating-map-inclusion-double-negation-im x nnip =
    ( ( x ,
        ( Î» np â†’
          nnip
            ( Î» ip â†’
              is-double-negation-dense-unit-double-negation-im f
                ( pr1 ip)
                ( Î» Î·q â†’
                  np
                  ( pr1 Î·q ,
                    ap (inclusion-double-negation-im f) (pr2 Î·q) âˆ™ pr2 ip))))) ,
      ( refl))

  double-negation-eliminating-map-inclusion-double-negation-im :
    double-negation-im f â†’Â¬Â¬ X
  double-negation-eliminating-map-inclusion-double-negation-im =
    ( inclusion-double-negation-im f ,
      is-double-negation-eliminating-map-inclusion-double-negation-im)

  is-double-negation-stable-emb-inclusion-double-negation-im :
    is-double-negation-stable-emb (inclusion-double-negation-im f)
  is-double-negation-stable-emb-inclusion-double-negation-im =
    ( is-emb-inclusion-double-negation-im ,
      is-double-negation-eliminating-map-inclusion-double-negation-im)

  double-negation-stable-emb-double-negation-im : double-negation-im f â†ªÂ¬Â¬ X
  double-negation-stable-emb-double-negation-im =
    ( inclusion-double-negation-im f ,
      is-double-negation-stable-emb-inclusion-double-negation-im)

  Îµ-operator-map-inclusion-double-negation-im :
    Îµ-operator-map (inclusion-double-negation-im f)
  Îµ-operator-map-inclusion-double-negation-im =
    Îµ-operator-double-negation-eliminating-map
      ( double-negation-eliminating-map-inclusion-double-negation-im)
```

### The double negation image of a map into a truncated type is truncated

```agda
abstract
  is-trunc-double-negation-im :
    {l1 l2 : Level} (k : ğ•‹) {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-trunc (succ-ğ•‹ k) X â†’ is-trunc (succ-ğ•‹ k) (double-negation-im f)
  is-trunc-double-negation-im k f = is-trunc-emb k (emb-double-negation-im f)

double-negation-im-Truncated-Type :
  {l1 l2 : Level} (k : ğ•‹) (X : Truncated-Type l1 (succ-ğ•‹ k)) {A : UU l2}
  (f : A â†’ type-Truncated-Type X) â†’ Truncated-Type (l1 âŠ” l2) (succ-ğ•‹ k)
double-negation-im-Truncated-Type k X f =
  ( double-negation-im f ,
    is-trunc-double-negation-im k f (is-trunc-type-Truncated-Type X))
```

### The double negation image of a map into a proposition is a proposition

```agda
abstract
  is-prop-double-negation-im :
    {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-prop X â†’ is-prop (double-negation-im f)
  is-prop-double-negation-im = is-trunc-double-negation-im neg-two-ğ•‹

double-negation-im-Prop :
  {l1 l2 : Level} (X : Prop l1) {A : UU l2}
  (f : A â†’ type-Prop X) â†’ Prop (l1 âŠ” l2)
double-negation-im-Prop = double-negation-im-Truncated-Type neg-two-ğ•‹
```

### The double negation image of a map into a set is a set

```agda
abstract
  is-set-double-negation-im :
    {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-set X â†’ is-set (double-negation-im f)
  is-set-double-negation-im = is-trunc-double-negation-im neg-one-ğ•‹

double-negation-im-Set :
  {l1 l2 : Level} (X : Set l1) {A : UU l2}
  (f : A â†’ type-Set X) â†’ Set (l1 âŠ” l2)
double-negation-im-Set = double-negation-im-Truncated-Type (neg-one-ğ•‹)
```

### The double negation image of a map into a 1-type is a 1-type

```agda
abstract
  is-1-type-double-negation-im :
    {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-1-type X â†’ is-1-type (double-negation-im f)
  is-1-type-double-negation-im = is-trunc-double-negation-im zero-ğ•‹

double-negation-im-1-Type :
  {l1 l2 : Level} (X : 1-Type l1) {A : UU l2}
  (f : A â†’ type-1-Type X) â†’ 1-Type (l1 âŠ” l2)
double-negation-im-1-Type = double-negation-im-Truncated-Type zero-ğ•‹
```

### Injective double negation eliminating maps are embeddings

```agda
module _
  {l1 l2 : Level} {X : UU l1} {A : UU l2} {f : A â†’ X}
  where

  is-emb-is-injective-is-double-negation-eliminating-map :
    is-double-negation-eliminating-map f â†’ is-injective f â†’ is-emb f
  is-emb-is-injective-is-double-negation-eliminating-map K H =
    is-emb-comp
      ( inclusion-double-negation-im f)
      ( map-unit-double-negation-im f)
      ( is-emb-inclusion-double-negation-im f)
      ( is-emb-is-equiv
        ( is-equiv-is-split-surjective-is-injective
          ( map-unit-double-negation-im f)
          ( is-injective-right-factor
            ( inclusion-double-negation-im f)
            ( map-unit-double-negation-im f) H)
          ( Î» x â†’
            pr1 (K (pr1 x) (pr2 x)) ,
            is-injective-inclusion-double-negation-im f
              ( pr2 (K (pr1 x) (pr2 x))))))
```
