# Epimorphisms with respect to maps into sets

```agda
module foundation.epimorphisms-with-respect-to-sets where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.epimorphisms-with-respect-to-truncated-types
open import foundation.existential-quantification
open import foundation.function-extensionality
open import foundation.propositional-extensionality
open import foundation.propositional-truncations
open import foundation.sets
open import foundation.surjective-maps
open import foundation.unit-type
open import foundation.universe-levels

open import foundation-core.equivalences
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.propositions
open import foundation-core.truncation-levels
open import foundation-core.univalence
```

</details>

## Idea

An epimorphism with respect to maps into sets are maps `f : A â†’ B` suc that for
every set `C` the precomposition function `(B â†’ C) â†’ (A â†’ C)` is an embedding.

## Definition

```agda
is-epimorphism-Set :
  {l1 l2 : Level} (l : Level) {A : UU l1} {B : UU l2}
  (f : A â†’ B) â†’ UU (l1 âŠ” l2 âŠ” lsuc l)
is-epimorphism-Set l f =
  is-epimorphism-Truncated-Type l zero-ğ•‹ f
```

## Properties

### Surjective maps are epimorphisms with respect to maps into sets

```agda
abstract
  is-epimorphism-is-surjective-Set :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-surjective f â†’ {l : Level} â†’ is-epimorphism-Set l f
  is-epimorphism-is-surjective-Set H C =
    is-emb-is-injective
      ( is-set-function-type (is-set-type-Set C))
      ( Î» {g} {h} p â†’
        eq-htpy (Î» b â†’
          apply-universal-property-trunc-Prop
            ( H b)
            ( Id-Prop C (g b) (h b))
            ( Î» u â†’
              ( inv (ap g (pr2 u))) âˆ™
              ( ( htpy-eq p (pr1 u)) âˆ™
                ( ap h (pr2 u))))))
```

### Maps that are epimorphisms with respect to maps into sets are surjective

```agda
abstract
  is-surjective-is-epimorphism-Set :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    ({l : Level} â†’ is-epimorphism-Set l f) â†’ is-surjective f
  is-surjective-is-epimorphism-Set {l1} {l2} {A} {B} {f} H b =
    map-equiv
      ( equiv-eq
        ( ap
          ( pr1)
          ( htpy-eq
            ( is-injective-is-emb
              ( H (Prop-Set (l1 âŠ” l2)))
              { g}
              { h}
              ( eq-htpy
                ( Î» a â†’
                  eq-iff
                    ( Î» _ â†’ unit-trunc-Prop (pair a refl))
                    ( Î» _ â†’ raise-star))))
            ( b))))
      ( raise-star)
    where
    g : B â†’ Prop (l1 âŠ” l2)
    g y = raise-unit-Prop (l1 âŠ” l2)
    h : B â†’ Prop (l1 âŠ” l2)
    h y = âˆƒ-Prop A (Î» x â†’ f x ï¼ y)
```
