# Decidable subtypes

```agda
module foundation.decidable-subtypes where
```

<details><summary>Imports</summary>

```agda
open import foundation.1-types
open import foundation.booleans
open import foundation.coproduct-types
open import foundation.decidable-embeddings
open import foundation.decidable-maps
open import foundation.decidable-propositions
open import foundation.decidable-types
open import foundation.dependent-pair-types
open import foundation.equality-dependent-function-types
open import foundation.functoriality-cartesian-product-types
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.logical-equivalences
open import foundation.postcomposition-functions
open import foundation.propositional-maps
open import foundation.sets
open import foundation.structured-type-duality
open import foundation.subtypes
open import foundation.type-theoretic-principle-of-choice
open import foundation.universe-levels

open import foundation-core.embeddings
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.propositions
open import foundation-core.transport-along-identifications
open import foundation-core.truncated-types
open import foundation-core.truncation-levels

open import logic.double-negation-stable-subtypes
```

</details>

## Idea

A
{{#concept "decidable subtype" Disambiguation="of a type" Agda=is-decidable-subtype Agda=decidable-subtype}}
of a type consists of a family of
[decidable propositions](foundation-core.decidable-propositions.md) over it.

## Definitions

### Decidable subtypes

```agda
is-decidable-subtype-Prop :
  {l1 l2 : Level} {A : UU l1} ‚Üí subtype l2 A ‚Üí Prop (l1 ‚äî l2)
is-decidable-subtype-Prop {A = A} P =
  Œ†-Prop A (Œª a ‚Üí is-decidable-Prop (P a))

is-decidable-subtype : {l1 l2 : Level} {A : UU l1} ‚Üí subtype l2 A ‚Üí UU (l1 ‚äî l2)
is-decidable-subtype P = type-Prop (is-decidable-subtype-Prop P)

is-prop-is-decidable-subtype :
  {l1 l2 : Level} {A : UU l1} (P : subtype l2 A) ‚Üí
  is-prop (is-decidable-subtype P)
is-prop-is-decidable-subtype P = is-prop-type-Prop (is-decidable-subtype-Prop P)

decidable-subtype : {l1 : Level} (l : Level) (X : UU l1) ‚Üí UU (l1 ‚äî lsuc l)
decidable-subtype l X = X ‚Üí Decidable-Prop l
```

### The underlying subtype of a decidable subtype

```agda
module _
  {l1 l2 : Level} {A : UU l1} (P : decidable-subtype l2 A)
  where

  subtype-decidable-subtype : subtype l2 A
  subtype-decidable-subtype a = prop-Decidable-Prop (P a)

  is-decidable-decidable-subtype :
    is-decidable-subtype subtype-decidable-subtype
  is-decidable-decidable-subtype a =
    is-decidable-Decidable-Prop (P a)

  is-in-decidable-subtype : A ‚Üí UU l2
  is-in-decidable-subtype = is-in-subtype subtype-decidable-subtype

  is-prop-is-in-decidable-subtype :
    (a : A) ‚Üí is-prop (is-in-decidable-subtype a)
  is-prop-is-in-decidable-subtype =
    is-prop-is-in-subtype subtype-decidable-subtype
```

### The underlying type of a decidable subtype

```agda
module _
  {l1 l2 : Level} {A : UU l1} (P : decidable-subtype l2 A)
  where

  type-decidable-subtype : UU (l1 ‚äî l2)
  type-decidable-subtype = type-subtype (subtype-decidable-subtype P)

  inclusion-decidable-subtype : type-decidable-subtype ‚Üí A
  inclusion-decidable-subtype = inclusion-subtype (subtype-decidable-subtype P)

  is-emb-inclusion-decidable-subtype : is-emb inclusion-decidable-subtype
  is-emb-inclusion-decidable-subtype =
    is-emb-inclusion-subtype (subtype-decidable-subtype P)

  is-decidable-map-inclusion-decidable-subtype :
    is-decidable-map inclusion-decidable-subtype
  is-decidable-map-inclusion-decidable-subtype x =
    is-decidable-equiv
      ( equiv-fiber-pr1 (type-Decidable-Prop ‚àò P) x)
      ( is-decidable-Decidable-Prop (P x))

  is-injective-inclusion-decidable-subtype :
    is-injective inclusion-decidable-subtype
  is-injective-inclusion-decidable-subtype =
    is-injective-inclusion-subtype (subtype-decidable-subtype P)

  emb-decidable-subtype : type-decidable-subtype ‚Ü™ A
  emb-decidable-subtype = emb-subtype (subtype-decidable-subtype P)

  is-decidable-emb-inclusion-decidable-subtype :
    is-decidable-emb inclusion-decidable-subtype
  is-decidable-emb-inclusion-decidable-subtype =
    ( is-emb-inclusion-decidable-subtype ,
      is-decidable-map-inclusion-decidable-subtype)

  decidable-emb-decidable-subtype : type-decidable-subtype ‚Ü™·µà A
  decidable-emb-decidable-subtype =
    ( inclusion-decidable-subtype ,
      is-decidable-emb-inclusion-decidable-subtype)
```

### The decidable subtype associated to a decidable embedding

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X ‚Ü™·µà Y)
  where

  decidable-subtype-decidable-emb : decidable-subtype (l1 ‚äî l2) Y
  pr1 (decidable-subtype-decidable-emb y) =
    fiber (map-decidable-emb f) y
  pr2 (decidable-subtype-decidable-emb y) =
    is-decidable-prop-map-is-decidable-emb
      ( is-decidable-emb-map-decidable-emb f)
      ( y)

  compute-type-decidable-type-decidable-emb :
    type-decidable-subtype decidable-subtype-decidable-emb ‚âÉ X
  compute-type-decidable-type-decidable-emb =
    equiv-total-fiber (map-decidable-emb f)

  inv-compute-type-decidable-type-decidable-emb :
    X ‚âÉ type-decidable-subtype decidable-subtype-decidable-emb
  inv-compute-type-decidable-type-decidable-emb =
    inv-equiv-total-fiber (map-decidable-emb f)
```

## Examples

### The decidable subtypes of left and right elements in a coproduct type

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-decidable-is-left : (x : A + B) ‚Üí is-decidable (is-left x)
  is-decidable-is-left (inl x) = is-decidable-unit
  is-decidable-is-left (inr x) = is-decidable-empty

  is-left-Decidable-Prop : A + B ‚Üí Decidable-Prop lzero
  pr1 (is-left-Decidable-Prop x) = is-left x
  pr1 (pr2 (is-left-Decidable-Prop x)) = is-prop-is-left x
  pr2 (pr2 (is-left-Decidable-Prop x)) = is-decidable-is-left x

  is-decidable-is-right : (x : A + B) ‚Üí is-decidable (is-right x)
  is-decidable-is-right (inl x) = is-decidable-empty
  is-decidable-is-right (inr x) = is-decidable-unit

  is-right-Decidable-Prop : A + B ‚Üí Decidable-Prop lzero
  pr1 (is-right-Decidable-Prop x) = is-right x
  pr1 (pr2 (is-right-Decidable-Prop x)) = is-prop-is-right x
  pr2 (pr2 (is-right-Decidable-Prop x)) = is-decidable-is-right x
```

## Properties

### Types of decidable subtypes of any universe level are equivalent

```agda
module _
  {l1 : Level} (X : UU l1)
  where

  equiv-universes-decidable-subtype :
    (l l' : Level) ‚Üí decidable-subtype l X ‚âÉ decidable-subtype l' X
  equiv-universes-decidable-subtype l l' =
    equiv-Œ†
      ( Œª _ ‚Üí Decidable-Prop l')
      ( id-equiv)
      ( Œª _ ‚Üí equiv-universes-Decidable-Prop l l')

  iff-universes-decidable-subtype :
    (l l' : Level) (S : decidable-subtype l X) ‚Üí
    ( (x : X) ‚Üí
      type-Decidable-Prop (S x) ‚Üî
      type-Decidable-Prop
        ( map-equiv (equiv-universes-decidable-subtype l l') S x))
  iff-universes-decidable-subtype l l' S x =
    tr
      ( Œª P ‚Üí type-Decidable-Prop (S x) ‚Üî type-Decidable-Prop P)
      ( inv
        ( compute-map-equiv-Œ†
          ( Œª _ ‚Üí Decidable-Prop l')
          ( id-equiv)
          ( Œª _ ‚Üí equiv-universes-Decidable-Prop l l')
          ( S)
          ( x)))
      ( iff-universes-Decidable-Prop l l' (S x))
```

### Decidable subtypes are double negation stable

```agda
is-double-negation-stable-decidable-subtype :
  {l1 l2 : Level} {A : UU l1} (P : decidable-subtype l2 A) ‚Üí
  is-double-negation-stable-subtype (subtype-decidable-subtype P)
is-double-negation-stable-decidable-subtype P x =
  double-negation-elim-is-decidable (is-decidable-decidable-subtype P x)
```

### A decidable subtype of a `k+1`-truncated type is `k+1`-truncated

```agda
module _
  {l1 l2 : Level} (k : ùïã) {A : UU l1} (P : decidable-subtype l2 A)
  where

  abstract
    is-trunc-type-decidable-subtype :
      is-trunc (succ-ùïã k) A ‚Üí is-trunc (succ-ùïã k) (type-decidable-subtype P)
    is-trunc-type-decidable-subtype =
      is-trunc-type-subtype k (subtype-decidable-subtype P)

module _
  {l1 l2 : Level} {A : UU l1} (P : decidable-subtype l2 A)
  where

  abstract
    is-prop-type-decidable-subtype :
      is-prop A ‚Üí is-prop (type-decidable-subtype P)
    is-prop-type-decidable-subtype =
      is-prop-type-subtype (subtype-decidable-subtype P)

  abstract
    is-set-type-decidable-subtype : is-set A ‚Üí is-set (type-decidable-subtype P)
    is-set-type-decidable-subtype =
      is-set-type-subtype (subtype-decidable-subtype P)

  abstract
    is-1-type-type-decidable-subtype :
      is-1-type A ‚Üí is-1-type (type-decidable-subtype P)
    is-1-type-type-decidable-subtype =
      is-1-type-type-subtype (subtype-decidable-subtype P)

prop-decidable-subprop :
  {l1 l2 : Level} (A : Prop l1) (P : decidable-subtype l2 (type-Prop A)) ‚Üí
  Prop (l1 ‚äî l2)
prop-decidable-subprop A P = prop-subprop A (subtype-decidable-subtype P)

set-decidable-subset :
  {l1 l2 : Level} (A : Set l1) (P : decidable-subtype l2 (type-Set A)) ‚Üí
  Set (l1 ‚äî l2)
set-decidable-subset A P = set-subset A (subtype-decidable-subtype P)
```

### The type of decidable subtypes of a type is a set

```agda
is-set-decidable-subtype :
  {l1 l2 : Level} {X : UU l1} ‚Üí is-set (decidable-subtype l2 X)
is-set-decidable-subtype =
  is-set-function-type is-set-Decidable-Prop
```

### Extensionality of the type of decidable subtypes

```agda
module _
  {l1 l2 : Level} {A : UU l1} (P : decidable-subtype l2 A)
  where

  has-same-elements-decidable-subtype :
    {l3 : Level} ‚Üí decidable-subtype l3 A ‚Üí UU (l1 ‚äî l2 ‚äî l3)
  has-same-elements-decidable-subtype Q =
    has-same-elements-subtype
      ( subtype-decidable-subtype P)
      ( subtype-decidable-subtype Q)

  extensionality-decidable-subtype :
    (Q : decidable-subtype l2 A) ‚Üí
    (P Ôºù Q) ‚âÉ has-same-elements-decidable-subtype Q
  extensionality-decidable-subtype =
    extensionality-Œ† P
      ( Œª x Q ‚Üí (type-Decidable-Prop (P x)) ‚Üî (type-Decidable-Prop Q))
      ( Œª x Q ‚Üí extensionality-Decidable-Prop (P x) Q)

  has-same-elements-eq-decidable-subtype :
    (Q : decidable-subtype l2 A) ‚Üí
    (P Ôºù Q) ‚Üí has-same-elements-decidable-subtype Q
  has-same-elements-eq-decidable-subtype Q =
    map-equiv (extensionality-decidable-subtype Q)

  eq-has-same-elements-decidable-subtype :
    (Q : decidable-subtype l2 A) ‚Üí
    has-same-elements-decidable-subtype Q ‚Üí P Ôºù Q
  eq-has-same-elements-decidable-subtype Q =
    map-inv-equiv (extensionality-decidable-subtype Q)

  refl-extensionality-decidable-subtype :
    map-equiv (extensionality-decidable-subtype P) refl Ôºù (Œª x ‚Üí pair id id)
  refl-extensionality-decidable-subtype = refl
```

### The type of decidable subtypes of `A` is equivalent to the type of all decidable embeddings into a type `A`

```agda
equiv-Fiber-Decidable-Prop :
  (l : Level) {l1 : Level} (A : UU l1) ‚Üí
  Œ£ (UU (l1 ‚äî l)) (Œª X ‚Üí X ‚Ü™·µà A) ‚âÉ (decidable-subtype (l1 ‚äî l) A)
equiv-Fiber-Decidable-Prop l A =
  ( equiv-Fiber-structure l is-decidable-prop A) ‚àòe
  ( equiv-tot
    ( Œª X ‚Üí
      equiv-tot
        ( Œª f ‚Üí
          ( inv-distributive-Œ†-Œ£) ‚àòe
          ( equiv-product-left (equiv-is-prop-map-is-emb f)))))
```

### The type of decidable subtypes of `A` is equivalent to the type of all maps from `A` to booleans

```agda
equiv-decidable-subtype-map-bool :
  {l1 l2 : Level} ‚Üí (A : UU l1) ‚Üí decidable-subtype l2 A ‚âÉ (A ‚Üí bool)
equiv-decidable-subtype-map-bool A = equiv-postcomp A equiv-bool-Decidable-Prop
```
