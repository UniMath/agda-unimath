# The universal property of identity types

```agda
module foundation.universal-property-identity-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.dependent-universal-property-equivalences
open import foundation.embeddings
open import foundation.equivalences
open import foundation.full-subtypes
open import foundation.function-extensionality
open import foundation.functoriality-dependent-function-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.identity-types
open import foundation.preunivalence
open import foundation.univalence
open import foundation.universe-levels

open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation-core.families-of-equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.injective-maps
open import foundation-core.propositional-maps
open import foundation-core.propositions
open import foundation-core.torsorial-type-families
```

</details>

## Idea

The **universal property of identity types** characterizes families of maps out
of the [identity type](foundation-core.identity-types.md). This universal
property is also known as the **type theoretic Yoneda lemma**.

## Theorem

```agda
ev-refl :
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) â†’ a ï¼ x â†’ UU l2} â†’
  ((x : A) (p : a ï¼ x) â†’ B x p) â†’ B a refl
ev-refl a f = f a refl

ev-refl' :
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) â†’ x ï¼ a â†’ UU l2} â†’
  ((x : A) (p : x ï¼ a) â†’ B x p) â†’ B a refl
ev-refl' a f = f a refl

abstract
  is-equiv-ev-refl :
    {l1 l2 : Level} {A : UU l1} (a : A)
    {B : (x : A) â†’ a ï¼ x â†’ UU l2} â†’ is-equiv (ev-refl a {B})
  is-equiv-ev-refl a =
    is-equiv-is-invertible
      ( ind-Id a _)
      ( Î» b â†’ refl)
      ( Î» f â†’ eq-htpy
        ( Î» x â†’ eq-htpy
          ( ind-Id a
            ( Î» x' p' â†’ ind-Id a _ (f a refl) x' p' ï¼ f x' p')
            ( refl) x)))

equiv-ev-refl :
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) â†’ a ï¼ x â†’ UU l2} â†’
  ((x : A) (p : a ï¼ x) â†’ B x p) â‰ƒ (B a refl)
pr1 (equiv-ev-refl a) = ev-refl a
pr2 (equiv-ev-refl a) = is-equiv-ev-refl a

equiv-ev-refl' :
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) â†’ x ï¼ a â†’ UU l2} â†’
  ((x : A) (p : x ï¼ a) â†’ B x p) â‰ƒ B a refl
equiv-ev-refl' a {B} =
  ( equiv-ev-refl a) âˆ˜e
  ( equiv-Î -equiv-family (Î» x â†’ equiv-precomp-Î  (equiv-inv a x) (B x)))

is-equiv-ev-refl' :
  {l1 l2 : Level} {A : UU l1} (a : A)
  {B : (x : A) â†’ x ï¼ a â†’ UU l2} â†’ is-equiv (ev-refl' a {B})
is-equiv-ev-refl' a = is-equiv-map-equiv (equiv-ev-refl' a)
```

### The type of fiberwise maps from `Id a` to a torsorial type family `B` is equivalent to the type of fiberwise equivalences

Note that the type of fiberwise equivalences is a
[subtype](foundation-core.subtypes.md) of the type of fiberwise maps. By the
[fundamental theorem of identity types](foundation.fundamental-theorem-of-identity-types.md),
it is a [full subtype](foundation.full-subtypes.md), hence it is equivalent to
the whole type of fiberwise maps.

```agda
module _
  {l1 l2 : Level} {A : UU l1} (a : A) {B : A â†’ UU l2}
  (is-torsorial-B : is-torsorial B)
  where

  equiv-fam-map-fam-equiv-is-torsorial :
    ((x : A) â†’ (a ï¼ x) â‰ƒ B x) â‰ƒ ((x : A) â†’ (a ï¼ x) â†’ B x)
  equiv-fam-map-fam-equiv-is-torsorial =
    ( equiv-inclusion-is-full-subtype
      ( Î» h â†’ Î -Prop A (Î» a â†’ is-equiv-Prop (h a)))
      ( fundamental-theorem-id is-torsorial-B)) âˆ˜e
    ( equiv-fiberwise-equiv-fam-equiv _ _)
```

### `Id : A â†’ (A â†’ ğ’°)` is an embedding

We first show that [the preunivalence axiom](foundation.preunivalence.md)
implies that the map `Id : A â†’ (A â†’ ğ’°)` is an
[embedding](foundation.embeddings.md). Since the
[univalence axiom](foundation.univalence.md) implies preunivalence, it follows
that `Id : A â†’ (A â†’ ğ’°)` is an embedding under the postulates of agda-unimath.

#### Preunivalence implies that `Id : A â†’ (A â†’ ğ’°)` is an embedding

The proof that preunivalence implies that `Id : A â†’ (A â†’ ğ’°)` is an embedding
proceeds via the
[fundamental theorem of identity types](foundation.fundamental-theorem-of-identity-types.md)
by showing that the [fiber](foundation.fibers-of-maps.md) of `Id` at `Id a` is
[contractible](foundation.contractible-types.md) for each `a : A`. To see this,
we first note that this fiber has an element `(a , refl)`. Therefore it suffices
to show that this fiber is a proposition. We do this by constructing an
embedding

```text
  fiber Id (Id a) â†ª Î£ A (Id a).
```

Since the codomain of this embedding is contractible, the claim follows. The
above embedding is constructed as the composite of the following embeddings

```text
  Î£ (x : A), Id x ï¼ Id a
    â†ª Î£ (x : A), (y : A) â†’ (x ï¼ y) ï¼ (a ï¼ y)
    â†ª Î£ (x : A), (y : A) â†’ (x ï¼ y) â‰ƒ (a ï¼ y)
    â†ª Î£ (x : A), Î£ (e : (y : A) â†’ (x ï¼ y) â†’ (a ï¼ y)), (y : A) â†’ is-equiv (e y)
    â†ª Î£ (x : A), (y : A) â†’ (x ï¼ y) â†’ (a ï¼ y)
    â†ª Î£ (x : A), a ï¼ x.
```

In this composite, we used preunivalence at the second step.

```agda
module _
  {l : Level} (A : UU l)
  (L : (a x y : A) â†’ instance-preunivalence (Id x y) (Id a y))
  where

  emb-fiber-Id-preunivalent-Id :
    (a : A) â†’ fiber' Id (Id a) â†ª Î£ A (Id a)
  emb-fiber-Id-preunivalent-Id a =
    comp-emb
      ( comp-emb
        ( emb-equiv
          ( equiv-tot
            ( Î» x â†’
              ( equiv-ev-refl x) âˆ˜e
              ( equiv-fam-map-fam-equiv-is-torsorial x (is-torsorial-Id a)))))
        ( emb-tot
          ( Î» x â†’
            comp-emb
              ( emb-Î  (Î» y â†’ _ , L a x y))
              ( emb-equiv equiv-funext))))
      ( emb-equiv (inv-equiv (equiv-fiber Id (Id a))))

  is-emb-Id-preunivalent-Id : is-emb (Id {A = A})
  is-emb-Id-preunivalent-Id a =
    fundamental-theorem-id
      ( ( a , refl) ,
        ( Î» _ â†’
          is-injective-emb
            ( emb-fiber-Id-preunivalent-Id a)
            ( eq-is-contr (is-torsorial-Id a))))
      ( Î» _ â†’ ap Id)

module _
  (L : preunivalence-axiom) {l : Level} (A : UU l)
  where

  is-emb-Id-preunivalence-axiom : is-emb (Id {A = A})
  is-emb-Id-preunivalence-axiom =
    is-emb-Id-preunivalent-Id A (Î» a x y â†’ L (Id x y) (Id a y))
```

#### `Id : A â†’ (A â†’ ğ’°)` is an embedding

```agda
module _
  {l : Level} (A : UU l)
  where

  is-emb-Id : is-emb (Id {A = A})
  is-emb-Id = is-emb-Id-preunivalence-axiom preunivalence A
```

#### For any type family `B` over `A`, the type of pairs `(a , e)` consisting of `a : A` and a family of equivalences `e : (x : A) â†’ (a ï¼ x) â‰ƒ B x` is a proposition

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  is-proof-irrelevant-total-family-of-equivalences-Id :
    is-proof-irrelevant (Î£ A (Î» a â†’ (x : A) â†’ (a ï¼ x) â‰ƒ B x))
  is-proof-irrelevant-total-family-of-equivalences-Id (a , e) =
    is-contr-equiv
      ( Î£ A (Î» b â†’ (x : A) â†’ (b ï¼ x) â‰ƒ (a ï¼ x)))
      ( equiv-tot
        ( Î» b â†’
          equiv-Î -equiv-family
            ( Î» x â†’ equiv-postcomp-equiv (inv-equiv (e x)) (b ï¼ x))))
      ( is-contr-equiv'
        ( fiber Id (Id a))
        ( equiv-tot
          ( Î» b â†’
            equiv-Î -equiv-family (Î» x â†’ equiv-univalence) âˆ˜e equiv-funext))
        ( is-proof-irrelevant-is-prop
          ( is-prop-map-is-emb (is-emb-Id A) (Id a))
          ( a , refl)))

  is-prop-total-family-of-equivalences-Id :
    is-prop (Î£ A (Î» a â†’ (x : A) â†’ (a ï¼ x) â‰ƒ B x))
  is-prop-total-family-of-equivalences-Id =
    is-prop-is-proof-irrelevant
      ( is-proof-irrelevant-total-family-of-equivalences-Id)
```

### The type of point-preserving fiberwise equivalences between `Id x` and a pointed torsorial type family is contractible

**Proof:** Since `ev-refl` is an equivalence, it follows that its fibers are
contractible. Explicitly, given a point `b : B a`, the type of maps
`h : (x : A) â†’ (a = x) â†’ B x` such that `h a refl = b` is contractible. But the
type of fiberwise maps is equivalent to the type of fiberwise equivalences.

```agda
module _
  {l1 l2 : Level} {A : UU l1} {a : A} {B : A â†’ UU l2} (b : B a)
  (is-torsorial-B : is-torsorial B)
  where

  abstract
    is-torsorial-pointed-fam-equiv-is-torsorial :
      is-torsorial
        ( Î» (e : (x : A) â†’ (a ï¼ x) â‰ƒ B x) â†’
          map-equiv (e a) refl ï¼ b)
    is-torsorial-pointed-fam-equiv-is-torsorial =
      is-contr-equiv'
        ( fiber (ev-refl a {B = Î» x _ â†’ B x}) b)
        ( equiv-Î£ _
          ( inv-equiv
            ( equiv-fam-map-fam-equiv-is-torsorial a is-torsorial-B))
          ( Î» h â†’
            equiv-inv-concat
              ( inv
                ( ap
                  ( ev-refl a)
                  ( is-section-map-inv-equiv
                    ( equiv-fam-map-fam-equiv-is-torsorial a is-torsorial-B)
                    ( h))))
              ( b)))
        ( is-contr-map-is-equiv
          ( is-equiv-ev-refl a)
          ( b))
```

## See also

- In
  [`foundation.torsorial-type-families`](foundation.torsorial-type-families.md)
  we will show that the fiber of `Id : A â†’ (A â†’ ğ’°)` at `B : A â†’ ğ’°` is equivalent
  to `is-torsorial B`.

## References

- The fact that preunivalence, or axiom L, is sufficient to prove that
  `Id : A â†’ (A â†’ ğ’°)` is an embedding was first observed and formalized by MartÃ­n
  EscardÃ³,
  [https://www.cs.bham.ac.uk//~mhe/TypeTopology/UF.IdEmbedding.html](https://www.cs.bham.ac.uk//~mhe/TypeTopology/UF.IdEmbedding.html).

{{#bibliography}} {{#reference TypeTopology}}
