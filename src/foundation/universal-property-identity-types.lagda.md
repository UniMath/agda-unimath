# The universal property of identity types

```agda
module foundation.universal-property-identity-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.axiom-l
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.equivalences
open import foundation.full-subtypes
open import foundation.function-extensionality
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.identity-types
open import foundation.propositional-maps
open import foundation.type-theoretic-principle-of-choice
open import foundation.univalence
open import foundation.universe-levels

open import foundation-core.contractible-types
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.injective-maps
open import foundation-core.propositions
```

</details>

## Idea

The universal property of identity types characterizes families of maps out of
the identity type. This universal property is also known as the type theoretic
Yoneda lemma.

## Theorem

```agda
ev-refl :
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) â†’ a ï¼ x â†’ UU l2} â†’
  ((x : A) (p : a ï¼ x) â†’ B x p) â†’ B a refl
ev-refl a f = f a refl

abstract
  is-equiv-ev-refl :
    {l1 l2 : Level} {A : UU l1} (a : A)
    {B : (x : A) â†’ a ï¼ x â†’ UU l2} â†’ is-equiv (ev-refl a {B = B})
  is-equiv-ev-refl a =
    is-equiv-has-inverse
      ( ind-Id a _)
      ( Î» b â†’ refl)
      ( Î» f â†’ eq-htpy
        ( Î» x â†’ eq-htpy
          ( ind-Id a
            ( Î» x' p' â†’ ind-Id a _ (f a refl) x' p' ï¼ f x' p')
            ( refl) x)))

equiv-ev-refl :
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) â†’ a ï¼ x â†’ UU l2} â†’
  ((x : A) (p : a ï¼ x) â†’ B x p) â‰ƒ (B a refl)
pr1 (equiv-ev-refl a) = ev-refl a
pr2 (equiv-ev-refl a) = is-equiv-ev-refl a

equiv-ev-refl' :
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) â†’ x ï¼ a â†’ UU l2} â†’
  ((x : A) (p : x ï¼ a) â†’ B x p) â‰ƒ B a refl
equiv-ev-refl' a {B} =
  equiv-ev-refl a âˆ˜e equiv-map-Î  (Î» x â†’ equiv-precomp-Î  (equiv-inv a x) (B x))
```

### `Id : A â†’ (A â†’ ğ’°)` is an embedding

We first show that [axiom L](foundation.axiom-l.md) implies that the map
`Id : A â†’ (A â†’ ğ’°)` is an [embedding](foundation.embeddings.md). Since the
[univalence axiom](foundation.univalence.md) implies axiom L, it follows that
`Id : A â†’ (A â†’ ğ’°)` is an embedding under the postulates of agda-unimath.

#### Axiom L implies that `Id : A â†’ (A â†’ ğ’°)` is an embedding

The proof that axiom L implies that `Id : A â†’ (A â†’ ğ’°)` is an embedding proceeds
via the
[fundamental theorem of identity types](foundation.fundamental-theorem-of-identity-types.md)
by showing that the [fiber](foundation.fibers-of-maps.md) of `Id` at `Id a` is
[contractible](foundation.contractible-types.md) for each `a : A`. To see this,
we first note that this fiber has an element `(a , refl)`. Therefore it suffices
to show that this fiber is a proposition. We do this by constructing an
embedding

```text
  fib Id (Id a) â†ª Î£ A (Id a).
```

Since the codomain of this embedding is contractible, the claim follows. The
above embedding is constructed as the composite of the following embeddings

```text
  Î£ (x : A), Id x ï¼ Id a
    â†ª Î£ (x : A), (y : A) â†’ (x ï¼ y) ï¼ (a ï¼ y)
    â†ª Î£ (x : A), (y : A) â†’ (x ï¼ y) â‰ƒ (a ï¼ y)
    â†ª Î£ (x : A), Î£ (e : (y : A) â†’ (x ï¼ y) â†’ (a ï¼ y)), (y : A) â†’ is-equiv (e y)
    â†ª Î£ (x : A), (y : A) â†’ (x ï¼ y) â†’ (a ï¼ y)
    â†ª Î£ (x : A), a ï¼ x.
```

In this composite, we used axiom L at the second step.

```agda
module _
  {l : Level} (L : axiom-L l) (A : UU l)
  where

  is-emb-Id-axiom-L : is-emb (Id {A = A})
  is-emb-Id-axiom-L a =
    fundamental-theorem-id
      ( pair
        ( pair a refl)
        ( Î» _ â†’
          is-injective-emb
            ( emb-fib a)
            ( eq-is-contr (is-contr-total-path a))))
      ( Î» _ â†’ ap Id)
    where
    emb-fib : (a : A) â†’ fib' Id (Id a) â†ª Î£ A (Id a)
    emb-fib a =
      comp-emb
        ( comp-emb
          ( emb-equiv
            ( equiv-tot
              ( Î» x â†’
                ( equiv-ev-refl x) âˆ˜e
                ( ( equiv-inclusion-is-full-subtype
                    ( Î -Prop A âˆ˜ (is-equiv-Prop âˆ˜_))
                    ( fundamental-theorem-id (is-contr-total-path a))) âˆ˜e
                  ( distributive-Î -Î£)))))
          ( emb-Î£
            ( Î» x â†’ (y : A) â†’ Id x y â‰ƒ Id a y)
            ( id-emb)
            ( Î» x â†’
              comp-emb
                ( emb-Î  (Î» y â†’ emb-L L (Id x y) (Id a y)))
                ( emb-equiv equiv-funext))))
        ( emb-equiv (inv-equiv (equiv-fib Id (Id a))))
```

#### `Id : A â†’ (A â†’ ğ’°)` is an embedding

```agda
module _
  {l : Level} (A : UU l)
  where

  is-emb-Id : is-emb (Id {A = A})
  is-emb-Id = is-emb-Id-axiom-L (axiom-L-univalence univalence) A
```

#### For any type family `B` over `A`, the type of pairs `(a , e)` consisting of `a : A` and a family of equivalences `e : (x : A) â†’ (a ï¼ x) â‰ƒ B x` is a proposition

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  is-proof-irrelevant-total-family-of-equivalences-Id :
    is-proof-irrelevant (Î£ A (Î» a â†’ (x : A) â†’ (a ï¼ x) â‰ƒ B x))
  is-proof-irrelevant-total-family-of-equivalences-Id (a , e) =
    is-contr-equiv
      ( Î£ A (Î» b â†’ (x : A) â†’ (b ï¼ x) â‰ƒ (a ï¼ x)))
      ( equiv-tot
        ( Î» b â†’
          equiv-map-Î 
            ( Î» x â†’ equiv-postcomp-equiv (inv-equiv (e x)) (b ï¼ x))))
      ( is-contr-equiv'
        ( fib Id (Id a))
        ( equiv-tot
          ( Î» b â†’ equiv-map-Î  (Î» x â†’ equiv-univalence) âˆ˜e equiv-funext))
        ( is-proof-irrelevant-is-prop
          ( is-prop-map-is-emb (is-emb-Id A) (Id a))
          ( a , refl)))

  is-prop-total-family-of-equivalences-Id :
    is-prop (Î£ A (Î» a â†’ (x : A) â†’ (a ï¼ x) â‰ƒ B x))
  is-prop-total-family-of-equivalences-Id =
    is-prop-is-proof-irrelevant
      ( is-proof-irrelevant-total-family-of-equivalences-Id)
```

## See also

- In
  [`foundation.torsorial-type-families`](foundation.torsorial-type-families.md)
  we will show that the fiber of `Id : A â†’ (A â†’ ğ’°)`at`B : A â†’ ğ’°`is equivalent
  to`is-contr (Î£ A B)`.

## References

- The fact that axiom L is sufficient to prove that `Id : A â†’ (A â†’ ğ’°)` is an
  embedding was first observed and formalized by MartÃ­n EscardÃ³,
  [https://www.cs.bham.ac.uk//~mhe/TypeTopology/UF.IdEmbedding.html](https://www.cs.bham.ac.uk//~mhe/TypeTopology/UF.IdEmbedding.html).
