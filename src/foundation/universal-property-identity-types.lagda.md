# The universal property of identity types

```agda
module foundation.universal-property-identity-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.dependent-universal-property-equivalences
open import foundation.embeddings
open import foundation.equivalences
open import foundation.full-subtypes
open import foundation.function-extensionality
open import foundation.functoriality-dependent-function-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.identity-types
open import foundation.injective-maps
open import foundation.preunivalence
open import foundation.univalence
open import foundation.universe-levels

open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation-core.families-of-equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.propositional-maps
open import foundation-core.propositions
open import foundation-core.retractions
open import foundation-core.sections
open import foundation-core.torsorial-type-families
```

</details>

## Idea

The {{#concept "universal property of identity types" Agda=equiv-ev-refl}}
characterizes families of maps out of the
[identity type](foundation-core.identity-types.md). This universal property is
also known as the **type theoretic Yoneda lemma**.

## Theorem

```agda
module _
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) ‚Üí a Ôºù x ‚Üí UU l2}
  where

  ev-refl : ((x : A) (p : a Ôºù x) ‚Üí B x p) ‚Üí B a refl
  ev-refl f = f a refl

  is-retraction-ev-refl : is-retraction (ind-Id a B) ev-refl
  is-retraction-ev-refl = refl-htpy

  abstract
    is-section-ev-refl : is-section (ind-Id a B) ev-refl
    is-section-ev-refl f =
      eq-htpy
        ( Œª x ‚Üí
          eq-htpy
            ( ind-Id a
              ( Œª x' p' ‚Üí ind-Id a _ (f a refl) x' p' Ôºù f x' p')
              ( refl)
              ( x)))

  is-equiv-ev-refl : is-equiv ev-refl
  is-equiv-ev-refl =
    is-equiv-is-invertible (ind-Id a B) is-retraction-ev-refl is-section-ev-refl

  equiv-ev-refl : ((x : A) (p : a Ôºù x) ‚Üí B x p) ‚âÉ B a refl
  equiv-ev-refl = (ev-refl , is-equiv-ev-refl)

module _
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) ‚Üí x Ôºù a ‚Üí UU l2}
  where

  ev-refl' : ((x : A) (p : x Ôºù a) ‚Üí B x p) ‚Üí B a refl
  ev-refl' f = f a refl

  equiv-ev-refl' : ((x : A) (p : x Ôºù a) ‚Üí B x p) ‚âÉ B a refl
  equiv-ev-refl' =
    ( equiv-ev-refl a) ‚àòe
    ( equiv-Œ†-equiv-family (Œª x ‚Üí equiv-precomp-Œ† (equiv-inv a x) (B x)))

  is-equiv-ev-refl' : is-equiv ev-refl'
  is-equiv-ev-refl' = is-equiv-map-equiv equiv-ev-refl'
```

### The type of fiberwise maps from `Id a` to a torsorial type family `B` is equivalent to the type of fiberwise equivalences

Note that the type of fiberwise equivalences is a
[subtype](foundation-core.subtypes.md) of the type of fiberwise maps. By the
[fundamental theorem of identity types](foundation.fundamental-theorem-of-identity-types.md),
it is a [full subtype](foundation.full-subtypes.md), hence it is equivalent to
the whole type of fiberwise maps.

```agda
module _
  {l1 l2 : Level} {A : UU l1} (a : A) {B : A ‚Üí UU l2}
  (is-torsorial-B : is-torsorial B)
  where

  equiv-fam-map-fam-equiv-is-torsorial :
    ((x : A) ‚Üí (a Ôºù x) ‚âÉ B x) ‚âÉ ((x : A) ‚Üí (a Ôºù x) ‚Üí B x)
  equiv-fam-map-fam-equiv-is-torsorial =
    ( equiv-inclusion-is-full-subtype
      ( Œª h ‚Üí Œ†-Prop A (Œª a ‚Üí is-equiv-Prop (h a)))
      ( fundamental-theorem-id is-torsorial-B)) ‚àòe
    ( equiv-fiberwise-equiv-fam-equiv _ _)
```

### `Id : A ‚Üí (A ‚Üí ùí∞)` is an embedding

We first show that [injectivity](foundation-core.injective-maps.md) of the map

```text
  equiv-eq : {X Y : ùí∞} ‚Üí (X Ôºù Y) ‚Üí (X ‚âÉ Y)
```

for the identity types of `A` implies that the map `Id : A ‚Üí (A ‚Üí ùí∞)` is an
[embedding](foundation.embeddings.md), a result due to Mart√≠n Escard√≥
{{#cite TypeTopology}}. Since the [univalence axiom](foundation.univalence.md)
implies [the preunivalence axiom](foundation.preunivalence.md) implies
injectivity of `equiv-eq`, it follows that `Id : A ‚Üí (A ‚Üí ùí∞)` is an embedding
under the postulates of agda-unimath.

#### Injectivity of `equiv-eq` implies `Id : A ‚Üí (A ‚Üí ùí∞)` is an embedding

The proof that injectivity of `equiv-eq` implies that `Id : A ‚Üí (A ‚Üí ùí∞)` is an
embedding proceeds via the
[fundamental theorem of identity types](foundation.fundamental-theorem-of-identity-types.md)
by showing that the [fiber](foundation.fibers-of-maps.md) of `Id` at `Id a` is
[contractible](foundation.contractible-types.md) for each `a : A`. To see this,
we first note that this fiber has an element `(a , refl)`. Therefore it suffices
to show that this fiber is a proposition. We do this by constructing an
injection

```text
  fiber Id (Id a) ‚Ü£ Œ£ A (Id a).
```

Since the codomain of this injection is contractible, the claim follows. The
above injection is constructed as the following composite injection

```text
  Œ£ (x : A), Id a Ôºù Id x
  ‚âÉ Œ£ (x : A), Id x Ôºù Id a
  ‚âÉ Œ£ (x : A), ((y : A) ‚Üí (x Ôºù y) Ôºù (a Ôºù y))
  ‚Ü£ Œ£ (x : A), ((y : A) ‚Üí (x Ôºù y) ‚âÉ (a Ôºù y))
  ‚âÉ Œ£ (x : A), ((y : A) ‚Üí (x Ôºù y) ‚Üí (a Ôºù y))
  ‚âÉ Œ£ (x : A), a Ôºù x.
```

In this composite, the injectivity of `equiv-eq` is used in the third step.

```agda
module _
  {l : Level} (A : UU l)
  (L : (a x y : A) ‚Üí is-injective (equiv-eq {A = x Ôºù y} {B = a Ôºù y}))
  where

  injection-Id-is-injective-equiv-eq-Id :
    (a x : A) ‚Üí injection (Id a Ôºù Id x) (a Ôºù x)
  injection-Id-is-injective-equiv-eq-Id a x =
    comp-injection
      ( injection-equiv
        ( ( equiv-ev-refl x) ‚àòe
          ( equiv-fam-map-fam-equiv-is-torsorial x (is-torsorial-Id a))))
      ( comp-injection
        ( injection-Œ† (Œª y ‚Üí _ , L a x y))
        ( injection-equiv (equiv-funext ‚àòe equiv-inv (Id a) (Id x))))

  injection-fiber-Id-is-injective-equiv-eq-Id :
    (a : A) ‚Üí injection (fiber' Id (Id a)) (Œ£ A (Id a))
  injection-fiber-Id-is-injective-equiv-eq-Id a =
    injection-tot (injection-Id-is-injective-equiv-eq-Id a)

  is-emb-Id-is-injective-equiv-eq-Id : is-emb (Id {A = A})
  is-emb-Id-is-injective-equiv-eq-Id a =
    fundamental-theorem-id
      ( ( a , refl) ,
        ( Œª _ ‚Üí
          pr2
            ( injection-fiber-Id-is-injective-equiv-eq-Id a)
            ( eq-is-contr (is-torsorial-Id a))))
      ( Œª _ ‚Üí ap Id)
```

#### Preunivalence implies that `Id : A ‚Üí (A ‚Üí ùí∞)` is an embedding

Assuming preunivalence, then in particular `equiv-eq` is injective and so the
previous argument applies. However, in this case we do get a slightly stronger
result, since now the fiber inclusion

```text
  fiber Id (Id a) ‚Üí Œ£ A (Id a)
```

is a proper embedding.

```agda
module _
  {l : Level} (A : UU l)
  (L : (a x y : A) ‚Üí instance-preunivalence (x Ôºù y) (a Ôºù y))
  where

  emb-Id-is-injective-equiv-eq-Id : (a x : A) ‚Üí (Id a Ôºù Id x) ‚Ü™ (a Ôºù x)
  emb-Id-is-injective-equiv-eq-Id a x =
    comp-emb
      ( emb-equiv
        ( ( equiv-ev-refl x) ‚àòe
          ( equiv-fam-map-fam-equiv-is-torsorial x (is-torsorial-Id a))))
      ( comp-emb
        ( emb-Œ† (Œª y ‚Üí _ , L a x y))
        ( emb-equiv (equiv-funext ‚àòe equiv-inv (Id a) (Id x))))

  emb-fiber-Id-preunivalent-Id : (a : A) ‚Üí fiber' Id (Id a) ‚Ü™ Œ£ A (Id a)
  emb-fiber-Id-preunivalent-Id a =
    emb-tot (emb-Id-is-injective-equiv-eq-Id a)

  is-emb-Id-preunivalent-Id : is-emb (Id {A = A})
  is-emb-Id-preunivalent-Id =
    is-emb-Id-is-injective-equiv-eq-Id A
      ( Œª a x y ‚Üí is-injective-is-emb (L a x y))

module _
  (L : preunivalence-axiom) {l : Level} (A : UU l)
  where

  is-emb-Id-preunivalence-axiom : is-emb (Id {A = A})
  is-emb-Id-preunivalence-axiom =
    is-emb-Id-is-injective-equiv-eq-Id A
      ( Œª a x y ‚Üí is-injective-is-emb (L (x Ôºù y) (a Ôºù y)))
```

#### `Id : A ‚Üí (A ‚Üí ùí∞)` is an embedding

```agda
is-emb-Id : {l : Level} (A : UU l) ‚Üí is-emb (Id {A = A})
is-emb-Id = is-emb-Id-preunivalence-axiom preunivalence
```

### Characteriation of equality of `Id`

```agda
equiv-Id :
  {l : Level} {A : UU l} (a x : A) ‚Üí (Id a Ôºù Id x) ‚âÉ (a Ôºù x)
equiv-Id a x =
  ( equiv-ev-refl x) ‚àòe
  ( equiv-fam-map-fam-equiv-is-torsorial x (is-torsorial-Id a)) ‚àòe
  ( equiv-Œ†-equiv-family (Œª _ ‚Üí equiv-univalence)) ‚àòe
  ( equiv-funext) ‚àòe
  ( equiv-inv (Id a) (Id x))

equiv-fiber-Id : {l : Level} {A : UU l} (a : A) ‚Üí fiber' Id (Id a) ‚âÉ Œ£ A (Id a)
equiv-fiber-Id a = equiv-tot (equiv-Id a)
```

#### For any type family `B` over `A`, the type of pairs `(a , e)` consisting of `a : A` and a family of equivalences `e : (x : A) ‚Üí (a Ôºù x) ‚âÉ B x` is a proposition

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A ‚Üí UU l2}
  where

  is-proof-irrelevant-total-family-of-equivalences-Id :
    is-proof-irrelevant (Œ£ A (Œª a ‚Üí (x : A) ‚Üí (a Ôºù x) ‚âÉ B x))
  is-proof-irrelevant-total-family-of-equivalences-Id (a , e) =
    is-contr-equiv
      ( Œ£ A (Œª b ‚Üí (x : A) ‚Üí (b Ôºù x) ‚âÉ (a Ôºù x)))
      ( equiv-tot
        ( Œª b ‚Üí
          equiv-Œ†-equiv-family
            ( Œª x ‚Üí equiv-postcomp-equiv (inv-equiv (e x)) (b Ôºù x))))
      ( is-contr-equiv'
        ( fiber Id (Id a))
        ( equiv-tot
          ( Œª b ‚Üí
            equiv-Œ†-equiv-family (Œª x ‚Üí equiv-univalence) ‚àòe equiv-funext))
        ( is-proof-irrelevant-is-prop
          ( is-prop-map-is-emb (is-emb-Id A) (Id a))
          ( a , refl)))

  is-prop-total-family-of-equivalences-Id :
    is-prop (Œ£ A (Œª a ‚Üí (x : A) ‚Üí (a Ôºù x) ‚âÉ B x))
  is-prop-total-family-of-equivalences-Id =
    is-prop-is-proof-irrelevant
      ( is-proof-irrelevant-total-family-of-equivalences-Id)
```

### The type of point-preserving fiberwise equivalences between `Id x` and a pointed torsorial type family is contractible

**Proof:** Since `ev-refl` is an equivalence, it follows that its fibers are
contractible. Explicitly, given a point `b : B a`, the type of maps
`h : (x : A) ‚Üí (a = x) ‚Üí B x` such that `h a refl = b` is contractible. But the
type of fiberwise maps is equivalent to the type of fiberwise equivalences.

```agda
module _
  {l1 l2 : Level} {A : UU l1} {a : A} {B : A ‚Üí UU l2} (b : B a)
  (is-torsorial-B : is-torsorial B)
  where

  abstract
    is-torsorial-pointed-fam-equiv-is-torsorial :
      is-torsorial (Œª (e : (x : A) ‚Üí (a Ôºù x) ‚âÉ B x) ‚Üí map-equiv (e a) refl Ôºù b)
    is-torsorial-pointed-fam-equiv-is-torsorial =
      is-contr-equiv'
        ( fiber (ev-refl a) b)
        ( equiv-Œ£ _
          ( inv-equiv (equiv-fam-map-fam-equiv-is-torsorial a is-torsorial-B))
          ( Œª h ‚Üí
            equiv-inv-concat
              ( inv
                ( ap
                  ( ev-refl a)
                  ( is-section-map-inv-equiv
                    ( equiv-fam-map-fam-equiv-is-torsorial a is-torsorial-B)
                    ( h))))
              ( b)))
        ( is-contr-map-is-equiv (is-equiv-ev-refl a) b)
```

## See also

- In
  [`foundation.torsorial-type-families`](foundation.torsorial-type-families.md)
  we show that the fiber of `Id : A ‚Üí (A ‚Üí ùí∞)` at `B : A ‚Üí ùí∞` is equivalent to
  `is-torsorial B`.

## References

It was first observed and proved by [Evan Cavallo](https://ecavallo.net/) that
preunivalence, or Axiom L, is sufficient to deduce that `Id : A ‚Üí (A ‚Üí ùí∞)` is an
embedding. It was later observed and formalized by Mart√≠n Escard√≥ that assuming
the map `equiv-eq : (X Ôºù Y) ‚Üí (X ‚âÉ Y)` is injective is enough.
{{#cite TypeTopology}} Mart√≠n Escard√≥'s formalizations can be found here:
[https://www.cs.bham.ac.uk//~mhe/TypeTopology/UF.IdEmbedding.html](https://www.cs.bham.ac.uk//~mhe/TypeTopology/UF.IdEmbedding.html).

{{#bibliography}} {{#reference TypeTopology}} {{#reference Esc17YetAnother}}
