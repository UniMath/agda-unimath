# Functoriality of function types

```agda
module foundation.functoriality-function-types where

open import foundation-core.functoriality-function-types public
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.functoriality-dependent-function-types
open import foundation.sections
open import foundation.unit-type
open import foundation.universe-levels

open import foundation-core.commuting-squares-of-maps
open import foundation-core.constant-maps
open import foundation-core.embeddings
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.propositional-maps
open import foundation-core.truncated-maps
open import foundation-core.truncation-levels

open import synthetic-homotopy-theory.cocones-under-spans
```

</details>

## Properties

### Equivalent types have equivalent function types

```agda
module _
  { l1 l2 l3 l4 : Level}
  { A' : UU l1} {B' : UU l2} {A : UU l3} (B : UU l4)
  ( e : A' â‰ƒ A) (f : B' â‰ƒ B)
  where

  map-equiv-function-type : (A' â†’ B') â†’ (A â†’ B)
  map-equiv-function-type h = map-equiv f âˆ˜ (h âˆ˜ map-inv-equiv e)

  compute-map-equiv-function-type :
    (h : A' â†’ B') (x : A') â†’
    map-equiv-function-type h (map-equiv e x) ï¼ map-equiv f (h x)
  compute-map-equiv-function-type h x =
    ap (map-equiv f âˆ˜ h) (is-retraction-map-inv-equiv e x)

  is-equiv-map-equiv-function-type : is-equiv map-equiv-function-type
  is-equiv-map-equiv-function-type =
    is-equiv-comp
      ( precomp (map-equiv (inv-equiv e)) B)
      ( postcomp A' (map-equiv f))
      ( is-equiv-postcomp-equiv f A')
      ( is-equiv-precomp-equiv (inv-equiv e) B)

  equiv-function-type : (A' â†’ B') â‰ƒ (A â†’ B)
  pr1 equiv-function-type = map-equiv-function-type
  pr2 equiv-function-type = is-equiv-map-equiv-function-type
```

### Two maps being homotopic is equivalent to them being homotopic after pre- or postcomposition by an equivalence

```agda
module _
  { l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  where

  equiv-htpy-precomp-htpy :
    ( f g : B â†’ C) (e : A â‰ƒ B) â†’
    ( (f âˆ˜ map-equiv e) ~ (g âˆ˜ map-equiv e)) â‰ƒ
    ( f ~ g)
  equiv-htpy-precomp-htpy f g e =
    equiv-Î 
      ( eq-value f g)
      ( e)
      ( Î» a â†’ id-equiv)

  equiv-htpy-postcomp-htpy :
    ( e : B â‰ƒ C) (f g : A â†’ B) â†’
    ( f ~ g) â‰ƒ
    ( (map-equiv e âˆ˜ f) ~ (map-equiv e âˆ˜ g))
  equiv-htpy-postcomp-htpy e f g =
    equiv-Î -equiv-family
      ( Î» a â†’ equiv-ap e (f a) (g a))
```

### The fibers of `precomp`

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (X : UU l3)
  where

  compute-fiber-precomp :
    (g : B â†’ X) â†’
    fiber (precomp f X) (g âˆ˜ f) â‰ƒ
    Î£ (B â†’ X) (Î» h â†’ coherence-square-maps f f h g)
  compute-fiber-precomp g =
    equiv-tot ( Î» h â†’ equiv-funext) âˆ˜e
    equiv-fiber (precomp f X) (g âˆ˜ f)

  compute-total-fiber-precomp :
    Î£ (B â†’ X) (Î» g â†’ fiber (precomp f X) (g âˆ˜ f)) â‰ƒ cocone f f X
  compute-total-fiber-precomp =
    equiv-tot compute-fiber-precomp

  diagonal-into-fibers-precomp :
    (B â†’ X) â†’ Î£ (B â†’ X) (Î» g â†’ fiber (precomp f X) (g âˆ˜ f))
  diagonal-into-fibers-precomp = map-section-family (Î» g â†’ g , refl)
```

### A map is truncated iff postcomposition by it is truncated

```agda
module _
  {l1 l2 : Level} (k : ğ•‹) {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  is-trunc-map-postcomp-is-trunc-map :
    is-trunc-map k f â†’
    {l3 : Level} (A : UU l3) â†’ is-trunc-map k (postcomp A f)
  is-trunc-map-postcomp-is-trunc-map is-trunc-f A =
    is-trunc-map-map-Î -is-trunc-map' k
      ( const A unit star)
      ( const unit (X â†’ Y) f)
      ( const unit (is-trunc-map k f) is-trunc-f)

  is-trunc-map-is-trunc-map-postcomp :
    ({l3 : Level} (A : UU l3) â†’ is-trunc-map k (postcomp A f)) â†’
    is-trunc-map k f
  is-trunc-map-is-trunc-map-postcomp is-trunc-postcomp-f =
    is-trunc-map-is-trunc-map-map-Î ' k
      ( const unit (X â†’ Y) f)
      ( Î» {l} {J} Î± â†’ is-trunc-postcomp-f {l} J)
      ( star)

module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  is-emb-postcomp-is-emb :
    is-emb f â†’
    {l3 : Level} (A : UU l3) â†’ is-emb (postcomp A f)
  is-emb-postcomp-is-emb is-emb-f A =
    is-emb-is-prop-map
      ( is-trunc-map-postcomp-is-trunc-map neg-one-ğ•‹ f
        ( is-prop-map-is-emb is-emb-f)
        ( A))

  is-emb-is-emb-postcomp :
    ({l3 : Level} (A : UU l3) â†’ is-emb (postcomp A f)) â†’
    is-emb f
  is-emb-is-emb-postcomp is-emb-postcomp-f =
    is-emb-is-prop-map
      ( is-trunc-map-is-trunc-map-postcomp neg-one-ğ•‹ f
        ( is-prop-map-is-emb âˆ˜ is-emb-postcomp-f))

emb-postcomp :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (f : X â†ª Y) (A : UU l3) â†’
  (A â†’ X) â†ª (A â†’ Y)
pr1 (emb-postcomp f A) = postcomp A (map-emb f)
pr2 (emb-postcomp f A) = is-emb-postcomp-is-emb (map-emb f) (is-emb-map-emb f) A
```

## See also

- Functorial properties of dependent function types are recorded in
  [`foundation.functoriality-dependent-function-types`](foundation.functoriality-dependent-function-types.md).
- Arithmetical laws involving dependent function types are recorded in
  [`foundation.type-arithmetic-dependent-function-types`](foundation.type-arithmetic-dependent-function-types.md).
- Equality proofs in dependent function types are characterized in
  [`foundation.equality-dependent-function-types`](foundation.equality-dependent-function-types.md).
