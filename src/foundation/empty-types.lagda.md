# Empty types

```agda
{-# OPTIONS --without-K --exact-split --safe #-}

module foundation.empty-types where

open import foundation.dependent-pair-types using (pair; pr1; pr2)
open import foundation.embeddings using (is-emb; _â†ª_)
open import foundation.equivalences using
  ( is-equiv; is-equiv-has-inverse; _â‰ƒ_; inv-equiv; _âˆ˜e_)
open import foundation.functions using (_âˆ˜_; id)
open import foundation.homotopies using (_~_)
open import foundation.propositions using (is-prop; UU-Prop; is-trunc-is-prop)
open import foundation.raising-universe-levels using (raise; equiv-raise)
open import foundation.sets using (is-set; UU-Set)
open import foundation.truncated-types using
  ( is-trunc; UU-Truncated-Type)
open import foundation.truncation-levels using (ğ•‹; succ-ğ•‹)
open import foundation.universe-levels using (Level; lzero; UU)
```

## Idea

An empty type is a type with no elements. The (standard) empty type is introduces as an inductive type with no constructors. With the standard empty type available, we will say that a type is empty if it maps into the standard empty type.

## Definition

```agda
data empty : UU lzero where

ind-empty : {l : Level} {P : empty â†’ UU l} â†’ ((x : empty) â†’ P x)
ind-empty ()

ex-falso : {l : Level} {A : UU l} â†’ empty â†’ A
ex-falso = ind-empty

is-empty : {l : Level} â†’ UU l â†’ UU l
is-empty A = A â†’ empty

is-nonempty : {l : Level} â†’ UU l â†’ UU l
is-nonempty A = is-empty (is-empty A)
```

We raise the empty type to an arbitrary universe level

```agda
raise-empty : (l : Level) â†’ UU l
raise-empty l = raise l empty

equiv-raise-empty : (l : Level) â†’ empty â‰ƒ raise-empty l
equiv-raise-empty l = equiv-raise l empty
```

## Properties

### The map `ex-falso` is an embedding

```agda
module _
  {l : Level} {A : UU l}
  where
  
  abstract
    is-emb-ex-falso : is-emb (ex-falso {A = A})
    is-emb-ex-falso ()

  ex-falso-emb : empty â†ª A
  pr1 ex-falso-emb = ex-falso
  pr2 ex-falso-emb = is-emb-ex-falso
```

### Any map into an empty type is an equivalence

```agda
abstract
  is-equiv-is-empty :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    is-empty B â†’ is-equiv f
  is-equiv-is-empty f H =
    is-equiv-has-inverse
      ( ex-falso âˆ˜ H)
      ( Î» y â†’ ex-falso (H y))
      ( Î» x â†’ ex-falso (H (f x)))

abstract
  is-equiv-is-empty' :
    {l : Level} {A : UU l} (f : is-empty A) â†’ is-equiv f
  is-equiv-is-empty' f = is-equiv-is-empty f id

equiv-is-empty :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ is-empty A â†’ is-empty B â†’ A â‰ƒ B
equiv-is-empty f g =
  ( inv-equiv (pair g (is-equiv-is-empty g id))) âˆ˜e
  ( pair f (is-equiv-is-empty f id))
```

### The empty type is a proposition

```agda
abstract
  is-prop-empty : is-prop empty
  is-prop-empty ()

empty-Prop : UU-Prop lzero
pr1 empty-Prop = empty
pr2 empty-Prop = is-prop-empty
```

### The empty type is a set

```agda
is-set-empty : is-set empty
is-set-empty ()

empty-Set : UU-Set lzero
pr1 empty-Set = empty
pr2 empty-Set = is-set-empty
```

### The empty type is k-truncated for any k â‰¥ 1

```agda
abstract
  is-trunc-empty : (k : ğ•‹) â†’ is-trunc (succ-ğ•‹ k) empty
  is-trunc-empty k ()

empty-Truncated-Type : (k : ğ•‹) â†’ UU-Truncated-Type (succ-ğ•‹ k) lzero
pr1 (empty-Truncated-Type k) = empty
pr2 (empty-Truncated-Type k) = is-trunc-empty k

abstract
  is-trunc-is-empty :
    {l : Level} (k : ğ•‹) {A : UU l} â†’ is-empty A â†’ is-trunc (succ-ğ•‹ k) A
  is-trunc-is-empty k f = is-trunc-is-prop k (Î» x â†’ ex-falso (f x))
```
