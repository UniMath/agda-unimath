---
title: Functoriality of set truncation
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation.functoriality-set-truncation where

open import foundation.contractible-types using
  ( is-contr; center; eq-is-contr)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.embeddings using (is-emb; _â†ª_)
open import foundation.equivalences using
  ( map-inv-is-equiv; is-equiv; _â‰ƒ_; map-equiv; is-equiv-map-equiv;
    is-equiv-htpy-equiv)
open import foundation.fibers-of-maps using (fib)
open import foundation.function-extensionality using (htpy-eq)
open import foundation.functions using (_âˆ˜_; id)
open import foundation.functoriality-dependent-pair-types using (tot)
open import foundation.functoriality-truncation using
  ( unique-map-trunc; map-trunc; naturality-unit-trunc;
    htpy-uniqueness-map-trunc; id-map-trunc; comp-map-trunc;
    htpy-trunc; map-equiv-trunc; is-equiv-map-equiv-trunc; equiv-trunc)
open import foundation.homotopies using
  ( _~_; refl-htpy; _Â·l_; _âˆ™h_; _Â·r_; inv-htpy)
open import foundation.identity-types using (_ï¼_; ap; _âˆ™_; inv; refl)
open import foundation.images using
  ( im; inclusion-im; is-emb-inclusion-im; map-unit-im; triangle-unit-im;
    is-surjective-map-unit-im; unit-im; eq-Eq-im; im-Set)
open import foundation.injective-maps using
  ( is-injective; is-emb-is-injective; is-injective-is-emb)
open import foundation.propositional-truncations using
  ( apply-universal-property-trunc-Prop; trunc-Prop; unit-trunc-Prop)
open import foundation.propositions using (function-Prop)
open import foundation.set-truncations using
  ( type-trunc-Set; unit-trunc-Set; universal-property-trunc-Set; trunc-Set;
    dependent-universal-property-trunc-Set; map-equiv-trunc-Î£-Set;
    trunc-Î£-Set; equiv-trunc-Î£-Set; apply-effectiveness-unit-trunc-Set;
    apply-dependent-universal-property-trunc-Set'; is-surjective-unit-trunc-Set;
    is-set-type-trunc-Set; is-set-truncation-trunc-Set)
open import foundation.sets using (set-Prop; Id-Prop)
open import foundation.slice using
  ( hom-slice; equiv-slice; htpy-hom-slice; comp-hom-slice; hom-equiv-slice)
open import foundation.surjective-maps using (is-surjective)
open import foundation.truncation-levels using (zero-ğ•‹)
open import foundation.uniqueness-image using (uniqueness-im)
open import foundation.uniqueness-set-truncations using
  ( is-set-truncation-is-equiv-is-set-truncation)
open import foundation.universal-property-image using
  ( is-image; is-image-is-surjective)
open import foundation.universal-property-set-truncation using
  ( is-set-truncation)
open import foundation.universe-levels using (Level; UU)
```

## Idea

The universal property of the set truncation implies that the set truncation acts functorially on maps between types.

## Definition

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  abstract
    unique-map-trunc-Set :
      is-contr
        ( Î£ ( type-trunc-Set A â†’ type-trunc-Set B)
            ( Î» h â†’ (h âˆ˜ unit-trunc-Set) ~ (unit-trunc-Set âˆ˜ f)))
    unique-map-trunc-Set = unique-map-trunc zero-ğ•‹ f

  map-trunc-Set :
    type-trunc-Set A â†’ type-trunc-Set B
  map-trunc-Set = map-trunc zero-ğ•‹ f

  naturality-unit-trunc-Set :
    (map-trunc-Set âˆ˜ unit-trunc-Set) ~ (unit-trunc-Set âˆ˜ f)
  naturality-unit-trunc-Set = naturality-unit-trunc zero-ğ•‹ f

  htpy-uniqueness-map-trunc-Set :
    (h : type-trunc-Set A â†’ type-trunc-Set B) â†’
    (H : (h âˆ˜ unit-trunc-Set) ~ (unit-trunc-Set âˆ˜ f)) â†’
    map-trunc-Set ~ h
  htpy-uniqueness-map-trunc-Set = htpy-uniqueness-map-trunc zero-ğ•‹ f
```

## Properties

### The functorial action of set truncations preserves identity maps

```agda
id-map-trunc-Set :
  {l1 : Level} {A : UU l1} â†’ map-trunc-Set (id {A = A}) ~ id
id-map-trunc-Set = id-map-trunc zero-ğ•‹
```

### The functorial action of set truncations preserves composition

```agda
comp-map-trunc-Set :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  (g : B â†’ C) (f : A â†’ B) â†’
  map-trunc-Set (g âˆ˜ f) ~ (map-trunc-Set g âˆ˜ map-trunc-Set f)
comp-map-trunc-Set = comp-map-trunc zero-ğ•‹
```

### The functorial action of set truncations preserves homotopies

```
htpy-trunc-Set :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f g : A â†’ B} â†’
  (f ~ g) â†’ (map-trunc-Set f ~ map-trunc-Set g)
htpy-trunc-Set = htpy-trunc
```

### The functorial action of set truncations preserves equivalences

```agda
map-equiv-trunc-Set :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  (A â‰ƒ B) â†’ type-trunc-Set A â†’ type-trunc-Set B
map-equiv-trunc-Set = map-equiv-trunc zero-ğ•‹

is-equiv-map-trunc-Set :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  (e : A â‰ƒ B) â†’ is-equiv (map-equiv-trunc-Set e)
is-equiv-map-trunc-Set = is-equiv-map-equiv-trunc zero-ğ•‹

equiv-trunc-Set :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  (A â‰ƒ B) â†’ (type-trunc-Set A â‰ƒ type-trunc-Set B)
equiv-trunc-Set = equiv-trunc zero-ğ•‹
```

```agda
module _
  {l1 l2 : Level} (A : UU l1) (B : A â†’ UU l2)
  where

  square-trunc-Î£-Set :
    ( map-equiv-trunc-Î£-Set A B âˆ˜ unit-trunc-Set) ~
    ( unit-trunc-Set âˆ˜ tot (Î» x â†’ unit-trunc-Set))
  square-trunc-Î£-Set =
    pr2 (center (trunc-Î£-Set A B))

  htpy-map-equiv-trunc-Î£-Set :
    map-trunc-Set (tot (Î» x â†’ unit-trunc-Set)) ~ (map-equiv-trunc-Î£-Set A B)
  htpy-map-equiv-trunc-Î£-Set =
    htpy-uniqueness-map-trunc-Set
      ( tot (Î» x â†’ unit-trunc-Set))
      ( map-equiv-trunc-Î£-Set A B)
      ( square-trunc-Î£-Set)

  abstract
    is-equiv-map-trunc-tot-unit-trunc-Set :
      is-equiv (map-trunc-Set (tot (Î» (x : A) â†’ unit-trunc-Set {A = B x})))
    is-equiv-map-trunc-tot-unit-trunc-Set =
      is-equiv-htpy-equiv
        ( equiv-trunc-Î£-Set A B)
        ( htpy-map-equiv-trunc-Î£-Set)
```

### The set truncation functor preserves injective maps

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  abstract
    is-injective-map-trunc-Set :
      is-injective f â†’ is-injective (map-trunc-Set f)
    is-injective-map-trunc-Set H {x} {y} =
      apply-dependent-universal-property-trunc-Set'
        ( Î» u â†’
          set-Prop
            ( function-Prop (map-trunc-Set f u ï¼ map-trunc-Set f y)
            ( Id-Prop (trunc-Set A) u y) ))
        ( Î» a â†’
          apply-dependent-universal-property-trunc-Set'
          ( Î» v â†’
            set-Prop
              ( function-Prop
                ( map-trunc-Set f (unit-trunc-Set a) ï¼ map-trunc-Set f v)
                ( Id-Prop (trunc-Set A) (unit-trunc-Set a) v)))
          ( Î» b p â†’
            apply-universal-property-trunc-Prop
              ( apply-effectiveness-unit-trunc-Set
                ( ( inv (naturality-unit-trunc-Set f a)) âˆ™
                  ( p âˆ™ (naturality-unit-trunc-Set f b))))
              ( Id-Prop (trunc-Set A) (unit-trunc-Set a) (unit-trunc-Set b))
              ( Î» q â†’ ap unit-trunc-Set (H q)))
          ( y))
        ( x)
```

### The set truncation functor preserves surjective maps

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  abstract
    is-surjective-map-trunc-Set :
      is-surjective f â†’ is-surjective (map-trunc-Set f)
    is-surjective-map-trunc-Set H =
      apply-dependent-universal-property-trunc-Set'
        ( Î» x â†’ set-Prop (trunc-Prop (fib (map-trunc-Set f) x)))
        ( Î» b â†’
          apply-universal-property-trunc-Prop
            ( H b)
            ( trunc-Prop (fib (map-trunc-Set f) (unit-trunc-Set b)))
            ( Î» { (pair a p) â†’
                  unit-trunc-Prop
                    ( pair
                      ( unit-trunc-Set a)
                      ( naturality-unit-trunc-Set f a âˆ™ ap unit-trunc-Set p))}))
```

### If the set truncation of a map `f` is surjective, then `f` is surjective

```
  abstract
    is-surjective-is-surjective-map-trunc-Set :
      is-surjective (map-trunc-Set f) â†’ is-surjective f
    is-surjective-is-surjective-map-trunc-Set H b =
      apply-universal-property-trunc-Prop
        ( H (unit-trunc-Set b))
        ( trunc-Prop (fib f b))
        ( Î» { (pair x p) â†’
              apply-universal-property-trunc-Prop
                ( is-surjective-unit-trunc-Set A x)
                ( trunc-Prop (fib f b))
                ( Î» { (pair a refl) â†’
                      apply-universal-property-trunc-Prop
                        ( apply-effectiveness-unit-trunc-Set
                          ( inv (naturality-unit-trunc-Set f a) âˆ™ p))
                        ( trunc-Prop (fib f b))
                        ( Î» q â†’ unit-trunc-Prop (pair a q))})})
```

### Set truncation preserves the image of a map

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  inclusion-trunc-im-Set : type-trunc-Set (im f) â†’ type-trunc-Set B
  inclusion-trunc-im-Set = map-trunc-Set (inclusion-im f)

  abstract
    is-emb-inclusion-trunc-im-Set : is-emb inclusion-trunc-im-Set
    is-emb-inclusion-trunc-im-Set =
      is-emb-is-injective
        ( is-set-type-trunc-Set)
        ( is-injective-map-trunc-Set
          ( inclusion-im f)
          ( is-injective-is-emb (is-emb-inclusion-im f)))

  emb-trunc-im-Set : type-trunc-Set (im f) â†ª type-trunc-Set B
  emb-trunc-im-Set = pair inclusion-trunc-im-Set is-emb-inclusion-trunc-im-Set

  abstract
    is-injective-inclusion-trunc-im-Set : is-injective inclusion-trunc-im-Set
    is-injective-inclusion-trunc-im-Set =
      is-injective-is-emb is-emb-inclusion-trunc-im-Set

  map-hom-slice-trunc-im-Set : type-trunc-Set A â†’ type-trunc-Set (im f)
  map-hom-slice-trunc-im-Set = map-trunc-Set (map-unit-im f)

  triangle-hom-slice-trunc-im-Set :
    map-trunc-Set f ~ (inclusion-trunc-im-Set âˆ˜ map-trunc-Set (map-unit-im f))
  triangle-hom-slice-trunc-im-Set =
    ( htpy-trunc-Set (triangle-unit-im f)) âˆ™h
    ( comp-map-trunc-Set (inclusion-im f) (map-unit-im f))

  hom-slice-trunc-im-Set : hom-slice (map-trunc-Set f) inclusion-trunc-im-Set
  hom-slice-trunc-im-Set =
    pair map-hom-slice-trunc-im-Set triangle-hom-slice-trunc-im-Set

  abstract
    is-surjective-map-hom-slice-trunc-im-Set :
      is-surjective map-hom-slice-trunc-im-Set
    is-surjective-map-hom-slice-trunc-im-Set =
      is-surjective-map-trunc-Set
        ( map-unit-im f)
        ( is-surjective-map-unit-im f)

  abstract
    is-image-trunc-im-Set :
      {l : Level} â†’
      is-image l
        ( map-trunc-Set f)
        ( emb-trunc-im-Set)
        ( hom-slice-trunc-im-Set)
    is-image-trunc-im-Set =
      is-image-is-surjective
        ( map-trunc-Set f)
        ( emb-trunc-im-Set)
        ( hom-slice-trunc-im-Set)
        ( is-surjective-map-hom-slice-trunc-im-Set)

  abstract
    unique-equiv-trunc-im-Set :
      is-contr
        ( Î£ ( equiv-slice
              ( inclusion-im (map-trunc-Set f))
              ( inclusion-trunc-im-Set))
            ( Î» e â†’
              htpy-hom-slice (map-trunc-Set f)
                ( inclusion-trunc-im-Set)
                ( comp-hom-slice (map-trunc-Set f)
                  ( inclusion-im (map-trunc-Set f))
                  ( inclusion-trunc-im-Set)
                  ( hom-equiv-slice
                    ( inclusion-im (map-trunc-Set f))
                    ( inclusion-trunc-im-Set)
                    ( e))
                  ( unit-im (map-trunc-Set f)))
                ( hom-slice-trunc-im-Set)))
    unique-equiv-trunc-im-Set =
      uniqueness-im
        ( map-trunc-Set f)
        ( emb-trunc-im-Set)
        ( hom-slice-trunc-im-Set)
        ( is-image-trunc-im-Set)

  equiv-slice-trunc-im-Set :
    equiv-slice (inclusion-im (map-trunc-Set f)) inclusion-trunc-im-Set
  equiv-slice-trunc-im-Set = pr1 (center unique-equiv-trunc-im-Set)

  equiv-trunc-im-Set : im (map-trunc-Set f) â‰ƒ type-trunc-Set (im f)
  equiv-trunc-im-Set = pr1 equiv-slice-trunc-im-Set

  map-equiv-trunc-im-Set : im (map-trunc-Set f) â†’ type-trunc-Set (im f)
  map-equiv-trunc-im-Set = map-equiv equiv-trunc-im-Set

  triangle-trunc-im-Set :
    ( inclusion-im (map-trunc-Set f)) ~
    ( inclusion-trunc-im-Set âˆ˜ map-equiv-trunc-im-Set)
  triangle-trunc-im-Set = pr2 equiv-slice-trunc-im-Set

  htpy-hom-slice-trunc-im-Set :
    htpy-hom-slice
      ( map-trunc-Set f)
      ( inclusion-trunc-im-Set)
      ( comp-hom-slice
        ( map-trunc-Set f)
        ( inclusion-im (map-trunc-Set f))
        ( inclusion-trunc-im-Set)
        ( hom-equiv-slice
          ( inclusion-im (map-trunc-Set f))
          ( inclusion-trunc-im-Set)
          ( equiv-slice-trunc-im-Set))
        ( unit-im (map-trunc-Set f)))
      ( hom-slice-trunc-im-Set)
  htpy-hom-slice-trunc-im-Set =
    pr2 (center unique-equiv-trunc-im-Set)

  htpy-map-hom-slice-trunc-im-Set :
    ( map-equiv-trunc-im-Set âˆ˜ (map-unit-im (map-trunc-Set f))) ~
    ( map-hom-slice-trunc-im-Set)
  htpy-map-hom-slice-trunc-im-Set =
    pr1 htpy-hom-slice-trunc-im-Set

  tetrahedron-map-hom-slice-trunc-im-Set :
    ( ( triangle-trunc-im-Set Â·r map-unit-im (map-trunc-Set f)) âˆ™h
      ( inclusion-trunc-im-Set Â·l htpy-map-hom-slice-trunc-im-Set)) ~
    ( triangle-hom-slice-trunc-im-Set)
  tetrahedron-map-hom-slice-trunc-im-Set =
    pr2 htpy-hom-slice-trunc-im-Set

  unit-im-map-trunc-Set :
    im f â†’ im (map-trunc-Set f)
  unit-im-map-trunc-Set x =
    pair
      ( unit-trunc-Set (pr1 x))
      ( apply-universal-property-trunc-Prop (pr2 x)
        ( trunc-Prop (fib (map-trunc-Set f) (unit-trunc-Set (pr1 x))))
        ( Î» u â†’
          unit-trunc-Prop
            ( pair
              ( unit-trunc-Set (pr1 u))
              ( naturality-unit-trunc-Set f (pr1 u) âˆ™ ap unit-trunc-Set (pr2 u)))))

  left-square-unit-im-map-trunc-Set :
    ( map-unit-im (map-trunc-Set f) âˆ˜ unit-trunc-Set) ~
    ( unit-im-map-trunc-Set âˆ˜ map-unit-im f)
  left-square-unit-im-map-trunc-Set a =
    eq-Eq-im
      ( map-trunc-Set f)
      ( map-unit-im (map-trunc-Set f) (unit-trunc-Set a))
      ( unit-im-map-trunc-Set (map-unit-im f a))
      ( naturality-unit-trunc-Set f a)

  right-square-unit-im-map-trunc-Set :
    ( inclusion-im (map-trunc-Set f) âˆ˜ unit-im-map-trunc-Set) ~
    ( unit-trunc-Set âˆ˜ inclusion-im f)
  right-square-unit-im-map-trunc-Set x = refl

  abstract
    is-set-truncation-im-map-trunc-Set :
      {l : Level} â†’
      is-set-truncation l
        ( im-Set (trunc-Set B) (map-trunc-Set f))
        ( unit-im-map-trunc-Set)
    is-set-truncation-im-map-trunc-Set =
      is-set-truncation-is-equiv-is-set-truncation
        ( im-Set (trunc-Set B) (map-trunc-Set f))
        ( unit-im-map-trunc-Set)
        ( trunc-Set (im f))
        ( unit-trunc-Set)
        ( Î» b â†’
          is-injective-inclusion-trunc-im-Set
            ( ( inv (triangle-trunc-im-Set (unit-im-map-trunc-Set b))) âˆ™
              ( inv (naturality-unit-trunc-Set (inclusion-im f) b))))
        ( is-set-truncation-trunc-Set (im f))
        ( is-equiv-map-equiv equiv-trunc-im-Set)
```
