# Retractions

```agda
module foundation.retractions where

open import foundation-core.retractions public
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.morphisms-coslice
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.contractible-types
open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.propositions
open import foundation-core.retracts-of-types
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Properties

### Characterizing the identity type of `retraction f`

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B}
  where

  coherence-htpy-retraction :
    (g h : retraction f) â†’ map-retraction f g ~ map-retraction f h â†’ UU l1
  coherence-htpy-retraction = coherence-htpy-hom-coslice

  htpy-retraction : retraction f â†’ retraction f â†’ UU (l1 âŠ” l2)
  htpy-retraction = htpy-hom-coslice

  extensionality-retraction :
    (g h : retraction f) â†’ (g ï¼ h) â‰ƒ htpy-retraction g h
  extensionality-retraction = extensionality-hom-coslice

  eq-htpy-retraction :
    ( g h : retraction f)
    ( H : map-retraction f g ~ map-retraction f h)
    ( K :
      ( is-retraction-map-retraction f g) ~
      ( (H Â·r f) âˆ™h is-retraction-map-retraction f h)) â†’
    g ï¼ h
  eq-htpy-retraction = eq-htpy-hom-coslice
```

### If the left factor of a composite has a retraction, then the type of retractions of the right factor is a retract of the type of retractions of the composite

```agda
is-retraction-retraction-left-map-triangle :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B)
  (H : f ~ (g âˆ˜ h)) (rg : retraction g) â†’
  ( ( retraction-top-map-triangle f g h H) âˆ˜
    ( retraction-left-map-triangle f g h H rg)) ~ id
is-retraction-retraction-left-map-triangle f g h H (l , L) (k , K) =
  eq-htpy-retraction
    ( ( retraction-top-map-triangle f g h H
        ( retraction-left-map-triangle f g h H (l , L) (k , K))))
    ( k , K)
    ( k Â·l L)
    ( homotopy-reasoning
      (((k âˆ˜ l) Â·l inv-htpy H) âˆ™h ((k âˆ˜ l) Â·l H âˆ™h (k Â·l (L Â·r h) âˆ™h K)))
      ~ (inv-htpy ((k âˆ˜ l) Â·l H) âˆ™h ((k âˆ˜ l) Â·l H âˆ™h (k Â·l (L Â·r h) âˆ™h K)))
      by
        ap-concat-htpy'
          ( (k âˆ˜ l) Â·l H âˆ™h (k Â·l (L Â·r h) âˆ™h K))
          ( left-whisker-inv-htpy (k âˆ˜ l) H)
      ~ ((inv-htpy ((k âˆ˜ l) Â·l H) âˆ™h (k âˆ˜ l) Â·l H) âˆ™h (k Â·l (L Â·r h) âˆ™h K))
      by
      ( inv-htpy-assoc-htpy
        ( inv-htpy ((k âˆ˜ l) Â·l H))
        ( (k âˆ˜ l) Â·l H)
        ( (k Â·l (L Â·r h)) âˆ™h K))
      ~ (k Â·l L Â·r h âˆ™h is-retraction-map-retraction h (k , K))
      by
        ap-concat-htpy' ((k Â·l (L Â·r h)) âˆ™h K) (left-inv-htpy ((k âˆ˜ l) Â·l H)))

retraction-right-factor-retract-of-retraction-left-factor :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ g âˆ˜ h) â†’
  retraction g â†’ (retraction h) retract-of (retraction f)
pr1 (retraction-right-factor-retract-of-retraction-left-factor f g h H rg) =
  retraction-left-map-triangle f g h H rg
pr1
  ( pr2
    ( retraction-right-factor-retract-of-retraction-left-factor f g h H rg)) =
  retraction-top-map-triangle f g h H
pr2
  ( pr2
    ( retraction-right-factor-retract-of-retraction-left-factor f g h H rg)) =
  is-retraction-retraction-left-map-triangle f g h H rg
```

### The type of retractions is `k`-truncated if the domain is `k`-truncated

```agda
module _
  {l1 l2 : Level} {k : ğ•‹} {A : UU l1} {B : UU l2} {f : A â†’ B}
  where

  is-trunc-retraction : is-trunc k A â†’ is-trunc k (retraction f)
  is-trunc-retraction is-trunc-A =
    is-trunc-Î£
      ( is-trunc-function-type k is-trunc-A)
      ( Î» r â†’
        is-trunc-Î  k (Î» x â†’ is-trunc-succ-is-trunc k is-trunc-A (r (f x)) x))
```

### When the domain is contractible, the type of retractions is contractible

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B}
  where

  is-contr-retraction : is-contr A â†’ is-contr (retraction f)
  is-contr-retraction = is-trunc-retraction
```

### When the domain is a proposition, the type of retractions is a proposition

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B}
  where

  is-prop-retraction : is-prop A â†’ is-prop (retraction f)
  is-prop-retraction = is-trunc-retraction
```
