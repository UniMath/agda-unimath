# Extensional W-types

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation.extensional-W-types where

open import foundation.contractible-types using
  ( is-contr; is-contr-equiv; is-contr-equiv')
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.elementhood-relation-W-types using (_âˆˆ-ğ•_)
open import foundation.equality-dependent-function-types using
  ( is-contr-total-Eq-Î )
open import foundation.equivalences using
  ( _â‰ƒ_; id-equiv; map-equiv; is-contr-total-htpy-equiv; is-equiv;
    map-inv-is-equiv; map-inv-equiv; _âˆ˜e_; isretr-map-inv-equiv; inv-equiv;
    is-equiv-Prop)
open import foundation.functions using (_âˆ˜_)
open import foundation.functoriality-dependent-function-types using
  ( equiv-Î )
open import foundation.functoriality-dependent-pair-types using
  ( equiv-tot; equiv-Î£)
open import foundation.fundamental-theorem-of-identity-types using
  ( fundamental-theorem-id; fundamental-theorem-id')
open import foundation.homotopies using (_~_; refl-htpy; is-contr-total-htpy)
open import foundation.identity-types using
  ( Id; equiv-concat; ap; equiv-tr; refl)
open import foundation.propositional-truncations using
  ( type-trunc-Prop; apply-universal-property-trunc-Prop)
open import foundation.propositions using (Î -Prop)
open import foundation.slice using (equiv-fam-equiv-equiv-slice)
open import foundation.type-arithmetic-dependent-pair-types using
  ( right-unit-law-Î£-is-contr; equiv-left-swap-Î£; assoc-Î£)
open import foundation.univalent-type-families using (is-univalent)
open import foundation.universe-levels using (Level; UU; _âŠ”_)
open import foundation.W-types using
  ( ğ•; tree-ğ•; symbol-ğ•; inv-equiv-structure-ğ•-Alg)
```

## Idea

A W-type `ğ• A B` is said to be extensional if for any two elements `S T : ğ• A B` the induced map

```md
  Id S T â†’ ((U : ğ• A B) â†’ (U âˆˆ-ğ• S) â‰ƒ (U âˆˆ-ğ• T))
```

is an equivalence.

## Definition

### Extensional equality on W-types

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  extensional-Eq-eq-ğ• : 
    {x y : ğ• A B} â†’ Id x y â†’ (z : ğ• A B) â†’ (z âˆˆ-ğ• x) â‰ƒ (z âˆˆ-ğ• y)
  extensional-Eq-eq-ğ• refl z = id-equiv

is-extensional-ğ• :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ UU l2) â†’ UU (l1 âŠ” l2)
is-extensional-ğ• A B =
  (x y : ğ• A B) â†’ is-equiv (extensional-Eq-eq-ğ• {x = x} {y})
  
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  Eq-ext-ğ• : ğ• A B â†’ ğ• A B â†’ UU (l1 âŠ” l2)
  Eq-ext-ğ• x y = (z : ğ• A B) â†’ (z âˆˆ-ğ• x) â‰ƒ (z âˆˆ-ğ• y)

  refl-Eq-ext-ğ• : (x : ğ• A B) â†’ Eq-ext-ğ• x x
  refl-Eq-ext-ğ• x z = id-equiv

  Eq-ext-eq-ğ• : {x y : ğ• A B} â†’ Id x y â†’ Eq-ext-ğ• x y
  Eq-ext-eq-ğ• {x} refl = refl-Eq-ext-ğ• x
```

## Properties

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  Eq-Eq-ext-ğ• : (x y : ğ• A B) (u v : Eq-ext-ğ• x y) â†’ UU (l1 âŠ” l2)
  Eq-Eq-ext-ğ• x y u v =
    (z : ğ• A B) â†’ map-equiv (u z) ~ map-equiv (v z)

  refl-Eq-Eq-ext-ğ• : (x y : ğ• A B) (u : Eq-ext-ğ• x y) â†’ Eq-Eq-ext-ğ• x y u u
  refl-Eq-Eq-ext-ğ• x y u z = refl-htpy

  is-contr-total-Eq-Eq-ext-ğ• :
    (x y : ğ• A B) (u : Eq-ext-ğ• x y) â†’
    is-contr (Î£ (Eq-ext-ğ• x y) (Eq-Eq-ext-ğ• x y u))
  is-contr-total-Eq-Eq-ext-ğ• x y u =
    is-contr-total-Eq-Î 
      ( Î» z e â†’ map-equiv (u z) ~ map-equiv e)
      ( Î» z â†’ is-contr-total-htpy-equiv (u z))

  Eq-Eq-ext-eq-ğ• :
    (x y : ğ• A B) (u v : Eq-ext-ğ• x y) â†’ Id u v â†’ Eq-Eq-ext-ğ• x y u v
  Eq-Eq-ext-eq-ğ• x y u .u refl = refl-Eq-Eq-ext-ğ• x y u

  is-equiv-Eq-Eq-ext-eq-ğ• :
    (x y : ğ• A B) (u v : Eq-ext-ğ• x y) â†’ is-equiv (Eq-Eq-ext-eq-ğ• x y u v)
  is-equiv-Eq-Eq-ext-eq-ğ• x y u =
    fundamental-theorem-id u
      ( refl-Eq-Eq-ext-ğ• x y u)
      ( is-contr-total-Eq-Eq-ext-ğ• x y u)
      ( Eq-Eq-ext-eq-ğ• x y u)

  eq-Eq-Eq-ext-ğ• :
    {x y : ğ• A B} {u v : Eq-ext-ğ• x y} â†’ Eq-Eq-ext-ğ• x y u v â†’ Id u v
  eq-Eq-Eq-ext-ğ• {x} {y} {u} {v} =
    map-inv-is-equiv (is-equiv-Eq-Eq-ext-eq-ğ• x y u v)

  equiv-total-Eq-ext-ğ• :
    (x : ğ• A B) â†’ Î£ (ğ• A B) (Eq-ext-ğ• x) â‰ƒ Î£ A (Î» a â†’ B (symbol-ğ• x) â‰ƒ B a)
  equiv-total-Eq-ext-ğ• (tree-ğ• a f) =
    ( ( equiv-tot
            ( Î» x â†’
              ( ( right-unit-law-Î£-is-contr
                  ( Î» e â†’ is-contr-total-htpy (f âˆ˜ map-inv-equiv e))) âˆ˜e
                ( equiv-tot
                  ( Î» e â†’
                    equiv-tot
                      ( Î» g â†’
                        equiv-Î 
                          ( Î» y â†’ Id (f (map-inv-equiv e y)) (g y))
                          ( e)
                          ( Î» y â†’
                            equiv-concat
                              ( ap f (isretr-map-inv-equiv e y))
                              ( g (map-equiv e y))))))) âˆ˜e
              ( ( equiv-left-swap-Î£) âˆ˜e 
                ( equiv-tot
                  ( Î» g â†’
                    inv-equiv (equiv-fam-equiv-equiv-slice f g)))))) âˆ˜e
          ( assoc-Î£
            ( A)
            ( Î» x â†’ B x â†’ ğ• A B)
            ( Î» t â†’ Eq-ext-ğ• (tree-ğ• a f) (tree-ğ• (pr1 t) (pr2 t))))) âˆ˜e
        ( equiv-Î£
          ( Î» (t : Î£ A (Î» x â†’ B x â†’ ğ• A B)) â†’
            Eq-ext-ğ• (tree-ğ• a f) (tree-ğ• (pr1 t) (pr2 t)))
          ( inv-equiv-structure-ğ•-Alg)
          ( H))
    where
    H : (z : ğ• A (Î» x â†’ B x)) â†’
        Eq-ext-ğ• ( tree-ğ• a f) z â‰ƒ
        Eq-ext-ğ•
          ( tree-ğ• a f)
          ( tree-ğ•
            ( pr1 (map-equiv inv-equiv-structure-ğ•-Alg z))
            ( pr2 (map-equiv inv-equiv-structure-ğ•-Alg z)))
    H (tree-ğ• b g) = id-equiv

  is-contr-total-Eq-ext-is-univalent-ğ• :
    is-univalent B â†’ (x : ğ• A B) â†’ is-contr (Î£ (ğ• A B) (Eq-ext-ğ• x))
  is-contr-total-Eq-ext-is-univalent-ğ• H (tree-ğ• a f) =
    is-contr-equiv
      ( Î£ A (Î» x â†’ B a â‰ƒ B x))
      ( equiv-total-Eq-ext-ğ• (tree-ğ• a f))
      ( fundamental-theorem-id' a id-equiv (Î» x â†’ equiv-tr B) (H a))

  is-extensional-is-univalent-ğ• :
    is-univalent B â†’ is-extensional-ğ• A B
  is-extensional-is-univalent-ğ• H x =
    fundamental-theorem-id x
      ( Î» z â†’ id-equiv)
      ( is-contr-total-Eq-ext-is-univalent-ğ• H x)
      ( Î» y â†’ extensional-Eq-eq-ğ• {y = y})

  is-univalent-is-extensional-ğ• :
    type-trunc-Prop (ğ• A B) â†’ is-extensional-ğ• A B â†’ is-univalent B
  is-univalent-is-extensional-ğ• p H x =
    apply-universal-property-trunc-Prop p
      ( Î -Prop A (Î» y â†’ is-equiv-Prop (Î» (Î³ : Id x y) â†’ equiv-tr B Î³)))
      ( Î» w â†’
        fundamental-theorem-id x
          ( id-equiv)
          ( is-contr-equiv'
            ( Î£ (ğ• A B) (Eq-ext-ğ• (tree-ğ• x (Î» y â†’ w))))
            ( equiv-total-Eq-ext-ğ• (tree-ğ• x (Î» y â†’ w)))
            ( fundamental-theorem-id'
              ( tree-ğ• x (Î» y â†’ w))
              ( Î» z â†’ id-equiv)
              ( Î» z â†’ extensional-Eq-eq-ğ•)
              ( H (tree-ğ• x (Î» y â†’ w)))))
          ( Î» y â†’  equiv-tr B {y = y}))
```
