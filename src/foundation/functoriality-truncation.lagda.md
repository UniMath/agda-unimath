# Functoriality of truncations

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation.functoriality-truncation where

open import foundation.contractible-types using
  ( is-contr; center; contraction)
open import foundation.dependent-pair-types using
  ( Î£; pr1; pr2; pair)
open import foundation.equivalences using
  ( _â‰ƒ_; map-equiv; map-inv-equiv; isretr-map-inv-equiv;
    issec-map-inv-equiv)
open import foundation.function-extensionality using (htpy-eq)
open import foundation.functions using (_âˆ˜_; id)
open import foundation.homotopies using
  ( _~_; refl-htpy; _Â·l_; _âˆ™h_; _Â·r_; inv-htpy)
open import foundation.identity-types using (ap)
open import foundation.truncation-levels using (ğ•‹)
open import foundation.truncations using
  ( type-trunc; unit-trunc; universal-property-trunc;
    trunc)
open import foundation.universe-levels using (Level; UU)
```

## Idea

The universal property of truncations can be used to define the functorial action of truncations.

## Definition

```agda
abstract
  unique-functor-trunc :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) (f : A â†’ B) â†’
    is-contr
      ( Î£ ( type-trunc k A â†’ type-trunc k B)
          ( Î» h â†’ (h âˆ˜ unit-trunc) ~ (unit-trunc âˆ˜ f)))
  unique-functor-trunc {l1} {l2} {A} {B} k f =
    universal-property-trunc k A (trunc k B) (unit-trunc âˆ˜ f)

abstract
  functor-trunc :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) â†’
    (A â†’ B) â†’ type-trunc k A â†’ type-trunc k B
  functor-trunc k f =
    pr1 (center (unique-functor-trunc k f))
```

## Properties

### Truncations of homotopic maps are homotopic

```agda
  htpy-functor-trunc :
    { l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) (f : A â†’ B) â†’
    ( (functor-trunc k f) âˆ˜ unit-trunc) ~ (unit-trunc âˆ˜ f)
  htpy-functor-trunc k f =
    pr2 (center (unique-functor-trunc k f))

  htpy-uniqueness-functor-trunc :
    { l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) (f : A â†’ B) â†’
    ( h : type-trunc k A â†’ type-trunc k B) â†’
    ( ( h âˆ˜ unit-trunc) ~ (unit-trunc âˆ˜ f)) â†’
    (functor-trunc k f) ~ h
  htpy-uniqueness-functor-trunc k f h H =
    htpy-eq (ap pr1 (contraction (unique-functor-trunc k f) (pair h H)))

  htpy-trunc :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {k : ğ•‹} {f g : A â†’ B} â†’
    (f ~ g) â†’ (functor-trunc k f ~ functor-trunc k g)
  htpy-trunc {k = k} {f} {g} H =
    htpy-uniqueness-functor-trunc
      ( k)
      ( f)
      ( functor-trunc k g)
      ( htpy-functor-trunc k g âˆ™h
        inv-htpy (unit-trunc Â·l H))
```

### The truncation of the identity map is the identity map

```agda
abstract
  id-functor-trunc :
    { l1 : Level} {A : UU l1} (k : ğ•‹) â†’ functor-trunc k (id {A = A}) ~ id
  id-functor-trunc {l1} {A} k =
    htpy-uniqueness-functor-trunc k id id refl-htpy
```

### The truncation of a composite is the composite of the truncations

```agda
abstract
  comp-functor-trunc :
    { l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3} (k : ğ•‹)
    ( g : B â†’ C) (f : A â†’ B) â†’
    ( functor-trunc k (g âˆ˜ f)) ~
    ( (functor-trunc k g) âˆ˜ (functor-trunc k f))
  comp-functor-trunc k g f =
    htpy-uniqueness-functor-trunc k
      ( g âˆ˜ f)
      ( (functor-trunc k g) âˆ˜ (functor-trunc k f))
      ( ( (functor-trunc k g) Â·l (htpy-functor-trunc k f)) âˆ™h
        ( ( htpy-functor-trunc k g) Â·r f))
```

### Truncations of equivalences are equivalences

```agda
abstract
  map-equiv-trunc :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) â†’
    (A â‰ƒ B) â†’ type-trunc k A â†’ type-trunc k B
  map-equiv-trunc k e = functor-trunc k (map-equiv e)

abstract
  map-inv-equiv-trunc :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) â†’
    (A â‰ƒ B) â†’ type-trunc k B â†’ type-trunc k A
  map-inv-equiv-trunc k e = functor-trunc k (map-inv-equiv e)

abstract
  equiv-trunc :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) â†’
    (A â‰ƒ B) â†’ (type-trunc k A â‰ƒ type-trunc k B)
  pr1 (equiv-trunc k e) = map-equiv-trunc k e
  pr2 (equiv-trunc k e) =
    pair
      ( pair
        ( map-inv-equiv-trunc k e)
        ( inv-htpy (comp-functor-trunc k (map-equiv e) (map-inv-equiv e)) âˆ™h
          ( htpy-trunc (issec-map-inv-equiv e) âˆ™h
            id-functor-trunc k)))
      ( pair
        ( map-inv-equiv-trunc k e)
        ( inv-htpy (comp-functor-trunc k (map-inv-equiv e) (map-equiv e)) âˆ™h
          ( htpy-trunc (isretr-map-inv-equiv e) âˆ™h
            id-functor-trunc k)))
```
