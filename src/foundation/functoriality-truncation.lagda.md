# Functoriality of truncations

```agda
module foundation.functoriality-truncation where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.truncations
open import foundation.universe-levels

open import foundation-core.commuting-squares-of-maps
open import foundation-core.contractible-types
open import foundation-core.equivalences
open import foundation-core.function-extensionality
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.truncation-levels
open import foundation-core.whiskering-homotopies
```

</details>

## Idea

The universal property of truncations can be used to define the functorial
action of truncations.

## Definition

```agda
module _
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} (f : A â†’ B)
  where

  unique-map-trunc :
    is-contr
      ( Î£ ( type-trunc k A â†’ type-trunc k B)
          ( coherence-square-maps f unit-trunc unit-trunc))
  unique-map-trunc =
    universal-property-trunc k A (trunc k B) (unit-trunc âˆ˜ f)

  map-trunc : type-trunc k A â†’ type-trunc k B
  map-trunc = pr1 (center unique-map-trunc)

  coherence-square-map-trunc :
    coherence-square-maps f unit-trunc unit-trunc map-trunc
  coherence-square-map-trunc = pr2 (center unique-map-trunc)
```

## Properties

### Truncations of homotopic maps are homotopic

```agda
naturality-unit-trunc :
  { l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) (f : A â†’ B) â†’
  ( (map-trunc k f) âˆ˜ unit-trunc) ~ (unit-trunc âˆ˜ f)
naturality-unit-trunc k f =
  pr2 (center (unique-map-trunc k f))

htpy-uniqueness-map-trunc :
  { l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) (f : A â†’ B) â†’
  ( h : type-trunc k A â†’ type-trunc k B) â†’
  ( ( h âˆ˜ unit-trunc) ~ (unit-trunc âˆ˜ f)) â†’
  (map-trunc k f) ~ h
htpy-uniqueness-map-trunc k f h H =
  htpy-eq (ap pr1 (contraction (unique-map-trunc k f) (pair h H)))

htpy-trunc :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {k : ğ•‹} {f g : A â†’ B} â†’
  (f ~ g) â†’ (map-trunc k f ~ map-trunc k g)
htpy-trunc {k = k} {f} {g} H =
  htpy-uniqueness-map-trunc
    ( k)
    ( f)
    ( map-trunc k g)
    ( naturality-unit-trunc k g âˆ™h
      inv-htpy (unit-trunc Â·l H))
```

### The truncation of the identity map is the identity map

```agda
id-map-trunc :
  { l1 : Level} {A : UU l1} (k : ğ•‹) â†’ map-trunc k (id {A = A}) ~ id
id-map-trunc {l1} {A} k =
  htpy-uniqueness-map-trunc k id id refl-htpy
```

### The truncation of a composite is the composite of the truncations

```agda
preserves-comp-map-trunc :
  { l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3} (k : ğ•‹)
  ( g : B â†’ C) (f : A â†’ B) â†’
  ( map-trunc k (g âˆ˜ f)) ~
  ( (map-trunc k g) âˆ˜ (map-trunc k f))
preserves-comp-map-trunc k g f =
  htpy-uniqueness-map-trunc k
    ( g âˆ˜ f)
    ( (map-trunc k g) âˆ˜ (map-trunc k f))
    ( ( (map-trunc k g) Â·l (naturality-unit-trunc k f)) âˆ™h
      ( ( naturality-unit-trunc k g) Â·r f))
```

### The functorial action of truncations preserves equivalences

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (k : ğ•‹) (e : A â‰ƒ B)
  where

  map-equiv-trunc : type-trunc k A â†’ type-trunc k B
  map-equiv-trunc = map-trunc k (map-equiv e)

  map-inv-equiv-trunc : type-trunc k B â†’ type-trunc k A
  map-inv-equiv-trunc = map-trunc k (map-inv-equiv e)

  is-equiv-map-equiv-trunc : is-equiv map-equiv-trunc
  is-equiv-map-equiv-trunc =
    pair
      ( pair
        ( map-inv-equiv-trunc)
        ( inv-htpy
          ( preserves-comp-map-trunc k (map-equiv e) (map-inv-equiv e)) âˆ™h
          ( htpy-trunc (is-section-map-inv-equiv e) âˆ™h
            id-map-trunc k)))
      ( pair
        ( map-inv-equiv-trunc)
        ( inv-htpy
          ( preserves-comp-map-trunc k (map-inv-equiv e) (map-equiv e)) âˆ™h
          ( htpy-trunc (is-retraction-map-inv-equiv e) âˆ™h
            id-map-trunc k)))

  equiv-trunc : (type-trunc k A â‰ƒ type-trunc k B)
  pr1 equiv-trunc = map-equiv-trunc
  pr2 equiv-trunc = is-equiv-map-equiv-trunc
```
