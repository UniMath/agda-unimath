# Extensions of types in a global subuniverse

```agda
module foundation.extensions-types-global-subuniverses where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.commuting-triangles-of-maps
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.equivalences
open import foundation.extensions-types
open import foundation.fibers-of-maps
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.global-subuniverses
open import foundation.homotopies
open import foundation.homotopy-induction
open import foundation.identity-types
open import foundation.propositional-maps
open import foundation.propositions
open import foundation.torsorial-type-families
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.univalence
open import foundation.universe-levels
```

</details>

## Idea

Consider a type `X`. An
{{#concept "extension" Disambiguation="type in global subuniverse" Agda=extension-type-global-subuniverse}}
of `X` in a [global subuniverse](foundation.global-subuniverses.md) `ğ’«` is an
object in the coslice under `X` in `ğ’«`, i.e., it consists of a type `Y` in `ğ’«`
and a map `f : X â†’ Y`.

In the above definition of extensions of types in a global subuniverse our aim
is to capture the most general concept of what it means to be an extension of a
type in a subuniverse.

Our notion of extensions of types are not to be confused with extension types of
cubical type theory or
[extension types of simplicial type theory](https://arxiv.org/abs/1705.07442).

## Definitions

### The predicate on an extension of types of being in a global subuniverse

```agda
module _
  {Î± : Level â†’ Level} {l1 l2 : Level}
  (ğ’« : global-subuniverse Î±) {X : UU l1}
  (E : extension-type l2 X)
  where

  is-in-global-subuniverse-extension-type-Prop : Prop (Î± l2)
  is-in-global-subuniverse-extension-type-Prop =
    is-in-global-subuniverse-Prop ğ’« (type-extension-type E)

  is-in-global-subuniverse-extension-type : UU (Î± l2)
  is-in-global-subuniverse-extension-type =
    is-in-global-subuniverse ğ’« (type-extension-type E)

  is-prop-is-in-global-subuniverse-extension-type :
    is-prop is-in-global-subuniverse-extension-type
  is-prop-is-in-global-subuniverse-extension-type =
    is-prop-is-in-global-subuniverse ğ’« (type-extension-type E)
```

### Extensions of types in a subuniverse

```agda
extension-type-global-subuniverse :
  {Î± : Level â†’ Level} {l1 : Level}
  (ğ’« : global-subuniverse Î±) (l2 : Level) (X : UU l1) â†’
  UU (Î± l2 âŠ” l1 âŠ” lsuc l2)
extension-type-global-subuniverse ğ’« l2 X =
  Î£ (type-global-subuniverse ğ’« l2) (Î» Y â†’ X â†’ inclusion-global-subuniverse ğ’« Y)

module _
  {Î± : Level â†’ Level} {l1 l2 : Level} (ğ’« : global-subuniverse Î±) {X : UU l1}
  (Y : extension-type-global-subuniverse ğ’« l2 X)
  where

  type-global-subuniverse-extension-type-global-subuniverse :
    type-global-subuniverse ğ’« l2
  type-global-subuniverse-extension-type-global-subuniverse = pr1 Y

  type-extension-type-global-subuniverse : UU l2
  type-extension-type-global-subuniverse =
    inclusion-global-subuniverse ğ’«
      type-global-subuniverse-extension-type-global-subuniverse

  is-in-global-subuniverse-type-extension-type-global-subuniverse :
    is-in-global-subuniverse ğ’« type-extension-type-global-subuniverse
  is-in-global-subuniverse-type-extension-type-global-subuniverse =
    is-in-global-subuniverse-inclusion-global-subuniverse ğ’«
      type-global-subuniverse-extension-type-global-subuniverse

  inclusion-extension-type-global-subuniverse :
    X â†’ type-extension-type-global-subuniverse
  inclusion-extension-type-global-subuniverse = pr2 Y

  extension-type-extension-type-global-subuniverse : extension-type l2 X
  extension-type-extension-type-global-subuniverse =
    type-extension-type-global-subuniverse ,
    inclusion-extension-type-global-subuniverse
```

## Properties

### Extensions of types in a subuniverse are extensions of types by types in the subuniverse

```agda
module _
  {Î± : Level â†’ Level} {l1 l2 : Level} (ğ’« : global-subuniverse Î±) {X : UU l1}
  where

  compute-extension-type-global-subuniverse :
    extension-type-global-subuniverse ğ’« l2 X â‰ƒ
    Î£ (extension-type l2 X) (is-in-global-subuniverse-extension-type ğ’«)
  compute-extension-type-global-subuniverse = equiv-right-swap-Î£
```

### The inclusion of extensions of types in a subuniverse into extensions of types is an embedding

```agda
module _
  {Î± : Level â†’ Level} {l1 l2 : Level} (ğ’« : global-subuniverse Î±) {X : UU l1}
  where

  compute-fiber-extension-type-extension-type-global-subuniverse :
    (Y : extension-type l2 X) â†’
    fiber (extension-type-extension-type-global-subuniverse ğ’«) Y â‰ƒ
    is-in-global-subuniverse ğ’« (type-extension-type Y)
  compute-fiber-extension-type-extension-type-global-subuniverse Y =
    equivalence-reasoning
    Î£ ( Î£ (Î£ (UU l2) (Î» Z â†’ is-in-global-subuniverse ğ’« Z)) (Î» Z â†’ X â†’ pr1 Z))
      ( Î» E â†’ extension-type-extension-type-global-subuniverse ğ’« E ï¼ Y)
    â‰ƒ Î£ ( Î£ (extension-type l2 X) (is-in-global-subuniverse-extension-type ğ’«))
        ( Î» E â†’ pr1 E ï¼ Y)
    by
      equiv-Î£-equiv-base
        ( Î» E â†’ pr1 E ï¼ Y)
        ( compute-extension-type-global-subuniverse ğ’«)
    â‰ƒ Î£ ( Î£ (extension-type l2 X) (Î» E â†’ E ï¼ Y))
        ( is-in-global-subuniverse-extension-type ğ’« âˆ˜ pr1)
      by equiv-right-swap-Î£
    â‰ƒ is-in-global-subuniverse-extension-type ğ’« Y
    by left-unit-law-Î£-is-contr (is-torsorial-Id' Y) (Y , refl)

  is-prop-map-extension-type-extension-type-global-subuniverse :
    is-prop-map (extension-type-extension-type-global-subuniverse ğ’«)
  is-prop-map-extension-type-extension-type-global-subuniverse Y =
    is-prop-equiv
      ( compute-fiber-extension-type-extension-type-global-subuniverse Y)
      ( is-prop-is-in-global-subuniverse ğ’« (type-extension-type Y))

  is-emb-extension-type-extension-type-global-subuniverse :
    is-emb (extension-type-extension-type-global-subuniverse ğ’«)
  is-emb-extension-type-extension-type-global-subuniverse =
    is-emb-is-prop-map
      is-prop-map-extension-type-extension-type-global-subuniverse
```

### Characterization of identifications of extensions of types in a subuniverse

```agda
equiv-extension-type-global-subuniverse :
  {Î± : Level â†’ Level} {l1 l2 l3 : Level}
  (ğ’« : global-subuniverse Î±) {X : UU l1} â†’
  extension-type-global-subuniverse ğ’« l2 X â†’
  extension-type-global-subuniverse ğ’« l3 X â†’ UU (l1 âŠ” l2 âŠ” l3)
equiv-extension-type-global-subuniverse ğ’« U V =
  equiv-extension-type
    ( extension-type-extension-type-global-subuniverse ğ’« U)
    ( extension-type-extension-type-global-subuniverse ğ’« V)

module _
  {Î± : Level â†’ Level} {l1 l2 : Level} (ğ’« : global-subuniverse Î±) {X : UU l1}
  where

  id-equiv-extension-type-global-subuniverse :
    (U : extension-type-global-subuniverse ğ’« l2 X) â†’
    equiv-extension-type-global-subuniverse ğ’« U U
  id-equiv-extension-type-global-subuniverse U =
    id-equiv-extension-type
      ( extension-type-extension-type-global-subuniverse ğ’« U)

  equiv-eq-extension-type-global-subuniverse :
    (U V : extension-type-global-subuniverse ğ’« l2 X) â†’
    U ï¼ V â†’ equiv-extension-type-global-subuniverse ğ’« U V
  equiv-eq-extension-type-global-subuniverse U V p =
    equiv-eq-extension-type
      ( extension-type-extension-type-global-subuniverse ğ’« U)
      ( extension-type-extension-type-global-subuniverse ğ’« V)
      ( ap (extension-type-extension-type-global-subuniverse ğ’«) p)

  is-equiv-equiv-eq-extension-type-global-subuniverse :
    (U V : extension-type-global-subuniverse ğ’« l2 X) â†’
    is-equiv (equiv-eq-extension-type-global-subuniverse U V)
  is-equiv-equiv-eq-extension-type-global-subuniverse U V =
    is-equiv-comp
      ( equiv-eq-extension-type
        ( extension-type-extension-type-global-subuniverse ğ’« U)
        ( extension-type-extension-type-global-subuniverse ğ’« V))
      ( ap (extension-type-extension-type-global-subuniverse ğ’«))
      ( is-emb-extension-type-extension-type-global-subuniverse ğ’« U V)
      ( is-equiv-equiv-eq-extension-type
        ( extension-type-extension-type-global-subuniverse ğ’« U)
        ( extension-type-extension-type-global-subuniverse ğ’« V))

  extensionality-extension-type-global-subuniverse :
    (U V : extension-type-global-subuniverse ğ’« l2 X) â†’
    (U ï¼ V) â‰ƒ equiv-extension-type-global-subuniverse ğ’« U V
  extensionality-extension-type-global-subuniverse U V =
    equiv-eq-extension-type-global-subuniverse U V ,
    is-equiv-equiv-eq-extension-type-global-subuniverse U V

  eq-equiv-extension-type-global-subuniverse :
    (U V : extension-type-global-subuniverse ğ’« l2 X) â†’
    equiv-extension-type-global-subuniverse ğ’« U V â†’ U ï¼ V
  eq-equiv-extension-type-global-subuniverse U V =
    map-inv-equiv (extensionality-extension-type-global-subuniverse U V)
```
