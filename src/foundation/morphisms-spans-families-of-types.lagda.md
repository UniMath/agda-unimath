# Morphisms of spans on families of types

```agda
module foundation.morphisms-spans-families-of-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.commuting-triangles-of-homotopies
open import foundation.dependent-pair-types
open import foundation.equality-dependent-function-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopy-induction
open import foundation.spans-families-of-types
open import foundation.structure-identity-principle
open import foundation.universe-levels
open import foundation.whiskering-homotopies

open import foundation-core.commuting-triangles-of-maps
open import foundation-core.equivalences
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.torsorial-type-families
```

</details>

## Idea

Consider two [spans](foundation.spans-families-of-types.md) `ğ’® := (S , f)` and `ğ’¯ := (T , g)` on a family of types `A : I â†’ ğ’°`. A {{#concept "morphism" Disambiguation="span on a family of types" Agda=hom-span-family-of-types}} from `ğ’®` to `ğ’¯` consists of a map `h : S â†’ T` and a [homotopy](foundation-core.homotopies.md) witnessing that the triangle

```text
        h
    S ----> T
     \     /
  f i \   / g i
       âˆ¨ âˆ¨
       A i
```

[commutes](foundation-core.commuting-triangles-of-maps.md) for every `i : I`.

## Definitions

### Morphisms of spans on families of types

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2}
  (ğ’® : span-family-of-types l3 A) (ğ’¯ : span-family-of-types l4 A)
  where

  hom-span-family-of-types : UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  hom-span-family-of-types =
    Î£ ( spanning-type-span-family-of-types ğ’® â†’
        spanning-type-span-family-of-types ğ’¯)
      ( Î» h â†’
        (i : I) â†’
        coherence-triangle-maps
          ( map-span-family-of-types ğ’® i)
          ( map-span-family-of-types ğ’¯ i)
          ( h))

module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2}
  (ğ’® : span-family-of-types l3 A) (ğ’¯ : span-family-of-types l4 A)
  (h : hom-span-family-of-types ğ’® ğ’¯)
  where

  map-hom-span-family-of-types :
    spanning-type-span-family-of-types ğ’® â†’
    spanning-type-span-family-of-types ğ’¯
  map-hom-span-family-of-types = pr1 h

  coherence-triangle-hom-span-family-of-types :
    (i : I) â†’
    coherence-triangle-maps
      ( map-span-family-of-types ğ’® i)
      ( map-span-family-of-types ğ’¯ i)
      ( map-hom-span-family-of-types)
  coherence-triangle-hom-span-family-of-types =
    pr2 h
```

### Homotopopies of morphisms of spans on families of types

Consider two spans `ğ’® := (S , f)` and `ğ’¯ := (T , g)` on a family of types `A : I â†’ ğ’°`, and consider two morphisms `(h , H)` and `(k , K)` between them. A {{#concept "homotopy" Disambiguation="morphism between spans on families of types" Agda=htpy-homs-apn-family-of-types}} is a pair `(Î± , Î²)` consisting of a homotopy

```text
  Î± : h ~ k
```

and a family `Î²` of homotopies witnessing that the triangle

```text
              f i
             /   \
        H i / Î² i \ K i
           âˆ¨       âˆ¨
  (g i âˆ˜ h) ------> (g i âˆ˜ k)
            g i Â· Î±
```

commutes for each `i : I`.

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2}
  (ğ’® : span-family-of-types l3 A) (ğ’¯ : span-family-of-types l4 A)
  (h k : hom-span-family-of-types ğ’® ğ’¯)
  where

  coherence-htpy-hom-span-family-of-types :
    map-hom-span-family-of-types ğ’® ğ’¯ h ~ map-hom-span-family-of-types ğ’® ğ’¯ k â†’
    UU (l1 âŠ” l2 âŠ” l3)
  coherence-htpy-hom-span-family-of-types Î± =
    (i : I) â†’
    coherence-triangle-homotopies'
      ( coherence-triangle-hom-span-family-of-types ğ’® ğ’¯ k i)
      ( map-span-family-of-types ğ’¯ i Â·l Î±)
      ( coherence-triangle-hom-span-family-of-types ğ’® ğ’¯ h i)

  htpy-hom-span-family-of-types : UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  htpy-hom-span-family-of-types =
    Î£ ( map-hom-span-family-of-types ğ’® ğ’¯ h ~ map-hom-span-family-of-types ğ’® ğ’¯ k)
      ( coherence-htpy-hom-span-family-of-types)

  module _
    (Î± : htpy-hom-span-family-of-types)
    where

    htpy-htpy-hom-span-family-of-types :
      map-hom-span-family-of-types ğ’® ğ’¯ h ~ map-hom-span-family-of-types ğ’® ğ’¯ k
    htpy-htpy-hom-span-family-of-types = pr1 Î±

    coh-htpy-hom-span-family-of-types :
      coherence-htpy-hom-span-family-of-types htpy-htpy-hom-span-family-of-types
    coh-htpy-hom-span-family-of-types = pr2 Î±
```

### The reflexivity homotopy on a morphism of spans on families of types

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2}
  (ğ’® : span-family-of-types l3 A) (ğ’¯ : span-family-of-types l4 A)
  (h : hom-span-family-of-types ğ’® ğ’¯)
  where

  refl-htpy-hom-span-family-of-types :
    htpy-hom-span-family-of-types ğ’® ğ’¯ h h
  pr1 refl-htpy-hom-span-family-of-types = refl-htpy
  pr2 refl-htpy-hom-span-family-of-types i = right-unit-htpy
```

## Properties

### Characterization if identifications of morphisms of spans on families of types

```agda
module _
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2}
  (ğ’® : span-family-of-types l3 A) (ğ’¯ : span-family-of-types l4 A)
  (h : hom-span-family-of-types ğ’® ğ’¯)
  where

  htpy-eq-hom-span-family-of-types :
    (k : hom-span-family-of-types ğ’® ğ’¯) â†’
    h ï¼ k â†’ htpy-hom-span-family-of-types ğ’® ğ’¯ h k
  htpy-eq-hom-span-family-of-types .h refl =
    refl-htpy-hom-span-family-of-types ğ’® ğ’¯ h

  is-torsorial-htpy-hom-span-family-of-types :
    is-torsorial (htpy-hom-span-family-of-types ğ’® ğ’¯ h)
  is-torsorial-htpy-hom-span-family-of-types =
    is-torsorial-Eq-structure
      ( is-torsorial-htpy _)
      ( map-hom-span-family-of-types ğ’® ğ’¯ h , refl-htpy)
      ( is-torsorial-Eq-Î  (Î» i â†’ is-torsorial-htpy _))

  is-equiv-htpy-eq-hom-span-family-of-types :
    (k : hom-span-family-of-types ğ’® ğ’¯) â†’
    is-equiv (htpy-eq-hom-span-family-of-types k)
  is-equiv-htpy-eq-hom-span-family-of-types =
    fundamental-theorem-id
      ( is-torsorial-htpy-hom-span-family-of-types)
      ( htpy-eq-hom-span-family-of-types)

  extensionality-hom-span-family-of-types :
    (k : hom-span-family-of-types ğ’® ğ’¯) â†’
    (h ï¼ k) â‰ƒ htpy-hom-span-family-of-types ğ’® ğ’¯ h k
  pr1 (extensionality-hom-span-family-of-types k) =
    htpy-eq-hom-span-family-of-types k
  pr2 (extensionality-hom-span-family-of-types k) =
    is-equiv-htpy-eq-hom-span-family-of-types k

  eq-htpy-hom-span-family-of-types :
    (k : hom-span-family-of-types ğ’® ğ’¯) â†’
    htpy-hom-span-family-of-types ğ’® ğ’¯ h k â†’ h ï¼ k
  eq-htpy-hom-span-family-of-types k =
    map-inv-equiv (extensionality-hom-span-family-of-types k)
```
