---
title: Connected components of types
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation.connected-components where

open import foundation.connected-types using (is-path-connected; is-path-connected-mere-eq)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.equality-dependent-pair-types using (eq-pair-Î£)
open import foundation.identity-types using (_ï¼_; refl; inv)
open import foundation.propositional-truncations using
  ( trunc-Prop; type-trunc-Prop; unit-trunc-Prop; apply-universal-property-trunc-Prop;
    all-elements-equal-type-trunc-Prop; is-prop-type-trunc-Prop)
open import foundation.propositions using (is-trunc-is-prop)
open import foundation.truncated-types using (is-trunc; is-trunc-Î£)
open import foundation.truncation-levels using (ğ•‹; succ-ğ•‹)
open import foundation.universe-levels using (UU; Level)
```

## Idea

The connected component of a type `A` at an element `a : A` is the type of all `x : A` that are merely equal to `a`.

## Definition

```agda
module _
  {l : Level} (A : UU l) (a : A)
  where

  connected-component : UU l
  connected-component =
    Î£ A (Î» x â†’ type-trunc-Prop (x ï¼ a))

  value-connected-component :
    connected-component â†’ A
  value-connected-component X = pr1 X

  abstract
    mere-equality-connected-component : (X : connected-component) â†’
      type-trunc-Prop (value-connected-component X ï¼ a)
    mere-equality-connected-component X = pr2 X
```

## Properties

### Connected components are path-connected

```agda
abstract
  is-path-connected-connected-component :
    {l : Level} (A : UU l) (a : A) â†’
    is-path-connected (connected-component A a)
  is-path-connected-connected-component A a =
    is-path-connected-mere-eq
      ( pair a (unit-trunc-Prop refl))
      ( Î» (pair x p) â†’
        apply-universal-property-trunc-Prop
          ( p)
          ( trunc-Prop (pair a (unit-trunc-Prop refl) ï¼ pair x p))
          ( Î» p' â†’
            unit-trunc-Prop
              ( eq-pair-Î£
                ( inv p')
                ( all-elements-equal-type-trunc-Prop _ p))))
```

### If `A` is (k+1)-truncated, then the connected component of `a` in `A` is (k+1)-truncated.

```agda
is-trunc-connected-component :
  {l : Level} {k : ğ•‹} (A : UU l) (a : A) â†’
  is-trunc (succ-ğ•‹ k) A â†’ is-trunc (succ-ğ•‹ k) (connected-component A a)
is-trunc-connected-component {l} {k} A a H =
  is-trunc-Î£ H (Î» x â†’ is-trunc-is-prop k is-prop-type-trunc-Prop)

```

