# Precomposition of functions

```agda
module foundation.precomposition-functions where

open import foundation-core.precomposition-functions public
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.precomposition-dependent-functions
open import foundation.sections
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.commuting-squares-of-maps
open import foundation-core.commuting-triangles-of-maps
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.retractions
open import foundation-core.type-theoretic-principle-of-choice
```

</details>

## Idea

Given a function `f : A â†’ B` and a type `X`, the **precomposition function** by
`f`

```text
  - âˆ˜ f : (B â†’ X) â†’ (A â†’ X)
```

is defined by `Î» h x â†’ h (f x)`.

The precomposition function was already defined in
[`foundation-core.precomposition-functions`](foundation-core.precomposition-functions.md).
In this file we derive some further properties of the precomposition function.

## Properties

### Precomposition preserves homotopies

```agda
htpy-precomp :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {f g : A â†’ B} (H : f ~ g) (C : UU l3) â†’
  precomp f C ~ precomp g C
htpy-precomp H C h = eq-htpy (h Â·l H)

compute-htpy-precomp-refl-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (C : UU l3) â†’
  htpy-precomp (refl-htpy' f) C ~ refl-htpy
compute-htpy-precomp-refl-htpy f C h = eq-htpy-refl-htpy (h âˆ˜ f)
```

### Precomposition preserves concatenation of homotopies

```agda
compute-concat-htpy-precomp :
  { l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  { f g h : A â†’ B} (H : f ~ g) (K : g ~ h) (C : UU l3) â†’
  htpy-precomp (H âˆ™h K) C ~ htpy-precomp H C âˆ™h htpy-precomp K C
compute-concat-htpy-precomp H K C k =
  ( ap
    ( eq-htpy)
    ( eq-htpy (distributive-left-whisker-comp-concat k H K))) âˆ™
  ( eq-htpy-concat-htpy (k Â·l H) (k Â·l K))
```

### If precomposition `- âˆ˜ f : (Y â†’ X) â†’ (X â†’ X)` has a section, then `f` has a retraction

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  retraction-map-section-precomp : section (precomp f X) â†’ retraction f
  retraction-map-section-precomp (s , S) = (s id , htpy-eq (S id))
```

### If `f` has a retraction then `- âˆ˜ f : (Y â†’ X) â†’ (X â†’ X)` has a section

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y)
  where

  section-precomp-retraction-map : retraction f â†’ section (precomp f X)
  section-precomp-retraction-map (r , H) = (precomp r X , htpy-precomp H X)
```

### Equivalences induce an equivalence from the type of homotopies between two maps to the type of homotopies between their precomposites

```agda
module _
  { l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  where

  equiv-htpy-precomp-htpy :
    (f g : B â†’ C) (e : A â‰ƒ B) â†’ (f ~ g) â‰ƒ (f âˆ˜ map-equiv e ~ g âˆ˜ map-equiv e)
  equiv-htpy-precomp-htpy f g e =
    equiv-htpy-precomp-htpy-Î  f g e
```

### Computations of the fibers of `precomp`

The fiber of `- âˆ˜ f : (B â†’ X) â†’ (A â†’ X)` at `g âˆ˜ f : B â†’ X` is equivalent to the
type of maps `h : B â†’ X` equipped with a homotopy witnessing that the square

```text
        f
    A -----> B
    |        |
  f |        | h
    âˆ¨        âˆ¨
    B -----> X
        g
```

commutes.

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) {X : UU l3}
  where

  compute-extension-fiber-precomp' :
    (g : A â†’ X) â†’
    fiber (precomp f X) g â‰ƒ Î£ (B â†’ X) (Î» h â†’ coherence-triangle-maps' g h f)
  compute-extension-fiber-precomp' g = equiv-tot (Î» _ â†’ equiv-funext)

  compute-extension-fiber-precomp :
    (g : A â†’ X) â†’
    fiber (precomp f X) g â‰ƒ Î£ (B â†’ X) (Î» h â†’ coherence-triangle-maps g h f)
  compute-extension-fiber-precomp g =
    equiv-tot (Î» _ â†’ equiv-funext) âˆ˜e equiv-fiber (precomp f X) g

  compute-fiber-precomp :
    (g : B â†’ X) â†’
    fiber (precomp f X) (g âˆ˜ f) â‰ƒ
    Î£ (B â†’ X) (Î» h â†’ coherence-square-maps f f h g)
  compute-fiber-precomp g = compute-extension-fiber-precomp (g âˆ˜ f)

  compute-total-fiber-precomp :
    Î£ (B â†’ X) (Î» g â†’ fiber (precomp f X) (g âˆ˜ f)) â‰ƒ
    Î£ (B â†’ X) (Î» u â†’ Î£ (B â†’ X) (Î» v â†’ u âˆ˜ f ~ v âˆ˜ f))
  compute-total-fiber-precomp = equiv-tot compute-fiber-precomp

  diagonal-into-fibers-precomp :
    (B â†’ X) â†’ Î£ (B â†’ X) (Î» g â†’ fiber (precomp f X) (g âˆ˜ f))
  diagonal-into-fibers-precomp = map-section-family (Î» g â†’ g , refl)
```

### The action on identifications of precomposition by a map

Consider a map `f : A â†’ B` and two functions `g h : B â†’ C`. Then the
[action on identifications](foundation.action-on-identifications-functions.md)
of `precomp f C` fits in a
[commuting square](foundation-core.commuting-squares-of-maps.md)

```text
                     ap (precomp f C)
       (g = h) --------------------------> (g âˆ˜ f = h âˆ˜ f)
          |                                       |
  htpy-eq |                                       | htpy-eq
          âˆ¨                                       âˆ¨
       (g ~ h) --------------------------> (g âˆ˜ f ~ h âˆ˜ f).
                precomp f (eq-value g h)
```

Similarly, the action on identifications of `precomp f C` also fits in a
commuting square

```text
                precomp f (eq-value g h)
       (g ~ h) --------------------------> (g âˆ˜ f ~ h âˆ˜ f)
          |                                       |
  eq-htpy |                                       | eq-htpy
          âˆ¨                                       âˆ¨
       (g = h) --------------------------> (g âˆ˜ f = h âˆ˜ f).
                     ap (precomp f C)
```

commutes.

```agda
module _
  { l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  (f : A â†’ B) {g h : B â†’ C}
  where

  coherence-htpy-eq-ap-precomp :
    coherence-square-maps
      ( ap (precomp f C))
      ( htpy-eq)
      ( htpy-eq)
      ( precomp-Î  f (eq-value g h))
  coherence-htpy-eq-ap-precomp =
    coherence-htpy-eq-ap-precomp-Î  f

  coherence-htpy-eq-ap-precomp' :
    coherence-square-maps'
      ( ap (precomp f C))
      ( htpy-eq)
      ( htpy-eq)
      ( precomp-Î  f (eq-value g h))
  coherence-htpy-eq-ap-precomp' =
    coherence-htpy-eq-ap-precomp-Î ' f

  coherence-eq-htpy-ap-precomp :
    coherence-square-maps
      ( precomp-Î  f (eq-value g h))
      ( eq-htpy)
      ( eq-htpy)
      ( ap (precomp f C))
  coherence-eq-htpy-ap-precomp =
    coherence-eq-htpy-ap-precomp-Î  f

  coherence-eq-htpy-ap-precomp' :
    coherence-square-maps'
      ( precomp-Î  f (eq-value g h))
      ( eq-htpy)
      ( eq-htpy)
      ( ap (precomp f C))
  coherence-eq-htpy-ap-precomp' =
    coherence-eq-htpy-ap-precomp-Î ' f
```

### The precomposition map at a dependent pair type

Given a map `f : X â†’ Y` and a family `B : A â†’ ð’°` we have a
[commuting square](foundation-core.commuting-squares-of-maps.md)

```text
                                precomp f (Î£ A B)
             (Y â†’ Î£ A B) ------------------------------> (X â†’ Î£ A B)
                  |                                           |
                ~ |                                           | ~
                  âˆ¨                                           âˆ¨
  Î£ (a : Y â†’ A) ((y : Y) â†’ B (a y)) --------> Î£ (a : X â†’ A) ((x : X) â†’ B (a x)).
                  map-Î£ (precomp f A) (Î» a â†’ precomp f (B âˆ˜ a))
```

```agda
module _
  {l1 l2 l3 l4 : Level} {X : UU l1} {Y : UU l2} {A : UU l3} {B : A â†’ UU l4}
  {f : X â†’ Y}
  where

  coherence-precomp-Î£ :
    coherence-square-maps
      ( precomp f (Î£ A B))
      ( map-distributive-Î -Î£)
      ( map-distributive-Î -Î£)
      ( map-Î£
        ( Î» a â†’ (x : X) â†’ B (a x))
        ( precomp f A)
        ( Î» a â†’ precomp-Î  f (B âˆ˜ a)))
  coherence-precomp-Î£ = coherence-precomp-Î -Î£
```
