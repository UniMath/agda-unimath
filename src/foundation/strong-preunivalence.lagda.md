# The strong preunivalence axiom

```agda
module foundation.strong-preunivalence where
```

<details><summary>Imports</summary>

```agda
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.equivalences
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types-structures
open import foundation.preunivalence
open import foundation.propositional-maps
open import foundation.propositions
open import foundation.sets
open import foundation.small-types
open import foundation.univalence
open import foundation.universe-levels

open import foundation-core.function-types
open import foundation-core.identity-types
open import foundation-core.subtypes
```

</details>

## Idea

{{#concept "The strong preunivalence axiom" Agda=strong-preunivalence-axiom}} is
a common generalization of the [univalence axiom](foundation.univalence.md) and
[axiom K](foundation-core.sets.md). It asserts that for any type `X : ùí∞` and any
other universe `ùí±`, the [smallness predicate](foundation-core.small-types.md)
`is-small ùí± X ‚âê Œ£ (Y : ùí±), (X ‚âÉ Y)` is a [set](foundation-core.sets.md).

The strong preunivalence axiom is a strengthening of
[the preunivalence axiom](foundation.preunivalence.md) in the following way. If
we restrict to `ùí± ‚âê ùí∞`, the
[fundamental theorem of identity types for subuniverses](foundation.fundamental-theorem-of-identity-types-subuniverses.md)
says that `Œ£ (Y : ùí∞), (X ‚âÉ Y)` is a set if and only if _every_ binary family of
maps

```text
  (X Y : ùí∞) ‚Üí (X Ôºù Y) ‚Üí (X ‚âÉ Y)
```

is a family of [embeddings](foundation-core.embeddings.md). The preunivalence
axiom asserts that the particular binary family of maps defined by identity
induction as `refl ‚Ü¶ id-equiv` is an embedding.

While the strong preunivalence axiom is a strengthening of the preunivalence
axiom, it is still common generalization of the
[univalence axiom](foundation.univalence.md) and
[axiom K](foundation-core.sets.md). If we assume the univalence axiom then
`is-small` is a proposition, and if we assume axiom K then every type is a set.

## Definitions

```agda
instance-strong-preunivalence :
  {l1 : Level} (l2 : Level) (X : UU l1) ‚Üí UU (l1 ‚äî lsuc l2)
instance-strong-preunivalence l2 X = is-set (is-small l2 X)

strong-preunivalence-axiom-Level : (l1 l2 : Level) ‚Üí UU (lsuc l1 ‚äî lsuc l2)
strong-preunivalence-axiom-Level l1 l2 =
  (X : UU l1) ‚Üí instance-strong-preunivalence l2 X

strong-preunivalence-axiom : UUœâ
strong-preunivalence-axiom =
  {l1 l2 : Level} ‚Üí strong-preunivalence-axiom-Level l1 l2
```

## Properties

### The strong preunivalence axiom strengthens the preunivalence axiom

```agda
based-preunivalence-instance-strong-preunivalence :
  {l : Level} (X : UU l) ‚Üí
  instance-strong-preunivalence l X ‚Üí based-preunivalence-axiom X
based-preunivalence-instance-strong-preunivalence X L Y =
  is-emb-is-prop-map
    ( backward-implication-fundamental-theorem-unbased-id-subuniverse
      ( is-prop-Prop)
      ( L)
      ( X)
      ( Œª _ ‚Üí equiv-eq)
      ( Y))

preunivalence-axiom-strong-preunivalence-axiom :
  strong-preunivalence-axiom ‚Üí preunivalence-axiom
preunivalence-axiom-strong-preunivalence-axiom L X =
  based-preunivalence-instance-strong-preunivalence X (L X)
```

### The strong preunivalence axiom generalizes axiom K

To show that preunivalence generalizes axiom K, we assume axiom K for the types
in question and for the universe itself.

```agda
instance-strong-preunivalence-instance-axiom-K :
  {l1 : Level} (l2 : Level) (A : UU l1) ‚Üí
  instance-axiom-K (UU l2) ‚Üí
  ((B : UU l2) ‚Üí instance-axiom-K (A ‚âÉ B)) ‚Üí
  instance-strong-preunivalence l2 A
instance-strong-preunivalence-instance-axiom-K l2 A K-Type K-A‚âÉB =
  is-set-Œ£ (is-set-axiom-K K-Type) (is-set-axiom-K ‚àò K-A‚âÉB)

strong-preunivalence-axiom-axiom-K : axiom-K ‚Üí strong-preunivalence-axiom
strong-preunivalence-axiom-axiom-K K {l1} {l2} A =
  instance-strong-preunivalence-instance-axiom-K l2 A
    ( K (UU l2))
    ( Œª B ‚Üí K (A ‚âÉ B))
```

### The strong preunivalence axiom generalizes the univalence axiom

```agda
strong-preunivalence-axiom-univalence-axiom :
  univalence-axiom ‚Üí strong-preunivalence-axiom
strong-preunivalence-axiom-univalence-axiom UA {l1} {l2} A =
  is-set-is-prop
  ( is-prop-is-proof-irrelevant
    ( Œª (X , e) ‚Üí
      is-contr-equiv'
        ( is-small l2 X)
        ( equiv-tot (equiv-precomp-equiv e))
        ( is-torsorial-equiv-based-univalence X (UA X))))
```

### Strong preunivalence holds in univalent foundations

```agda
strong-preunivalence : strong-preunivalence-axiom
strong-preunivalence = strong-preunivalence-axiom-univalence-axiom univalence
```

## See also

- [Preunivalent categories](category-theory.preunivalent-categories.md)
