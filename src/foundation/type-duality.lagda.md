# Type duality

```agda
open import foundation.function-extensionality-axiom

module
  foundation.type-duality
  (funext : function-extensionality)
  where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.equivalences funext
open import foundation.function-extensionality funext

open import foundation.fundamental-theorem-of-identity-types
open import foundation.locally-small-types funext
open import foundation.raising-universe-levels-unit-type funext
open import foundation.slice funext
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.unit-type
open import foundation.univalence funext
open import foundation.universal-property-equivalences funext
open import foundation.universe-levels

open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation-core.embeddings
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-function-types funext
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.small-types funext
open import foundation-core.torsorial-type-families

open import trees.polynomial-endofunctors funext
```

</details>

## Idea

Given a [univalent](foundation.univalence.md) universe `ùí∞`, we can define two
closely related functors acting on all types. First there is the covariant
functor given by

```text
  P_ùí∞(A) := Œ£ (X : ùí∞), X ‚Üí A.
```

This is a [polynomial endofunctor](trees.polynomial-endofunctors.md). Second,
there is the contravariant functor given by

```text
  P^ùí∞(A) := A ‚Üí ùí∞.
```

If the type `A` is [locally `ùí∞`-small](foundation.locally-small-types.md), then
there is a map `œÜ_A : P_ùí∞(A) ‚Üí P^ùí∞(A)`. This map is natural in `A`, and it is
always an [embedding](foundation-core.embeddings.md). Furthermore, the map `œÜ_A`
is an [equivalence](foundation-core.equivalences.md) if and only if `A` is
[`ùí∞`-small](foundation-core.small-types.md).

## Definitions

### The polynomial endofunctor of a universe

```agda
type-polynomial-endofunctor-UU :
  (l : Level) {l1 : Level} (A : UU l1) ‚Üí UU (lsuc l ‚äî l1)
type-polynomial-endofunctor-UU l = Slice l

map-polynomial-endofunctor-UU :
  (l : Level) {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B) ‚Üí
  type-polynomial-endofunctor-UU l A ‚Üí type-polynomial-endofunctor-UU l B
map-polynomial-endofunctor-UU l = map-polynomial-endofunctor (UU l) (Œª X ‚Üí X)
```

### Type families

```agda
type-exp-UU : (l : Level) {l1 : Level} ‚Üí UU l1 ‚Üí UU (lsuc l ‚äî l1)
type-exp-UU l A = A ‚Üí UU l

map-exp-UU :
  (l : Level) {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B) ‚Üí
  type-exp-UU l B ‚Üí type-exp-UU l A
map-exp-UU l f P = P ‚àò f
```

## Properties

### If `A` is locally `l`-small, then we can construct an embedding `type-polynomial-endofunctor l A ‚Ü™ type-exp-UU A`

```agda
map-type-duality :
  {l l1 : Level} {A : UU l1} ‚Üí is-locally-small l A ‚Üí
  type-polynomial-endofunctor-UU l A ‚Üí type-exp-UU l A
map-type-duality H (X , f) a =
  Œ£ X (Œª x ‚Üí type-is-small (H (f x) a))

is-emb-map-type-duality :
  {l l1 : Level} {A : UU l1} (H : is-locally-small l A) ‚Üí
  is-emb (map-type-duality H)
is-emb-map-type-duality {l} {l1} {A} H (X , f) =
  fundamental-theorem-id
    ( is-contr-equiv
      ( Œ£ ( type-polynomial-endofunctor-UU l A) ((X , f) Ôºù_))
      ( equiv-tot
        ( Œª (Y , g) ‚Üí
          ( inv-equiv (extensionality-Slice (X , f) (Y , g))) ‚àòe
          ( inv-equiv (equiv-fam-equiv-equiv-slice f g)) ‚àòe
          ( equiv-Œ†-equiv-family
            ( Œª a ‚Üí
              ( equiv-postcomp-equiv
                ( equiv-tot (Œª y ‚Üí inv-equiv (equiv-is-small (H (g y) a))))
                ( fiber f a)) ‚àòe
              ( equiv-precomp-equiv
                ( equiv-tot (Œª x ‚Üí equiv-is-small (H (f x) a)))
                ( Œ£ Y (Œª y ‚Üí type-is-small (H (g y) a)))) ‚àòe
              ( equiv-univalence))) ‚àòe
          ( equiv-funext)))
      ( is-torsorial-Id (X , f)))
    ( Œª Y ‚Üí ap (map-type-duality H))

emb-type-duality :
  {l l1 : Level} {A : UU l1} ‚Üí is-locally-small l A ‚Üí
  type-polynomial-endofunctor-UU l A ‚Ü™ type-exp-UU l A
pr1 (emb-type-duality H) = map-type-duality H
pr2 (emb-type-duality H) = is-emb-map-type-duality H
```

### A type `A` is small if and only if `P_ùí∞(A) ‚Ü™ P^ùí∞(A)` is an equivalence

#### The forward direction

```agda
module _
  {l l1 : Level} {A : UU l1} (H : is-small l A)
  where

  map-inv-type-duality :
    type-exp-UU l A ‚Üí type-polynomial-endofunctor-UU l A
  pr1 (map-inv-type-duality B) =
    type-is-small (is-small-Œ£ H (Œª a ‚Üí is-small' {l} {B a}))
  pr2 (map-inv-type-duality B) =
    ( pr1) ‚àò
    ( map-inv-equiv (equiv-is-small (is-small-Œ£ H (Œª a ‚Üí is-small' {l} {B a}))))

  is-section-map-inv-type-duality :
    map-type-duality (is-locally-small-is-small H) ‚àò map-inv-type-duality ~ id
  is-section-map-inv-type-duality B =
    eq-equiv-fam
      ( Œª a ‚Üí
        equivalence-reasoning
          map-type-duality
            ( is-locally-small-is-small H)
            ( map-inv-type-duality B)
            ( a)
          ‚âÉ fiber
            ( ( pr1 {B = B}) ‚àò
              ( map-inv-equiv
                ( equiv-is-small
                  ( is-small-Œ£ H (Œª a ‚Üí is-small'))))) a
            by
            equiv-tot
              ( Œª x ‚Üí
                inv-equiv
                  ( equiv-is-small
                    ( is-locally-small-is-small H
                      ( pr2 (map-inv-type-duality B) x)
                      ( a))))
          ‚âÉ Œ£ ( fiber (pr1 {B = B}) a)
              ( Œª b ‚Üí
                fiber
                  ( map-inv-equiv
                    ( equiv-is-small
                      ( is-small-Œ£ H (Œª a ‚Üí is-small' {l} {B a}))))
                  ( pr1 b))
            by compute-fiber-comp pr1 _ a
          ‚âÉ fiber (pr1 {B = B}) a
            by
            right-unit-law-Œ£-is-contr
              ( Œª b ‚Üí
                is-contr-map-is-equiv
                  ( is-equiv-map-inv-equiv
                    ( equiv-is-small
                      ( is-small-Œ£ H (Œª a ‚Üí is-small' {l} {B a}))))
                  ( pr1 b))
          ‚âÉ B a
            by
            equiv-fiber-pr1 B a)

  is-retraction-map-inv-type-duality :
    map-inv-type-duality ‚àò map-type-duality (is-locally-small-is-small H) ~ id
  is-retraction-map-inv-type-duality X =
    is-injective-is-emb
      ( is-emb-map-type-duality (is-locally-small-is-small H))
      ( is-section-map-inv-type-duality
        ( map-type-duality (is-locally-small-is-small H) X))

  is-equiv-map-type-duality :
    is-equiv (map-type-duality (is-locally-small-is-small H))
  is-equiv-map-type-duality =
    is-equiv-is-invertible
      map-inv-type-duality
      is-section-map-inv-type-duality
      is-retraction-map-inv-type-duality

  type-duality : type-polynomial-endofunctor-UU l A ‚âÉ type-exp-UU l A
  pr1 type-duality = map-type-duality (is-locally-small-is-small H)
  pr2 type-duality = is-equiv-map-type-duality
```

#### The converse direction

```agda
module _
  {l l1 : Level} {A : UU l1} (H : is-locally-small l A)
  where

  is-small-is-equiv-map-type-duality :
    is-equiv (map-type-duality H) ‚Üí is-small l A
  pr1 (is-small-is-equiv-map-type-duality E) =
    pr1 (map-inv-is-equiv E (Œª _ ‚Üí raise-unit l))
  pr2 (is-small-is-equiv-map-type-duality E) =
    inv-equiv
      ( ( pr2 (map-inv-is-equiv E (Œª _ ‚Üí raise-unit l))) ,
        ( is-equiv-is-contr-map
          ( Œª a ‚Üí
            is-contr-equiv
              ( raise-unit l)
              ( ( equiv-eq-fam _ _
                  ( is-section-map-inv-is-equiv E (Œª _ ‚Üí raise-unit l))
                  ( a)) ‚àòe
                ( equiv-tot
                  ( Œª x ‚Üí
                    equiv-is-small
                      ( H ( pr2 (map-inv-is-equiv E (Œª _ ‚Üí raise-unit l)) x)
                          ( a)))))
              ( is-contr-raise-unit))))
```

### Type duality formulated using `l1 ‚äî l2`

```agda
Fiber : {l l1 : Level} (A : UU l1) ‚Üí Slice l A ‚Üí A ‚Üí UU (l1 ‚äî l)
Fiber A f = fiber (pr2 f)

Pr1 : {l l1 : Level} (A : UU l1) ‚Üí (A ‚Üí UU l) ‚Üí Slice (l1 ‚äî l) A
pr1 (Pr1 A B) = Œ£ A B
pr2 (Pr1 A B) = pr1

is-section-Pr1 :
  {l1 l2 : Level} {A : UU l1} ‚Üí (Fiber {l1 ‚äî l2} A ‚àò Pr1 {l1 ‚äî l2} A) ~ id
is-section-Pr1 B = eq-equiv-fam (equiv-fiber-pr1 B)

is-retraction-Pr1 :
  {l1 l2 : Level} {A : UU l1} ‚Üí (Pr1 {l1 ‚äî l2} A ‚àò Fiber {l1 ‚äî l2} A) ~ id
is-retraction-Pr1 {A = A} (X , f) =
  eq-equiv-slice
    ( Pr1 A (Fiber A (X , f)))
    ( X , f)
    ( equiv-total-fiber f , triangle-map-equiv-total-fiber f)

is-equiv-Fiber :
  {l1 : Level} (l2 : Level) (A : UU l1) ‚Üí is-equiv (Fiber {l1 ‚äî l2} A)
is-equiv-Fiber l2 A =
  is-equiv-is-invertible
    ( Pr1 A)
    ( is-section-Pr1 {l2 = l2})
    ( is-retraction-Pr1 {l2 = l2})

equiv-Fiber :
  {l1 : Level} (l2 : Level) (A : UU l1) ‚Üí Slice (l1 ‚äî l2) A ‚âÉ (A ‚Üí UU (l1 ‚äî l2))
pr1 (equiv-Fiber l2 A) = Fiber A
pr2 (equiv-Fiber l2 A) = is-equiv-Fiber l2 A

is-equiv-Pr1 :
  {l1 : Level} (l2 : Level) (A : UU l1) ‚Üí is-equiv (Pr1 {l1 ‚äî l2} A)
is-equiv-Pr1 {l1} l2 A =
  is-equiv-is-invertible
    ( Fiber A)
    ( is-retraction-Pr1 {l2 = l2})
    ( is-section-Pr1 {l2 = l2})

equiv-Pr1 :
  {l1 : Level} (l2 : Level) (A : UU l1) ‚Üí (A ‚Üí UU (l1 ‚äî l2)) ‚âÉ Slice (l1 ‚äî l2) A
pr1 (equiv-Pr1 l2 A) = Pr1 A
pr2 (equiv-Pr1 l2 A) = is-equiv-Pr1 l2 A
```

The type of all function from `A ‚Üí B` is equivalent to the type of function
`Y : B ‚Üí ùí∞` with an equivalence `A ‚âÉ Œ£ B Y `

```agda
fiber-Œ£ :
  {l1 l2 : Level} (X : UU l1) (A : UU l2) ‚Üí
  (X ‚Üí A) ‚âÉ Œ£ (A ‚Üí UU (l2 ‚äî l1)) (Œª Y ‚Üí X ‚âÉ Œ£ A Y)
fiber-Œ£ {l1} {l2} X A =
  ( equiv-Œ£
    ( Œª Z ‚Üí X ‚âÉ Œ£ A Z)
    ( equiv-Fiber l1 A)
    ( Œª s ‚Üí inv-equiv ( equiv-postcomp-equiv (equiv-total-fiber (pr2 s)) X))) ‚àòe
  ( equiv-right-swap-Œ£) ‚àòe
  ( inv-left-unit-law-Œ£-is-contr
    ( is-contr-is-small-lmax l2 X)
    ( is-small-lmax l2 X)) ‚àòe
  ( equiv-precomp (inv-equiv (equiv-is-small (is-small-lmax l2 X))) A)
```

## See also

- In [`foundation.binary-type-duality`](foundation.binary-type-duality.md) we
  show that [binary relations](foundation.binary-relations.md) are equivalently
  described as [spans of types](foundation.spans.md).
