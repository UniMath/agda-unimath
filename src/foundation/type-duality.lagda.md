# Type duality

```agda
module foundation.type-duality where
```

```agda
open import foundation-core.fibers-of-maps
open import foundation-core.injective-maps
open import foundation.cartesian-product-types
open import foundation.contractible-maps
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.equality-cartesian-product-types
open import foundation.equality-dependent-pair-types
open import foundation.equational-reasoning
open import foundation.equivalences
open import foundation.equivalence-extensionality
open import foundation.function-extensionality
open import foundation.functions
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.inhabited-types
open import foundation.locally-small-types
open import foundation.polynomial-endofunctors
open import foundation.propositional-maps
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.slice
open import foundation.small-types
open import foundation.structure
open import foundation.surjective-maps
open import foundation.transport
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.type-theoretic-principle-of-choice
open import foundation.unit-type
open import foundation.univalence
open import foundation.universe-levels
```

</details>

## Idea

Given a univalent universe `ğ’°`, we can define two closely related functors acting on all types. First there is the covariant functor given by

```md
  P_ğ’°(A) := Î£ (X : ğ’°), X â†’ A.
```

This is a polynomial endofunctor. Second, there is the contravariant functor given by

```md
  P^ğ’°(A) := A â†’ ğ’°.
```

If the type `A` is locally ğ’°-small, then there is a map `Ï†_A : P_ğ’°(A) â†’ P^ğ’°(A)`. This map is natural in `A`, and it is always an embedding. Furthermore, the map `Ï†_A` is an equivalence if and only if `A` is ğ’°-small.

## Definitions

### The polynomial endofunctor of a universe

```agda
type-polynomial-endofunctor-UU :
  (l : Level) {l1 : Level} (A : UU l1) â†’ UU (lsuc l âŠ” l1)
type-polynomial-endofunctor-UU l = Slice l

map-polynomial-endofunctor-UU :
  (l : Level) {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  type-polynomial-endofunctor-UU l A â†’ type-polynomial-endofunctor-UU l B
map-polynomial-endofunctor-UU l = map-polynomial-endofunctor (UU l) (Î» X â†’ X)
```

### Type families

```agda
type-exp-UU : (l : Level) {l1 : Level} â†’ UU l1 â†’ UU (lsuc l âŠ” l1)
type-exp-UU l A = A â†’ UU l

map-exp-UU :
  (l : Level) {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  type-exp-UU l B â†’ type-exp-UU l A
map-exp-UU l f P = P âˆ˜ f
```

## Properties

### If `A` is locally `l`-small, then we can construct an embedding `type-polynomial-endofunctor l A â†ª type-exp-UU A`

```agda
map-type-duality :
  {l l1 : Level} {A : UU l1} â†’ is-locally-small l A â†’
  type-polynomial-endofunctor-UU l A â†’ type-exp-UU l A
map-type-duality H (X , f) a =
  Î£ X (Î» x â†’ type-is-small (H (f x) a))

is-emb-map-type-duality :
  {l l1 : Level} {A : UU l1} (H : is-locally-small l A) â†’
  is-emb (map-type-duality H)
is-emb-map-type-duality
  {l} {l1} {A} H (X , f) =
  fundamental-theorem-id
    ( is-contr-equiv
      ( Î£ ( type-polynomial-endofunctor-UU l A) (Î» Y â†’ (X , f) ï¼ Y))
      ( equiv-tot
        ( Î» (Y , g) â†’
          equivalence-reasoning
            ( map-type-duality H
                (X , f) ï¼
              map-type-duality H
                (Y , g))
            â‰ƒ ( (a : A) â†’
                Î£ X (Î» x â†’ type-is-small (H (f x) a)) ï¼
                Î£ Y (Î» y â†’ type-is-small (H (g y) a)))
              by equiv-funext
            â‰ƒ ( (a : A) â†’
                Î£ X (Î» x â†’ type-is-small (H (f x) a)) â‰ƒ
                Î£ Y (Î» y â†’ type-is-small (H (g y) a)))
              by equiv-map-Î  (Î» a â†’ equiv-univalence)
            â‰ƒ ( (a : A) â†’
                fib f a â‰ƒ Î£ Y (Î» y â†’ type-is-small (H (g y) a)))
              by
              equiv-map-Î 
                ( Î» a â†’
                  equiv-precomp-equiv
                    ( equiv-tot (Î» x â†’ equiv-is-small (H (f x) a)))
                    ( Î£ Y (Î» y â†’ type-is-small (H (g y) a))))
            â‰ƒ ( (a : A) â†’ fib f a â‰ƒ fib g a)
              by
              equiv-map-Î 
                ( Î» a â†’
                  equiv-postcomp-equiv
                    ( equiv-tot (Î» y â†’ inv-equiv (equiv-is-small (H (g y) a))))
                    ( fib f a))
            â‰ƒ equiv-slice f g
              by inv-equiv (equiv-fam-equiv-equiv-slice f g)
            â‰ƒ ( (X , f) ï¼ (Y , g))
              by
              inv-equiv (extensionality-Slice (X , f) (Y , g)) ))
      ( is-contr-total-path (X , f)))
    ( Î» Y â†’ ap (map-type-duality H))

emb-type-duality :
  {l l1 : Level} {A : UU l1} â†’ is-locally-small l A â†’
  type-polynomial-endofunctor-UU l A â†ª type-exp-UU l A
pr1 (emb-type-duality H) =
  map-type-duality H
pr2 (emb-type-duality H) =
  is-emb-map-type-duality H
```

### A type `A` is small if and only if `P_ğ’°(A) â†ª P^ğ’°(A)` is an equivalence

#### The forward direction

```agda
module _
  {l l1 : Level} {A : UU l1} (H : is-small l A)
  where

  map-inv-type-duality :
    type-exp-UU l A â†’ type-polynomial-endofunctor-UU l A
  pr1 (map-inv-type-duality B) =
    type-is-small (is-small-Î£ {l3 = l} {l4 = l} H (Î» a â†’ is-small' {l} {B a}))
  pr2 (map-inv-type-duality B) =
    ( pr1) âˆ˜
    ( map-inv-equiv
      ( equiv-is-small
        ( is-small-Î£ {l3 = l} {l4 = l} H (Î» a â†’ is-small' {l} {B a}))))

  issec-map-inv-type-duality :
    ( map-type-duality (is-locally-small-is-small H) âˆ˜ map-inv-type-duality) ~
    id
  issec-map-inv-type-duality B =
    eq-equiv-fam
      ( Î» a â†’
        equivalence-reasoning
          map-type-duality
            ( is-locally-small-is-small H)
            ( map-inv-type-duality B)
            ( a)
          â‰ƒ fib
            ( ( pr1 {B = B}) âˆ˜
              ( map-inv-equiv
                ( equiv-is-small
                  ( is-small-Î£ H (Î» a â†’ is-small'))))) a
            by
            equiv-tot
              ( Î» x â†’
                inv-equiv
                  ( equiv-is-small
                    ( is-locally-small-is-small H
                      ( pr2 (map-inv-type-duality B) x)
                      ( a))))
          â‰ƒ Î£ ( fib (pr1 {B = B}) a)
              ( Î» b â†’
                fib
                  ( map-inv-equiv
                    ( equiv-is-small
                      ( is-small-Î£ H (Î» a â†’ is-small' {l} {B a}))))
                  ( pr1 b))
            by equiv-compute-fib-comp pr1 _ a
          â‰ƒ fib (pr1 {B = B}) a
            by
            right-unit-law-Î£-is-contr
              ( Î» b â†’
                is-contr-map-is-equiv
                  ( is-equiv-map-inv-equiv
                    ( equiv-is-small
                      ( is-small-Î£ H (Î» a â†’ is-small' {l} {B a}))))
                  ( pr1 b))
          â‰ƒ B a
            by
            equiv-fib-pr1 B a)

  isretr-map-inv-type-duality :
    ( map-inv-type-duality âˆ˜ map-type-duality (is-locally-small-is-small H)) ~
    id
  isretr-map-inv-type-duality X =
    is-injective-is-emb
      ( is-emb-map-type-duality (is-locally-small-is-small H))
      ( issec-map-inv-type-duality
        ( map-type-duality (is-locally-small-is-small H) X))

  is-equiv-map-type-duality :
    is-equiv (map-type-duality (is-locally-small-is-small H))
  is-equiv-map-type-duality =
    is-equiv-has-inverse
      map-inv-type-duality
      issec-map-inv-type-duality
      isretr-map-inv-type-duality

  type-duality : type-polynomial-endofunctor-UU l A â‰ƒ type-exp-UU l A
  pr1 type-duality = map-type-duality (is-locally-small-is-small H)
  pr2 type-duality = is-equiv-map-type-duality
```

#### The converse direction

```agda
module _
  {l l1 : Level} {A : UU l1} (H : is-locally-small l A)
  where

  is-small-is-equiv-map-type-duality :
    is-equiv (map-type-duality H) â†’ is-small l A
  pr1 (is-small-is-equiv-map-type-duality E) =
    pr1 (map-inv-is-equiv E (Î» a â†’ raise-unit l))
  pr2 (is-small-is-equiv-map-type-duality E) =
    inv-equiv
      ( pair
        ( pr2 (map-inv-is-equiv E (Î» a â†’ raise-unit l)))
        ( is-equiv-is-contr-map
          ( Î» a â†’
            is-contr-equiv
              ( raise-unit l)
              ( ( equiv-eq-fam _ _
                  ( issec-map-inv-is-equiv E (Î» _ â†’ raise-unit l))
                  ( a)) âˆ˜e
                ( equiv-tot
                  ( Î» x â†’
                    equiv-is-small
                      ( H ( pr2 (map-inv-is-equiv E (Î» _ â†’ raise-unit l)) x)
                          ( a)))))
              ( is-contr-raise-unit))))
```

### Type duality formulated using `l1 âŠ” l2`

```agda
Fib : {l l1 : Level} (A : UU l1) â†’ Slice l A â†’ A â†’ UU (l1 âŠ” l)
Fib A f = fib (pr2 f)

Pr1 : {l l1 : Level} (A : UU l1) â†’ (A â†’ UU l) â†’ Slice (l1 âŠ” l) A
pr1 (Pr1 A B) = Î£ A B
pr2 (Pr1 A B) = pr1

issec-Pr1 :
  {l1 l2 : Level} {A : UU l1} â†’ (Fib {l1 âŠ” l2} A âˆ˜ Pr1 {l1 âŠ” l2} A) ~ id
issec-Pr1 B = eq-equiv-fam (equiv-fib-pr1 B)

isretr-Pr1 :
  {l1 l2 : Level} {A : UU l1} â†’ (Pr1 {l1 âŠ” l2} A âˆ˜ Fib {l1 âŠ” l2} A) ~ id
isretr-Pr1 {A = A} (pair X f) =
  eq-equiv-slice
    ( Pr1 A (Fib A (pair X f)))
    ( pair X f)
    ( pair (equiv-total-fib f) (triangle-map-equiv-total-fib f))

is-equiv-Fib :
  {l1 : Level} (l2 : Level) (A : UU l1) â†’ is-equiv (Fib {l1 âŠ” l2} A)
is-equiv-Fib l2 A =
  is-equiv-has-inverse (Pr1 A) (issec-Pr1 {l2 = l2}) (isretr-Pr1 {l2 = l2})

equiv-Fib :
  {l1 : Level} (l2 : Level) (A : UU l1) â†’ Slice (l1 âŠ” l2) A â‰ƒ (A â†’ UU (l1 âŠ” l2))
pr1 (equiv-Fib l2 A) = Fib A
pr2 (equiv-Fib l2 A) = is-equiv-Fib l2 A

is-equiv-Pr1 :
  {l1 : Level} (l2 : Level) (A : UU l1) â†’ is-equiv (Pr1 {l1 âŠ” l2} A)
is-equiv-Pr1 {l1} l2 A =
  is-equiv-has-inverse (Fib A) (isretr-Pr1 {l2 = l2}) (issec-Pr1 {l2 = l2})

equiv-Pr1 :
  {l1 : Level} (l2 : Level) (A : UU l1) â†’ (A â†’ UU (l1 âŠ” l2)) â‰ƒ Slice (l1 âŠ” l2) A
pr1 (equiv-Pr1 l2 A) = Pr1 A
pr2 (equiv-Pr1 l2 A) = is-equiv-Pr1 l2 A
```

### Structured type duality

```agda
Slice-structure :
  {l1 l2 : Level} (l : Level) (P : UU (l1 âŠ” l) â†’ UU l2) (B : UU l1) â†’
  UU (l1 âŠ” l2 âŠ” lsuc l)
Slice-structure l P B = Î£ (UU l) (Î» A â†’ hom-structure P A B)

equiv-Fib-structure :
  {l1 l2 : Level} (l : Level) (P : UU (l1 âŠ” l) â†’ UU l2) (B : UU l1) â†’
  Slice-structure (l1 âŠ” l) P B â‰ƒ fam-structure P B
equiv-Fib-structure {l1} {l3} l P B =
  ( ( inv-distributive-Î -Î£) âˆ˜e
    ( equiv-Î£
      ( Î» C â†’ (b : B) â†’ P (C b))
      ( equiv-Fib l B)
      ( Î» f â†’ equiv-map-Î  (Î» b â†’ id-equiv)))) âˆ˜e
  ( inv-assoc-Î£ (UU (l1 âŠ” l)) (Î» A â†’ A â†’ B) (Î» f â†’ structure-map P (pr2 f)))

equiv-fixed-Slice-structure :
  (l : Level) (P : UU l â†’ UU l) (A : UU l) (B : UU l)
  ( slice : Slice-structure l P B) â†’
  ( A â‰ƒ
      Î£ B
        ( Î» b â†’ pr1 ( ( map-equiv (equiv-Fib-structure l P B) slice) b))) â‰ƒ
   ( pr1 (slice) ï¼ A)
equiv-fixed-Slice-structure l P A B slice =
  ( ( inv-equiv equiv-univalence) âˆ˜e
  ( ( equiv-inv-equiv )âˆ˜e
  ( equiv-postcomp-equiv (equiv-total-fib (pr1 (pr2 slice))) A)))

```

### Subtype duality

```agda
Slice-emb : (l : Level) {l1 : Level} (A : UU l1) â†’ UU (lsuc l âŠ” l1)
Slice-emb l A = Î£ (UU l) (Î» X â†’ X â†ª A)

equiv-Fib-Prop :
  (l : Level) {l1 : Level} (A : UU l1) â†’
  Slice-emb (l1 âŠ” l) A â‰ƒ (A â†’ Prop (l1 âŠ” l))
equiv-Fib-Prop l A =
  ( equiv-Fib-structure l is-prop A) âˆ˜e
  ( equiv-tot (Î» X â†’ equiv-tot equiv-is-prop-map-is-emb))

Slice-surjection : (l : Level) {l1 : Level} (A : UU l1) â†’ UU (lsuc l âŠ” l1)
Slice-surjection l A = Î£ (UU l) (Î» X â†’ X â†  A)

equiv-Fib-trunc-Prop :
  (l : Level) {l1 : Level} (A : UU l1) â†’
  Slice-surjection (l1 âŠ” l) A â‰ƒ (A â†’ Inhabited-Type (l1 âŠ” l))
equiv-Fib-trunc-Prop l A =
  ( equiv-Fib-structure l is-inhabited A)
```

The type of all function from `A â†’ B` is equivalent to the type of function `Y : B â†’ ğ’°` with an equivalence `A â‰ƒ Î£ B Y `

```agda
module _
  {l1 l2 : Level} (A : UU l1) (B : UU l2)
  where
  map-type-duality-functions : (A â†’ B) â†’ Î£ (B â†’ UU (l1 âŠ” l2)) (Î» Y â†’ A â‰ƒ Î£ B Y)
  pr1 (map-type-duality-functions f) = fib f
  pr2 (map-type-duality-functions f) = inv-equiv-total-fib f

  map-inv-type-duality-functions :  Î£ ((B â†’ UU l2)) (Î» Y â†’ A â‰ƒ Î£ B Y) â†’ (A â†’ B)
  map-inv-type-duality-functions ( Y , e) = pr1 âˆ˜ map-equiv e

issec-map-inv-type-duality-functions :
  {l1 l2 : Level} {A : UU l1} {B : UU (l1 âŠ” l2)}â†’
  (map-type-duality-functions A B âˆ˜ map-inv-type-duality-functions A B) ~ id
issec-map-inv-type-duality-functions {A = A} {B = B} (Y , e) =
  eq-pair-Î£
    ( eq-htpy
      ( Î» b â†’
        eq-equiv
          ( Î£ A ( Î» a â†’ (pr1 ((map-equiv e) a) ï¼ b)))
          ( Y b)
          ( equiv-fib-pr1 Y b âˆ˜e
            ( equiv-Î£-equiv-base
                ( Î» x â†’ pr1 x ï¼ b)
                ( e)))))
    ( eq-htpy-equiv
      ( Î» a â†’
        ( ( ap
            ( Î» f â†’ map-equiv f a)
            ( lemma
              ( e)
              ( Î» b â†’
                equiv-fib-pr1 Y b âˆ˜e equiv-Î£-equiv-base (Î» x â†’ pr1 x ï¼ b) e)
              ( pr2
                ( map-type-duality-functions A B
                  ( map-inv-type-duality-functions A B (Y , e)))))) âˆ™
          ( eq-pair-Î£ refl refl))))
  where
    lemma-tr :
      {l1 l2 : Level} {A : UU l1} {B : UU l2} {Y0 Y1 : B â†’ UU l2} â†’
      (e : A â‰ƒ Î£ B Y1) (p : Y0 ï¼ Y1) (h : A â‰ƒ Î£ B Y0) â†’
      ( tr
        ( Î» Y â†’ A â‰ƒ Î£ B Y)
        ( p)
        ( h)) ï¼
      ( equiv-Î£ Y1 id-equiv ( Î» b â†’ equiv-eq (htpy-eq p b)) âˆ˜e h)
    lemma-tr e refl h =
      ( ( inv (left-unit-law-equiv h)) âˆ™
        ( ap (Î» f â†’ f âˆ˜e h) ( eq-htpy-equiv refl-htpy)))

    lemma :
      {l1 l2 : Level} {A : UU l1} {B : UU l2} {Y0 Y1 : B â†’ UU l2} â†’
      (e : A â‰ƒ Î£ B Y1) (H : (b : B) â†’ (Y0 b â‰ƒ Y1 b)) (h : A â‰ƒ Î£ B Y0) â†’
      ( tr
        ( Î» Y â†’ A â‰ƒ Î£ B Y)
        ( eq-htpy ( Î» b â†’ eq-equiv ( Y0 b) ( Y1 b) ( H b)))
        ( h)) ï¼
      ( equiv-Î£ Y1 id-equiv ( Î» b â†’ H b) âˆ˜e h)
    lemma  {Y0 = Y0} {Y1 = Y1} e H h =
      ( ( lemma-tr
          ( e)
          ( ( eq-htpy ( Î» b â†’ eq-equiv ( Y0 b) ( Y1 b) ( H b))))
          ( h)) âˆ™
        ( ap
          ( Î» p â†’ equiv-Î£ Y1 id-equiv p âˆ˜e h )
          ( ( ap
              ( Î» p â†’ equiv-eq âˆ˜ p)
              ( issec-eq-htpy (Î» b â†’ eq-equiv (Y0 b) (Y1 b) (H b)))) âˆ™
            ( eq-htpy
              ( Î» b â†’
                issec-map-inv-is-equiv (univalence (Y0 b) (Y1 b)) (H b))))))

isretr-map-inv-type-duality-functions :
  {l1 l2 : Level} {A : UU l1} {B : UU (l1 âŠ” l2)}â†’
  (map-inv-type-duality-functions A B âˆ˜ map-type-duality-functions A B) ~ id
isretr-map-inv-type-duality-functions = refl-htpy

is-equiv-map-type-duality-functions :
  {l1 : Level} (l2 : Level) (A : UU l1) (B : UU (l1 âŠ” l2)) â†’
  is-equiv (map-type-duality-functions A B)
is-equiv-map-type-duality-functions l2 A B =
  is-equiv-has-inverse
    ( map-inv-type-duality-functions A B)
    ( issec-map-inv-type-duality-functions {l2 = l2})
    ( isretr-map-inv-type-duality-functions {l2 = l2})

type-duality-functions :
  {l1 : Level} (l2 : Level) (A : UU l1) (B : UU (l1 âŠ” l2))â†’
  (A â†’ B) â‰ƒ Î£ (B â†’ UU (l1 âŠ” l2)) (Î» Y â†’ A â‰ƒ Î£ B Y)
pr1 (type-duality-functions l2 A B) = map-type-duality-functions A B
pr2 (type-duality-functions l2 A B) = is-equiv-map-type-duality-functions l2 A B
```
