# The complement of the image of a map

```agda
module foundation.complements-images where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.fundamental-theorem-of-identity-types
open import foundation.negation
open import foundation.subtype-identity-principle
open import foundation.universe-levels

open import foundation-core.1-types
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.identity-types
open import foundation-core.injective-maps
open import foundation-core.propositions
open import foundation-core.sets
open import foundation-core.subtypes
open import foundation-core.torsorial-type-families
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
```

</details>

## Idea

The {{#concept "complement" Disambiguation="of the image of a map" Agda=nonim}}
of the [image](foundation.images.md) of a map `f : A â†’ B` is the collection of
elements `y` in `B` such that the fiber of `f` over `y` is
[empty](foundation.empty-types.md).

## Definitions

```agda
module _
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  where

  subtype-nonim : subtype (l1 âŠ” l2) X
  subtype-nonim x = neg-type-Prop (fiber f x)

  is-in-nonim : X â†’ UU (l1 âŠ” l2)
  is-in-nonim = is-in-subtype subtype-nonim

  nonim : UU (l1 âŠ” l2)
  nonim = type-subtype subtype-nonim

  inclusion-nonim : nonim â†’ X
  inclusion-nonim = inclusion-subtype subtype-nonim
```

## Properties

### We characterize the identity type of `nonim f`

```agda
module _
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  where

  Eq-nonim : nonim f â†’ nonim f â†’ UU l1
  Eq-nonim x y = (pr1 x ï¼ pr1 y)

  refl-Eq-nonim : (x : nonim f) â†’ Eq-nonim x x
  refl-Eq-nonim x = refl

  Eq-eq-nonim : (x y : nonim f) â†’ x ï¼ y â†’ Eq-nonim x y
  Eq-eq-nonim x .x refl = refl-Eq-nonim x

  abstract
    is-torsorial-Eq-nonim :
      (x : nonim f) â†’ is-torsorial (Eq-nonim x)
    is-torsorial-Eq-nonim (x , p) =
      is-torsorial-Eq-subtype (is-torsorial-Id x) (Î» x â†’ is-prop-neg) x refl p

  abstract
    is-equiv-Eq-eq-nonim : (x y : nonim f) â†’ is-equiv (Eq-eq-nonim x y)
    is-equiv-Eq-eq-nonim x =
      fundamental-theorem-id (is-torsorial-Eq-nonim x) (Eq-eq-nonim x)

  equiv-Eq-eq-nonim : (x y : nonim f) â†’ (x ï¼ y) â‰ƒ Eq-nonim x y
  equiv-Eq-eq-nonim x y = (Eq-eq-nonim x y , is-equiv-Eq-eq-nonim x y)

  eq-Eq-nonim : (x y : nonim f) â†’ Eq-nonim x y â†’ x ï¼ y
  eq-Eq-nonim x y = map-inv-is-equiv (is-equiv-Eq-eq-nonim x y)
```

### The nonimage inclusion is an embedding

```agda
abstract
  is-emb-inclusion-nonim :
    {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-emb (inclusion-nonim f)
  is-emb-inclusion-nonim f = is-emb-inclusion-subtype (neg-type-Prop âˆ˜ fiber f)

emb-nonim :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’ nonim f â†ª X
emb-nonim f = (inclusion-nonim f , is-emb-inclusion-nonim f)
```

### The nonimage inclusion is injective

```agda
abstract
  is-injective-inclusion-nonim :
    {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-injective (inclusion-nonim f)
  is-injective-inclusion-nonim f =
    is-injective-is-emb (is-emb-inclusion-nonim f)
```

### The nonimage of a map into a truncated type is truncated

```agda
abstract
  is-trunc-nonim :
    {l1 l2 : Level} (k : ğ•‹) {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-trunc (succ-ğ•‹ k) X â†’ is-trunc (succ-ğ•‹ k) (nonim f)
  is-trunc-nonim k f = is-trunc-emb k (emb-nonim f)

nonim-Truncated-Type :
  {l1 l2 : Level} (k : ğ•‹) (X : Truncated-Type l1 (succ-ğ•‹ k)) {A : UU l2}
  (f : A â†’ type-Truncated-Type X) â†’ Truncated-Type (l1 âŠ” l2) (succ-ğ•‹ k)
nonim-Truncated-Type k X f =
  ( nonim f , is-trunc-nonim k f (is-trunc-type-Truncated-Type X))
```

### The nonimage of a map into a proposition is a proposition

```agda
abstract
  is-prop-nonim :
    {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-prop X â†’ is-prop (nonim f)
  is-prop-nonim = is-trunc-nonim neg-two-ğ•‹

nonim-Prop :
    {l1 l2 : Level} (X : Prop l1) {A : UU l2}
    (f : A â†’ type-Prop X) â†’ Prop (l1 âŠ” l2)
nonim-Prop X f = nonim-Truncated-Type neg-two-ğ•‹ X f
```

### The nonimage of a map into a set is a set

```agda
abstract
  is-set-nonim :
    {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-set X â†’ is-set (nonim f)
  is-set-nonim = is-trunc-nonim neg-one-ğ•‹

nonim-Set :
  {l1 l2 : Level} (X : Set l1) {A : UU l2}
  (f : A â†’ type-Set X) â†’ Set (l1 âŠ” l2)
nonim-Set X f = nonim-Truncated-Type (neg-one-ğ•‹) X f
```

### The nonimage of a map into a 1-type is a 1-type

```agda
abstract
  is-1-type-nonim :
    {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
    is-1-type X â†’ is-1-type (nonim f)
  is-1-type-nonim = is-trunc-nonim zero-ğ•‹

nonim-1-Type :
  {l1 l2 : Level} (X : 1-Type l1) {A : UU l2}
  (f : A â†’ type-1-Type X) â†’ 1-Type (l1 âŠ” l2)
nonim-1-Type X f = nonim-Truncated-Type zero-ğ•‹ X f
```
