# Propositional maps

```agda
module foundation.propositional-maps where

open import foundation-core.propositional-maps public
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.function-types
open import foundation.logical-equivalences
open import foundation.truncated-maps
open import foundation.universe-levels

open import foundation-core.equivalences
open import foundation-core.homotopies
open import foundation-core.propositions
open import foundation-core.truncation-levels
```

</details>

## Properties

### Being a propositional map is a property

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-prop-is-prop-map : (f : A â†’ B) â†’ is-prop (is-prop-map f)
  is-prop-is-prop-map f = is-prop-is-trunc-map neg-one-ğ•‹ f

  is-prop-map-Prop : (A â†’ B) â†’ Prop (l1 âŠ” l2)
  pr1 (is-prop-map-Prop f) = is-prop-map f
  pr2 (is-prop-map-Prop f) = is-prop-is-prop-map f
```

### Being a propositional map is equivalent to being an embedding

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  equiv-is-emb-is-prop-map : (f : A â†’ B) â†’ is-prop-map f â‰ƒ is-emb f
  equiv-is-emb-is-prop-map f =
    equiv-iff
      ( is-prop-map-Prop f)
      ( is-emb-Prop f)
      ( is-emb-is-prop-map)
      ( is-prop-map-is-emb)

  equiv-is-prop-map-is-emb : (f : A â†’ B) â†’ is-emb f â‰ƒ is-prop-map f
  equiv-is-prop-map-is-emb f =
    equiv-iff
      ( is-emb-Prop f)
      ( is-prop-map-Prop f)
      ( is-prop-map-is-emb)
      ( is-emb-is-prop-map)
```

### Propositional maps are closed under homotopies

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f g : A â†’ B} (H : f ~ g)
  where

  is-prop-map-htpy : is-prop-map g â†’ is-prop-map f
  is-prop-map-htpy = is-trunc-map-htpy neg-one-ğ•‹ H
```

### Propositional maps are closed under composition

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (g : B â†’ X) (h : A â†’ B)
  where

  is-prop-map-comp : is-prop-map g â†’ is-prop-map h â†’ is-prop-map (g âˆ˜ h)
  is-prop-map-comp = is-trunc-map-comp neg-one-ğ•‹ g h

comp-prop-map :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {X : UU l3} (g : prop-map B X) (h : prop-map A B) â†’
  prop-map A X
pr1 (comp-prop-map g h) = pr1 g âˆ˜ pr1 h
pr2 (comp-prop-map g h) =
  is-prop-map-comp (pr1 g) (pr1 h) (pr2 g) (pr2 h)
```

### In a commuting triangle `f ~ g âˆ˜ h`, if `g` and `h` are propositional maps, then `f` is a propositional map

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h))
  where

  is-prop-map-left-map-triangle :
    is-prop-map g â†’ is-prop-map h â†’ is-prop-map f
  is-prop-map-left-map-triangle =
    is-trunc-map-left-map-triangle neg-one-ğ•‹ f g h H
```

### In a commuting triangle `f ~ g âˆ˜ h`, if `f` and `g` are propositional maps, then `h` is a propositional map

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h))
  where

  is-prop-map-top-map-triangle :
    is-prop-map g â†’ is-prop-map f â†’ is-prop-map h
  is-prop-map-top-map-triangle =
    is-trunc-map-top-map-triangle neg-one-ğ•‹ f g h H
```

### If a composite `g âˆ˜ h` and its left factor `g` are propositional maps, then its right factor `h` is a propositional map

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (g : B â†’ X) (h : A â†’ B)
  where

  is-prop-map-right-factor :
    is-prop-map g â†’ is-prop-map (g âˆ˜ h) â†’ is-prop-map h
  is-prop-map-right-factor =
    is-trunc-map-right-factor neg-one-ğ•‹ g h
```

### A `-1`-truncated map is `k+1`-truncated

```agda
abstract
  is-trunc-map-is-prop-map :
    {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-prop-map f â†’ is-trunc-map (succ-ğ•‹ k) f
  is-trunc-map-is-prop-map neg-two-ğ•‹ H = H
  is-trunc-map-is-prop-map (succ-ğ•‹ k) H =
    is-trunc-map-succ-is-trunc-map (succ-ğ•‹ k) (is-trunc-map-is-prop-map k H)
```
