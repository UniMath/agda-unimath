# Iterated dependent pair types

```agda
module foundation.iterated-dependent-pair-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.dependent-pair-types
open import foundation.telescopes
open import foundation.universe-levels

open import foundation-core.cartesian-product-types
open import foundation-core.contractible-types
```

</details>

## Idea

**Iterated dependent pair types** are defined by iteratively applying the
[dependent pair](foundation.dependent-pair-types.md) operator `Î£`. More
formally, `iterated-Î£` is defined as an operation `telescope l n â†’ UU l` from
the type of [telescopes](foundation.telescopes.md) to the universe of types of
universe level `l`. For example, the iterated dependent pair type of the
telescope

```text
  Aâ‚€ : ğ’° lâ‚€
  Aâ‚ : Aâ‚€ â†’ ğ’° lâ‚
  Aâ‚‚ : (xâ‚€ : Aâ‚€) â†’ Aâ‚ xâ‚€ â†’ ğ’° lâ‚‚
  Aâ‚ƒ : (xâ‚€ : Aâ‚€) (xâ‚ : Aâ‚ xâ‚€) â†’ Aâ‚‚ xâ‚€ xâ‚ â†’ ğ’° lâ‚ƒ
```

is the dependent pair type

```text
  Î£ Aâ‚€ (Î» xâ‚€ â†’ Î£ (Aâ‚ xâ‚€) (Î» xâ‚ â†’ Î£ (Aâ‚‚ xâ‚€ xâ‚) (Aâ‚ƒ xâ‚€ xâ‚)))
```

of universe level `lâ‚€ âŠ” lâ‚ âŠ” lâ‚‚ âŠ” lâ‚ƒ`.

## Definitions

### Iterated dependent products of iterated type families

```agda
iterated-Î£ : {l : Level} {n : â„•} â†’ telescope l n â†’ UU l
iterated-Î£ (base-telescope A) = A
iterated-Î£ (cons-telescope {X = X} A) = Î£ X (Î» x â†’ iterated-Î£ (A x))
```

### Iterated elements of iterated type families

```agda
data
  iterated-element : {l : Level} {n : â„•} â†’ telescope l n â†’ UUÏ‰
  where
  base-iterated-element :
    {l1 : Level} {A : UU l1} â†’ A â†’ iterated-element (base-telescope A)
  cons-iterated-element :
    {l1 l2 : Level} {n : â„•} {X : UU l1} {Y : X â†’ telescope l2 n} â†’
    (x : X) â†’ iterated-element (Y x) â†’ iterated-element (cons-telescope Y)
```

### Iterated pairing

```agda
iterated-pair :
  {l : Level} {n : â„•} {A : telescope l n} â†’
  iterated-element A â†’ iterated-Î£ A
iterated-pair (base-iterated-element x) = x
pr1 (iterated-pair (cons-iterated-element y a)) = y
pr2 (iterated-pair (cons-iterated-element y a)) = iterated-pair a
```

## Properties

### Contractiblity of iterated Î£-types

```agda
is-contr-Î£-telescope : {l : Level} {n : â„•} â†’ telescope l n â†’ UU l
is-contr-Î£-telescope (base-telescope A) = is-contr A
is-contr-Î£-telescope (cons-telescope {X = X} A) =
  (is-contr X) Ã— (Î£ X (Î» x â†’ is-contr-Î£-telescope (A x)))

is-contr-iterated-Î£ :
  {l : Level} (n : â„•) {{A : telescope l n}} â†’
  is-contr-Î£-telescope A â†’ is-contr (iterated-Î£ A)
is-contr-iterated-Î£ .0 {{base-telescope A}} is-contr-A = is-contr-A
is-contr-iterated-Î£ ._ {{cons-telescope A}} (is-contr-X , x , H) =
  is-contr-Î£ is-contr-X x (is-contr-iterated-Î£ _ {{A x}} H)
```

### Contractiblity of iterated Î£-types without choice of contracting center

```agda
is-contr-Î£-telescope' : {l : Level} {n : â„•} â†’ telescope l n â†’ UU l
is-contr-Î£-telescope' (base-telescope A) = is-contr A
is-contr-Î£-telescope' (cons-telescope {X = X} A) =
  (is-contr X) Ã— ((x : X) â†’ is-contr-Î£-telescope' (A x))

is-contr-iterated-Î£' :
  {l : Level} (n : â„•) {{A : telescope l n}} â†’
  is-contr-Î£-telescope' A â†’ is-contr (iterated-Î£ A)
is-contr-iterated-Î£' .0 {{base-telescope A}} is-contr-A = is-contr-A
is-contr-iterated-Î£' ._ {{cons-telescope A}} (is-contr-X , H) =
  is-contr-Î£' is-contr-X (Î» x â†’ is-contr-iterated-Î£' _ {{A x}} (H x))
```

## See also

- [Iterated Î -types](foundation.iterated-dependent-product-types.md)
