# Functorialty of pullbacks

```agda
open import foundation.function-extensionality-axiom

module
  foundation.functoriality-pullbacks
  (funext : function-extensionality)
  where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cones-over-cospan-diagrams funext
open import foundation.cospan-diagrams
open import foundation.dependent-pair-types
open import foundation.equality-dependent-pair-types funext
open import foundation.function-types funext
open import foundation.homotopies funext
open import foundation.homotopies funext-morphisms-cospan-diagrams
open import foundation.morphisms-cospan-diagrams
open import foundation.pullback-cones funext
open import foundation.standard-pullbacks funext
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import foundation-core.equivalences
open import foundation-core.identity-types
open import foundation-core.pullbacks funext
```

</details>

## Idea

The construction of the [standard pullback](foundation-core.pullbacks.md) is
functorial.

## Definitions

### The functorial action on maps of pullbacks

```agda
module _
  {l1 l2 l3 l1' l2' l3' : Level}
  (ğ’® : cospan-diagram l1 l2 l3)
  (ğ’¯ : cospan-diagram l1' l2' l3')
  where

  map-standard-pullback :
    hom-cospan-diagram ğ’® ğ’¯ â†’
    standard-pullback (left-map-cospan-diagram ğ’®) (right-map-cospan-diagram ğ’®) â†’
    standard-pullback (left-map-cospan-diagram ğ’¯) (right-map-cospan-diagram ğ’¯)
  map-standard-pullback (hA , hB , hX , HA , HB) (a' , b' , p') =
    ( hA a' , hB b' , HA a' âˆ™ ap hX p' âˆ™ inv (HB b'))

  map-pullback-cone :
    {l4 l4' : Level} (c : pullback-cone ğ’® l4) (c' : pullback-cone ğ’¯ l4') â†’
    hom-cospan-diagram ğ’® ğ’¯ â†’
    domain-pullback-cone ğ’® c â†’ domain-pullback-cone ğ’¯ c'
  map-pullback-cone c c' h x =
    map-inv-is-equiv
      ( is-pullback-pullback-cone ğ’¯ c')
      ( map-standard-pullback h
        ( gap
          ( left-map-cospan-diagram ğ’®)
          ( right-map-cospan-diagram ğ’®)
          ( cone-pullback-cone ğ’® c)
          ( x)))
```

## Properties

### The functorial action on maps of pullbacks preserves identities

```agda
module _
  {l1 l2 l3 : Level} (ğ’® : cospan-diagram l1 l2 l3)
  where

  preserves-id-map-standard-pullback :
    map-standard-pullback ğ’® ğ’® (id-hom-cospan-diagram ğ’®) ~ id
  preserves-id-map-standard-pullback x =
    eq-pair-eq-fiber
      ( eq-pair-eq-fiber
        ( right-unit âˆ™ ap-id (coherence-square-standard-pullback x)))

  preserves-id-map-pullback-cone :
    {l4 : Level} (c : pullback-cone ğ’® l4) â†’
    map-pullback-cone ğ’® ğ’® c c (id-hom-cospan-diagram ğ’®) ~ id
  preserves-id-map-pullback-cone c =
    ( ( map-inv-is-equiv (is-pullback-pullback-cone ğ’® c)) Â·l
      ( preserves-id-map-standard-pullback) Â·r
      ( gap
        ( left-map-cospan-diagram ğ’®)
        ( right-map-cospan-diagram ğ’®)
        ( cone-pullback-cone ğ’® c))) âˆ™h
    ( is-retraction-map-inv-is-equiv (is-pullback-pullback-cone ğ’® c))
```

### The functorial action on maps of pullbacks preserves composition

```agda
module _
  {l1 l2 l3 l1' l2' l3' l1'' l2'' l3'' : Level}
  (ğ’® : cospan-diagram l1 l2 l3)
  (ğ’¯ : cospan-diagram l1' l2' l3')
  (â„› : cospan-diagram l1'' l2'' l3'')
  where

  preserves-comp-map-standard-pullback :
    (h : hom-cospan-diagram ğ’¯ â„›)
    (h' : hom-cospan-diagram ğ’® ğ’¯) â†’
    map-standard-pullback ğ’® â„› (comp-hom-cospan-diagram ğ’® ğ’¯ â„› h h') ~
    map-standard-pullback ğ’¯ â„› h âˆ˜ map-standard-pullback ğ’® ğ’¯ h'
  preserves-comp-map-standard-pullback
    ( hA , hB , hX , H , K) (hA' , hB' , hX' , H' , K') (x , y , p) =
    eq-pair-eq-fiber
      ( eq-pair-eq-fiber
        ( ( ap
            ( H (hA' x) âˆ™ ap hX (H' x) âˆ™ ap (hX âˆ˜ hX') p âˆ™_)
            ( ( distributive-inv-concat (K (hB' y)) (ap hX (K' y))) âˆ™
              ( ap (_âˆ™ inv (K (hB' y))) (inv (ap-inv hX (K' y)))))) âˆ™
          ( inv
            ( assoc
              ( H (hA' x) âˆ™ ap hX (H' x) âˆ™ ap (hX âˆ˜ hX') p)
              ( ap hX (inv (K' y)))
              ( inv (K (hB' y))))) âˆ™
          ( ap
            ( _âˆ™ (inv (K (hB' y))))
            ( ( assoc
                ( H (hA' x) âˆ™ ap hX (H' x))
                ( ap (hX âˆ˜ hX') p)
                ( ap hX (inv (K' y)))) âˆ™
              ( ap
                ( H (hA' x) âˆ™ ap hX (H' x) âˆ™_)
                ( ( ap (_âˆ™ ap hX (inv (K' y))) (ap-comp hX hX' p)) âˆ™
                  ( inv (ap-concat hX (ap hX' p) (inv (K' y))))) âˆ™
                ( ( assoc
                    ( H (hA' x))
                    ( ap hX (H' x))
                    ( ap hX (ap hX' p âˆ™ inv (K' y)))) âˆ™
                  ( ap
                    ( H (hA' x) âˆ™_)
                    ( ( inv (ap-concat hX (H' x) (ap hX' p âˆ™ inv (K' y)))) âˆ™
                      ( ap
                        ( ap hX)
                        ( inv (assoc (H' x) (ap hX' p) (inv (K' y)))))))))))))

  preserves-comp-map-pullback-cone :
    {l4 l4' l4'' : Level}
    (c : pullback-cone ğ’® l4)
    (c' : pullback-cone ğ’¯ l4')
    (c'' : pullback-cone â„› l4'')
    (h : hom-cospan-diagram ğ’¯ â„›)
    (h' : hom-cospan-diagram ğ’® ğ’¯) â†’
    map-pullback-cone ğ’® â„› c c'' (comp-hom-cospan-diagram ğ’® ğ’¯ â„› h h') ~
    map-pullback-cone ğ’¯ â„› c' c'' h âˆ˜
    map-pullback-cone ğ’® ğ’¯ c c' h'
  preserves-comp-map-pullback-cone c c' c'' h h' =
    ( ( map-inv-is-equiv (is-pullback-pullback-cone â„› c'')) Â·l
      ( ( preserves-comp-map-standard-pullback h h') âˆ™h
        ( ( map-standard-pullback ğ’¯ â„› h) Â·l
          ( inv-htpy
            ( is-section-map-inv-is-equiv (is-pullback-pullback-cone ğ’¯ c'))) Â·r
          ( map-standard-pullback ğ’® ğ’¯ h'))) Â·r
      ( gap
        ( left-map-cospan-diagram ğ’®)
        ( right-map-cospan-diagram ğ’®)
        ( cone-pullback-cone ğ’® c)))
```

### The functorial action on maps of pullbacks preserves homotopies

We show that the functorial action on maps of pullbacks preserves homotopies
without appealing to the function extensionality axiom.

```agda
module _
  {l1 l2 l3 l1' l2' l3' : Level}
  where

  coherence-preserves-htpy-map-standard-pullback :
    (ğ’® : cospan-diagram l1 l2 l3)
    (ğ’¯ : cospan-diagram l1' l2' l3')
    (h h' : hom-cospan-diagram ğ’® ğ’¯)
    (H : htpy-hom-cospan-diagram ğ’® ğ’¯ h h') â†’
    (t :
      standard-pullback
        ( left-map-cospan-diagram ğ’®)
        ( right-map-cospan-diagram ğ’®)) â†’
    coherence-Eq-standard-pullback
      ( left-map-cospan-diagram ğ’¯)
      ( right-map-cospan-diagram ğ’¯)
      ( map-standard-pullback ğ’® ğ’¯ h t)
      ( map-standard-pullback ğ’® ğ’¯ h' t)
      ( htpy-left-map-htpy-hom-cospan-diagram ğ’® ğ’¯ h h' H
        ( vertical-map-standard-pullback t))
      ( htpy-right-map-htpy-hom-cospan-diagram ğ’® ğ’¯ h h' H
        ( horizontal-map-standard-pullback t))
  coherence-preserves-htpy-map-standard-pullback
    ğ’®@(A , B , X , f , g)
    ğ’¯@(A' , B' , X' , f' , g')
    h@(hA , hB , hX , HA , HB)
    h'@(hA' , hB' , hX' , HA' , HB')
    (HHA , HHB , HHX , K , L)
    (x , y , p) =
    equational-reasoning
    ap f' (HHA x) âˆ™ ((HA' x âˆ™ ap hX' p) âˆ™ inv (HB' y))
    ï¼ (HA x âˆ™ (ap hX p âˆ™ HHX (g y))) âˆ™ inv (HB' y)
    by
      ( inv (assoc (ap f' (HHA x)) (HA' x âˆ™ ap hX' p) (inv (HB' y)))) âˆ™
      ( ap
        ( _âˆ™ inv (HB' y))
        ( ( inv (assoc (ap f' (HHA x)) (HA' x) (ap hX' p))) âˆ™
          ( ap (_âˆ™ ap hX' p) (inv (K x))) âˆ™
          ( assoc (HA x) (HHX (f x)) (ap hX' p)) âˆ™
          ( ap (HA x âˆ™_) (nat-htpy HHX p))))
    ï¼ HA x âˆ™ (ap hX p âˆ™ (inv (HB y) âˆ™ ap g' (HHB y)))
    by
      ( assoc (HA x) (ap hX p âˆ™ HHX (g y)) (inv (HB' y))) âˆ™
      ( ap
        ( HA x âˆ™_)
        ( ( assoc (ap hX p) (HHX (g y)) (inv (HB' y))) âˆ™
          ( ap
            ( ap hX p âˆ™_)
            ( double-transpose-eq-concat'
              ( ap g' (HHB y))
              ( HB y)
              ( HB' y)
              ( HHX (g y))
              ( L y)))))
    ï¼ ((HA x âˆ™ ap hX p) âˆ™ inv (HB y)) âˆ™ ap g' (HHB y)
    by
      ( inv (assoc (HA x) (ap hX p) (inv (HB y) âˆ™ ap g' (HHB y)))) âˆ™
      ( inv (assoc (HA x âˆ™ ap hX p) (inv (HB y)) (ap g' (HHB y))))

  preserves-htpy-map-standard-pullback :
    (ğ’® : cospan-diagram l1 l2 l3)
    (ğ’¯ : cospan-diagram l1' l2' l3')
    (h h' : hom-cospan-diagram ğ’® ğ’¯)
    (H : htpy-hom-cospan-diagram ğ’® ğ’¯ h h') â†’
    map-standard-pullback ğ’® ğ’¯ h ~ map-standard-pullback ğ’® ğ’¯ h'
  preserves-htpy-map-standard-pullback ğ’® ğ’¯ h h' H t =
    eq-Eq-standard-pullback
      ( left-map-cospan-diagram ğ’¯)
      ( right-map-cospan-diagram ğ’¯)
      ( htpy-left-map-htpy-hom-cospan-diagram ğ’® ğ’¯ h h' H
        ( vertical-map-standard-pullback t))
      ( htpy-right-map-htpy-hom-cospan-diagram ğ’® ğ’¯ h h' H
        ( horizontal-map-standard-pullback t))
      ( coherence-preserves-htpy-map-standard-pullback ğ’® ğ’¯ h h' H t)

module _
  {l1 l2 l3 l1' l2' l3' : Level}
  (ğ’® : cospan-diagram l1 l2 l3)
  (ğ’¯ : cospan-diagram l1' l2' l3')
  {l4 l4' : Level}
  (c : pullback-cone ğ’® l4)
  (c' : pullback-cone ğ’¯ l4')
  (h h' : hom-cospan-diagram ğ’® ğ’¯)
  (H : htpy-hom-cospan-diagram ğ’® ğ’¯ h h')
  where

  preserves-htpy-map-pullback-cone :
    map-pullback-cone ğ’® ğ’¯ c c' h ~
    map-pullback-cone ğ’® ğ’¯ c c' h'
  preserves-htpy-map-pullback-cone =
    ( map-inv-is-equiv (is-pullback-pullback-cone ğ’¯ c')) Â·l
    ( preserves-htpy-map-standard-pullback ğ’® ğ’¯ h h' H) Â·r
    ( gap
      ( left-map-cospan-diagram ğ’®)
      ( right-map-cospan-diagram ğ’®)
      ( cone-pullback-cone ğ’® c))
```

## Table of files about pullbacks

The following table lists files that are about pullbacks as a general concept.

{{#include tables/pullbacks.md}}
