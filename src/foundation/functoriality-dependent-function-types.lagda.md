# Functoriality of dependent function types

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation.functoriality-dependent-function-types where

open import foundation-core.homotopies using (_~_; _Â·l_; _Â·r_)

open import foundation.constant-maps using (const)
open import foundation.contractible-maps using
  ( is-equiv-is-contr-map; is-contr-map-is-equiv)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import
  foundation.distributivity-of-dependent-functions-over-dependent-pairs using
  ( distributive-Î -Î£)
open import foundation.equivalences using
  ( _â‰ƒ_; _âˆ˜e_; is-fiberwise-equiv; is-equiv; map-equiv; is-equiv-map-equiv;
    issec-map-inv-equiv; map-inv-equiv; coherence-map-inv-equiv;
    isretr-map-inv-equiv; is-equiv-comp'; issec-map-inv-is-equiv;
    map-inv-is-equiv; is-equiv-precomp-Î -is-equiv; is-equiv-map-inv-is-equiv;
    id-equiv; equiv-ap; htpy-equiv; refl-htpy-equiv; ind-htpy-equiv;
    comp-htpy-equiv)
open import foundation.fibers-of-maps using (fib)
open import foundation.function-extensionality using (eq-htpy; equiv-eq-htpy)
open import foundation.functions using (map-Î ; map-Î '; _âˆ˜_; precomp-Î ; id)
open import foundation.functoriality-dependent-pair-types using
  ( equiv-tot; equiv-Î£)
open import foundation.identity-types using
  ( Id; tr; ap; _âˆ™_; tr-ap; is-equiv-tr; refl)
open import foundation.truncated-maps using (is-trunc-map)
open import foundation.truncated-types using (is-trunc-equiv'; is-trunc-Î )
open import foundation.truncation-levels using (ğ•‹; neg-two-ğ•‹; succ-ğ•‹)
open import foundation.unit-type using (unit)
open import foundation.universal-property-unit-type using
  ( equiv-universal-property-unit)
open import foundation.universe-levels using (Level; UU; _âŠ”_)
```

## Idea

The type constructor for dependent function types acts contravariantly in its first argument, and covariantly in its second argument.

## Properties

### The operation `map-Î ` preserves homotopies

```agda
htpy-map-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {f f' : (i : I) â†’ A i â†’ B i} (H : (i : I) â†’ (f i) ~ (f' i)) â†’
  (map-Î  f) ~ (map-Î  f')
htpy-map-Î  H h = eq-htpy (Î» i â†’ H i (h i))

htpy-map-Î ' :
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {J : UU l4} (Î± : J â†’ I) {f f' : (i : I) â†’ A i â†’ B i} â†’
  ((i : I) â†’ (f i) ~ (f' i)) â†’ (map-Î ' Î± f ~ map-Î ' Î± f')
htpy-map-Î ' Î± H = htpy-map-Î  (Î» j â†’ H (Î± j))
```

### We compute the fibers of map-Î 

```agda
equiv-fib-map-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (f : (i : I) â†’ A i â†’ B i) (h : (i : I) â†’ B i) â†’
  ((i : I) â†’ fib (f i) (h i)) â‰ƒ fib (map-Î  f) h
equiv-fib-map-Î  f h =
  equiv-tot (Î» x â†’ equiv-eq-htpy) âˆ˜e distributive-Î -Î£
```

### Truncated families of maps induce truncated maps on dependent function types

```agda
abstract
  is-trunc-map-map-Î  :
    (k : ğ•‹) {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
    (f : (i : I) â†’ A i â†’ B i) â†’
    ((i : I) â†’ is-trunc-map k (f i)) â†’ is-trunc-map k (map-Î  f)
  is-trunc-map-map-Î  k {I = I} f H h =
    is-trunc-equiv' k
      ( (i : I) â†’ fib (f i) (h i))
      ( equiv-fib-map-Î  f h)
      ( is-trunc-Î  k (Î» i â†’ H i (h i)))
```

### Families of equivalences induce equivalences on dependent function types

```agda
abstract
  is-equiv-map-Î  :
    {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
    (f : (i : I) â†’ A i â†’ B i) (is-equiv-f : is-fiberwise-equiv f) â†’
    is-equiv (map-Î  f)
  is-equiv-map-Î  f is-equiv-f =
    is-equiv-is-contr-map
      ( is-trunc-map-map-Î  neg-two-ğ•‹ f
        ( Î» i â†’ is-contr-map-is-equiv (is-equiv-f i)))

equiv-map-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (e : (i : I) â†’ (A i) â‰ƒ (B i)) â†’ ((i : I) â†’ A i) â‰ƒ ((i : I) â†’ B i)
pr1 (equiv-map-Î  e) = map-Î  (Î» i â†’ map-equiv (e i))
pr2 (equiv-map-Î  e) = is-equiv-map-Î  _ (Î» i â†’ is-equiv-map-equiv (e i))
```

### An equivalence of base types and a family of equivalences induce an equivalence on dependent function types

```agda
module _
  { l1 l2 l3 l4 : Level}
  { A' : UU l1} {B' : A' â†’ UU l2} {A : UU l3} (B : A â†’ UU l4)
  ( e : A' â‰ƒ A) (f : (a' : A') â†’ B' a' â‰ƒ B (map-equiv e a'))
  where
  
  map-equiv-Î  : ((a' : A') â†’ B' a') â†’ ((a : A) â†’ B a)
  map-equiv-Î  =
    ( map-Î 
      ( Î» a â†’
        ( tr B (issec-map-inv-equiv e a)) âˆ˜
        ( map-equiv (f (map-inv-equiv e a))))) âˆ˜
    ( precomp-Î  (map-inv-equiv e) B')

  compute-map-equiv-Î  :
    (h : (a' : A') â†’ B' a') (a' : A') â†’
    Id ( map-equiv-Î  h (map-equiv e a')) (map-equiv (f a') (h a'))
  compute-map-equiv-Î  h a' =
    ( ap
      ( Î» t â†’
        tr B t ( map-equiv
                 ( f (map-inv-equiv e (map-equiv e a')))
                 ( h (map-inv-equiv e (map-equiv e a')))))
      ( coherence-map-inv-equiv e a')) âˆ™
    ( ( tr-ap
        ( map-equiv e)
        ( Î» _ â†’ id)
        ( isretr-map-inv-equiv e a')
        ( map-equiv
          ( f (map-inv-equiv e (map-equiv e a')))
          ( h (map-inv-equiv e (map-equiv e a'))))) âˆ™
      ( Î± ( map-inv-equiv e (map-equiv e a'))
          ( isretr-map-inv-equiv e a')))
    where
    Î± : (x : A') (p : Id x a') â†’
        Id ( tr (B âˆ˜ map-equiv e) p (map-equiv (f x) (h x)))
           ( map-equiv (f a') (h a'))
    Î± x refl = refl

  abstract
    is-equiv-map-equiv-Î  : is-equiv map-equiv-Î 
    is-equiv-map-equiv-Î  =
      is-equiv-comp'
        ( map-Î  (Î» a â†’
          ( tr B (issec-map-inv-is-equiv (is-equiv-map-equiv e) a)) âˆ˜
          ( map-equiv (f (map-inv-is-equiv (is-equiv-map-equiv e) a)))))
        ( precomp-Î  (map-inv-is-equiv (is-equiv-map-equiv e)) B')
        ( is-equiv-precomp-Î -is-equiv
          ( map-inv-is-equiv (is-equiv-map-equiv e))
          ( is-equiv-map-inv-is-equiv (is-equiv-map-equiv e))
          ( B'))
        ( is-equiv-map-Î  _
          ( Î» a â†’ is-equiv-comp'
            ( tr B (issec-map-inv-is-equiv (is-equiv-map-equiv e) a))
            ( map-equiv (f (map-inv-is-equiv (is-equiv-map-equiv e) a)))
            ( is-equiv-map-equiv
              ( f (map-inv-is-equiv (is-equiv-map-equiv e) a)))
            ( is-equiv-tr B (issec-map-inv-is-equiv (is-equiv-map-equiv e) a))))

  equiv-Î  : ((a' : A') â†’ B' a') â‰ƒ ((a : A) â†’ B a)
  pr1 equiv-Î  = map-equiv-Î 
  pr2 equiv-Î  = is-equiv-map-equiv-Î 
```

### The functorial action of dependent function types preserves identity morphisms

```agda
id-map-equiv-Î  :
  { l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) â†’
  ( map-equiv-Î  B (id-equiv {A = A}) (Î» a â†’ id-equiv {A = B a})) ~ id
id-map-equiv-Î  B h = eq-htpy (compute-map-equiv-Î  B id-equiv (Î» a â†’ id-equiv) h)
```

### The fibers of `map-Î '`.

```agda
equiv-fib-map-Î ' :
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {J : UU l4} (Î± : J â†’ I) (f : (i : I) â†’ A i â†’ B i)
  (h : (j : J) â†’ B (Î± j)) â†’
  ((j : J) â†’ fib (f (Î± j)) (h j)) â‰ƒ fib (map-Î ' Î± f) h
equiv-fib-map-Î ' Î± f h =
  equiv-tot (Î» x â†’ equiv-eq-htpy) âˆ˜e distributive-Î -Î£
```

### A family of truncated maps over any map induces a truncated map on dependent function types

```agda
is-trunc-map-map-Î -is-trunc-map' :
  (k : ğ•‹) {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {J : UU l4} (Î± : J â†’ I) (f : (i : I) â†’ A i â†’ B i) â†’
  ((i : I) â†’ is-trunc-map k (f i)) â†’ is-trunc-map k (map-Î ' Î± f)
is-trunc-map-map-Î -is-trunc-map' k {J = J} Î± f H h =
  is-trunc-equiv' k
    ( (j : J) â†’ fib (f (Î± j)) (h j))
    ( equiv-fib-map-Î ' Î± f h)
    ( is-trunc-Î  k (Î» j â†’ H (Î± j) (h j)))

is-trunc-map-is-trunc-map-map-Î ' :
  (k : ğ•‹) {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (f : (i : I) â†’ A i â†’ B i) â†’
  ({l : Level} {J : UU l} (Î± : J â†’ I) â†’ is-trunc-map k (map-Î ' Î± f)) â†’
  (i : I) â†’ is-trunc-map k (f i)
is-trunc-map-is-trunc-map-map-Î ' k {A = A} {B} f H i b =
  is-trunc-equiv' k
    ( fib (map-Î  (Î» (x : unit) â†’ f i)) (const unit (B i) b))
    ( equiv-Î£
      ( Î» a â†’ Id (f i a) b)
      ( equiv-universal-property-unit (A i))
      ( Î» h â†’ equiv-ap
        ( equiv-universal-property-unit (B i))
        ( map-Î  (Î» x â†’ f i) h)
        ( const unit (B i) b)))
    ( H (Î» x â†’ i) (const unit (B i) b))
```

```agda
HTPY-map-equiv-Î  :
  { l1 l2 l3 l4 : Level}
  { A' : UU l1} (B' : A' â†’ UU l2) {A : UU l3} (B : A â†’ UU l4)
  ( e e' : A' â‰ƒ A) (H : htpy-equiv e e') â†’
  UU (l1 âŠ” (l2 âŠ” (l3 âŠ” l4)))
HTPY-map-equiv-Î  {A' = A'} B' {A} B e e' H =
  ( f : (a' : A') â†’ B' a' â‰ƒ B (map-equiv e a')) â†’
  ( f' : (a' : A') â†’ B' a' â‰ƒ B (map-equiv e' a')) â†’
  ( K : (a' : A') â†’
        ((tr B (H a')) âˆ˜ (map-equiv (f a'))) ~ (map-equiv (f' a'))) â†’
  ( map-equiv-Î  B e f) ~ (map-equiv-Î  B e' f')

htpy-map-equiv-Î -refl-htpy :
  { l1 l2 l3 l4 : Level}
  { A' : UU l1} {B' : A' â†’ UU l2} {A : UU l3} (B : A â†’ UU l4)
  ( e : A' â‰ƒ A) â†’
  HTPY-map-equiv-Î  B' B e e (refl-htpy-equiv e)
htpy-map-equiv-Î -refl-htpy {B' = B'} B e f f' K =
  ( htpy-map-Î 
    ( Î» a â†’
      ( tr B (issec-map-inv-is-equiv (is-equiv-map-equiv e) a)) Â·l
      ( K (map-inv-is-equiv (is-equiv-map-equiv e) a)))) Â·r
  ( precomp-Î  (map-inv-is-equiv (is-equiv-map-equiv e)) B')

abstract
  htpy-map-equiv-Î  :
    { l1 l2 l3 l4 : Level}
    { A' : UU l1} {B' : A' â†’ UU l2} {A : UU l3} (B : A â†’ UU l4)
    ( e e' : A' â‰ƒ A) (H : htpy-equiv e e') â†’
    HTPY-map-equiv-Î  B' B e e' H
  htpy-map-equiv-Î  {B' = B'} B e e' H f f' K =
    ind-htpy-equiv e
      ( HTPY-map-equiv-Î  B' B e)
      ( htpy-map-equiv-Î -refl-htpy B e)
      e' H f f' K
  
  comp-htpy-map-equiv-Î  :
    { l1 l2 l3 l4 : Level}
    { A' : UU l1} {B' : A' â†’ UU l2} {A : UU l3} (B : A â†’ UU l4)
    ( e : A' â‰ƒ A) â†’
    Id ( htpy-map-equiv-Î  {B' = B'} B e e (refl-htpy-equiv e))
      ( ( htpy-map-equiv-Î -refl-htpy B e))
  comp-htpy-map-equiv-Î  {B' = B'} B e =
    comp-htpy-equiv e
      ( HTPY-map-equiv-Î  B' B e)
      ( htpy-map-equiv-Î -refl-htpy B e)

map-automorphism-Î  :
  { l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  ( e : A â‰ƒ A) (f : (a : A) â†’ B a â‰ƒ B (map-equiv e a)) â†’
  ( (a : A) â†’ B a) â†’ ((a : A) â†’ B a)
map-automorphism-Î  {B = B} e f =
  ( map-Î  (Î» a â†’ (map-inv-is-equiv (is-equiv-map-equiv (f a))))) âˆ˜
  ( precomp-Î  (map-equiv e) B)

abstract
  is-equiv-map-automorphism-Î  :
    { l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
    ( e : A â‰ƒ A) (f : (a : A) â†’ B a â‰ƒ B (map-equiv e a)) â†’
    is-equiv (map-automorphism-Î  e f)
  is-equiv-map-automorphism-Î  {B = B} e f =
    is-equiv-comp' _ _
      ( is-equiv-precomp-Î -is-equiv _ (is-equiv-map-equiv e) B)
      ( is-equiv-map-Î  _
        ( Î» a â†’ is-equiv-map-inv-is-equiv (is-equiv-map-equiv (f a))))

automorphism-Î  :
  { l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  ( e : A â‰ƒ A) (f : (a : A) â†’ B a â‰ƒ B (map-equiv e a)) â†’
  ( (a : A) â†’ B a) â‰ƒ ((a : A) â†’ B a)
pr1 (automorphism-Î  e f) = map-automorphism-Î  e f
pr2 (automorphism-Î  e f) = is-equiv-map-automorphism-Î  e f
```
