# Propositional resizing

```agda
open import foundation.truncations-exist
open import foundation-core.univalence
open import foundation.function-extensionality-axiom

module foundation.propositional-resizing
  (funext : function-extensionality)
  (univalence : univalence-axiom)
  (truncations : truncations-exist)
  where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.dependent-products-propositions funext
open import foundation.universe-levels

open import foundation-core.equivalences
open import foundation-core.propositions
open import foundation-core.small-types funext univalence truncations
open import foundation-core.subtypes funext
```

</details>

## Idea

We say that a universe `ùí±` satisfies `ùí∞`-small
{{#concept "propositional resizing" Agda=propositional-resizing-Level}} if there
is a type `Œ©` in `ùí∞` [equipped](foundation.structure.md) with a
[subtype](foundation-core.subtypes.md) `Q` such that for each proposition `P` in
`ùí±` there is an element `u : Œ©` such that `Q u ‚âÉ P`. Such a type `Œ©` is called
an `ùí∞`-small {{#concept "classifier" Disambiguation="of small subobjects"}} of
`ùí±`-small subobjects.

## Definitions

### The predicate on a type of being a subtype classifier of a given universe level

```agda
is-subtype-classifier :
  {l1 l2 : Level} (l3 : Level) ‚Üí Œ£ (UU l1) (subtype l2) ‚Üí UU (l1 ‚äî l2 ‚äî lsuc l3)
is-subtype-classifier l3 Œ© =
  (P : Prop l3) ‚Üí Œ£ (pr1 Œ©) (Œª u ‚Üí type-equiv-Prop P (pr2 Œ© u))

module _
  {l1 l2 l3 : Level}
  (Œ© : Œ£ (UU l1) (subtype l2)) (H : is-subtype-classifier l3 Œ©)
  (P : Prop l3)
  where

  object-prop-is-subtype-classifier : pr1 Œ©
  object-prop-is-subtype-classifier = pr1 (H P)

  prop-is-small-prop-is-subtype-classifier : Prop l2
  prop-is-small-prop-is-subtype-classifier =
    pr2 Œ© object-prop-is-subtype-classifier

  type-is-small-prop-is-subtype-classifier : UU l2
  type-is-small-prop-is-subtype-classifier =
    type-Prop prop-is-small-prop-is-subtype-classifier

  equiv-is-small-prop-is-subtype-classifier :
    type-Prop P ‚âÉ type-is-small-prop-is-subtype-classifier
  equiv-is-small-prop-is-subtype-classifier = pr2 (H P)

  is-small-prop-is-subtype-classifier : is-small l2 (type-Prop P)
  is-small-prop-is-subtype-classifier =
    ( type-is-small-prop-is-subtype-classifier ,
      equiv-is-small-prop-is-subtype-classifier)
```

### Propositional resizing between specified universes

```agda
propositional-resizing-Level' :
  (l1 l2 l3 : Level) ‚Üí UU (lsuc l1 ‚äî lsuc l2 ‚äî lsuc l3)
propositional-resizing-Level' l1 l2 l3 =
  Œ£ (Œ£ (UU l1) (subtype l2)) (is-subtype-classifier l3)

propositional-resizing-Level :
  (l1 l2 : Level) ‚Üí UU (lsuc l1 ‚äî lsuc l2)
propositional-resizing-Level l1 l2 =
  propositional-resizing-Level' l1 l1 l2
```

```agda
module _
  {l1 l2 l3 : Level} (œÅ : propositional-resizing-Level' l1 l2 l3)
  where

  subtype-classifier-propositional-resizing-Level : Œ£ (UU l1) (subtype l2)
  subtype-classifier-propositional-resizing-Level = pr1 œÅ

  type-subtype-classifier-propositional-resizing-Level : UU l1
  type-subtype-classifier-propositional-resizing-Level =
    pr1 subtype-classifier-propositional-resizing-Level

  is-subtype-classifier-subtype-classifier-propositional-resizing-Level :
    is-subtype-classifier l3 subtype-classifier-propositional-resizing-Level
  is-subtype-classifier-subtype-classifier-propositional-resizing-Level = pr2 œÅ

module _
  {l1 l2 l3 : Level} (œÅ : propositional-resizing-Level' l1 l2 l3) (P : Prop l3)
  where

  object-prop-propositional-resizing-Level :
    type-subtype-classifier-propositional-resizing-Level œÅ
  object-prop-propositional-resizing-Level =
    object-prop-is-subtype-classifier
      ( subtype-classifier-propositional-resizing-Level œÅ)
      ( is-subtype-classifier-subtype-classifier-propositional-resizing-Level œÅ)
      ( P)

  prop-is-small-prop-propositional-resizing-Level : Prop l2
  prop-is-small-prop-propositional-resizing-Level =
    prop-is-small-prop-is-subtype-classifier
      ( subtype-classifier-propositional-resizing-Level œÅ)
      ( is-subtype-classifier-subtype-classifier-propositional-resizing-Level œÅ)
      ( P)

  type-is-small-prop-propositional-resizing-Level : UU l2
  type-is-small-prop-propositional-resizing-Level =
    type-is-small-prop-is-subtype-classifier
      ( subtype-classifier-propositional-resizing-Level œÅ)
      ( is-subtype-classifier-subtype-classifier-propositional-resizing-Level œÅ)
      ( P)

  equiv-is-small-prop-propositional-resizing-Level :
    type-Prop P ‚âÉ type-is-small-prop-propositional-resizing-Level
  equiv-is-small-prop-propositional-resizing-Level =
    equiv-is-small-prop-is-subtype-classifier
      ( subtype-classifier-propositional-resizing-Level œÅ)
      ( is-subtype-classifier-subtype-classifier-propositional-resizing-Level œÅ)
      ( P)

  is-small-prop-propositional-resizing-Level : is-small l2 (type-Prop P)
  is-small-prop-propositional-resizing-Level =
    is-small-prop-is-subtype-classifier
      ( subtype-classifier-propositional-resizing-Level œÅ)
      ( is-subtype-classifier-subtype-classifier-propositional-resizing-Level œÅ)
      ( P)
```

### The propositional resizing axiom

```agda
propositional-resizing : UUœâ
propositional-resizing = (l1 l2 : Level) ‚Üí propositional-resizing-Level l1 l2
```

```agda
module _
  (œÅ : propositional-resizing) (l1 l2 : Level)
  where

  subtype-classifier-propositional-resizing : Œ£ (UU l1) (subtype l1)
  subtype-classifier-propositional-resizing = pr1 (œÅ l1 l2)

  type-subtype-classifier-propositional-resizing : UU l1
  type-subtype-classifier-propositional-resizing =
    type-subtype-classifier-propositional-resizing-Level (œÅ l1 l2)

  is-subtype-classifier-subtype-classifier-propositional-resizing :
    is-subtype-classifier l2 subtype-classifier-propositional-resizing
  is-subtype-classifier-subtype-classifier-propositional-resizing =
    is-subtype-classifier-subtype-classifier-propositional-resizing-Level
      ( œÅ l1 l2)

module _
  (œÅ : propositional-resizing) (l1 : Level) {l2 : Level} (P : Prop l2)
  where

  object-prop-propositional-resizing :
    type-subtype-classifier-propositional-resizing œÅ l1 l2
  object-prop-propositional-resizing =
    object-prop-propositional-resizing-Level (œÅ l1 l2) P

  prop-is-small-prop-propositional-resizing : Prop l1
  prop-is-small-prop-propositional-resizing =
    prop-is-small-prop-propositional-resizing-Level (œÅ l1 l2) P

  type-is-small-prop-propositional-resizing : UU l1
  type-is-small-prop-propositional-resizing =
    type-is-small-prop-propositional-resizing-Level (œÅ l1 l2) P

  equiv-is-small-prop-propositional-resizing :
    type-Prop P ‚âÉ type-is-small-prop-propositional-resizing
  equiv-is-small-prop-propositional-resizing =
    equiv-is-small-prop-propositional-resizing-Level (œÅ l1 l2) P

  is-small-prop-propositional-resizing : is-small l1 (type-Prop P)
  is-small-prop-propositional-resizing =
    is-small-prop-propositional-resizing-Level (œÅ l1 l2) P
```

## See also

- [The large locale of propositions](foundation.large-locale-of-propositions.md)

## Table of files about propositional logic

The following table gives an overview of basic constructions in propositional
logic and related considerations.

{{#include tables/propositional-logic.md}}
