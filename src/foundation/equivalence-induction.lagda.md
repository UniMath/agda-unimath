# Equivalence induction

```agda
module foundation.equivalence-induction where
```

<details><summary>Imports</summary>

```agda
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.identity-systems
open import foundation.subuniverses
open import foundation.univalence
open import foundation.universal-property-dependent-pair-types
open import foundation.universe-levels

open import foundation-core.commuting-triangles-of-maps
open import foundation-core.contractible-maps
open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.sections
open import foundation-core.singleton-induction
```

</details>

## Idea

**Equivalence induction** is the principle asserting that for any type family

```text
  P : (B : ğ’°) (e : A â‰ƒ B) â†’ ğ’°
```

of types indexed by all [equivalences](foundation.equivalences.md) with domain
`A`, there is a [section](foundation.sections.md) of the evaluation map

```text
  ((B : ğ’°) (e : A â‰ƒ B) â†’ P B e) â†’ P A id-equiv.
```

The principle of equivalence induction is equivalent to the
[univalence axiom](foundation.univalence.md).

By equivalence induction, it follows that any type family `P : ğ’° â†’ ğ’±` on the
universe has an
[action on equivalences](foundation.action-on-equivalences-type-families.md)

```text
  (A â‰ƒ B) â†’ P A â‰ƒ P B.
```

## Statement

```agda
module _
  {l1 : Level} {A : UU l1}
  where

  ev-id-equiv :
    {l : Level} (P : (B : UU l1) â†’ (A â‰ƒ B) â†’ UU l) â†’
    ((B : UU l1) (e : A â‰ƒ B) â†’ P B e) â†’ P A id-equiv
  ev-id-equiv P f = f A id-equiv

  induction-principle-equivalences :
    {l : Level} (P : (B : UU l1) (e : A â‰ƒ B) â†’ UU l) â†’ UU (lsuc l1 âŠ” l)
  induction-principle-equivalences P = section (ev-id-equiv P)

  triangle-ev-id-equiv :
    {l : Level}
    (P : (Î£ (UU l1) (Î» X â†’ A â‰ƒ X)) â†’ UU l) â†’
    coherence-triangle-maps
      ( ev-point (A , id-equiv) {P})
      ( ev-id-equiv (Î» X e â†’ P (X , e)))
      ( ev-pair {A = UU l1} {B = Î» X â†’ A â‰ƒ X} {C = P})
  triangle-ev-id-equiv P f = refl
```

## Properties

### Equivalence induction is equivalent to the contractibility of the total space of equivalences

#### Contractibility of the total space of equivalences implies equivalence induction

```agda
module _
  {l1 : Level} {A : UU l1}
  where

  abstract
    is-identity-system-is-contr-total-equiv :
      is-contr (Î£ (UU l1) (Î» X â†’ A â‰ƒ X)) â†’
      {l : Level} â†’
      (P : (Î£ (UU l1) (Î» X â†’ A â‰ƒ X)) â†’ UU l) â†’
      induction-principle-equivalences (Î» B e â†’ P (B , e))
    is-identity-system-is-contr-total-equiv c P =
      section-left-factor
        ( ev-id-equiv (Î» X e â†’ P (X , e)))
        ( ev-pair)
        ( is-singleton-is-contr
          ( A , id-equiv)
          ( ( A , id-equiv) ,
            ( Î» t â†’
              ( inv (contraction c (A , id-equiv))) âˆ™ (contraction c t)))
          ( P))
```

#### Equivalence induction implies contractibility of the total space of equivalences

```agda
module _
  {l1 : Level} {A : UU l1}
  where

  abstract
    is-contr-total-is-identity-system-equiv :
      ( {l : Level} â†’ is-identity-system l (Î» X â†’ A â‰ƒ X) A id-equiv) â†’
      is-contr (Î£ (UU l1) (Î» X â†’ A â‰ƒ X))
    is-contr-total-is-identity-system-equiv ind =
      is-contr-is-singleton
        ( Î£ (UU l1) (Î» X â†’ A â‰ƒ X))
        ( A , id-equiv)
        ( Î» P â†’ section-comp
          ( ev-id-equiv (Î» X e â†’ P (X , e)))
          ( ev-pair {A = UU l1} {B = Î» X â†’ A â‰ƒ X} {C = P})
          ( ind-Î£ , refl-htpy)
          ( ind (Î» X e â†’ P (X , e))))
```

### Equivalence induction in a universe

```agda
module _
  {l1 l2 : Level} {A : UU l1} (P : (B : UU l1) (e : A â‰ƒ B) â†’ UU l2)
  where

  abstract
    is-identity-system-equiv : section (ev-id-equiv P)
    is-identity-system-equiv =
      is-identity-system-is-contr-total-equiv
        ( is-contr-total-equiv _)
        ( Î» t â†’ P (pr1 t) (pr2 t))

  ind-equiv :
    P A id-equiv â†’ {B : UU l1} (e : A â‰ƒ B) â†’ P B e
  ind-equiv p {B} = pr1 is-identity-system-equiv p B

  compute-ind-equiv :
    (u : P A id-equiv) â†’ ind-equiv u id-equiv ï¼ u
  compute-ind-equiv = pr2 is-identity-system-equiv
```

### Equivalence induction in a subuniverse

```agda
module _
  {l1 l2 l3 : Level} (P : subuniverse l1 l2) (A : type-subuniverse P)
  where

  ev-id-equiv-subuniverse :
    {F : (B : type-subuniverse P) â†’ equiv-subuniverse P A B â†’ UU l3} â†’
    ((B : type-subuniverse P) (e : equiv-subuniverse P A B) â†’ F B e) â†’
    F A id-equiv
  ev-id-equiv-subuniverse f = f A id-equiv

  triangle-ev-id-equiv-subuniverse :
    (F : (B : type-subuniverse P) â†’ equiv-subuniverse P A B â†’ UU l3) â†’
    coherence-triangle-maps
      ( ev-point (A , id-equiv))
      ( ev-id-equiv-subuniverse {F})
      ( ev-pair)
  triangle-ev-id-equiv-subuniverse F E = refl

  abstract
    is-identity-system-equiv-subuniverse :
      (F : (B : type-subuniverse P) â†’ equiv-subuniverse P A B â†’ UU l3) â†’
      section (ev-id-equiv-subuniverse {F})
    is-identity-system-equiv-subuniverse =
      is-identity-system-is-torsorial A id-equiv
        ( is-contr-total-equiv-subuniverse P A)

  ind-equiv-subuniverse :
    (F : (B : type-subuniverse P) â†’ equiv-subuniverse P A B â†’ UU l3) â†’
    F A id-equiv â†’ (B : type-subuniverse P) (e : equiv-subuniverse P A B) â†’
    F B e
  ind-equiv-subuniverse F =
    pr1 (is-identity-system-equiv-subuniverse F)

  compute-ind-equiv-subuniverse :
    (F : (B : type-subuniverse P) â†’ equiv-subuniverse P A B â†’ UU l3) â†’
    (u : F A id-equiv) â†’
    ind-equiv-subuniverse F u A id-equiv ï¼ u
  compute-ind-equiv-subuniverse F =
    pr2 (is-identity-system-equiv-subuniverse F)
```

### The evaluation map `ev-id-equiv` is an equivalence

```agda
module _
  {l1 l2 : Level} {A : UU l1} (P : (B : UU l1) (e : A â‰ƒ B) â†’ UU l2)
  where

  is-equiv-ev-id-equiv : is-equiv (ev-id-equiv P)
  is-equiv-ev-id-equiv =
    is-equiv-left-factor-htpy
      ( ev-point (A , id-equiv))
      ( ev-id-equiv P)
      ( ev-pair)
      ( triangle-ev-id-equiv (Î» u â†’ P (pr1 u) (pr2 u)))
      ( dependent-universal-property-contr-is-contr
        ( A , id-equiv)
        ( is-contr-total-equiv A)
        ( Î» u â†’ P (pr1 u) (pr2 u)))
      ( is-equiv-ev-pair)

  is-contr-map-ev-id-equiv : is-contr-map (ev-id-equiv P)
  is-contr-map-ev-id-equiv = is-contr-map-is-equiv is-equiv-ev-id-equiv
```

### The evaluation map `ev-id-equiv-subuniverse` is an equivalence

```agda
module _
  {l1 l2 l3 : Level} (P : subuniverse l1 l2) (X : type-subuniverse P)
  (F : (Y : type-subuniverse P) (e : equiv-subuniverse P X Y) â†’ UU l3)
  where

  is-equiv-ev-id-equiv-subuniverse :
    is-equiv (ev-id-equiv-subuniverse P X {F})
  is-equiv-ev-id-equiv-subuniverse =
    is-equiv-left-factor-htpy
      ( ev-point (X , id-equiv))
      ( ev-id-equiv-subuniverse P X)
      ( ev-pair)
      ( triangle-ev-id-equiv-subuniverse P X F)
      ( dependent-universal-property-contr-is-contr
        ( X , id-equiv)
        ( is-contr-total-equiv-subuniverse P X)
        ( Î» E â†’ F (pr1 E) (pr2 E)))
      ( is-equiv-ev-pair)

  is-contr-map-ev-id-equiv-subuniverse :
    is-contr-map (ev-id-equiv-subuniverse P X {F})
  is-contr-map-ev-id-equiv-subuniverse =
    is-contr-map-is-equiv is-equiv-ev-id-equiv-subuniverse
```

### Equivalence induction implies that postcomposing by an equivalence is an equivalence

Of course we already know that this fact follows from
[function extensionality](foundation.function-extensionality.md). However, we
prove it again from equivalence induction so that we can prove that
[univalence implies function extensionality](foundation.univalence-implies-function-extensionality.md).

```agda
abstract
  is-equiv-postcomp-univalence :
    {l1 l2 : Level} {X Y : UU l1} (A : UU l2) (e : X â‰ƒ Y) â†’
    is-equiv (postcomp A (map-equiv e))
  is-equiv-postcomp-univalence {X = X} A =
    ind-equiv (Î» Y e â†’ is-equiv (postcomp A (map-equiv e))) is-equiv-id
```
