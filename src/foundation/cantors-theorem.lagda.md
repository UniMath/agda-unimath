# Cantor's theorem

```agda
module foundation.cantors-theorem where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.decidable-propositions
open import foundation.decidable-subtypes
open import foundation.dependent-pair-types
open import foundation.double-negation-stable-propositions
open import foundation.function-extensionality
open import foundation.logical-equivalences
open import foundation.negation
open import foundation.powersets
open import foundation.surjective-maps
open import foundation.universe-levels

open import foundation-core.fibers-of-maps

open import logic.de-morgan-propositions
open import logic.de-morgan-subtypes
open import logic.double-negation-dense-maps
open import logic.double-negation-stable-subtypes
```

</details>

## Idea

{{#concept "Cantor's theorem" Agda=theorem-Cantor WD="Cantor's theorem" WDID=Q474881}}
shows that there is [no](foundation-core.negation.md)
[surjective map](foundation.surjective-maps.md) from a type onto its
[powerset](foundation.powersets.md).

```text
  Â¬ (A â†  ğ’«(A))
```

In fact, no map `A â†’ PA` into a
[complement](foundation.complements-subtypes.md)-closed
[subset](foundation-core.subtypes.md) `PA` of the powerset may be
[double negation dense](logic.double-negation-dense-maps.md).

## Theorem

**Proof.** The proof is an instance of an argument _by diagonalization_. Given a
function `f : A â†’ ğ’«(A)` we may define an element of the powerset `ğ’«(A)` that `f`
cannot possibly hit. This subtype is defined by

```text
  B := {x âˆˆ A | x âˆ‰ f(x)}
```

which is given formally by the predicate `x â†¦ Â¬ (f x x)`. If this subtype were
to be hit by `f`, that would mean there is a `Î¾ âˆˆ A` such that `f(Î¾) = B`. This
would have to be a fixed point of the negation operation, since

```text
  f(Î¾)(Î¾) = B(Î¾) = Â¬ (f(Î¾)(Î¾)),
```

but negation has no fixed points.

Cantor's theorem is the [63rd](literature.100-theorems.md#63) theorem on
[Freek Wiedijk](http://www.cs.ru.nl/F.Wiedijk/)'s list of
[100 theorems](literature.100-theorems.md) {{#cite 100theorems}}.

```agda
module _
  {l1 l2 : Level} {X : UU l1} (f : X â†’ powerset l2 X)
  where

  subtype-theorem-Cantor : powerset l2 X
  subtype-theorem-Cantor x = neg-Prop (f x x)

  abstract
    not-in-image-subtype-theorem-Cantor : Â¬ (fiber f subtype-theorem-Cantor)
    not-in-image-subtype-theorem-Cantor (Î¾ , Î±) =
      no-fixed-points-neg-Prop (f Î¾ Î¾) (iff-eq (htpy-eq Î± Î¾))

    theorem-double-negation-dense-Cantor : Â¬ (is-double-negation-dense-map f)
    theorem-double-negation-dense-Cantor H =
      H subtype-theorem-Cantor not-in-image-subtype-theorem-Cantor

    theorem-Cantor : Â¬ (is-surjective f)
    theorem-Cantor =
      map-neg
        ( is-double-negation-dense-map-is-surjective)
        ( theorem-double-negation-dense-Cantor)
```

## Alternative statements

### Cantor's theorem for the set of decidable subtypes

**Statement.** There is no surjective map from a type `X` to its set of
decidable subtypes `ğ’«áµˆ(X)`.

```agda
module _
  {l1 l2 : Level} {X : UU l1} (f : X â†’ decidable-subtype l2 X)
  where

  map-theorem-decidable-Cantor : decidable-subtype l2 X
  map-theorem-decidable-Cantor x = neg-Decidable-Prop (f x x)

  abstract
    not-in-image-map-theorem-decidable-Cantor :
      Â¬ (fiber f map-theorem-decidable-Cantor)
    not-in-image-map-theorem-decidable-Cantor (x , Î±) =
      no-fixed-points-neg-Decidable-Prop
        ( f x x)
        ( iff-eq (ap prop-Decidable-Prop (htpy-eq Î± x)))

    theorem-double-negation-dense-decidable-Cantor :
      Â¬ (is-double-negation-dense-map f)
    theorem-double-negation-dense-decidable-Cantor H =
      H map-theorem-decidable-Cantor not-in-image-map-theorem-decidable-Cantor

    theorem-decidable-Cantor : Â¬ (is-surjective f)
    theorem-decidable-Cantor =
      map-neg
        ( is-double-negation-dense-map-is-surjective)
        ( theorem-double-negation-dense-decidable-Cantor)
```

### Cantor's theorem for the set of double negation stable subtypes

**Statement.** There is no surjective map from a type `X` to its set of double
negation stable subtypes `ğ’«^Â¬Â¬(X)`.

```agda
module _
  {l1 l2 : Level} {X : UU l1} (f : X â†’ double-negation-stable-subtype l2 X)
  where

  map-theorem-double-negation-stable-Cantor :
    double-negation-stable-subtype l2 X
  map-theorem-double-negation-stable-Cantor x =
    neg-Double-Negation-Stable-Prop (f x x)

  abstract
    not-in-image-map-theorem-double-negation-stable-Cantor :
      Â¬ (fiber f map-theorem-double-negation-stable-Cantor)
    not-in-image-map-theorem-double-negation-stable-Cantor (x , Î±) =
      no-fixed-points-neg-Double-Negation-Stable-Prop
        ( f x x)
        ( iff-eq (ap prop-Double-Negation-Stable-Prop (htpy-eq Î± x)))

    theorem-double-negation-dense-double-negation-stable-Cantor :
      Â¬ (is-double-negation-dense-map f)
    theorem-double-negation-dense-double-negation-stable-Cantor H =
      H map-theorem-double-negation-stable-Cantor
        not-in-image-map-theorem-double-negation-stable-Cantor

    theorem-double-negation-stable-Cantor : Â¬ (is-surjective f)
    theorem-double-negation-stable-Cantor =
      map-neg
        ( is-double-negation-dense-map-is-surjective)
        ( theorem-double-negation-dense-double-negation-stable-Cantor)
```

### Cantor's theorem for the set of De Morgan subtypes

**Statement.** There is no surjective map from a type `X` to its set of De
Morgan subtypes `ğ’«áµˆáµ(X)`.

```agda
module _
  {l1 l2 : Level} {X : UU l1} (f : X â†’ de-morgan-subtype l2 X)
  where

  map-theorem-de-morgan-Cantor : de-morgan-subtype l2 X
  map-theorem-de-morgan-Cantor x = neg-De-Morgan-Prop (f x x)

  abstract
    not-in-image-map-theorem-de-morgan-Cantor :
      Â¬ (fiber f map-theorem-de-morgan-Cantor)
    not-in-image-map-theorem-de-morgan-Cantor (x , Î±) =
      no-fixed-points-neg-De-Morgan-Prop
        ( f x x)
        ( iff-eq (ap prop-De-Morgan-Prop (htpy-eq Î± x)))

    theorem-double-negation-dense-de-morgan-Cantor :
      Â¬ (is-double-negation-dense-map f)
    theorem-double-negation-dense-de-morgan-Cantor H =
      H map-theorem-de-morgan-Cantor not-in-image-map-theorem-de-morgan-Cantor

    theorem-de-morgan-Cantor : Â¬ (is-surjective f)
    theorem-de-morgan-Cantor =
      map-neg
        ( is-double-negation-dense-map-is-surjective)
        ( theorem-double-negation-dense-de-morgan-Cantor)
```

## References

A proof of Cantor's theorem first appeared in {{#cite Cantor1890/91}} where it
was considered in the context of [infinite sets](set-theory.infinite-sets.md).

{{#bibliography}} {{#reference Cantor1890/91}}

## See also

- Cantor's theorem generalizes
  [Cantor's diagonal argument](set-theory.cantors-diagonal-argument.md), which
  shows that the [set](foundation-core.sets.md) of
  [infinite sequences](lists.sequences.md) on a set with at least two distinct
  elements is [uncountable](set-theory.uncountable-sets.md).
- Cantor's theorem is generalized by
  [Lawvere's fixed point theorem](foundation.lawveres-fixed-point-theorem.md).

## External links

- [Cantor's theorem](https://ncatlab.org/nlab/show/Cantor%27s+theorem) at $n$Lab
- [Cantor's theorem](https://en.wikipedia.org/wiki/Cantor%27s_theorem) at
  Wikipedia
- [Cantor's theorem](https://www.britannica.com/science/Cantors-theorem) at
  Britannica
