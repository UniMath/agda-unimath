# Binary type duality

```agda
module foundation.binary-type-duality where
```

<details><summary>Imports</summary>

```agda
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.equivalences-spans
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.spans
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.type-duality
open import foundation.type-theoretic-principle-of-choice
open import foundation.universal-property-dependent-pair-types
open import foundation.universe-levels

open import foundation-core.cartesian-product-types
open import foundation-core.homotopies
open import foundation-core.identity-types
```

</details>

## Idea

Using the principles of [type duality](foundation.type-duality.md) and
[type theoretic principle of choice](foundation.type-theoretic-principle-of-choice.md),
we can show that the type of spans `A <-- S --> B` is
[equivalent](foundation.equivalences.md) to the type of type-valued binary
relations `A â†’ B â†’ ğ“¤`.

## Theorem

### Binary spans with fixed domain and codomain are equivalent to binary relations

```agda
module _
  { l1 l2 l : Level} (A : UU l1) (B : UU l2)
  where

  binary-type-duality :
    ( A â†’ B â†’ UU (l1 âŠ” l2 âŠ” l)) â‰ƒ span-fixed-domain-codomain (l1 âŠ” l2 âŠ” l) A B
  binary-type-duality =
    ( associative-Î£ (UU (l1 âŠ” l2 âŠ” l)) (Î» X â†’ X â†’ A) (Î» T â†’ pr1 T â†’ B)) âˆ˜e
    ( equiv-Î£ (Î» T â†’ pr1 T â†’ B) (equiv-Pr1 (l2 âŠ” l) A) (Î» P â†’ equiv-ind-Î£)) âˆ˜e
    ( distributive-Î -Î£) âˆ˜e
    ( equiv-Î -equiv-family (Î» a â†’ equiv-Pr1 (l1 âŠ” l) B))

  span-fixed-domain-codomain-binary-relation :
    (A â†’ B â†’ UU (l1 âŠ” l2 âŠ” l)) â†’ span-fixed-domain-codomain (l1 âŠ” l2 âŠ” l) A B
  pr1 (span-fixed-domain-codomain-binary-relation R) =
    Î£ A (Î» a â†’ Î£ B (Î» b â†’ R a b))
  pr1 (pr2 (span-fixed-domain-codomain-binary-relation R)) =
    pr1
  pr2 (pr2 (span-fixed-domain-codomain-binary-relation R)) =
    pr1 âˆ˜ pr2

  compute-span-fixed-domain-codomain-binary-relation :
    map-equiv binary-type-duality ~
    span-fixed-domain-codomain-binary-relation
  compute-span-fixed-domain-codomain-binary-relation = refl-htpy

  binary-relation-span-fixed-domain-codomain :
    span-fixed-domain-codomain (l1 âŠ” l2 âŠ” l) A B â†’ (A â†’ B â†’ UU (l1 âŠ” l2 âŠ” l))
  binary-relation-span-fixed-domain-codomain S a b =
    Î£ ( spanning-type-span-fixed-domain-codomain S)
      ( Î» s â†’
        ( left-map-span-fixed-domain-codomain S s ï¼ a) Ã—
        ( right-map-span-fixed-domain-codomain S s ï¼ b))

  compute-binary-relation-span-fixed-domain-codomain :
    map-inv-equiv binary-type-duality ~
    binary-relation-span-fixed-domain-codomain
  compute-binary-relation-span-fixed-domain-codomain S =
    inv
      ( map-eq-transpose-equiv binary-type-duality
        ( eq-equiv-span-fixed-domain-codomain _ _
          ( ( ( equiv-pr1
                ( Î» s â†’
                  is-torsorial-path
                    ( left-map-span-fixed-domain-codomain S s))) âˆ˜e
              ( equiv-left-swap-Î£) âˆ˜e
              ( equiv-tot
                ( Î» a â†’
                  ( equiv-tot
                    ( Î» s â†’
                      ( equiv-pr1
                        ( Î» _ â†’
                          is-torsorial-path
                            ( right-map-span-fixed-domain-codomain S s))) âˆ˜e
                      ( equiv-left-swap-Î£))) âˆ˜e
                  ( equiv-left-swap-Î£)))) ,
            ( inv-htpy (pr1 âˆ˜ pr2 âˆ˜ pr2 âˆ˜ pr2)) ,
            ( inv-htpy (pr2 âˆ˜ pr2 âˆ˜ pr2 âˆ˜ pr2)))))
```
