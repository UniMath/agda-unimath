---
title: Fiber inclusions
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module foundation.fiber-inclusions where

open import foundation.0-maps using (is-0-map)
open import foundation.1-types using
  ( is-1-type; 1-Type; type-1-Type; is-1-type-type-1-Type)
open import foundation.cones-pullbacks using (cone)
open import foundation.contractible-maps using
  ( is-contr-map; is-contr-map-is-equiv)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.embeddings using (is-emb; _â†ª_)
open import foundation.equality-dependent-pair-types using
  ( equiv-pair-eq-Î£)
open import foundation.equivalences using (_â‰ƒ_; _âˆ˜e_; is-equiv-comp)
open import foundation.faithful-maps using
  ( is-faithful; is-faithful-is-0-map; faithful-map)
open import foundation.fibers-of-maps using
  ( fib; cone-fiber; map-inv-fib-pr1; is-equiv-map-inv-fib-pr1;
    is-pullback-cone-fiber)
open import foundation.functoriality-dependent-pair-types using
  ( equiv-tot)
open import foundation.homotopies using (refl-htpy)
open import foundation.identity-types using (_ï¼_; is-equiv-tr)
open import foundation.propositional-maps using
  ( is-prop-map; is-emb-is-prop-map)
open import foundation.propositions using (is-prop)
open import foundation.pullbacks using (is-pullback; gap)
open import foundation.sets using (is-set; Set; type-Set; is-set-type-Set)
open import foundation.truncated-maps using (is-trunc-map)
open import foundation.truncated-types using
  ( is-trunc; is-trunc-equiv'; is-trunc-equiv)
open import foundation.truncation-levels using
  ( ğ•‹; succ-ğ•‹; neg-two-ğ•‹; neg-one-ğ•‹; zero-ğ•‹)
open import foundation.type-arithmetic-dependent-pair-types using
  ( right-unit-law-Î£-is-contr; equiv-left-swap-Î£)
open import foundation.unit-type using
  ( raise-unit; raise-star; pt; terminal-map)
open import foundation.universe-levels using (Level; UU)
```

## Idea

Given a family `B` of types over `A` and an element `a : A`, then the fiber inclusion of `B` at a is a map `B a â†’ Î£ A B`.

## Definition

```agda
module _
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2)
  where
  
  fiber-inclusion : (x : A) â†’ B x â†’ Î£ A B
  pr1 (fiber-inclusion x y) = x
  pr2 (fiber-inclusion x y) = y

  fib-fiber-inclusion :
    (a : A) (t : Î£ A B) â†’ fib (fiber-inclusion a) t â‰ƒ (a ï¼ pr1 t)
  fib-fiber-inclusion a t =
    ( ( right-unit-law-Î£-is-contr
        ( Î» p â†’ is-contr-map-is-equiv (is-equiv-tr B p) (pr2 t))) âˆ˜e
      ( equiv-left-swap-Î£)) âˆ˜e
    ( equiv-tot (Î» b â†’ equiv-pair-eq-Î£ (pair a b) t))
```

## Properties

### The fiber inclusions are truncated maps for any type family B if and only if A is truncated

```
module _
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1}
  where
  
  is-trunc-is-trunc-map-fiber-inclusion :
    ((B : A â†’ UU l2) (a : A) â†’ is-trunc-map k (fiber-inclusion B a)) â†’
    is-trunc (succ-ğ•‹ k) A
  is-trunc-is-trunc-map-fiber-inclusion H x y =
    is-trunc-equiv' k
      ( fib (fiber-inclusion B x) (pair y raise-star))
      ( fib-fiber-inclusion B x (pair y raise-star))
      ( H B x (pair y raise-star))
    where
    B : A â†’ UU l2
    B a = raise-unit l2

  is-trunc-map-fiber-inclusion-is-trunc :
    (B : A â†’ UU l2) (a : A) â†’
    is-trunc (succ-ğ•‹ k) A â†’ is-trunc-map k (fiber-inclusion B a)
  is-trunc-map-fiber-inclusion-is-trunc B a H t =
    is-trunc-equiv k
      ( a ï¼ pr1 t)
      ( fib-fiber-inclusion B a t)
      ( H a (pr1 t))

module _
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2)
  where

  is-contr-map-fiber-inclusion :
    (x : A) â†’ is-prop A â†’ is-contr-map (fiber-inclusion B x)
  is-contr-map-fiber-inclusion =
    is-trunc-map-fiber-inclusion-is-trunc neg-two-ğ•‹ B

  is-prop-map-fiber-inclusion :
    (x : A) â†’ is-set A â†’ is-prop-map (fiber-inclusion B x)
  is-prop-map-fiber-inclusion =
    is-trunc-map-fiber-inclusion-is-trunc neg-one-ğ•‹ B

  is-0-map-fiber-inclusion :
    (x : A) â†’ is-1-type A â†’ is-0-map (fiber-inclusion B x)
  is-0-map-fiber-inclusion =
    is-trunc-map-fiber-inclusion-is-trunc zero-ğ•‹ B

  is-emb-fiber-inclusion : is-set A â†’ (x : A) â†’ is-emb (fiber-inclusion B x)
  is-emb-fiber-inclusion H x =
    is-emb-is-prop-map (is-prop-map-fiber-inclusion x H)

  emb-fiber-inclusion : is-set A â†’ (x : A) â†’ B x â†ª Î£ A B
  pr1 (emb-fiber-inclusion H x) = fiber-inclusion B x
  pr2 (emb-fiber-inclusion H x) = is-emb-fiber-inclusion H x

  is-faithful-fiber-inclusion :
    is-1-type A â†’ (x : A) â†’ is-faithful (fiber-inclusion B x)
  is-faithful-fiber-inclusion H x =
    is-faithful-is-0-map (is-0-map-fiber-inclusion x H)

fiber-inclusion-emb :
  {l1 l2 : Level} (A : Set l1) (B : type-Set A â†’ UU l2) â†’
  (x : type-Set A) â†’ B x â†ª Î£ (type-Set A) B
pr1 (fiber-inclusion-emb A B x) = fiber-inclusion B x
pr2 (fiber-inclusion-emb A B x) = is-emb-fiber-inclusion B (is-set-type-Set A) x

fiber-inclusion-faithful-map :
  {l1 l2 : Level} (A : 1-Type l1) (B : type-1-Type A â†’ UU l2) â†’
  (x : type-1-Type A) â†’ faithful-map (B x) (Î£ (type-1-Type A) B)
pr1 (fiber-inclusion-faithful-map A B x) = fiber-inclusion B x
pr2 (fiber-inclusion-faithful-map A B x) =
  is-faithful-fiber-inclusion B (is-1-type-type-1-Type A) x
```

### Any fiber inclusion is a pullback of a point inclusion

```agda
module _
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) (a : A)
  where
  
  cone-fiber-fam : cone (pr1 {B = B}) (pt a) (B a)
  pr1 cone-fiber-fam = fiber-inclusion B a
  pr1 (pr2 cone-fiber-fam) = terminal-map
  pr2 (pr2 cone-fiber-fam) = refl-htpy

  abstract
    is-pullback-cone-fiber-fam :
      is-pullback (pr1 {B = B}) (pt a) cone-fiber-fam
    is-pullback-cone-fiber-fam =
      is-equiv-comp
        ( gap (pr1 {B = B}) (pt a) cone-fiber-fam)
        ( gap (pr1 {B = B}) (pt a) (cone-fiber (pr1 {B = B}) a))
        ( map-inv-fib-pr1 B a)
        ( refl-htpy)
        ( is-equiv-map-inv-fib-pr1 B a)
        ( is-pullback-cone-fiber pr1 a)
```
