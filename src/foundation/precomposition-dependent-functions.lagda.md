# Precomposition of dependent functions

```agda
module foundation.precomposition-dependent-functions where

open import foundation-core.precomposition-dependent-functions public
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-homotopies
open import foundation.dependent-identifications
open import foundation.dependent-pair-types
open import foundation.dependent-universal-property-equivalences
open import foundation.function-extensionality
open import foundation.sections
open import foundation.transport-along-homotopies
open import foundation.transport-along-identifications
open import foundation.universe-levels

open import foundation-core.commuting-squares-of-maps
open import foundation-core.commuting-triangles-of-maps
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.truncated-maps
open import foundation-core.truncation-levels
open import foundation-core.type-theoretic-principle-of-choice
```

</details>

## Properties

### Computations of the fibers of `precomp-Î `

The fiber of `- âˆ˜ f : ((b : B) â†’ U b) â†’ ((a : A) â†’ U (f a))` at
`g âˆ˜ f : (b : B) â†’ U b` is equivalent to the type of maps `h : (b : B) â†’ U b`
equipped with a homotopy witnessing that the square

```text
        f
    A -----> B
    |        |
  f |        | h
    âˆ¨        âˆ¨
    B ---> U âˆ˜ f
        g
```

commutes.

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (U : B â†’ UU l3)
  where

  compute-extension-fiber-precomp-Î ' :
    (g : (a : A) â†’ U (f a)) â†’
    fiber (precomp-Î  f U) g â‰ƒ
    Î£ ((b : B) â†’ U b) (Î» h â†’ (a : A) â†’ (h âˆ˜ f) a ï¼ g a)
  compute-extension-fiber-precomp-Î ' g =
    equiv-tot (Î» h â†’ equiv-funext)

  compute-extension-fiber-precomp-Î  :
    (g : (a : A) â†’ U (f a)) â†’
    fiber (precomp-Î  f U) g â‰ƒ Î£ ((b : B) â†’ U b) (Î» h â†’ g ~ h âˆ˜ f)
  compute-extension-fiber-precomp-Î  g =
    equiv-tot (Î» h â†’ equiv-funext) âˆ˜e equiv-fiber (precomp-Î  f U) g

  compute-fiber-precomp-Î  :
    (g : (b : B) â†’ U b) â†’
    fiber (precomp-Î  f U) (g âˆ˜ f) â‰ƒ Î£ ((b : B) â†’ U b) (Î» h â†’ g âˆ˜ f ~ h âˆ˜ f)
  compute-fiber-precomp-Î  g =
    compute-extension-fiber-precomp-Î  (g âˆ˜ f)

  compute-total-fiber-precomp-Î  :
    Î£ ((b : B) â†’ U b) (Î» g â†’ fiber (precomp-Î  f U) (g âˆ˜ f)) â‰ƒ
    Î£ ((b : B) â†’ U b) (Î» u â†’ Î£ ((b : B) â†’ U b) (Î» v â†’ u âˆ˜ f ~ v âˆ˜ f))
  compute-total-fiber-precomp-Î  = equiv-tot compute-fiber-precomp-Î 

  diagonal-into-fibers-precomp-Î  :
    ((b : B) â†’ U b) â†’ Î£ ((b : B) â†’ U b) (Î» g â†’ fiber (precomp-Î  f U) (g âˆ˜ f))
  diagonal-into-fibers-precomp-Î  = map-section-family (Î» g â†’ (g , refl))
```

- In
  [`foundation.universal-property-family-of-fibers-of-maps`](foundation.universal-property-family-of-fibers-of-maps.md)
  we compute the fiber family of dependent precomposition maps as a dependent
  product
  ```text
    dependent-product-characterization-fiber-precomp-Î  :
      fiber (precomp-Î  f U) g â‰ƒ
      ( (b : B) â†’
        Î£ (u : U b),
          (((a , p) : fiber f b) â†’ dependent-identification U p (g a) u)).
  ```

### The action of dependent precomposition on homotopies

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {f g : A â†’ B} (H : f ~ g) (C : B â†’ UU l3) (h : (y : B) â†’ C y)
  where

  eq-htpy-precomp-Î  : (Î» x â†’ tr C (H x) (precomp-Î  f C h x)) ï¼ precomp-Î  g C h
  eq-htpy-precomp-Î  = eq-htpy (htpy-htpy-precomp-Î  H C h)

  htpy-precomp-Î  :
    dependent-identification
      ( Î» v â†’ (a : A) â†’ C (v a))
      ( eq-htpy H)
      ( precomp-Î  f C h)
      ( precomp-Î  g C h)
  htpy-precomp-Î  =
    compute-tr-htpy (Î» _ â†’ C) H (precomp-Î  f C h) âˆ™ eq-htpy-precomp-Î 

  eq-htpy-precomp-Î ' :
    precomp-Î  f C h ï¼ (Î» x â†’ inv-tr C (H x) (precomp-Î  g C h x))
  eq-htpy-precomp-Î ' = eq-htpy (htpy-htpy-precomp-Î ' H C h)

  htpy-precomp-Î ' :
    dependent-identification'
      ( Î» v â†’ (a : A) â†’ C (v a))
      ( eq-htpy H)
      ( precomp-Î  f C h)
      ( precomp-Î  g C h)
  htpy-precomp-Î ' =
    eq-htpy-precomp-Î ' âˆ™ inv (compute-inv-tr-htpy (Î» _ â†’ C) H (precomp-Î  g C h))
```

### Equivalences induce an equivalence from the type of homotopies between two dependent functions to the type of homotopies between their precomposites

```agda
module _
  { l1 l2 l3 : Level} {A : UU l1}
  where

  equiv-htpy-precomp-htpy-Î  :
    {B : UU l2} {C : B â†’ UU l3} (f g : (b : B) â†’ C b) (e : A â‰ƒ B) â†’
    (f ~ g) â‰ƒ (f âˆ˜ map-equiv e ~ g âˆ˜ map-equiv e)
  equiv-htpy-precomp-htpy-Î  f g e =
    equiv-precomp-Î  e (eq-value f g)
```

### The action on identifications of precomposition of dependent functions

Consider a map `f : A â†’ B` and two dependent functions `g h : (x : B) â†’ C x`.
Then the square

```text
                     ap (precomp-Î  f C)
       (g ï¼ h) ---------------------------> (g âˆ˜ f ï¼ h âˆ˜ f)
          |                                         |
  htpy-eq |                                         | htpy-eq
          âˆ¨                                         âˆ¨
       (g ~ h) ----------------------------> (g âˆ˜ f ~ h âˆ˜ f)
                precomp-Î  f (eq-value g h)
```

[commutes](foundation-core.commuting-squares-of-maps.md).

Similarly, the map `ap (precomp-Î  f C)` fits in a commuting square

```text
                precomp-Î  f (eq-value g h)
       (g ~ h) ----------------------------> (g âˆ˜ f ~ h âˆ˜ f)
          |                                         |
  eq-htpy |                                         | eq-htpy
          âˆ¨                                         âˆ¨
       (g ï¼ h) ---------------------------> (g âˆ˜ f ï¼ h âˆ˜ f).
                     ap (precomp-Î  f C)
```

```agda
module _
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) {C : B â†’ UU l3}
  {g h : (b : B) â†’ C b}
  where

  coherence-htpy-eq-ap-precomp-Î  :
    coherence-square-maps
      ( ap (precomp-Î  f C) {g} {h})
      ( htpy-eq)
      ( htpy-eq)
      ( precomp-Î  f (eq-value g h))
  coherence-htpy-eq-ap-precomp-Î  refl = refl

  coherence-htpy-eq-ap-precomp-Î ' :
    coherence-square-maps'
      ( ap (precomp-Î  f C) {g} {h})
      ( htpy-eq)
      ( htpy-eq)
      ( precomp-Î  f (eq-value g h))
  coherence-htpy-eq-ap-precomp-Î ' = inv-htpy coherence-htpy-eq-ap-precomp-Î 

  coherence-eq-htpy-ap-precomp-Î  :
    coherence-square-maps
      ( precomp-Î  f (eq-value g h))
      ( eq-htpy)
      ( eq-htpy)
      ( ap (precomp-Î  f C) {g} {h})
  coherence-eq-htpy-ap-precomp-Î  =
    vertical-inv-equiv-coherence-square-maps
      ( ap (precomp-Î  f C))
      ( equiv-funext)
      ( equiv-funext)
      ( precomp-Î  f (eq-value g h))
      ( coherence-htpy-eq-ap-precomp-Î )

  coherence-eq-htpy-ap-precomp-Î ' :
    coherence-square-maps'
      ( precomp-Î  f (eq-value g h))
      ( eq-htpy)
      ( eq-htpy)
      ( ap (precomp-Î  f C) {g} {h})
  coherence-eq-htpy-ap-precomp-Î ' = inv-htpy coherence-eq-htpy-ap-precomp-Î 
```

### Precomposing functions `Î  B C` by `f : A â†’ B` is `k+1`-truncated if and only if precomposing homotopies is `k`-truncated

```agda
module _
  {l1 l2 l3 : Level} {k : ð•‹} {A : UU l1} {B : UU l2} {f : A â†’ B}
  {C : B â†’ UU l3}
  where abstract

  is-trunc-map-succ-precomp-Î  :
    ((g h : (b : B) â†’ C b) â†’ is-trunc-map k (precomp-Î  f (eq-value g h))) â†’
    is-trunc-map (succ-ð•‹ k) (precomp-Î  f C)
  is-trunc-map-succ-precomp-Î  H =
    is-trunc-map-succ-is-trunc-map-ap k (precomp-Î  f C)
      ( Î» g h â†’
        is-trunc-map-top-is-trunc-map-bottom-is-equiv k
          ( ap (precomp-Î  f C))
          ( htpy-eq)
          ( htpy-eq)
          ( precomp-Î  f (eq-value g h))
          ( coherence-htpy-eq-ap-precomp-Î  f)
          ( funext g h)
          ( funext (g âˆ˜ f) (h âˆ˜ f))
          ( H g h))

  is-trunc-map-precomp-Î -eq-value-is-trunc-map-succ-precomp-Î  :
    is-trunc-map (succ-ð•‹ k) (precomp-Î  f C) â†’
    (g h : (b : B) â†’ C b) â†’ is-trunc-map k (precomp-Î  f (eq-value g h))
  is-trunc-map-precomp-Î -eq-value-is-trunc-map-succ-precomp-Î  H g h =
    is-trunc-map-top-is-trunc-map-bottom-is-equiv k
      ( precomp-Î  f (eq-value g h))
      ( eq-htpy)
      ( eq-htpy)
      ( ap (precomp-Î  f C))
      ( coherence-eq-htpy-ap-precomp-Î  f)
      ( is-equiv-eq-htpy g h)
      ( is-equiv-eq-htpy (g âˆ˜ f) (h âˆ˜ f))
      ( is-trunc-map-ap-is-trunc-map-succ k (precomp-Î  f C) H g h)
```

### The dependent precomposition map at a dependent pair type

Given a map `f : X â†’ Y` and a family `B : (y : Y) â†’ A y â†’ ð’°` we have a
[commuting square](foundation-core.commuting-squares-of-maps.md)

```text
                                     precomp-Î  f (Î» y â†’ Î£ (A y) (B y))
            ((y : Y) â†’ Î£ (A y) (B y)) -----------------------------> ((x : X) â†’ Î£ (A (f x)) (B (f x)))
                       |                                                          |
                     ~ |                                                          | ~
                       âˆ¨                                                          âˆ¨
  Î£ (a : (y : Y) â†’ A y) ((y : Y) â†’ B y (a y)) --------> Î£ (a : (x : X) â†’ A (f x)) ((x : X) â†’ B (f x) (a x)).
                       map-Î£ (precomp-Î  f A) (Î» a â†’ precomp-Î  f (Î» y â†’ B y (a y)))
```

```agda
module _
  {l1 l2 l3 l4 : Level}
  {X : UU l1} {Y : UU l2} {A : Y â†’ UU l3} {B : (y : Y) â†’ A y â†’ UU l4}
  {f : X â†’ Y}
  where

  coherence-precomp-Î -Î£ :
    coherence-square-maps
      ( precomp-Î  f (Î» y â†’ Î£ (A y) (B y)))
      ( map-distributive-Î -Î£)
      ( map-distributive-Î -Î£)
      ( map-Î£
        ( Î» a â†’ (x : X) â†’ B (f x) (a x))
        ( precomp-Î  f A)
        ( Î» a â†’ precomp-Î  f (Î» y â†’ B y (a y))))
  coherence-precomp-Î -Î£ = refl-htpy
```
