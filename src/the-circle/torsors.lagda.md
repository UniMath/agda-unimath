---
title: Formalisation of the Symmetry Book
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module the-circle.torsors where

open import the-circle.universal-cover public
open import the-circle.integers public

Endo : (l : Level) ‚Üí UU (lsuc l)
Endo l = Œ£ (UU l) (Œª X ‚Üí X ‚Üí X)

module _
  {l : Level} (X : Endo l)
  where

  type-Endo : UU l
  type-Endo = pr1 X

  endomorphism-Endo : type-Endo ‚Üí type-Endo
  endomorphism-Endo = pr2 X

‚Ñ§-Endo : Endo lzero
pr1 ‚Ñ§-Endo = ‚Ñ§
pr2 ‚Ñ§-Endo = succ-‚Ñ§

module _
  {l1 l2 : Level} (X : Endo l1) (Y : Endo l2)
  where

  equiv-Endo : UU (l1 ‚äî l2)
  equiv-Endo =
    Œ£ ( type-Endo X ‚âÉ type-Endo Y)
      ( Œª e ‚Üí
        ( map-equiv e ‚àò endomorphism-Endo X) ~
        ( endomorphism-Endo Y ‚àò map-equiv e))

  mere-equiv-Endo : UU (l1 ‚äî l2)
  mere-equiv-Endo = type-trunc-Prop equiv-Endo

module _
  {l1 : Level} (X : Endo l1)
  where

  equiv-id-Endo : equiv-Endo X X
  pr1 equiv-id-Endo = equiv-id
  pr2 equiv-id-Endo = refl-htpy
  
  refl-mere-equiv-Endo : mere-equiv-Endo X X
  refl-mere-equiv-Endo = unit-trunc-Prop equiv-id-Endo

  equiv-eq-Endo : (Y : Endo l1) ‚Üí Id X Y ‚Üí equiv-Endo X Y
  equiv-eq-Endo .X refl = equiv-id-Endo
  
  is-contr-total-equiv-Endo : is-contr (Œ£ (Endo l1) (equiv-Endo X))
  is-contr-total-equiv-Endo =
    is-contr-total-Eq-structure
      ( Œª Y f e ‚Üí (map-equiv e ‚àò endomorphism-Endo X) ~ (f ‚àò map-equiv e))
      ( is-contr-total-equiv (type-Endo X))
      ( pair (type-Endo X) equiv-id)
      ( is-contr-total-htpy (endomorphism-Endo X))

  is-equiv-equiv-eq-Endo : (Y : Endo l1) ‚Üí is-equiv (equiv-eq-Endo Y)
  is-equiv-equiv-eq-Endo =
    fundamental-theorem-id X
      equiv-id-Endo
      is-contr-total-equiv-Endo
      equiv-eq-Endo

  eq-equiv-Endo : (Y : Endo l1) ‚Üí equiv-Endo X Y ‚Üí Id X Y
  eq-equiv-Endo Y = map-inv-is-equiv (is-equiv-equiv-eq-Endo Y)

module _
  {l1 : Level} (X : Endo l1)
  where
  
  Endo-Torsor : UU (lsuc l1)
  Endo-Torsor = Œ£ (Endo l1) (mere-equiv-Endo X)

  endo-Endo-Torsor : Endo-Torsor ‚Üí Endo l1
  endo-Endo-Torsor = pr1

  type-Endo-Torsor : Endo-Torsor ‚Üí UU l1
  type-Endo-Torsor = pr1 ‚àò endo-Endo-Torsor

  endomorphism-Endo-Torsor :
    (T : Endo-Torsor) ‚Üí type-Endo-Torsor T ‚Üí type-Endo-Torsor T
  endomorphism-Endo-Torsor T = pr2 (endo-Endo-Torsor T)

  mere-equiv-Endo-Torsor :
    (T : Endo-Torsor) ‚Üí mere-equiv-Endo X (endo-Endo-Torsor T)
  mere-equiv-Endo-Torsor T = pr2 T

  canonical-Endo-Torsor : Endo-Torsor
  pr1 canonical-Endo-Torsor = X
  pr2 canonical-Endo-Torsor = refl-mere-equiv-Endo X

module _
  {l1 : Level} (X : Endo l1)
  where

  equiv-Endo-Torsor : (T S : Endo-Torsor X) ‚Üí UU l1
  equiv-Endo-Torsor T S =
    equiv-Endo (endo-Endo-Torsor X T) (endo-Endo-Torsor X S)

  equiv-id-Endo-Torsor : (T : Endo-Torsor X) ‚Üí equiv-Endo-Torsor T T
  equiv-id-Endo-Torsor T = equiv-id-Endo (endo-Endo-Torsor X T)

  equiv-eq-Endo-Torsor : (T S : Endo-Torsor X) ‚Üí Id T S ‚Üí equiv-Endo-Torsor T S
  equiv-eq-Endo-Torsor T .T refl = equiv-id-Endo-Torsor T
  
  is-contr-total-equiv-Endo-Torsor :
    is-contr
      ( Œ£ ( Endo-Torsor X)
          ( Œª T ‚Üí equiv-Endo-Torsor (canonical-Endo-Torsor X) T))
  is-contr-total-equiv-Endo-Torsor =
    is-contr-total-Eq-substructure
      ( is-contr-total-equiv-Endo X)
      ( Œª Y ‚Üí is-prop-type-trunc-Prop)
      ( X)
      ( equiv-id-Endo X)
      ( refl-mere-equiv-Endo X)

  is-equiv-equiv-eq-Endo-Torsor :
    (T : Endo-Torsor X) ‚Üí
    is-equiv (equiv-eq-Endo-Torsor (canonical-Endo-Torsor X) T)
  is-equiv-equiv-eq-Endo-Torsor =
    fundamental-theorem-id
      ( canonical-Endo-Torsor X)
      ( equiv-id-Endo-Torsor (canonical-Endo-Torsor X))
      ( is-contr-total-equiv-Endo-Torsor)
      ( equiv-eq-Endo-Torsor (canonical-Endo-Torsor X))

‚Ñ§-Torsor : UU (lsuc lzero)
‚Ñ§-Torsor = Endo-Torsor ‚Ñ§-Endo

module _
  (X : ‚Ñ§-Torsor)
  where

  endo-‚Ñ§-Torsor : Endo lzero
  endo-‚Ñ§-Torsor = pr1 X
  
  type-‚Ñ§-Torsor : UU lzero
  type-‚Ñ§-Torsor = pr1 (pr1 X)
  
  endomorphism-‚Ñ§-Torsor : type-‚Ñ§-Torsor ‚Üí type-‚Ñ§-Torsor
  endomorphism-‚Ñ§-Torsor = pr2 (pr1 X)
  
module _
  where

  canonical-‚Ñ§-Torsor : ‚Ñ§-Torsor
  pr1 canonical-‚Ñ§-Torsor = ‚Ñ§-Endo
  pr2 canonical-‚Ñ§-Torsor = refl-mere-equiv-Endo ‚Ñ§-Endo

  ‚Ñ§-Torsor-Pointed-Type : Pointed-Type (lsuc lzero)
  pr1 ‚Ñ§-Torsor-Pointed-Type = ‚Ñ§-Torsor
  pr2 ‚Ñ§-Torsor-Pointed-Type = canonical-‚Ñ§-Torsor

  equiv-‚Ñ§-Torsor : (T S : ‚Ñ§-Torsor) ‚Üí UU lzero
  equiv-‚Ñ§-Torsor = equiv-Endo-Torsor ‚Ñ§-Endo

  equiv-id-‚Ñ§-Torsor : (T : ‚Ñ§-Torsor) ‚Üí equiv-‚Ñ§-Torsor T T
  equiv-id-‚Ñ§-Torsor = equiv-id-Endo-Torsor ‚Ñ§-Endo

  equiv-eq-‚Ñ§-Torsor : (T S : ‚Ñ§-Torsor) ‚Üí Id T S ‚Üí equiv-‚Ñ§-Torsor T S
  equiv-eq-‚Ñ§-Torsor = equiv-eq-Endo-Torsor ‚Ñ§-Endo
  
  is-contr-total-equiv-‚Ñ§-Torsor :
    is-contr
      ( Œ£ ( ‚Ñ§-Torsor)
          ( Œª T ‚Üí equiv-‚Ñ§-Torsor (canonical-‚Ñ§-Torsor) T))
  is-contr-total-equiv-‚Ñ§-Torsor =
    is-contr-total-equiv-Endo-Torsor ‚Ñ§-Endo

  is-equiv-equiv-eq-‚Ñ§-Torsor :
    (T : ‚Ñ§-Torsor) ‚Üí
    is-equiv (equiv-eq-‚Ñ§-Torsor (canonical-‚Ñ§-Torsor) T)
  is-equiv-equiv-eq-‚Ñ§-Torsor = is-equiv-equiv-eq-Endo-Torsor ‚Ñ§-Endo

  equiv-equiv-eq-‚Ñ§-Torsor :
    (T : ‚Ñ§-Torsor) ‚Üí
    Id canonical-‚Ñ§-Torsor T ‚âÉ equiv-‚Ñ§-Torsor canonical-‚Ñ§-Torsor T
  pr1 (equiv-equiv-eq-‚Ñ§-Torsor T) = equiv-eq-‚Ñ§-Torsor canonical-‚Ñ§-Torsor T
  pr2 (equiv-equiv-eq-‚Ñ§-Torsor T) = is-equiv-equiv-eq-‚Ñ§-Torsor T

  map-left-factor-compute-Œ©-‚Ñ§-Torsor :
    equiv-‚Ñ§-Torsor canonical-‚Ñ§-Torsor canonical-‚Ñ§-Torsor ‚Üí ‚Ñ§
  map-left-factor-compute-Œ©-‚Ñ§-Torsor e = map-equiv (pr1 e) zero-‚Ñ§

  abstract
    is-equiv-map-left-factor-compute-Œ©-‚Ñ§-Torsor :
      is-equiv map-left-factor-compute-Œ©-‚Ñ§-Torsor
    is-equiv-map-left-factor-compute-Œ©-‚Ñ§-Torsor =
      is-equiv-is-contr-map
        ( Œª x ‚Üí
          is-contr-equiv
            ( hom-Pointed-Type-With-Aut
                ‚Ñ§-Pointed-Type-With-Aut
                ‚Ñ§-Pointed-Type-With-Aut)
            ( ( right-unit-law-Œ£-is-contr
                { B = Œª f ‚Üí is-equiv (pr1 f)}
                ( Œª f ‚Üí
                  is-proof-irrelevant-is-prop
                    ( is-subtype-is-equiv (pr1 f))
                    ( is-equiv-htpy id
                      ( htpy-eq
                        ( ap
                          ( pr1)
                          { x = f}
                          { y = pair id (pair refl refl-htpy)}
                          ( eq-is-contr
                            ( is-initial-‚Ñ§-Pointed-Type-With-Aut
                              ‚Ñ§-Pointed-Type-With-Aut))))
                      ( is-equiv-id)))) ‚àòe
              ( ( equiv-right-swap-Œ£) ‚àòe
                ( ( assoc-Œ£
                    ( ‚Ñ§ ‚âÉ ‚Ñ§)
                    ( Œª e ‚Üí Id (map-equiv e zero-‚Ñ§) zero-‚Ñ§)
                    ( Œª e ‚Üí
                      ( map-equiv (pr1 e) ‚àò succ-‚Ñ§) ~
                      ( succ-‚Ñ§ ‚àò map-equiv (pr1 e)))) ‚àòe
                  ( ( equiv-right-swap-Œ£) ‚àòe
                    ( equiv-Œ£
                      ( Œª e ‚Üí Id (map-equiv (pr1 e) zero-‚Ñ§) zero-‚Ñ§)
                      ( equiv-Œ£
                        ( Œª e ‚Üí (map-equiv e ‚àò succ-‚Ñ§) ~ (succ-‚Ñ§ ‚àò map-equiv e))
                        ( equiv-postcomp-equiv (equiv-add-‚Ñ§ (neg-‚Ñ§ x)) ‚Ñ§)
                        ( Œª e ‚Üí
                          equiv-map-Œ†
                            ( Œª k ‚Üí
                              ( equiv-concat'
                                ( add-‚Ñ§ (neg-‚Ñ§ x) (map-equiv e (succ-‚Ñ§ k)))
                                ( right-successor-law-add-‚Ñ§
                                  ( neg-‚Ñ§ x)
                                  ( map-equiv e k))) ‚àòe
                              ( equiv-ap
                                ( equiv-add-‚Ñ§ (neg-‚Ñ§ x))
                                ( map-equiv e (succ-‚Ñ§ k))
                                ( succ-‚Ñ§ (map-equiv e k))))))
                      ( Œª e ‚Üí
                        ( equiv-concat'
                          ( add-‚Ñ§ (neg-‚Ñ§ x) (map-equiv (pr1 e) zero-‚Ñ§))
                          ( left-inverse-law-add-‚Ñ§ x)) ‚àòe
                        ( equiv-ap
                          ( equiv-add-‚Ñ§ (neg-‚Ñ§ x))
                          ( map-equiv (pr1 e) zero-‚Ñ§)
                          ( x))))))))
            ( is-initial-‚Ñ§-Pointed-Type-With-Aut ‚Ñ§-Pointed-Type-With-Aut))

  equiv-left-factor-compute-Œ©-‚Ñ§-Torsor :
    equiv-‚Ñ§-Torsor canonical-‚Ñ§-Torsor canonical-‚Ñ§-Torsor ‚âÉ ‚Ñ§
  pr1 equiv-left-factor-compute-Œ©-‚Ñ§-Torsor = map-left-factor-compute-Œ©-‚Ñ§-Torsor
  pr2 equiv-left-factor-compute-Œ©-‚Ñ§-Torsor =
    is-equiv-map-left-factor-compute-Œ©-‚Ñ§-Torsor

  compute-Œ©-‚Ñ§-Torsor : type-Œ© ‚Ñ§-Torsor-Pointed-Type ‚âÉ ‚Ñ§
  compute-Œ©-‚Ñ§-Torsor =
    ( equiv-left-factor-compute-Œ©-‚Ñ§-Torsor) ‚àòe
    ( equiv-equiv-eq-‚Ñ§-Torsor canonical-‚Ñ§-Torsor)

‚Ñ§-Torsor-ùïä¬π : ùïä¬π ‚Üí ‚Ñ§-Torsor
pr1 (pr1 (‚Ñ§-Torsor-ùïä¬π x)) = Id x x
pr2 (pr1 (‚Ñ§-Torsor-ùïä¬π x)) = {!!}
pr2 (‚Ñ§-Torsor-ùïä¬π x) = {!!}

```
