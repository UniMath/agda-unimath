# Raising the universe levels of real numbers

```agda
module real-numbers.raising-universe-levels-real-numbers where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.rational-numbers
open import elementary-number-theory.strict-inequality-rational-numbers

open import foundation.cartesian-product-types
open import foundation.conjunction
open import foundation.dependent-pair-types
open import foundation.disjunction
open import foundation.existential-quantification
open import foundation.function-types
open import foundation.functoriality-cartesian-product-types
open import foundation.functoriality-disjunction
open import foundation.identity-types
open import foundation.inhabited-subtypes
open import foundation.logical-equivalences
open import foundation.negation
open import foundation.raising-universe-levels
open import foundation.subtypes
open import foundation.universe-levels

open import logic.functoriality-existential-quantification

open import real-numbers.dedekind-real-numbers
open import real-numbers.lower-dedekind-real-numbers
open import real-numbers.similarity-real-numbers
open import real-numbers.upper-dedekind-real-numbers
```

</details>

## Idea

For every [universe](foundation.universe-levels.md) `ğ’°` there is a type of
[real numbers](real-numbers.dedekind-real-numbers.md) `â„` relative to `ğ’°`,
`â„ ğ’°`. Given a larger universe `ğ’±`, then we may
{{#concept "raise" Disambiguation="a dedekind real number" Agda=raise-â„}} a real
number `x` from the universe `ğ’°` to a
[similar](real-numbers.similarity-real-numbers.md) real number in the universe
`ğ’±`.

## Definition

### Raising lower Dedekind real numbers

```agda
module _
  {l0 : Level} (l : Level) (x : lower-â„ l0)
  where

  cut-raise-lower-â„ : subtype (l0 âŠ” l) â„š
  cut-raise-lower-â„ = raise-subtype l (cut-lower-â„ x)

  abstract
    is-inhabited-cut-raise-lower-â„ : is-inhabited-subtype cut-raise-lower-â„
    is-inhabited-cut-raise-lower-â„ =
      map-tot-exists (Î» _ â†’ map-raise) (is-inhabited-cut-lower-â„ x)

    is-rounded-cut-raise-lower-â„ :
      (q : â„š) â†’
      is-in-subtype cut-raise-lower-â„ q â†”
      exists â„š (Î» r â†’ le-â„š-Prop q r âˆ§ cut-raise-lower-â„ r)
    pr1 (is-rounded-cut-raise-lower-â„ q) (map-raise q<x) =
      map-tot-exists
        ( Î» _ â†’ map-product id map-raise)
        ( forward-implication (is-rounded-cut-lower-â„ x q) q<x)
    pr2 (is-rounded-cut-raise-lower-â„ q) âˆƒr =
      map-raise
        ( backward-implication
          ( is-rounded-cut-lower-â„ x q)
          ( map-tot-exists (Î» _ â†’ map-product id map-inv-raise) âˆƒr))

  raise-lower-â„ : lower-â„ (l0 âŠ” l)
  raise-lower-â„ =
    cut-raise-lower-â„ ,
    is-inhabited-cut-raise-lower-â„ ,
    is-rounded-cut-raise-lower-â„
```

### Raising upper Dedekind real numbers

```agda
module _
  {l0 : Level} (l : Level) (x : upper-â„ l0)
  where

  cut-raise-upper-â„ : subtype (l0 âŠ” l) â„š
  cut-raise-upper-â„ = raise-subtype l (cut-upper-â„ x)

  abstract
    is-inhabited-cut-raise-upper-â„ : is-inhabited-subtype cut-raise-upper-â„
    is-inhabited-cut-raise-upper-â„ =
      map-tot-exists (Î» _ â†’ map-raise) (is-inhabited-cut-upper-â„ x)

    is-rounded-cut-raise-upper-â„ :
      (q : â„š) â†’
      is-in-subtype cut-raise-upper-â„ q â†”
      exists â„š (Î» p â†’ le-â„š-Prop p q âˆ§ cut-raise-upper-â„ p)
    pr1 (is-rounded-cut-raise-upper-â„ q) (map-raise x<q) =
      map-tot-exists
        ( Î» _ â†’ map-product id map-raise)
        ( forward-implication (is-rounded-cut-upper-â„ x q) x<q)
    pr2 (is-rounded-cut-raise-upper-â„ q) âˆƒp =
      map-raise
        ( backward-implication
          ( is-rounded-cut-upper-â„ x q)
          ( map-tot-exists (Î» _ â†’ map-product id map-inv-raise) âˆƒp))

  raise-upper-â„ : upper-â„ (l0 âŠ” l)
  raise-upper-â„ =
    cut-raise-upper-â„ ,
    is-inhabited-cut-raise-upper-â„ ,
    is-rounded-cut-raise-upper-â„
```

### Raising Dedekind real numbers

```agda
module _
  {l0 : Level} (l : Level) (x : â„ l0)
  where

  lower-real-raise-â„ : lower-â„ (l0 âŠ” l)
  lower-real-raise-â„ = raise-lower-â„ l (lower-real-â„ x)

  upper-real-raise-â„ : upper-â„ (l0 âŠ” l)
  upper-real-raise-â„ = raise-upper-â„ l (upper-real-â„ x)

  abstract
    is-disjoint-cut-raise-â„ :
      (q : â„š) â†’
      Â¬
        ( is-in-cut-lower-â„ lower-real-raise-â„ q Ã—
          is-in-cut-upper-â„ upper-real-raise-â„ q)
    is-disjoint-cut-raise-â„ q (map-raise q<x , map-raise x<q) =
      is-disjoint-cut-â„ x q (q<x , x<q)

    is-located-lower-upper-cut-raise-â„ :
      (p q : â„š) â†’ le-â„š p q â†’
      type-disjunction-Prop
        ( cut-lower-â„ lower-real-raise-â„ p)
        ( cut-upper-â„ upper-real-raise-â„ q)
    is-located-lower-upper-cut-raise-â„ p q p<q =
      map-disjunction
        ( map-raise)
        ( map-raise)
        ( is-located-lower-upper-cut-â„ x p<q)

  raise-â„ : â„ (l0 âŠ” l)
  raise-â„ =
    real-lower-upper-â„
      ( lower-real-raise-â„)
      ( upper-real-raise-â„)
      ( is-disjoint-cut-raise-â„)
      ( is-located-lower-upper-cut-raise-â„)
```

## Properties

### Reals are similar to their raised-universe equivalents

```agda
abstract opaque
  unfolding sim-â„

  sim-raise-â„ : {l0 : Level} (l : Level) (x : â„ l0) â†’ sim-â„ x (raise-â„ l x)
  pr1 (sim-raise-â„ l x) _ = map-raise
  pr2 (sim-raise-â„ l x) _ = map-inv-raise

abstract
  sim-raise-â„' : {l0 : Level} (l : Level) (x : â„ l0) â†’ sim-â„ (raise-â„ l x) x
  sim-raise-â„' l x = symmetric-sim-â„ (sim-raise-â„ l x)

  sim-raise-raise-â„ :
    {l0 : Level} (l1 l2 : Level) (x : â„ l0) â†’
    sim-â„ (raise-â„ l1 x) (raise-â„ l2 x)
  sim-raise-raise-â„ l1 l2 x =
    transitive-sim-â„ _ _ _ (sim-raise-â„ l2 x) (sim-raise-â„' l1 x)
```

### Raising a real to its own level is the identity

```agda
eq-raise-â„ : {l : Level} â†’ (x : â„ l) â†’ x ï¼ raise-â„ l x
eq-raise-â„ {l} x = eq-sim-â„ (sim-raise-â„ l x)
```
