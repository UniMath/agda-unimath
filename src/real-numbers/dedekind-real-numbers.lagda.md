# Dedekind real numbers

```agda
module real-numbers.dedekind-real-numbers where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.inequality-rational-numbers
open import elementary-number-theory.rational-numbers
open import elementary-number-theory.strict-inequality-rational-numbers

open import foundation.binary-transport
open import foundation.cartesian-product-types
open import foundation.complements-subtypes
open import foundation.conjunction
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.disjunction
open import foundation.embeddings
open import foundation.empty-types
open import foundation.existential-quantification
open import foundation.function-types
open import foundation.functoriality-cartesian-product-types
open import foundation.functoriality-dependent-pair-types
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.negation
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.sets
open import foundation.subtypes
open import foundation.transport-along-identifications
open import foundation.truncated-types
open import foundation.universal-quantification
open import foundation.universe-levels

open import foundation-core.truncation-levels

open import logic.functoriality-existential-quantification

open import real-numbers.lower-dedekind-real-numbers
open import real-numbers.upper-dedekind-real-numbers
```

</details>

## Idea

A
{{#concept "Dedekind cut" Agda=is-dedekind-cut WD="dedekind cut" WDID=Q851333}}
consists of a [pair](foundation.dependent-pair-types.md) `(L , U)` of a
[lower Dedekind cut](real-numbers.lower-dedekind-real-numbers) and an
[upper Dedekind cut](real-numbers.upper-dedekind-real-numbers) that also satisfy
the following conditions:

1. _Disjointness_. `L` and `U` are disjoint subsets of `â„š`.
2. _Locatedness_. If `q < r` then `q âˆˆ L` or `r âˆˆ U`.

The type of {{#concept "Dedekind real numbers" Agda=â„}} is the type of all
Dedekind cuts. The Dedekind real numbers will be taken as the standard
definition of the real numbers in the agda-unimath library.

## Definition

### Dedekind cuts

```agda
module _
  {l1 l2 : Level} (L : subtype l1 â„š) (U : subtype l2 â„š)
  where

  is-disjoint-cut-Prop : Prop (l1 âŠ” l2)
  is-disjoint-cut-Prop = âˆ€' â„š (Î» q â†’ Â¬' (L q âˆ§ U q))

  is-disjoint-cut : UU (l1 âŠ” l2)
  is-disjoint-cut = type-Prop is-disjoint-cut-Prop

  is-located-cut-Prop : Prop (l1 âŠ” l2)
  is-located-cut-Prop = âˆ€' â„š (Î» q â†’ âˆ€' â„š (Î» r â†’ le-â„š-Prop q r â‡’ (L q âˆ¨ U r)))

  is-located-cut : UU (l1 âŠ” l2)
  is-located-cut = type-Prop is-located-cut-Prop

  is-dedekind-cut-Prop : Prop (l1 âŠ” l2)
  is-dedekind-cut-Prop =
    is-lower-dedekind-cut-Prop L âˆ§
    is-upper-dedekind-cut-Prop U âˆ§
    is-disjoint-cut-Prop âˆ§
    is-located-cut-Prop

  is-dedekind-cut : UU (l1 âŠ” l2)
  is-dedekind-cut = type-Prop is-dedekind-cut-Prop

  is-prop-is-dedekind-cut : is-prop is-dedekind-cut
  is-prop-is-dedekind-cut = is-prop-type-Prop is-dedekind-cut-Prop
```

### The Dedekind real numbers

```agda
â„ : (l : Level) â†’ UU (lsuc l)
â„ l = Î£ (subtype l â„š) (Î» L â†’ Î£ (subtype l â„š) (is-dedekind-cut L))

real-dedekind-cut : {l : Level} (L U : subtype l â„š) â†’ is-dedekind-cut L U â†’ â„ l
real-dedekind-cut L U H = L , U , H

real-lower-upper-â„ :
  {l : Level} â†’
  (L : lower-â„ l) (U : upper-â„ l) â†’
  is-disjoint-cut (cut-lower-â„ L) (cut-upper-â„ U) â†’
  is-located-cut (cut-lower-â„ L) (cut-upper-â„ U) â†’
  â„ l
real-lower-upper-â„ (L , lower-dedekind-L) (U , lower-dedekind-U) H K =
  L , U , lower-dedekind-L , lower-dedekind-U , H , K

module _
  {l : Level} (x : â„ l)
  where

  lower-cut-â„ : subtype l â„š
  lower-cut-â„ = pr1 x

  upper-cut-â„ : subtype l â„š
  upper-cut-â„ = pr1 (pr2 x)

  lower-real-â„ : lower-â„ l
  lower-real-â„ = lower-cut-â„ , pr1 (pr2 (pr2 x))

  upper-real-â„ : upper-â„ l
  upper-real-â„ = upper-cut-â„ , pr1 (pr2 (pr2 (pr2 x)))

  is-in-lower-cut-â„ : â„š â†’ UU l
  is-in-lower-cut-â„ = is-in-subtype lower-cut-â„

  is-in-upper-cut-â„ : â„š â†’ UU l
  is-in-upper-cut-â„ = is-in-subtype upper-cut-â„

  is-dedekind-cut-cut-â„ : is-dedekind-cut lower-cut-â„ upper-cut-â„
  is-dedekind-cut-cut-â„ = pr2 (pr2 x)

  is-inhabited-lower-cut-â„ : exists â„š lower-cut-â„
  is-inhabited-lower-cut-â„ = is-inhabited-cut-lower-â„ lower-real-â„

  is-inhabited-upper-cut-â„ : exists â„š upper-cut-â„
  is-inhabited-upper-cut-â„ = is-inhabited-cut-upper-â„ upper-real-â„

  is-rounded-lower-cut-â„ :
    (q : â„š) â†’
    is-in-lower-cut-â„ q â†”
    exists â„š (Î» r â†’ (le-â„š-Prop q r) âˆ§ (lower-cut-â„ r))
  is-rounded-lower-cut-â„ = is-rounded-cut-lower-â„ lower-real-â„

  is-rounded-upper-cut-â„ :
    (r : â„š) â†’
    is-in-upper-cut-â„ r â†”
    exists â„š (Î» q â†’ (le-â„š-Prop q r) âˆ§ (upper-cut-â„ q))
  is-rounded-upper-cut-â„ = is-rounded-cut-upper-â„ upper-real-â„

  is-disjoint-cut-â„ : (q : â„š) â†’ Â¬ (is-in-lower-cut-â„ q Ã— is-in-upper-cut-â„ q)
  is-disjoint-cut-â„ = pr1 (pr2 (pr2 (pr2 (pr2 x))))

  is-located-lower-upper-cut-â„ :
    (q r : â„š) â†’ le-â„š q r â†’
    type-disjunction-Prop (lower-cut-â„ q) (upper-cut-â„ r)
  is-located-lower-upper-cut-â„ = pr2 (pr2 (pr2 (pr2 (pr2 x))))

  cut-â„ : subtype l â„š
  cut-â„ q =
    coproduct-Prop
      ( lower-cut-â„ q)
      ( upper-cut-â„ q)
      ( ev-pair ( is-disjoint-cut-â„ q))

  is-in-cut-â„ : â„š â†’ UU l
  is-in-cut-â„ = is-in-subtype cut-â„
```

## Properties

### The Dedekind real numbers form a set

```agda
abstract
  is-set-â„ : (l : Level) â†’ is-set (â„ l)
  is-set-â„ l =
    is-set-Î£
      ( is-set-function-type (is-trunc-Truncated-Type neg-one-ð•‹))
      ( Î» x â†’
        is-set-Î£
          ( is-set-function-type (is-trunc-Truncated-Type neg-one-ð•‹))
          ( Î» y â†’
            ( is-set-is-prop
              ( is-prop-type-Prop
                ( is-dedekind-cut-Prop x y)))))

â„-Set : (l : Level) â†’ Set (lsuc l)
â„-Set l = â„ l , is-set-â„ l
```

## Properties of lower/upper Dedekind cuts

### Lower and upper Dedekind cuts are closed under the standard ordering on the rationals

```agda
module _
  {l : Level} (x : â„ l) (p q : â„š)
  where

  le-lower-cut-â„ :
    le-â„š p q â†’
    is-in-lower-cut-â„ x q â†’
    is-in-lower-cut-â„ x p
  le-lower-cut-â„ H H' =
    ind-trunc-Prop
      ( Î» s â†’ lower-cut-â„ x p)
      ( rec-coproduct
          ( id)
          ( Î» I â†’ ex-falso (is-disjoint-cut-â„ x q (H' , I))))
      ( is-located-lower-upper-cut-â„ x p q H)

  leq-lower-cut-â„ :
    leq-â„š p q â†’
    is-in-lower-cut-â„ x q â†’
    is-in-lower-cut-â„ x p
  leq-lower-cut-â„ H H' =
    rec-coproduct
      ( Î» s â†’ le-lower-cut-â„ s H')
      ( Î» I â†’
        tr
          ( is-in-lower-cut-â„ x)
          ( antisymmetric-leq-â„š q p I H)
          ( H'))
      ( decide-le-leq-â„š p q)

  le-upper-cut-â„ :
    le-â„š p q â†’
    is-in-upper-cut-â„ x p â†’
    is-in-upper-cut-â„ x q
  le-upper-cut-â„ H H' =
    ind-trunc-Prop
      ( Î» s â†’ upper-cut-â„ x q)
      ( rec-coproduct
        ( Î» I â†’ ex-falso (is-disjoint-cut-â„ x p ( I , H')))
        ( id))
      ( is-located-lower-upper-cut-â„ x p q H)

  leq-upper-cut-â„ :
    leq-â„š p q â†’
    is-in-upper-cut-â„ x p â†’
    is-in-upper-cut-â„ x q
  leq-upper-cut-â„ H H' =
    rec-coproduct
      ( Î» s â†’ le-upper-cut-â„ s H')
      ( Î» I â†’
        tr
          ( is-in-upper-cut-â„ x)
          ( antisymmetric-leq-â„š p q H I)
          ( H'))
      ( decide-le-leq-â„š p q)
```

### Elements of the lower cut are lower bounds of the upper cut

```agda
module _
  {l : Level} (x : â„ l) (p q : â„š)
  where

  le-lower-upper-cut-â„ :
    is-in-lower-cut-â„ x p â†’
    is-in-upper-cut-â„ x q â†’
    le-â„š p q
  le-lower-upper-cut-â„ H H' =
    rec-coproduct
      ( id)
      ( Î» I â†’
        ex-falso
          ( is-disjoint-cut-â„ x p
              ( H , leq-upper-cut-â„ x q p I H')))
      ( decide-le-leq-â„š p q)
```

### Characterisation of each cut by the other

#### The lower cut is the subtype of rationals bounded above by some element of the complement of the upper cut

```agda
module _
  {l : Level} (x : â„ l)
  where

  is-lower-complement-upper-cut-â„-Prop : (p q : â„š) â†’ Prop l
  is-lower-complement-upper-cut-â„-Prop p q =
    ( le-â„š-Prop p q) âˆ§ (Â¬' (upper-cut-â„ x q))

  lower-complement-upper-cut-â„ : subtype l â„š
  lower-complement-upper-cut-â„ p =
    âˆƒ â„š (is-lower-complement-upper-cut-â„-Prop p)
```

```agda
module _
  {l : Level} (x : â„ l)
  where

  subset-lower-cut-lower-complement-upper-cut-â„ :
    lower-complement-upper-cut-â„ x âŠ† lower-cut-â„ x
  subset-lower-cut-lower-complement-upper-cut-â„ p =
    elim-exists
      ( lower-cut-â„ x p)
      ( Î» q I â†’
        map-right-unit-law-disjunction-is-empty-Prop
          ( lower-cut-â„ x p)
          ( upper-cut-â„ x q)
          ( pr2 I)
          ( is-located-lower-upper-cut-â„ x p q ( pr1 I)))

  subset-lower-complement-upper-cut-lower-cut-â„ :
    lower-cut-â„ x âŠ† lower-complement-upper-cut-â„ x
  subset-lower-complement-upper-cut-lower-cut-â„ p H =
    map-tot-exists
      ( Î» q â†’ map-product id (Î» L U â†’ is-disjoint-cut-â„ x q (L , U)))
      ( pr1 (is-rounded-lower-cut-â„ x p) H)

  eq-lower-cut-lower-complement-upper-cut-â„ :
    lower-complement-upper-cut-â„ x ï¼ lower-cut-â„ x
  eq-lower-cut-lower-complement-upper-cut-â„ =
    antisymmetric-leq-subtype
      ( lower-complement-upper-cut-â„ x)
      ( lower-cut-â„ x)
      ( subset-lower-cut-lower-complement-upper-cut-â„)
      ( subset-lower-complement-upper-cut-lower-cut-â„)
```

#### The upper cut is the subtype of rationals bounded below by some element of the complement of the lower cut

```agda
module _
  {l : Level} (x : â„ l)
  where

  is-upper-complement-lower-cut-â„-Prop : (q p : â„š) â†’ Prop l
  is-upper-complement-lower-cut-â„-Prop q p =
    (le-â„š-Prop p q) âˆ§ (Â¬' (lower-cut-â„ x p))

  upper-complement-lower-cut-â„ : subtype l â„š
  upper-complement-lower-cut-â„ q =
    âˆƒ â„š (is-upper-complement-lower-cut-â„-Prop q)
```

```agda
module _
  {l : Level} (x : â„ l)
  where

  subset-upper-cut-upper-complement-lower-cut-â„ :
    upper-complement-lower-cut-â„ x âŠ† upper-cut-â„ x
  subset-upper-cut-upper-complement-lower-cut-â„ q =
    elim-exists
      ( upper-cut-â„ x q)
      ( Î» p I â†’
        map-left-unit-law-disjunction-is-empty-Prop
          ( lower-cut-â„ x p)
          ( upper-cut-â„ x q)
          ( pr2 I)
          ( is-located-lower-upper-cut-â„ x p q ( pr1 I)))

  subset-upper-complement-lower-cut-upper-cut-â„ :
    upper-cut-â„ x âŠ† upper-complement-lower-cut-â„ x
  subset-upper-complement-lower-cut-upper-cut-â„ q H =
    map-tot-exists
      ( Î» p â†’ map-product id (Î» U L â†’ is-disjoint-cut-â„ x p (L , U)))
      ( pr1 (is-rounded-upper-cut-â„ x q) H)

  eq-upper-cut-upper-complement-lower-cut-â„ :
    upper-complement-lower-cut-â„ x ï¼ upper-cut-â„ x
  eq-upper-cut-upper-complement-lower-cut-â„ =
    antisymmetric-leq-subtype
      ( upper-complement-lower-cut-â„ x)
      ( upper-cut-â„ x)
      ( subset-upper-cut-upper-complement-lower-cut-â„)
      ( subset-upper-complement-lower-cut-upper-cut-â„)
```

### The lower/upper cut of a real determines the other

```agda
module _
  {l : Level} (x y : â„ l)
  where

  subset-lower-cut-upper-cut-â„ :
    upper-cut-â„ y âŠ† upper-cut-â„ x â†’ lower-cut-â„ x âŠ† lower-cut-â„ y
  subset-lower-cut-upper-cut-â„ H =
    binary-tr
      ( _âŠ†_)
      ( eq-lower-cut-lower-complement-upper-cut-â„ x)
      ( eq-lower-cut-lower-complement-upper-cut-â„ y)
      ( Î» p â†’ map-tot-exists (Î» q â†’ tot (Î» _ K â†’ K âˆ˜ H q)))

  subset-upper-cut-lower-cut-â„ :
    lower-cut-â„ x âŠ† lower-cut-â„ y â†’ upper-cut-â„ y âŠ† upper-cut-â„ x
  subset-upper-cut-lower-cut-â„ H =
    binary-tr
      ( _âŠ†_)
      ( eq-upper-cut-upper-complement-lower-cut-â„ y)
      ( eq-upper-cut-upper-complement-lower-cut-â„ x)
      ( Î» q â†’ map-tot-exists (Î» p â†’ tot (Î» _ K â†’ K âˆ˜ H p)))

module _
  {l : Level} (x y : â„ l)
  where

  eq-lower-cut-eq-upper-cut-â„ :
    upper-cut-â„ x ï¼ upper-cut-â„ y â†’ lower-cut-â„ x ï¼ lower-cut-â„ y
  eq-lower-cut-eq-upper-cut-â„ H =
    antisymmetric-leq-subtype
      ( lower-cut-â„ x)
      ( lower-cut-â„ y)
      ( subset-lower-cut-upper-cut-â„ x y
        ( pr2 âˆ˜ has-same-elements-eq-subtype
          ( upper-cut-â„ x)
          ( upper-cut-â„ y)
          ( H)))
      ( subset-lower-cut-upper-cut-â„ y x
        ( pr1 âˆ˜ has-same-elements-eq-subtype
          ( upper-cut-â„ x)
          ( upper-cut-â„ y)
          ( H)))

  eq-upper-cut-eq-lower-cut-â„ :
    lower-cut-â„ x ï¼ lower-cut-â„ y â†’ upper-cut-â„ x ï¼ upper-cut-â„ y
  eq-upper-cut-eq-lower-cut-â„ H =
    antisymmetric-leq-subtype
      ( upper-cut-â„ x)
      ( upper-cut-â„ y)
      ( subset-upper-cut-lower-cut-â„ y x
        ( pr2 âˆ˜ has-same-elements-eq-subtype
          ( lower-cut-â„ x)
          ( lower-cut-â„ y)
          ( H)))
      ( subset-upper-cut-lower-cut-â„ x y
        ( pr1 âˆ˜ has-same-elements-eq-subtype
          ( lower-cut-â„ x)
          ( lower-cut-â„ y)
          ( H)))
```

### The map from a real number to its lower cut is an embedding

```agda
module _
  {l : Level} (L : subtype l â„š)
  where

  has-upper-cut-Prop : Prop (lsuc l)
  has-upper-cut-Prop =
    pair
      ( Î£ (subtype l â„š) (is-dedekind-cut L))
      ( is-prop-all-elements-equal
        ( Î» U U' â†’
          eq-type-subtype
            ( is-dedekind-cut-Prop L)
            ( eq-upper-cut-eq-lower-cut-â„
              ( pair L U)
              ( pair L U')
              ( refl))))

is-emb-lower-cut : {l : Level} â†’ is-emb (lower-cut-â„ {l})
is-emb-lower-cut = is-emb-inclusion-subtype has-upper-cut-Prop
```

### Two real numbers with the same lower/upper cut are equal

```agda
module _
  {l : Level} (x y : â„ l)
  where

  eq-eq-lower-cut-â„ : lower-cut-â„ x ï¼ lower-cut-â„ y â†’ x ï¼ y
  eq-eq-lower-cut-â„ = eq-type-subtype has-upper-cut-Prop

  eq-eq-upper-cut-â„ : upper-cut-â„ x ï¼ upper-cut-â„ y â†’ x ï¼ y
  eq-eq-upper-cut-â„ = eq-eq-lower-cut-â„ âˆ˜ (eq-lower-cut-eq-upper-cut-â„ x y)
```

## References

This page follows parts of Section 11.2 in {{#cite UF13}}.

{{#bibliography}}

## External links

- [DedekindReals.Type](https://www.cs.bham.ac.uk/~mhe/TypeTopology/DedekindReals.Type.html)
  at TypeTopology
- [Dedekind cut](https://ncatlab.org/nlab/show/Dedekind+cut) at $n$Lab
- [Dedekind cut](https://en.wikipedia.org/wiki/Dedekind_cut) at Wikipedia
- [Construction of the real numbers by Dedekind cuts](https://en.wikipedia.org/wiki/Construction_of_the_real_numbers#Construction_by_Dedekind_cuts)
  at Wikipedia
- [Dedekind cut](https://www.britannica.com/science/Dedekind-cut) at Britannica
