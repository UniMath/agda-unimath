# Upper Dedekind real numbers

```agda
{-# OPTIONS --lossy-unification #-}

module real-numbers.upper-dedekind-real-numbers where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.addition-positive-rational-numbers
open import elementary-number-theory.addition-rational-numbers
open import elementary-number-theory.difference-rational-numbers
open import elementary-number-theory.inequality-rational-numbers
open import elementary-number-theory.positive-rational-numbers
open import elementary-number-theory.rational-numbers
open import elementary-number-theory.strict-inequality-rational-numbers

open import foundation.conjunction
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.existential-quantification
open import foundation.function-types
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.propositions
open import foundation.sets
open import foundation.similarity-subtypes
open import foundation.subtypes
open import foundation.transport-along-identifications
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.unit-type
open import foundation.universal-quantification
open import foundation.universe-levels

open import logic.functoriality-existential-quantification
```

</details>

## Idea

A [subtype](foundation-core.subtypes.md) `U` of
[the rational numbers](elementary-number-theory.rational-numbers.md) is an
{{#concept "upper Dedekind cut" Agda=is-upper-dedekind-cut}} if it satisfies the
following two conditions:

1. _Inhabitedness_. `U` is [inhabited](foundation.inhabited-subtypes.md).
2. _Upper roundedness_. A rational number `q` is in `U`
   [if and only if](foundation.logical-equivalences.md) there
   [exists](foundation.existential-quantification.md) `p < q` such that `p âˆˆ U`.

The {{#concept "upper Dedekind real numbers" Agda=upper-â„}} is the type of all
upper Dedekind cuts.

## Definition

### Upper Dedekind cuts

```agda
module _
  {l : Level}
  (U : subtype l â„š)
  where

  is-upper-dedekind-cut-Prop : Prop l
  is-upper-dedekind-cut-Prop =
    (âˆƒ â„š U) âˆ§
    (âˆ€' â„š (Î» q â†’ U q â‡” (âˆƒ â„š (Î» p â†’ le-â„š-Prop p q âˆ§ U p))))

  is-upper-dedekind-cut : UU l
  is-upper-dedekind-cut = type-Prop is-upper-dedekind-cut-Prop
```

## The upper Dedekind real numbers

```agda
upper-â„ : (l : Level) â†’ UU (lsuc l)
upper-â„ l = Î£ (subtype l â„š) is-upper-dedekind-cut

module _
  {l : Level}
  (x : upper-â„ l)
  where

  cut-upper-â„ : subtype l â„š
  cut-upper-â„ = pr1 x

  is-in-cut-upper-â„ : â„š â†’ UU l
  is-in-cut-upper-â„ = is-in-subtype cut-upper-â„

  is-upper-dedekind-cut-upper-â„ : is-upper-dedekind-cut cut-upper-â„
  is-upper-dedekind-cut-upper-â„ = pr2 x

  is-inhabited-cut-upper-â„ : exists â„š cut-upper-â„
  is-inhabited-cut-upper-â„ = pr1 (pr2 x)

  is-rounded-cut-upper-â„ :
    (q : â„š) â†’
    is-in-cut-upper-â„ q â†” exists â„š (Î» p â†’ le-â„š-Prop p q âˆ§ cut-upper-â„ p)
  is-rounded-cut-upper-â„ = pr2 (pr2 x)
```

## Properties

### The least upper Dedekind real

There is a least upper Dedekind real whose cut is all rational numbers. We call
this element **negative infinity**.

```agda
neg-infinity-upper-â„ : upper-â„ lzero
pr1 neg-infinity-upper-â„ _ = unit-Prop
pr1 (pr2 neg-infinity-upper-â„) = intro-exists zero-â„š star
pr1 (pr2 (pr2 neg-infinity-upper-â„) q) _ =
  map-tot-exists (Î» _ â†’ _, star) (exists-lesser-â„š q)
pr2 (pr2 (pr2 neg-infinity-upper-â„) q) _ = star
```

### The upper Dedekind reals form a set

```agda
abstract
  is-set-upper-â„ : (l : Level) â†’ is-set (upper-â„ l)
  is-set-upper-â„ l =
    is-set-Î£
      ( is-set-function-type (is-trunc-Truncated-Type neg-one-ð•‹))
      ( Î» q â†’ is-set-is-prop (is-prop-type-Prop (is-upper-dedekind-cut-Prop q)))
```

### Upper Dedekind cuts are closed under strict inequality on the rationals

```agda
module _
  {l : Level} (x : upper-â„ l) (p q : â„š)
  where

  abstract
    is-in-cut-le-â„š-upper-â„ :
      le-â„š p q â†’ is-in-cut-upper-â„ x p â†’ is-in-cut-upper-â„ x q
    is-in-cut-le-â„š-upper-â„ p<q p<x =
      backward-implication
        ( is-rounded-cut-upper-â„ x q)
        ( intro-exists p (p<q , p<x))
```

### Upper Dedekind cuts are closed under the addition of positive rational numbers

```agda
module _
  {l : Level} (x : upper-â„ l) (p : â„š) (d : â„šâº)
  where

  abstract
    is-in-cut-add-rational-â„šâº-upper-â„ :
      is-in-cut-upper-â„ x p â†’ is-in-cut-upper-â„ x (p +â„š rational-â„šâº d)
    is-in-cut-add-rational-â„šâº-upper-â„ =
      is-in-cut-le-â„š-upper-â„
        ( x)
        ( p)
        ( p +â„š rational-â„šâº d)
        ( le-right-add-rational-â„šâº p d)
```

### Upper Dedekind cuts are closed under inequality on the rationals

```agda
module _
  {l : Level} (x : upper-â„ l) (p q : â„š)
  where

  abstract
    is-in-cut-leq-â„š-upper-â„ :
      leq-â„š p q â†’ is-in-cut-upper-â„ x p â†’ is-in-cut-upper-â„ x q
    is-in-cut-leq-â„š-upper-â„ pâ‰¤q x<p with decide-le-leq-â„š p q
    ... | inl p<q = is-in-cut-le-â„š-upper-â„ x p q p<q x<p
    ... | inr qâ‰¤p =
      tr (is-in-cut-upper-â„ x) (antisymmetric-leq-â„š p q pâ‰¤q qâ‰¤p) x<p
```

### Two upper real numbers with the same cut are equal

```agda
module _
  {l : Level} (x y : upper-â„ l)
  where

  eq-eq-cut-upper-â„ : cut-upper-â„ x ï¼ cut-upper-â„ y â†’ x ï¼ y
  eq-eq-cut-upper-â„ = eq-type-subtype is-upper-dedekind-cut-Prop

  eq-sim-cut-upper-â„ : sim-subtype (cut-upper-â„ x) (cut-upper-â„ y) â†’ x ï¼ y
  eq-sim-cut-upper-â„ =
    eq-eq-cut-upper-â„ âˆ˜ eq-sim-subtype (cut-upper-â„ x) (cut-upper-â„ y)
```

## See also

- Lower Dedekind cuts, the dual structure, are defined in
  [`real-numbers.lower-dedekind-real-numbers`](real-numbers.lower-dedekind-real-numbers.md).
- Dedekind cuts, which form the usual real numbers, are defined in
  [`real-numbers.dedekind-real-numbers`](real-numbers.dedekind-real-numbers.md)

## References

This page follows the terminology used in the exercises of Section 11 in
{{#cite UF13}}.

{{#bibliography}}
