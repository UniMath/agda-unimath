# Lower Dedekind real numbers

```agda
{-# OPTIONS --lossy-unification #-}

module real-numbers.lower-dedekind-real-numbers where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.addition-positive-rational-numbers
open import elementary-number-theory.addition-rational-numbers
open import elementary-number-theory.difference-rational-numbers
open import elementary-number-theory.inequality-rational-numbers
open import elementary-number-theory.positive-rational-numbers
open import elementary-number-theory.rational-numbers
open import elementary-number-theory.strict-inequality-rational-numbers

open import foundation.conjunction
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.existential-quantification
open import foundation.function-types
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.powersets
open import foundation.propositions
open import foundation.sets
open import foundation.similarity-subtypes
open import foundation.subtypes
open import foundation.transport-along-identifications
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.unit-type
open import foundation.universal-quantification
open import foundation.universe-levels
```

</details>

## Idea

A [subtype](foundation-core.subtypes.md) `L` of
[the rational numbers](elementary-number-theory.rational-numbers.md) is a
{{#concept "lower Dedekind cut" Agda=is-lower-dedekind-cut}} if it satisfies the
following two conditions:

1. _Inhabitedness_. `L` is [inhabited](foundation.inhabited-subtypes.md).
2. _Lower roundedness_. A rational number `q` is in `L`
   [if and only if](foundation.logical-equivalences.md) there
   [exists](foundation.existential-quantification.md) `q < r` such that `r âˆˆ L`.

The {{#concept "lower Dedekind real numbers" Agda=lower-â„}} is the type of all
lower Dedekind cuts.

## Definition

### Lower Dedekind cuts

```agda
module _
  {l : Level}
  (L : subtype l â„š)
  where

  is-lower-dedekind-cut-Prop : Prop l
  is-lower-dedekind-cut-Prop =
    (âˆƒ â„š L) âˆ§
    (âˆ€' â„š (Î» q â†’ L q â‡” (âˆƒ â„š (Î» r â†’ le-â„š-Prop q r âˆ§ L r))))

  is-lower-dedekind-cut : UU l
  is-lower-dedekind-cut = type-Prop is-lower-dedekind-cut-Prop
```

## The lower Dedekind real numbers

```agda
lower-â„ : (l : Level) â†’ UU (lsuc l)
lower-â„ l = Î£ (subtype l â„š) is-lower-dedekind-cut

module _
  {l : Level}
  (x : lower-â„ l)
  where

  cut-lower-â„ : subtype l â„š
  cut-lower-â„ = pr1 x

  is-in-cut-lower-â„ : â„š â†’ UU l
  is-in-cut-lower-â„ = is-in-subtype cut-lower-â„

  is-lower-dedekind-cut-lower-â„ : is-lower-dedekind-cut cut-lower-â„
  is-lower-dedekind-cut-lower-â„ = pr2 x

  is-inhabited-cut-lower-â„ : exists â„š cut-lower-â„
  is-inhabited-cut-lower-â„ = pr1 (pr2 x)

  is-rounded-cut-lower-â„ :
    (q : â„š) â†’
    is-in-cut-lower-â„ q â†” exists â„š (Î» r â†’ le-â„š-Prop q r âˆ§ cut-lower-â„ r)
  is-rounded-cut-lower-â„ = pr2 (pr2 x)
```

## Properties

### The greatest lower Dedekind real

There is a largest lower Dedekind real whose cut is all rational numbers. We
call this element **infinity**.

```agda
infinity-lower-â„ : lower-â„ lzero
pr1 infinity-lower-â„ _ = unit-Prop
pr1 (pr2 infinity-lower-â„) = intro-exists zero-â„š star
pr1 (pr2 (pr2 infinity-lower-â„) q) _ =
  intro-exists (q +â„š one-â„š) (le-right-add-rational-â„šâº q one-â„šâº , star)
pr2 (pr2 (pr2 infinity-lower-â„) q) _ = star
```

### The lower Dedekind reals form a set

```agda
abstract
  is-set-lower-â„ : (l : Level) â†’ is-set (lower-â„ l)
  is-set-lower-â„ l =
    is-set-Î£
      ( is-set-function-type (is-trunc-Truncated-Type neg-one-ð•‹))
      ( Î» q â†’ is-set-is-prop (is-prop-type-Prop (is-lower-dedekind-cut-Prop q)))
```

### Lower Dedekind cuts are closed under strict inequality on the rationals

```agda
module _
  {l : Level} (x : lower-â„ l) (p q : â„š)
  where

  is-in-cut-le-â„š-lower-â„ :
    le-â„š p q â†’ is-in-cut-lower-â„ x q â†’ is-in-cut-lower-â„ x p
  is-in-cut-le-â„š-lower-â„ p<q q<x =
    backward-implication
      ( is-rounded-cut-lower-â„ x p)
      ( intro-exists q (p<q , q<x))
```

### Lower Dedekind cuts are closed under subtraction by positive rational numbers

```agda
module _
  {l : Level} (x : lower-â„ l) (p : â„š) (d : â„šâº)
  where

  is-in-cut-diff-rational-â„šâº-lower-â„ :
    is-in-cut-lower-â„ x p â†’ is-in-cut-lower-â„ x (p -â„š rational-â„šâº d)
  is-in-cut-diff-rational-â„šâº-lower-â„ =
    is-in-cut-le-â„š-lower-â„ x (p -â„š rational-â„šâº d) p (le-diff-rational-â„šâº p d)
```

### Lower Dedekind cuts are closed under inequality on the rationals

```agda
module _
  {l : Level} (x : lower-â„ l) (p q : â„š)
  where

  is-in-cut-leq-â„š-lower-â„ :
    leq-â„š p q â†’ is-in-cut-lower-â„ x q â†’ is-in-cut-lower-â„ x p
  is-in-cut-leq-â„š-lower-â„ pâ‰¤q q<x with decide-le-leq-â„š p q
  ... | inl p<q = is-in-cut-le-â„š-lower-â„ x p q p<q q<x
  ... | inr qâ‰¤p = tr (is-in-cut-lower-â„ x) (antisymmetric-leq-â„š q p qâ‰¤p pâ‰¤q) q<x
```

### Two lower real numbers with the same cut are equal

```agda
module _
  {l : Level} (x y : lower-â„ l)
  where

  eq-eq-cut-lower-â„ : cut-lower-â„ x ï¼ cut-lower-â„ y â†’ x ï¼ y
  eq-eq-cut-lower-â„ = eq-type-subtype is-lower-dedekind-cut-Prop

  eq-sim-cut-lower-â„ : sim-subtype (cut-lower-â„ x) (cut-lower-â„ y) â†’ x ï¼ y
  eq-sim-cut-lower-â„ =
    eq-eq-cut-lower-â„ âˆ˜ eq-sim-subtype (cut-lower-â„ x) (cut-lower-â„ y)
```

## See also

- Upper Dedekind cuts, the dual structure, are defined in
  [`real-numbers.upper-dedekind-real-numbers`](real-numbers.upper-dedekind-real-numbers.md).
- Dedekind cuts, which form the usual real numbers, are defined in
  [`real-numbers.dedekind-real-numbers`](real-numbers.dedekind-real-numbers.md)

## References

This page follows the terminology used in the exercises of Section 11 in
{{#cite UF13}}.

{{#bibliography}}
