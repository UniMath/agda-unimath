# Large âˆ-magmoids

```agda
{-# OPTIONS --guardedness #-}

module structured-types.large-infinity-magmoids where
```

<details><summary>Imports</summary>

```agda
open import foundation.universe-levels

open import structured-types.infinity-magmoids
open import structured-types.large-globular-types
```

</details>

## Idea

A {{#concept "large $âˆ$-magmoid" Agda=Large-âˆ-Magmoid}} is a
[large globular type](structured-types.large-globular-types.md) `A`
[equipped](foundation.structure.md) with a binary operator

```text
  - * - : (ğ‘›+1)-Cell A y z â†’ (ğ‘›+1)-Cell A x y â†’ (ğ‘›+1)-Cell A x z
```

at every level $n$.

## Definition

### The structure of a large $âˆ$-magmoid on a large globular type

```agda
record
  is-âˆ-magmoid-large-globular-structure
  {Î± : Level â†’ Level} {Î² : Level â†’ Level â†’ Level}
  {A : (l : Level) â†’ UU (Î± l)}
  (G : large-globular-structure Î² A) : UUÏ‰
  where

  field
    comp-1-cell-is-âˆ-magmoid-large-globular-structure :
      {l1 l2 l3 : Level} {x : A l1} {y : A l2} {z : A l3} â†’
      1-cell-large-globular-structure G y z â†’
      1-cell-large-globular-structure G x y â†’
      1-cell-large-globular-structure G x z

    is-âˆ-magmoid-globular-structure-1-cell-is-âˆ-magmoid-large-globular-structure :
      {l1 l2 : Level} (x : A l1) (y : A l2) â†’
      is-âˆ-magmoid-globular-structure
        ( globular-structure-1-cell-large-globular-structure G x y)

open is-âˆ-magmoid-large-globular-structure public

module _
  {Î± : Level â†’ Level} {Î² : Level â†’ Level â†’ Level}
  {A : (l : Level) â†’ UU (Î± l)}
  {G : large-globular-structure Î² A}
  (r : is-âˆ-magmoid-large-globular-structure G)
  where

  comp-2-cell-is-âˆ-magmoid-large-globular-structure :
    {l1 l2 : Level} {x : A l1} {y : A l2}
    {f g h : 1-cell-large-globular-structure G x y} â†’
    2-cell-large-globular-structure G g h â†’
    2-cell-large-globular-structure G f g â†’
    2-cell-large-globular-structure G f h
  comp-2-cell-is-âˆ-magmoid-large-globular-structure {x = x} {y} =
    comp-1-cell-is-âˆ-magmoid-globular-structure
      ( is-âˆ-magmoid-globular-structure-1-cell-is-âˆ-magmoid-large-globular-structure
        ( r)
        ( x)
        ( y))
```

### The type of $âˆ$-magmoid structures on a large type

```agda
record
  is-large-âˆ-magmoid
  {Î± : Level â†’ Level} (Î² : Level â†’ Level â†’ Level) (A : (l : Level) â†’ UU (Î± l))
  : UUÏ‰
  where

  field
    large-globular-structure-is-large-âˆ-magmoid :
      large-globular-structure Î² A

    is-âˆ-magmoid-large-globular-structure-is-large-âˆ-magmoid :
      is-âˆ-magmoid-large-globular-structure
        ( large-globular-structure-is-large-âˆ-magmoid)

open is-large-âˆ-magmoid public
```

### The type of $âˆ$-magmoids

```agda
record
  Large-âˆ-Magmoid (Î± : Level â†’ Level) (Î² : Level â†’ Level â†’ Level) : UUÏ‰
  where

  field
    0-cell-Large-âˆ-Magmoid : (l : Level) â†’ UU (Î± l)

    is-large-âˆ-magmoid-0-cell-Large-âˆ-Magmoid :
      is-large-âˆ-magmoid Î² 0-cell-Large-âˆ-Magmoid

open Large-âˆ-Magmoid public
```
