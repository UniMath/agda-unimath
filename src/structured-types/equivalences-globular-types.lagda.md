# Equivalences between globular types

```agda
{-# OPTIONS --guardedness #-}

module structured-types.equivalences-globular-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.identity-types
open import foundation.universe-levels

open import structured-types.globular-types
```

</details>

## Idea

An
{{#concept "equivalence" Disambiguation="globular types" Agda=equiv-Globular-Type}}
`f` between [globular types](structured-types.globular-types.md) `A` and `B` is
an equivalence `F‚ÇÄ` of $0$-cells, and for every pair of $n$-cells `x` and `y`,
an equivalence of $(n+1)$-cells

```text
  F‚Çô‚Çä‚ÇÅ : (ùëõ+1)-Cell A x y ‚âÉ (ùëõ+1)-Cell B (F‚Çô x) (F‚Çô y).
```

## Definitions

### Equivalences between globular types

```agda
record
  equiv-Globular-Type
  {l1 l2 l3 l4 : Level} (A : Globular-Type l1 l2) (B : Globular-Type l3 l4)
  : UU (l1 ‚äî l2 ‚äî l3 ‚äî l4)
  where
  coinductive
  field
    0-cell-equiv-Globular-Type :
      0-cell-Globular-Type A ‚âÉ 0-cell-Globular-Type B

  map-0-cell-equiv-Globular-Type :
      0-cell-Globular-Type A ‚Üí 0-cell-Globular-Type B
  map-0-cell-equiv-Globular-Type = pr1 0-cell-equiv-Globular-Type

  field
    globular-type-1-cell-equiv-Globular-Type :
      {x y : 0-cell-Globular-Type A} ‚Üí
      equiv-Globular-Type
        ( 1-cell-globular-type-Globular-Type A x y)
        ( 1-cell-globular-type-Globular-Type B
          ( map-0-cell-equiv-Globular-Type x)
          ( map-0-cell-equiv-Globular-Type y))

open equiv-Globular-Type public

module _
  {l1 l2 l3 l4 : Level}
  {A : Globular-Type l1 l2} {B : Globular-Type l3 l4}
  (F : equiv-Globular-Type A B)
  where

  1-cell-equiv-Globular-Type :
    {x y : 0-cell-Globular-Type A} ‚Üí
    1-cell-Globular-Type A x y ‚âÉ
    1-cell-Globular-Type B
      ( map-0-cell-equiv-Globular-Type F x)
      ( map-0-cell-equiv-Globular-Type F y)
  1-cell-equiv-Globular-Type =
    0-cell-equiv-Globular-Type (globular-type-1-cell-equiv-Globular-Type F)

  map-1-cell-equiv-Globular-Type :
    {x y : 0-cell-Globular-Type A} ‚Üí
    1-cell-Globular-Type A x y ‚Üí
    1-cell-Globular-Type B
      ( map-0-cell-equiv-Globular-Type F x)
      ( map-0-cell-equiv-Globular-Type F y)
  map-1-cell-equiv-Globular-Type =
    map-0-cell-equiv-Globular-Type (globular-type-1-cell-equiv-Globular-Type F)

module _
  {l1 l2 l3 l4 : Level}
  {A : Globular-Type l1 l2} {B : Globular-Type l3 l4}
  (F : equiv-Globular-Type A B)
  where

  2-cell-equiv-Globular-Type :
    {x y : 0-cell-Globular-Type A}
    {f g : 1-cell-Globular-Type A x y} ‚Üí
    2-cell-Globular-Type A f g ‚âÉ
    2-cell-Globular-Type B
      ( map-1-cell-equiv-Globular-Type F f)
      ( map-1-cell-equiv-Globular-Type F g)
  2-cell-equiv-Globular-Type =
    1-cell-equiv-Globular-Type (globular-type-1-cell-equiv-Globular-Type F)

  map-2-cell-equiv-Globular-Type :
    {x y : 0-cell-Globular-Type A}
    {f g : 1-cell-Globular-Type A x y} ‚Üí
    2-cell-Globular-Type A f g ‚Üí
    2-cell-Globular-Type B
      ( map-1-cell-equiv-Globular-Type F f)
      ( map-1-cell-equiv-Globular-Type F g)
  map-2-cell-equiv-Globular-Type =
    map-1-cell-equiv-Globular-Type (globular-type-1-cell-equiv-Globular-Type F)

module _
  {l1 l2 l3 l4 : Level}
  {A : Globular-Type l1 l2} {B : Globular-Type l3 l4}
  (F : equiv-Globular-Type A B)
  where

  3-cell-equiv-Globular-Type :
    {x y : 0-cell-Globular-Type A}
    {f g : 1-cell-Globular-Type A x y} ‚Üí
    {H K : 2-cell-Globular-Type A f g} ‚Üí
    3-cell-Globular-Type A H K ‚âÉ
    3-cell-Globular-Type B
      ( map-2-cell-equiv-Globular-Type F H)
      ( map-2-cell-equiv-Globular-Type F K)
  3-cell-equiv-Globular-Type =
    2-cell-equiv-Globular-Type (globular-type-1-cell-equiv-Globular-Type F)
```

### The identity equiv on a globular type

```agda
id-equiv-Globular-Type :
  {l1 l2 : Level} (A : Globular-Type l1 l2) ‚Üí equiv-Globular-Type A A
id-equiv-Globular-Type A =
  Œª where
  .0-cell-equiv-Globular-Type ‚Üí id-equiv
  .globular-type-1-cell-equiv-Globular-Type {x} {y} ‚Üí
    id-equiv-Globular-Type (1-cell-globular-type-Globular-Type A x y)
```

### Composition of equivalences of globular types

```agda
comp-equiv-Globular-Type :
  {l1 l2 l3 l4 l5 l6 : Level}
  {A : Globular-Type l1 l2}
  {B : Globular-Type l3 l4}
  {C : Globular-Type l5 l6} ‚Üí
  equiv-Globular-Type B C ‚Üí equiv-Globular-Type A B ‚Üí equiv-Globular-Type A C
comp-equiv-Globular-Type g f =
  Œª where
  .0-cell-equiv-Globular-Type ‚Üí
    0-cell-equiv-Globular-Type g ‚àòe 0-cell-equiv-Globular-Type f
  .globular-type-1-cell-equiv-Globular-Type ‚Üí
    comp-equiv-Globular-Type
      ( globular-type-1-cell-equiv-Globular-Type g)
      ( globular-type-1-cell-equiv-Globular-Type f)
```
