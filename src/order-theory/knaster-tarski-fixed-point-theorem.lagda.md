# The Knasterâ€“Tarski fixed point theorem

```agda
module order-theory.knaster-tarski-fixed-point-theorem where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.identity-types
open import foundation.large-binary-relations
open import foundation.sets
open import foundation.logical-equivalences
open import foundation.fixed-points-endofunctions
open import foundation.universe-levels

open import order-theory.greatest-lower-bounds-large-posets
open import order-theory.large-meet-semilattices
open import order-theory.large-posets
open import order-theory.large-preorders
open import order-theory.large-suplattices
open import order-theory.order-preserving-maps-posets
open import order-theory.least-upper-bounds-large-posets
open import order-theory.meet-semilattices
open import order-theory.posets
open import order-theory.preorders
open import order-theory.suplattices
open import order-theory.top-elements-large-posets
open import order-theory.upper-bounds-large-posets
```

</details>

## Idea

The
{{#concept "Knasterâ€“Tarski fixed point theorem" WD="Knasterâ€“Tarski theorem" WDID=Q609612}}
states that every order preserving endomap `f : ğ’œ â†’ ğ’œ` on a complete lattice has
a least and a greatest [fixed point](foundation.fixed-points-endofunctions.md).

## Theorem

### The Knasterâ€“Tarski fixed point theorem for suplattices

```agda
module _
  {l1 l2 l3 : Level}
  (ğ’œ : Suplattice l1 l2 (l1 âŠ” l2))
  (f : type-Suplattice ğ’œ â†’ type-Suplattice ğ’œ)
  (F : preserves-order-Poset (poset-Suplattice ğ’œ) (poset-Suplattice ğ’œ) f)
  where

  indexing-type-family-of-elements-knaster-tarski-Suplattice : UU (l1 âŠ” l2)
  indexing-type-family-of-elements-knaster-tarski-Suplattice =
    Î£ ( type-Suplattice ğ’œ) (Î» x â†’ leq-Suplattice ğ’œ x (f x))

  family-of-elements-knaster-tarski-Suplattice :
    indexing-type-family-of-elements-knaster-tarski-Suplattice â†’
    type-Suplattice ğ’œ
  family-of-elements-knaster-tarski-Suplattice = pr1

  point-knaster-tarski-Suplattice : type-Suplattice ğ’œ
  point-knaster-tarski-Suplattice =
    sup-Suplattice ğ’œ family-of-elements-knaster-tarski-Suplattice

  leq-point-knaster-tarski-Suplattice :
    leq-Suplattice ğ’œ
      ( point-knaster-tarski-Suplattice)
      ( f point-knaster-tarski-Suplattice)
  leq-point-knaster-tarski-Suplattice =
    forward-implication
      ( is-least-upper-bound-sup-Suplattice ğ’œ
        ( family-of-elements-knaster-tarski-Suplattice)
        ( f point-knaster-tarski-Suplattice))
      ( Î» w â†’
        transitive-leq-Suplattice ğ’œ _ _ _
          ( F ( pr1 w)
              ( point-knaster-tarski-Suplattice)
              ( leq-sup-Suplattice ğ’œ _ w))
          ( pr2 w))

  geq-point-knaster-tarski-Suplattice :
    leq-Suplattice ğ’œ
      ( f point-knaster-tarski-Suplattice)
      ( point-knaster-tarski-Suplattice)
  geq-point-knaster-tarski-Suplattice =
    leq-sup-Suplattice ğ’œ family-of-elements-knaster-tarski-Suplattice
      ( f point-knaster-tarski-Suplattice ,
        F point-knaster-tarski-Suplattice
          ( f point-knaster-tarski-Suplattice)
          ( leq-point-knaster-tarski-Suplattice))

  is-fixed-point-knaster-tarski-Suplattice :
    f ( point-knaster-tarski-Suplattice) ï¼
    point-knaster-tarski-Suplattice
  is-fixed-point-knaster-tarski-Suplattice =
    antisymmetric-leq-Suplattice ğ’œ
      ( f (point-knaster-tarski-Suplattice))
      ( point-knaster-tarski-Suplattice)
      ( geq-point-knaster-tarski-Suplattice)
      ( leq-point-knaster-tarski-Suplattice)

  fixed-point-knaster-tarski-Suplattice : fixed-point f
  fixed-point-knaster-tarski-Suplattice =
    point-knaster-tarski-Suplattice ,
    is-fixed-point-knaster-tarski-Suplattice

  greatest-fixed-point-knaster-tarski-Suplattice :
    (x : fixed-point f) â†’
    leq-Suplattice ğ’œ (pr1 x) point-knaster-tarski-Suplattice
  greatest-fixed-point-knaster-tarski-Suplattice (x , p) =
    leq-sup-Suplattice ğ’œ _
      ( x ,
        concatenate-leq-eq-Poset
          ( poset-Suplattice ğ’œ)
          ( refl-leq-Suplattice ğ’œ x)
          ( inv p))
```

## References

- <https://gist.github.com/TOTBWF/6890425f52066fa3bbfdd3e629565a4e> by Reed
  Mullanix

## External links

- [Knasterâ€“Tarski theorem](https://en.wikipedia.org/wiki/Knaster%E2%80%93Tarski_theorem)
  at Wikipedia
- [Tarski's Fixed Point Theorem](https://mathworld.wolfram.com/TarskisFixedPointTheorem.html)
  at Wolfram MathWorld
