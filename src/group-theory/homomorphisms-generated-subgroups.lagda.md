# Homomorphisms of generated subgroups

```agda
module group-theory.homomorphisms-generated-subgroups where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.cartesian-product-types
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.function-extensionality
open import foundation.identity-types
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.sets
open import foundation.subtypes
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.unit-type
open import foundation.universe-levels

open import group-theory.epimorphisms-groups
open import group-theory.full-subgroups
open import group-theory.generating-sets-groups
open import group-theory.groups
open import group-theory.homomorphisms-groups
open import group-theory.subgroups
open import group-theory.subgroups-generated-by-subsets-groups
open import group-theory.subsets-groups

open import lists.lists

open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

A [group homomorphism](group-theory.homomorphisms-groups.md) from a
[subgroup generated by a subset](group-theory.subgroups-generated-by-subsets-groups.md)
`S` is defined by its values on `S`.

## Theorem

```agda
module _
  {l1 l2 l3 : Level} (G : Group l1) (S : subset-Group l2 G) (G' : Group l3)
  where

  distributivity-hom-group-ev-formal-combination :
    ( f : hom-Group (group-Subgroup G (subgroup-subset-Group G S)) G') ‚Üí
    ( l : formal-combination-subset-Group G S) ‚Üí
    Id
      ( map-hom-Group
        ( group-Subgroup G (subgroup-subset-Group G S))
        ( G')
        ( f)
        ( ( ev-formal-combination-subset-Group G S l) ,
          ( unit-trunc-Prop (l , refl))))
      ( fold-list
        ( unit-Group G')
        ( Œª (s , x) ‚Üí
          mul-Group
            ( G')
            ( map-hom-Group
              ( group-Subgroup G (subgroup-subset-Group G S))
              ( G')
              ( f)
              ( ( ev-formal-combination-subset-Group G S (unit-list (s , x))) ,
                ( unit-trunc-Prop (unit-list (s , x) , refl)))))
        ( l))
  distributivity-hom-group-ev-formal-combination f nil =
    preserves-unit-hom-Group (group-Subgroup G (subgroup-subset-Group G S)) G' f
  distributivity-hom-group-ev-formal-combination f (cons (s , x) l) =
    ( ap
      ( map-hom-Group (group-Subgroup G (subgroup-subset-Group G S)) G' f)
      ( eq-pair-Œ£
        ( preserves-concat-ev-formal-combination-subset-Group G S
          ( unit-list (s , x))
          ( l))
        ( eq-is-prop is-prop-type-trunc-Prop))) ‚àô
    ( preserves-mul-hom-Group
      ( group-Subgroup G (subgroup-subset-Group G S))
      ( G')
      ( f)) ‚àô
    ( ap
      ( mul-Group G'
        ( map-hom-Group
          ( group-Subgroup G (subgroup-subset-Group G S))
          ( G')
          ( f)
          ( pair
            ( ev-formal-combination-subset-Group G S (unit-list (s , x)))
            ( unit-trunc-Prop (unit-list (s , x) , refl)))))
      ( distributivity-hom-group-ev-formal-combination f l))

  map-restriction-generating-subset-Subgroup :
    hom-Group (group-Subgroup G (subgroup-subset-Group G S)) G' ‚Üí
    type-subtype S ‚Üí type-Group G'
  map-restriction-generating-subset-Subgroup f x =
    map-hom-Group
      ( group-Subgroup G (subgroup-subset-Group G S))
      ( G')
      ( f)
      ( pair
        ( ev-formal-combination-subset-Group
          ( G)
          ( S)
          ( unit-list (inr star , x)))
        ( unit-trunc-Prop
          ( unit-list (inr star , x) , refl)))

  is-emb-map-restriction-generating-subset-Subgroup :
    is-emb (map-restriction-generating-subset-Subgroup)
  is-emb-map-restriction-generating-subset-Subgroup f g =
    is-equiv-is-invertible
      ( Œª P ‚Üí
        eq-htpy-hom-Group
          ( group-Subgroup G (subgroup-subset-Group G S))
          ( G')
          ( Œª x ‚Üí
            apply-universal-property-trunc-Prop
              ( pr2 x)
              ( Id-Prop
                ( set-Group G')
                ( map-hom-Group
                  ( group-Subgroup G (subgroup-subset-Group G S))
                  ( G')
                  ( f)
                  ( x))
                ( map-hom-Group
                  ( group-Subgroup G (subgroup-subset-Group G S))
                  ( G')
                  ( g)
                  ( x)))
              ( Œª (y , Q) ‚Üí
                ( ap
                  ( map-hom-Group
                    ( group-Subgroup G (subgroup-subset-Group G S))
                    ( G')
                    ( f))
                  ( eq-pair-Œ£ (inv Q) (eq-is-prop is-prop-type-trunc-Prop))) ‚àô
                ( distributivity-hom-group-ev-formal-combination f y) ‚àô
                ( ap
                  ( Œª F ‚Üí
                    fold-list (unit-Group G') (Œª Y ‚Üí mul-Group G' (F Y)) y)
                  ( eq-htpy (lemma (htpy-eq P)))) ‚àô
                ( inv
                  ( distributivity-hom-group-ev-formal-combination g y)) ‚àô
                ( ap
                  ( map-hom-Group
                    ( group-Subgroup G (subgroup-subset-Group G S))
                    ( G')
                    ( g))
                  ( eq-pair-Œ£
                    ( Q)
                    ( eq-is-prop is-prop-type-trunc-Prop))))))
      ( Œª p ‚Üí
        eq-is-prop
          ( is-trunc-Œ†
            ( zero-ùïã)
            ( Œª z ‚Üí is-set-type-Group G')
            ( Œª S ‚Üí map-restriction-generating-subset-Subgroup f S)
            ( Œª S ‚Üí map-restriction-generating-subset-Subgroup g S)))
      ( Œª p ‚Üí
        eq-is-prop
          ( is-set-hom-Group
            ( group-Subgroup G (subgroup-subset-Group G S))
            ( G')
            ( f)
            ( g)))
    where
    lemma :
      ( (x : type-subtype S) ‚Üí
        Id
          ( map-hom-Group
            ( group-Subgroup G (subgroup-subset-Group G S))
            ( G')
            ( f)
            ( pair
              ( ev-formal-combination-subset-Group
                ( G)
                ( S)
                ( unit-list (inr star , x)))
              ( unit-trunc-Prop (unit-list (pair (inr star) x) , refl))))
          ( map-hom-Group
            ( group-Subgroup G (subgroup-subset-Group G S))
            ( G')
            ( g)
            ( pair
              ( ev-formal-combination-subset-Group
                ( G)
                ( S)
                ( unit-list (inr star , x)))
              ( unit-trunc-Prop
                ( unit-list (pair (inr star) x) , refl))))) ‚Üí
      ( z : Fin 2 √ó type-subtype S) ‚Üí
      Id
        ( map-hom-Group
          ( group-Subgroup G (subgroup-subset-Group G S))
          ( G')
          ( f)
          ( pair
            ( ev-formal-combination-subset-Group G S (unit-list z))
            ( unit-trunc-Prop (unit-list z , refl))))
        ( map-hom-Group
          ( group-Subgroup G (subgroup-subset-Group G S))
          ( G')
          ( g)
          ( pair
            ( ev-formal-combination-subset-Group G S (unit-list z))
            ( unit-trunc-Prop (unit-list z , refl))))
    lemma P (inl (inr star) , x , s) =
      ( ap
        ( map-hom-Group (group-Subgroup G (subgroup-subset-Group G S)) G' f)
        ( eq-pair-Œ£
          ( right-unit-law-mul-Group G (inv-Group G x))
          ( eq-is-prop is-prop-type-trunc-Prop))) ‚àô
      ( preserves-inv-hom-Group
        ( group-Subgroup G (subgroup-subset-Group G S))
        ( G')
        ( f)) ‚àô
      ( ap
        ( inv-Group G')
        ( ( ap
          ( map-hom-Group
            ( group-Subgroup G (subgroup-subset-Group G S))
            ( G')
            ( f))
          ( eq-pair-Œ£
            { s =
              pair
                ( x)
                ( unit-trunc-Prop
                  ( pair
                    ( unit-list (inr star , x , s))
                    ( right-unit-law-mul-Group G x)))}
            ( inv (right-unit-law-mul-Group G x))
            ( eq-is-prop is-prop-type-trunc-Prop))) ‚àô
          ( ( P (x , s)) ‚àô
            ( ap
              ( map-hom-Group
                ( group-Subgroup G (subgroup-subset-Group G S))
                ( G')
                ( g))
              ( eq-pair-Œ£
                { s =
                  pair
                    ( mul-Group G x (unit-Group G))
                    ( unit-trunc-Prop
                      ( pair
                        ( unit-list (inr star , x , s))
                        ( refl)))}
                { t =
                  pair
                    ( x)
                    ( unit-trunc-Prop
                      ( pair
                        ( unit-list (inr star , x , s))
                        ( right-unit-law-mul-Group G x)))}
                ( right-unit-law-mul-Group G x)
                ( eq-is-prop is-prop-type-trunc-Prop)))))) ‚àô
      ( inv
        ( preserves-inv-hom-Group
          ( group-Subgroup G (subgroup-subset-Group G S))
          ( G')
          ( g))) ‚àô
      ( ap
        ( map-hom-Group
          ( group-Subgroup G (subgroup-subset-Group G S))
          ( G')
          ( g))
        ( eq-pair-Œ£
          ( inv (right-unit-law-mul-Group G (inv-Group G x)))
          ( eq-is-prop is-prop-type-trunc-Prop)))
    lemma P (inr star , x) = P x

  restriction-generating-subset-Subgroup :
    hom-Group (group-Subgroup G (subgroup-subset-Group G S)) G' ‚Ü™
      ( type-subtype S ‚Üí type-Group G')
  pr1 restriction-generating-subset-Subgroup =
    map-restriction-generating-subset-Subgroup
  pr2 restriction-generating-subset-Subgroup =
    is-emb-map-restriction-generating-subset-Subgroup

module _
  {l1 l2 l3 : Level}
  (G : Group l1) (S : subset-Group l2 G) (H : is-generating-set-Group G S)
  (G' : Group l3)
  where

  restriction-generating-subset-Group :
    hom-Group G G' ‚Ü™ (type-subtype S ‚Üí type-Group G')
  restriction-generating-subset-Group =
    comp-emb
      ( restriction-generating-subset-Subgroup G S G')
      ( pair
        ( Œª f ‚Üí
          comp-hom-Group
            ( group-Subgroup G (subgroup-subset-Group G S))
            ( G)
            ( G')
            ( f)
            ( pr1 , Œª {x} {y} ‚Üí refl))
        ( is-epi-iso-Group l3
          ( group-Subgroup G (subgroup-subset-Group G S))
          ( G)
          ( iso-inclusion-is-full-Subgroup G (subgroup-subset-Group G S) H)
          ( G')))

  eq-map-restriction-generating-subset-Group :
    ( f : hom-Group G G') (x : type-subtype S) ‚Üí
    map-emb restriction-generating-subset-Group f x Ôºù
      map-hom-Group G G' f (pr1 x)
  eq-map-restriction-generating-subset-Group f x =
    ap
      ( map-hom-Group G G' f)
      ( right-unit-law-mul-Group G (pr1 x))
```
