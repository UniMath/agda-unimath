# Russell's paradox

```agda
{-# OPTIONS --lossy-unification #-}

module set-theory.russells-paradox where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.functoriality-cartesian-product-types
open import foundation.identity-types
open import foundation.locally-small-types
open import foundation.logical-equivalences
open import foundation.negation
open import foundation.small-types
open import foundation.small-universes
open import foundation.surjective-maps
open import foundation.torsorial-type-families
open import foundation.type-arithmetic-cartesian-product-types
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universal-property-equivalences
open import foundation.universe-levels

open import foundation-core.contractible-types
open import foundation-core.empty-types
open import foundation-core.equivalences
open import foundation-core.functoriality-dependent-pair-types

open import trees.multisets
open import trees.small-multisets
open import trees.universal-multiset
```

</details>

## Idea

{{#concept "Russell's paradox" WD="Russell's paradox" WDID=Q33401 Agda=paradox-Russell}}
arises when a [set](foundation-core.sets.md) of all sets is assumed to exist.

**Proof outline.** Given a set `ğ’°` that contains all sets, then we may by
diagonalization construct a new set, called the _Russell set_, of all sets who
do not contain themselves as an element:

```text
  Russell := {X âˆˆ ğ’° | X âˆ‰ X}.
```

It may then be demonstrated that `Russell` contains itself as an element if and
only if it does not contain itself, but negation has no fixed points and so this
gives a contradiction. âˆ

In Russell's paradox it is of no importance that the elementhood relation takes
values in [propositions](foundation-core.propositions.md). In other words,
Russell's paradox arises similarly if there is a [multiset](trees.multisets.md)
of all multisets. We will construct Russell's paradox from the assumption that a
[universe](foundation.universe-levels.md) `ğ’°` is
[equivalent](foundation-core.equivalences.md) to a type `A : ğ’°`. We conclude
that there can be no universe that is contained in itself. Furthermore, using
[replacement](foundation.replacement.md) we show that for any type `A : ğ’°`,
there is no [surjective](foundation.surjective-maps.md) map `A â†’ ğ’°`.

## Definition

### Russell's multiset

```agda
Russell : (l : Level) â†’ ğ• (lsuc l)
Russell l =
  comprehension-ğ•
    ( universal-multiset-ğ• l)
    ( Î» X â†’ X âˆ‰-ğ• X)
```

## Properties

### If a universe is small with respect to another universe, then Russells multiset is also small

```agda
module _
  {l1 l2 : Level} (H : is-small-universe l2 l1)
  where

  is-small-Russell : is-small-ğ• l2 (Russell l1)
  is-small-Russell =
    is-small-comprehension-ğ• l2
      { lsuc l1}
      { universal-multiset-ğ• l1}
      { Î» X â†’ X âˆ‰-ğ• X}
      ( is-small-universal-multiset-ğ• l2 H)
      ( Î» X â†’ is-small-âˆ‰-ğ• l2 (K X) (K X))
    where
    K = is-small-multiset-ğ• (pr2 H)

  resize-Russell : ğ• l2
  resize-Russell = resize-ğ• (Russell l1) (is-small-Russell)

  is-small-resize-Russell :
    is-small-ğ• (lsuc l1) (resize-Russell)
  is-small-resize-Russell =
    is-small-resize-ğ• (Russell l1) (is-small-Russell)

  equiv-Russell-in-Russell :
    (Russell l1 âˆˆ-ğ• Russell l1) â‰ƒ (resize-Russell âˆˆ-ğ• resize-Russell)
  equiv-Russell-in-Russell =
    equiv-elementhood-resize-ğ• (is-small-Russell) (is-small-Russell)
```

### Russell's paradox obtained from the assumption that `ğ’°` is `ğ’°`-small

```agda
module _
  {l : Level} (H : is-small l (UU l))
  where

  equiv-in-notin-Russell :
    (Russell l âˆˆ-ğ• Russell l) â‰ƒ (Russell l âˆ‰-ğ• Russell l)
  equiv-in-notin-Russell =
    ( equiv-precomp (equiv-Russell-in-Russell K) empty) âˆ˜e
    ( left-unit-law-Î£-is-contr
      { B = (Î» t â†’ (pr1 t) âˆ‰-ğ• (pr1 t))}
      ( is-torsorial-Id' (resize-Russell K))
      ( resize-Russell K , refl)) âˆ˜e
    ( inv-associative-Î£) âˆ˜e
    ( equiv-tot
      ( Î» t â†’
        ( commutative-product) âˆ˜e
        ( equiv-product-right
          ( inv-equiv
            ( ( equiv-concat' _ (resize-resize-ğ• (is-small-Russell K))) âˆ˜e
              ( eq-resize-ğ•
                ( is-small-multiset-ğ• is-small-lsuc t)
                ( is-small-resize-Russell K))))))) âˆ˜e
    ( associative-Î£)
    where
      K : is-small-universe l l
      K = (H , (Î» X â†’ (X , id-equiv)))

  iff-in-notin-Russell :
    (Russell l âˆˆ-ğ• Russell l) â†” (Russell l âˆ‰-ğ• Russell l)
  iff-in-notin-Russell =
    iff-equiv equiv-in-notin-Russell

  paradox-Russell : empty
  paradox-Russell =
    no-fixed-points-neg (Russell l âˆˆ-ğ• Russell l) iff-in-notin-Russell
```

### There can be no surjective map `f : A â†  ğ’°` for any `A : ğ’°`

This result uses the axiom of replacement.

```agda
no-surjection-onto-universe-is-small :
  {l1 l2 : Level} {A : UU l1} â†’ is-small l2 A â†’
  (f : A â†’ UU l2) â†’ Â¬ (is-surjective f)
no-surjection-onto-universe-is-small e f H =
  paradox-Russell (is-small-is-surjective H e is-locally-small-UU)

no-surjection-onto-universe :
  {l : Level} {A : UU l} (f : A â†’ UU l) â†’ Â¬ (is-surjective f)
no-surjection-onto-universe =
  no-surjection-onto-universe-is-small is-small'
```

## External links

- [Russell's paradox](https://ncatlab.org/nlab/show/Russell%27s+paradox) at
  $n$Lab
- [Russell's paradox](https://en.wikipedia.org/wiki/Russell%27s_paradox) at
  Wikipedia
