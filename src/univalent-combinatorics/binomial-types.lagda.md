# The binomial types

```agda
{-# OPTIONS --without-K --exact-split #-}

module univalent-combinatorics.binomial-types where

open import elementary-number-theory.binomial-coefficients using (_choose-‚Ñï_)
open import elementary-number-theory.natural-numbers using (‚Ñï; zero-‚Ñï; succ-‚Ñï)

open import foundation.booleans using (bool; true; false)
open import foundation.connected-components-universes using
  ( component-UU-Level; type-component-UU-Level; mere-equiv-component-UU-Level;
    is-contr-component-UU-Level-empty)
open import foundation.contractible-maps using (is-contr-map-is-equiv)
open import foundation.contractible-types using
  ( is-contr; is-contr-equiv; equiv-is-contr)
open import foundation.coproduct-types using (coprod; inl; inr; ind-coprod)
open import foundation.decidable-embeddings using
  ( _‚Ü™d_; map-decidable-emb; is-emb-map-decidable-emb; is-decidable-emb;
    equiv-Fib-decidable-Prop; equiv-precomp-decidable-emb-equiv;
    is-prop-is-decidable-emb; is-decidable-emb-ex-falso)
open import foundation.decidable-propositions using
  ( decidable-Prop; type-decidable-Prop; equiv-bool-decidable-Prop;
    compute-equiv-bool-decidable-Prop; split-decidable-Prop)
open import foundation.dependent-pair-types using (Œ£; pair; pr1; pr2; ind-Œ£)
open import foundation.embeddings using (is-emb)
open import foundation.empty-types using
  ( empty; raise-empty; equiv-raise-empty; ex-falso; is-empty; empty-Prop;
    is-empty-raise-empty; raise-empty-Prop; equiv-is-empty)
open import foundation.equivalences using
  ( _‚âÉ_; equiv-postcomp-equiv; inv-equiv; _‚àòe_; map-equiv; id-equiv;
    equiv-precomp; is-equiv-map-equiv; equiv-precomp-equiv)
open import foundation.equivalences-maybe using (equiv-equiv-Maybe)
open import foundation.fibers-of-maps using (equiv-total-fib; fib; fib-comp)
open import foundation.functions using (_‚àò_; id)
open import foundation.functoriality-coproduct-types using (equiv-coprod)
open import foundation.functoriality-function-types using (equiv-postcomp)
open import foundation.functoriality-dependent-pair-types using
  ( equiv-Œ£; equiv-tot)
open import foundation.functoriality-propositional-truncation using
  ( equiv-trunc-Prop; functor-trunc-Prop)
open import foundation.logical-equivalences using (equiv-iff)
open import foundation.maybe using (Maybe)
open import foundation.mere-equivalences using
  ( mere-equiv; mere-equiv-Prop)
open import foundation.negation using (¬¨)
open import foundation.propositional-extensionality using
  ( is-contr-total-true-Prop; is-contr-total-false-Prop)
open import foundation.propositional-truncations using
  ( type-trunc-Prop; apply-universal-property-trunc-Prop; unit-trunc-Prop)
open import foundation.propositions using
  ( is-proof-irrelevant-is-prop; UU-Prop; type-Prop)
open import foundation.raising-universe-levels using (map-inv-raise)
open import foundation.type-arithmetic-cartesian-product-types using
  ( commutative-prod)
open import foundation.type-arithmetic-coproduct-types using
  ( left-distributive-Œ£-coprod; right-distributive-Œ£-coprod)
open import foundation.type-arithmetic-dependent-pair-types using
  ( inv-assoc-Œ£; assoc-Œ£; left-unit-law-Œ£-is-contr; right-unit-law-Œ£-is-contr)
open import foundation.type-arithmetic-empty-type using
  ( right-unit-law-coprod-is-empty)
open import foundation.type-arithmetic-unit-type using (left-unit-law-Œ£)
open import foundation.unit-type using
  ( is-contr-raise-unit; is-contr-unit; raise-unit-Prop; raise-star; unit; star)
open import foundation.universal-property-empty-type using
  ( universal-property-empty')
open import foundation.universal-property-maybe using
  ( equiv-universal-property-Maybe)
open import foundation.universe-levels using (Level; UU; lsuc; _‚äî_)

open import univalent-combinatorics.equivalences-standard-finite-types using
  ( Fin-add-‚Ñï)
open import univalent-combinatorics.finite-types using
  ( Fin-UU-Fin-Level; has-cardinality; has-cardinality-Prop; UU-Fin-Level;
    type-UU-Fin-Level; mere-equiv-UU-Fin-Level; UU-Fin; type-UU-Fin;
    mere-equiv-UU-Fin; has-finite-cardinality; is-finite;
    is-finite-has-finite-cardinality; has-finite-cardinality-is-finite; ùîΩ;
    type-ùîΩ; is-finite-type-ùîΩ; is-finite-equiv)
open import univalent-combinatorics.standard-finite-types using
  ( Fin; is-contr-Fin-one-‚Ñï)
```

## Idea

The binomial types are a categorification of the binomial coefficients. The binomial type `(A choose B)` is the type of decidable embeddings from types merely equal to `B` into `A`.

## Definitions

```agda
binomial-type-Level :
  (l : Level) {l1 l2 : Level} (X : UU l1) (Y : UU l2) ‚Üí UU (lsuc l ‚äî l1 ‚äî l2)
binomial-type-Level l X Y =
  Œ£ (component-UU-Level l Y) (Œª Z ‚Üí type-component-UU-Level Z ‚Ü™d X)

type-binomial-type-Level :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} ‚Üí binomial-type-Level l3 X Y ‚Üí UU l3
type-binomial-type-Level Z = type-component-UU-Level (pr1 Z)

abstract
  mere-compute-binomial-type-Level :
    {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2}
    (Z : binomial-type-Level l3 X Y) ‚Üí
    mere-equiv Y (type-binomial-type-Level Z)
  mere-compute-binomial-type-Level Z = mere-equiv-component-UU-Level (pr1 Z)

decidable-emb-binomial-type-Level :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type-Level l3 X Y) ‚Üí
  type-binomial-type-Level Z ‚Ü™d X
decidable-emb-binomial-type-Level Z = pr2 Z

map-decidable-emb-binomial-type-Level :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type-Level l3 X Y) ‚Üí
  type-binomial-type-Level Z ‚Üí X
map-decidable-emb-binomial-type-Level Z =
  map-decidable-emb (decidable-emb-binomial-type-Level Z)

abstract
  is-emb-map-emb-binomial-type-Level :
    {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2}
    (Z : binomial-type-Level l3 X Y) ‚Üí
    is-emb (map-decidable-emb-binomial-type-Level Z)
  is-emb-map-emb-binomial-type-Level Z =
    is-emb-map-decidable-emb (decidable-emb-binomial-type-Level Z)

-- We now define the standard binomial types

binomial-type : {l1 l2 : Level} (X : UU l1) (Y : UU l2) ‚Üí UU (lsuc (l1 ‚äî l2))
binomial-type {l1} {l2} X Y = binomial-type-Level (l1 ‚äî l2) X Y

type-binomial-type :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} ‚Üí binomial-type X Y ‚Üí UU (l1 ‚äî l2)
type-binomial-type Z = type-component-UU-Level (pr1 Z)

abstract
  mere-compute-binomial-type :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type X Y) ‚Üí
    mere-equiv Y (type-binomial-type Z)
  mere-compute-binomial-type Z = mere-equiv-component-UU-Level (pr1 Z)

decidable-emb-binomial-type :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type X Y) ‚Üí
  type-binomial-type Z ‚Ü™d X
decidable-emb-binomial-type Z = pr2 Z

map-decidable-emb-binomial-type :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type X Y) ‚Üí
  type-binomial-type Z ‚Üí X
map-decidable-emb-binomial-type Z =
  map-decidable-emb (decidable-emb-binomial-type Z)

abstract
  is-emb-map-emb-binomial-type :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type X Y) ‚Üí
    is-emb (map-decidable-emb-binomial-type Z)
  is-emb-map-emb-binomial-type Z =
    is-emb-map-decidable-emb (decidable-emb-binomial-type Z)

-- Proposition 17.5.6

binomial-type-Level' :
  (l : Level) {l1 l2 : Level} (A : UU l1) (B : UU l2) ‚Üí UU (lsuc l ‚äî l1 ‚äî l2)
binomial-type-Level' l A B =
  Œ£ ( A ‚Üí decidable-Prop l)
    ( Œª P ‚Üí mere-equiv B (Œ£ A (Œª x ‚Üí type-decidable-Prop (P x))))

compute-binomial-type-Level :
  (l : Level) {l1 l2 : Level} (A : UU l1) (B : UU l2) ‚Üí
  binomial-type-Level (l1 ‚äî l) A B ‚âÉ binomial-type-Level' (l1 ‚äî l) A B
compute-binomial-type-Level l {l1} {l2} A B =
  ( ( ( equiv-Œ£
        ( Œª P ‚Üí mere-equiv B (Œ£ A (Œª x ‚Üí type-decidable-Prop (P x))))
        ( equiv-Fib-decidable-Prop l A)
        ( Œª e ‚Üí
          equiv-trunc-Prop
            ( equiv-postcomp-equiv
              ( inv-equiv (equiv-total-fib (pr1 (pr2 e)))) B))) ‚àòe
      ( inv-assoc-Œ£
        ( UU (l1 ‚äî l))
        ( Œª X ‚Üí X ‚Ü™d A)
        ( Œª X ‚Üí mere-equiv B (pr1 X)))) ‚àòe
    ( equiv-tot (Œª X ‚Üí commutative-prod))) ‚àòe
  ( assoc-Œ£ (UU (l1 ‚äî l)) (Œª X ‚Üí mere-equiv B X) (Œª X ‚Üí (pr1 X) ‚Ü™d A))

binomial-type' :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) ‚Üí UU (lsuc (l1 ‚äî l2))
binomial-type' {l1} {l2} A B = binomial-type-Level' (l1 ‚äî l2) A B

compute-binomial-type :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) ‚Üí
  binomial-type A B ‚âÉ binomial-type' A B
compute-binomial-type {l1} {l2} A B =
  compute-binomial-type-Level (l1 ‚äî l2) A B

-- Remark 17.5.7

-- Note that the universe level of small-binomial-type is lower

small-binomial-type :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) ‚Üí UU (l1 ‚äî l2)
small-binomial-type A B =
  Œ£ (A ‚Üí bool) (Œª f ‚Üí mere-equiv B (fib f true))

compute-small-binomial-type :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) ‚Üí
  binomial-type A B ‚âÉ small-binomial-type A B
compute-small-binomial-type A B =
  ( equiv-Œ£
    ( Œª f ‚Üí mere-equiv B (fib f true))
    ( equiv-postcomp A equiv-bool-decidable-Prop)
    ( Œª P ‚Üí
      equiv-trunc-Prop
        ( equiv-postcomp-equiv
          ( equiv-tot (Œª a ‚Üí compute-equiv-bool-decidable-Prop (P a)))
          ( B)))) ‚àòe
  ( compute-binomial-type A B)
```

```agda
abstract
  binomial-type-over-empty :
    {l : Level} {X : UU l} ‚Üí is-contr (binomial-type X empty)
  binomial-type-over-empty {l} {X} =
    is-contr-equiv
      ( raise-empty l ‚Ü™d X)
      ( left-unit-law-Œ£-is-contr
        ( is-contr-component-UU-Level-empty l)
        ( Fin-UU-Fin-Level l zero-‚Ñï))
      ( is-contr-equiv
        ( empty ‚Ü™d X)
        ( equiv-precomp-decidable-emb-equiv (equiv-raise-empty l) X)
        ( is-contr-equiv
          ( is-decidable-emb ex-falso)
          ( left-unit-law-Œ£-is-contr (universal-property-empty' X) ex-falso)
          ( is-proof-irrelevant-is-prop
            ( is-prop-is-decidable-emb ex-falso)
            ( is-decidable-emb-ex-falso))))

abstract
  binomial-type-empty-under :
    {l : Level} {X : UU l} ‚Üí
    type-trunc-Prop X ‚Üí is-empty (binomial-type empty X)
  binomial-type-empty-under H Y =
    apply-universal-property-trunc-Prop H empty-Prop
      ( Œª x ‚Üí
        apply-universal-property-trunc-Prop (pr2 (pr1 Y)) empty-Prop
          ( Œª e ‚Üí map-decidable-emb (pr2 Y) (map-equiv e x)))

abstract
  recursion-binomial-type' :
    {l1 l2 : Level} (A : UU l1) (B : UU l2) ‚Üí
    binomial-type' (Maybe A) (Maybe B) ‚âÉ
    coprod (binomial-type' A B) (binomial-type' A (Maybe B))
  recursion-binomial-type' A B =
    ( ( ( left-distributive-Œ£-coprod
          ( A ‚Üí decidable-Prop _)
          ( Œª P ‚Üí mere-equiv B (Œ£ A _))
          ( Œª P ‚Üí mere-equiv (Maybe B) (Œ£ A _))) ‚àòe
        ( equiv-tot
          ( Œª P ‚Üí
            ( ( equiv-coprod
                ( ( ( equiv-iff
                      ( mere-equiv-Prop (Maybe B) (Maybe (Œ£ A _)))
                      ( mere-equiv-Prop B (Œ£ A _))
                      ( functor-trunc-Prop (equiv-equiv-Maybe))
                      ( functor-trunc-Prop
                        ( Œª e ‚Üí equiv-coprod e id-equiv))) ‚àòe
                    ( equiv-trunc-Prop
                      ( equiv-postcomp-equiv
                        ( equiv-coprod
                          ( id-equiv)
                          ( equiv-is-contr is-contr-raise-unit is-contr-unit))
                        ( Maybe B)))) ‚àòe
                  ( left-unit-law-Œ£-is-contr
                    ( is-contr-total-true-Prop)
                    ( pair (raise-unit-Prop _) raise-star)))
                ( ( equiv-trunc-Prop
                    ( equiv-postcomp-equiv
                      ( right-unit-law-coprod-is-empty
                        ( Œ£ A _)
                        ( raise-empty _)
                        ( is-empty-raise-empty))
                      ( Maybe B))) ‚àòe
                  ( left-unit-law-Œ£-is-contr
                    ( is-contr-total-false-Prop)
                    ( pair (raise-empty-Prop _) map-inv-raise)))) ‚àòe
              ( right-distributive-Œ£-coprod
                ( Œ£ (UU-Prop _) type-Prop)
                ( Œ£ (UU-Prop _) (¬¨ ‚àò type-Prop))
                ( ind-coprod _
                  ( Œª Q ‚Üí
                    mere-equiv (Maybe B) (coprod (Œ£ A _) (type-Prop (pr1 Q))))
                  ( Œª Q ‚Üí
                    mere-equiv
                      ( Maybe B)
                      ( coprod (Œ£ A _) (type-Prop (pr1 Q))))))) ‚àòe
            ( equiv-Œ£
              ( ind-coprod _
                ( Œª Q ‚Üí
                  mere-equiv
                    ( Maybe B)
                    ( coprod
                      ( Œ£ A (Œª a ‚Üí type-decidable-Prop (P a)))
                      ( type-Prop (pr1 Q))))
                ( Œª Q ‚Üí
                  mere-equiv
                    ( Maybe B)
                    ( coprod
                      ( Œ£ A (Œª a ‚Üí type-decidable-Prop (P a)))
                      ( type-Prop (pr1 Q)))))
              ( split-decidable-Prop)
              ( ind-Œ£
                ( Œª Q ‚Üí
                  ind-Œ£
                    ( Œª H ‚Üí
                      ind-coprod _ ( Œª q ‚Üí id-equiv) (Œª q ‚Üí id-equiv)))))))) ‚àòe
      ( assoc-Œ£
        ( A ‚Üí decidable-Prop _)
        ( Œª a ‚Üí decidable-Prop _)
        ( Œª t ‚Üí
          mere-equiv
            ( Maybe B)
            ( coprod
              ( Œ£ A (Œª a ‚Üí type-decidable-Prop (pr1 t a)))
              ( type-decidable-Prop (pr2 t)))))) ‚àòe
    ( equiv-Œ£
      ( Œª p ‚Üí
        mere-equiv
          ( Maybe B)
          ( coprod
            ( Œ£ A (Œª a ‚Üí type-decidable-Prop (pr1 p a)))
            ( type-decidable-Prop (pr2 p))))
      ( equiv-universal-property-Maybe)
      ( Œª u ‚Üí
        equiv-trunc-Prop
          ( equiv-postcomp-equiv
            ( ( equiv-coprod
                ( id-equiv)
                ( left-unit-law-Œ£ (Œª y ‚Üí type-decidable-Prop (u (inr y))))) ‚àòe
              ( right-distributive-Œ£-coprod A unit
                ( Œª x ‚Üí type-decidable-Prop (u x))))
            ( Maybe B))))

abstract
  binomial-type-Maybe :
    {l1 l2 : Level} (A : UU l1) (B : UU l2) ‚Üí
    binomial-type (Maybe A) (Maybe B) ‚âÉ
    coprod (binomial-type A B) (binomial-type A (Maybe B))
  binomial-type-Maybe A B =
    ( inv-equiv
      ( equiv-coprod
        ( compute-binomial-type A B)
        ( compute-binomial-type A (Maybe B))) ‚àòe
      ( recursion-binomial-type' A B)) ‚àòe
    ( compute-binomial-type (Maybe A) (Maybe B))

-- Theorem 17.5.9

equiv-small-binomial-type :
  {l1 l2 l3 l4 : Level} {A : UU l1} {A' : UU l2} {B : UU l3} {B' : UU l4} ‚Üí
  (A ‚âÉ A') ‚Üí (B ‚âÉ B') ‚Üí small-binomial-type A' B' ‚âÉ small-binomial-type A B
equiv-small-binomial-type {l1} {l2} {l3} {l4} {A} {A'} {B} {B'} e f =
  equiv-Œ£
    ( Œª P ‚Üí mere-equiv B (fib P true))
    ( equiv-precomp e bool)
    ( Œª P ‚Üí
      equiv-trunc-Prop
        ( ( equiv-postcomp-equiv
            ( inv-equiv
              ( ( right-unit-law-Œ£-is-contr
                  ( Œª u ‚Üí
                    is-contr-map-is-equiv (is-equiv-map-equiv e) (pr1 u))) ‚àòe
                ( fib-comp P (map-equiv e) true))) B) ‚àòe
          ( equiv-precomp-equiv f (fib P true))))

equiv-binomial-type :
  {l1 l2 l3 l4 : Level} {A : UU l1} {A' : UU l2} {B : UU l3} {B' : UU l4} ‚Üí
  (A ‚âÉ A') ‚Üí (B ‚âÉ B') ‚Üí binomial-type A' B' ‚âÉ binomial-type A B
equiv-binomial-type e f =
  ( ( inv-equiv (compute-small-binomial-type _ _)) ‚àòe
    ( equiv-small-binomial-type e f)) ‚àòe
  ( compute-small-binomial-type _ _)

binomial-type-Fin :
  (n m : ‚Ñï) ‚Üí binomial-type (Fin n) (Fin m) ‚âÉ Fin (n choose-‚Ñï m)
binomial-type-Fin zero-‚Ñï zero-‚Ñï =
  equiv-is-contr binomial-type-over-empty is-contr-Fin-one-‚Ñï
binomial-type-Fin zero-‚Ñï (succ-‚Ñï m) =
  equiv-is-empty (binomial-type-empty-under (unit-trunc-Prop (inr star))) id
binomial-type-Fin (succ-‚Ñï n) zero-‚Ñï =
  equiv-is-contr binomial-type-over-empty is-contr-Fin-one-‚Ñï
binomial-type-Fin (succ-‚Ñï n) (succ-‚Ñï m) =
  ( ( inv-equiv (Fin-add-‚Ñï (n choose-‚Ñï m) (n choose-‚Ñï succ-‚Ñï m))) ‚àòe
    ( equiv-coprod
      ( binomial-type-Fin n m)
      ( binomial-type-Fin n (succ-‚Ñï m)))) ‚àòe
  ( binomial-type-Maybe (Fin n) (Fin m))

has-cardinality-binomial-type :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {n m : ‚Ñï} ‚Üí
  has-cardinality n A ‚Üí has-cardinality m B ‚Üí
  has-cardinality (n choose-‚Ñï m) (binomial-type A B)
has-cardinality-binomial-type {A = A} {B} {n} {m} H K =
  apply-universal-property-trunc-Prop H
    ( has-cardinality-Prop (n choose-‚Ñï m) (binomial-type A B))
    ( Œª e ‚Üí
      apply-universal-property-trunc-Prop K
        ( has-cardinality-Prop (n choose-‚Ñï m) (binomial-type A B))
        ( Œª f ‚Üí
          unit-trunc-Prop
            ( inv-equiv
              ( binomial-type-Fin n m ‚àòe equiv-binomial-type e f))))

binomial-type-UU-Fin-Level :
  {l1 l2 : Level} {n m : ‚Ñï} ‚Üí UU-Fin-Level l1 n ‚Üí UU-Fin-Level l2 m ‚Üí
  UU-Fin-Level (lsuc l1 ‚äî lsuc l2) (n choose-‚Ñï m)
pr1 (binomial-type-UU-Fin-Level A B) =
  binomial-type (type-UU-Fin-Level A) (type-UU-Fin-Level B)
pr2 (binomial-type-UU-Fin-Level A B) =
  has-cardinality-binomial-type
    ( mere-equiv-UU-Fin-Level A)
    ( mere-equiv-UU-Fin-Level B)

binomial-type-UU-Fin :
  {n m : ‚Ñï} ‚Üí UU-Fin n ‚Üí UU-Fin m ‚Üí UU-Fin (n choose-‚Ñï m)
pr1 (binomial-type-UU-Fin {n} {m} A B) =
  small-binomial-type (type-UU-Fin A) (type-UU-Fin B)
pr2 (binomial-type-UU-Fin {n} {m} A B) =
  apply-universal-property-trunc-Prop
    ( has-cardinality-binomial-type
      ( mere-equiv-UU-Fin A)
      ( mere-equiv-UU-Fin B))
    ( mere-equiv-Prop
      ( Fin (n choose-‚Ñï m))
      ( small-binomial-type (pr1 A) (pr1 B)))
    ( Œª e ‚Üí
      unit-trunc-Prop
        ( ( compute-small-binomial-type (type-UU-Fin A) (type-UU-Fin B)) ‚àòe
          ( e)))

has-finite-cardinality-binomial-type :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} ‚Üí
  has-finite-cardinality A ‚Üí has-finite-cardinality B ‚Üí
  has-finite-cardinality (binomial-type A B)
pr1 (has-finite-cardinality-binomial-type (pair n H) (pair m K)) = n choose-‚Ñï m
pr2 (has-finite-cardinality-binomial-type (pair n H) (pair m K)) =
  has-cardinality-binomial-type H K

abstract
  is-finite-binomial-type :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} ‚Üí
    is-finite A ‚Üí is-finite B ‚Üí is-finite (binomial-type A B)
  is-finite-binomial-type H K =
    is-finite-has-finite-cardinality
      ( has-finite-cardinality-binomial-type
        ( has-finite-cardinality-is-finite H)
        ( has-finite-cardinality-is-finite K))

binomial-type-ùîΩ : ùîΩ ‚Üí ùîΩ ‚Üí ùîΩ
pr1 (binomial-type-ùîΩ A B) = small-binomial-type (type-ùîΩ A) (type-ùîΩ B)
pr2 (binomial-type-ùîΩ A B) =
  is-finite-equiv
    ( compute-small-binomial-type (type-ùîΩ A) (type-ùîΩ B))
    ( is-finite-binomial-type (is-finite-type-ùîΩ A) (is-finite-type-ùîΩ B))
```
