# The binomial types

```agda
module univalent-combinatorics.binomial-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.binomial-coefficients
open import elementary-number-theory.natural-numbers

open import foundation.booleans
open import foundation.connected-components-universes
open import foundation.contractible-maps
open import foundation.contractible-types
open import foundation.coproduct-types
open import foundation.decidable-embeddings
open import foundation.decidable-propositions
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.empty-types
open import foundation.equivalences
open import foundation.equivalences-maybe
open import foundation.fibers-of-maps
open import foundation.function-types
open import foundation.functoriality-coproduct-types
open import foundation.functoriality-dependent-pair-types
open import foundation.functoriality-function-types
open import foundation.functoriality-propositional-truncation
open import foundation.logical-equivalences
open import foundation.maybe
open import foundation.mere-equivalences
open import foundation.negation
open import foundation.propositional-extensionality
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.raising-universe-levels
open import foundation.type-arithmetic-cartesian-product-types
open import foundation.type-arithmetic-coproduct-types
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.type-arithmetic-empty-type
open import foundation.type-arithmetic-unit-type
open import foundation.unit-type
open import foundation.universal-property-empty-type
open import foundation.universal-property-maybe
open import foundation.universe-levels

open import univalent-combinatorics.coproduct-types
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

The binomial types are a categorification of the binomial coefficients. The
binomial type `(A choose B)` is the type of decidable embeddings from types
merely equal to `B` into `A`.

## Definitions

```agda
binomial-type-Level :
  (l : Level) {l1 l2 : Level} (X : UU l1) (Y : UU l2) â†’ UU (lsuc l âŠ” l1 âŠ” l2)
binomial-type-Level l X Y =
  Î£ (component-UU-Level l Y) (Î» Z â†’ type-component-UU-Level Z â†ªd X)

type-binomial-type-Level :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} â†’
  binomial-type-Level l3 X Y â†’ UU l3
type-binomial-type-Level Z = type-component-UU-Level (pr1 Z)

abstract
  mere-compute-binomial-type-Level :
    {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2}
    (Z : binomial-type-Level l3 X Y) â†’
    mere-equiv Y (type-binomial-type-Level Z)
  mere-compute-binomial-type-Level Z = mere-equiv-component-UU-Level (pr1 Z)

decidable-emb-binomial-type-Level :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type-Level l3 X Y) â†’
  type-binomial-type-Level Z â†ªd X
decidable-emb-binomial-type-Level Z = pr2 Z

map-decidable-emb-binomial-type-Level :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type-Level l3 X Y) â†’
  type-binomial-type-Level Z â†’ X
map-decidable-emb-binomial-type-Level Z =
  map-decidable-emb (decidable-emb-binomial-type-Level Z)

abstract
  is-emb-map-emb-binomial-type-Level :
    {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2}
    (Z : binomial-type-Level l3 X Y) â†’
    is-emb (map-decidable-emb-binomial-type-Level Z)
  is-emb-map-emb-binomial-type-Level Z =
    is-emb-map-decidable-emb (decidable-emb-binomial-type-Level Z)
```

### The standard binomial types

We now define the standard binomial types.

```agda
binomial-type : {l1 l2 : Level} (X : UU l1) (Y : UU l2) â†’ UU (lsuc (l1 âŠ” l2))
binomial-type {l1} {l2} X Y = binomial-type-Level (l1 âŠ” l2) X Y

type-binomial-type :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’ binomial-type X Y â†’ UU (l1 âŠ” l2)
type-binomial-type Z = type-component-UU-Level (pr1 Z)

abstract
  mere-compute-binomial-type :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type X Y) â†’
    mere-equiv Y (type-binomial-type Z)
  mere-compute-binomial-type Z = mere-equiv-component-UU-Level (pr1 Z)

decidable-emb-binomial-type :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type X Y) â†’
  type-binomial-type Z â†ªd X
decidable-emb-binomial-type Z = pr2 Z

map-decidable-emb-binomial-type :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type X Y) â†’
  type-binomial-type Z â†’ X
map-decidable-emb-binomial-type Z =
  map-decidable-emb (decidable-emb-binomial-type Z)

abstract
  is-emb-map-emb-binomial-type :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (Z : binomial-type X Y) â†’
    is-emb (map-decidable-emb-binomial-type Z)
  is-emb-map-emb-binomial-type Z =
    is-emb-map-decidable-emb (decidable-emb-binomial-type Z)
```

### Proposition 17.5.6

```agda
binomial-type-Level' :
  (l : Level) {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’ UU (lsuc l âŠ” l1 âŠ” l2)
binomial-type-Level' l A B =
  Î£ ( A â†’ Decidable-Prop l)
    ( Î» P â†’ mere-equiv B (Î£ A (type-Decidable-Prop âˆ˜ P)))

compute-binomial-type-Level :
  (l : Level) {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’
  binomial-type-Level (l1 âŠ” l) A B â‰ƒ binomial-type-Level' (l1 âŠ” l) A B
compute-binomial-type-Level l {l1} {l2} A B =
  ( ( ( equiv-Î£
        ( Î» P â†’ mere-equiv B (Î£ A (type-Decidable-Prop âˆ˜ P)))
        ( equiv-Fib-Decidable-Prop l A)
        ( Î» e â†’
          equiv-trunc-Prop
            ( equiv-postcomp-equiv
              ( inv-equiv (equiv-total-fib (pr1 (pr2 e)))) B))) âˆ˜e
      ( inv-associative-Î£
        ( UU (l1 âŠ” l))
        ( Î» X â†’ X â†ªd A)
        ( Î» X â†’ mere-equiv B (pr1 X)))) âˆ˜e
    ( equiv-tot (Î» X â†’ commutative-prod))) âˆ˜e
  ( associative-Î£ (UU (l1 âŠ” l)) (Î» X â†’ mere-equiv B X) (Î» X â†’ (pr1 X) â†ªd A))

binomial-type' :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’ UU (lsuc (l1 âŠ” l2))
binomial-type' {l1} {l2} A B = binomial-type-Level' (l1 âŠ” l2) A B

compute-binomial-type :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’
  binomial-type A B â‰ƒ binomial-type' A B
compute-binomial-type {l1} {l2} A B =
  compute-binomial-type-Level (l1 âŠ” l2) A B
```

### Remark 17.5.7

Note that the universe level of `small-binomial-type` is lower.

```agda
small-binomial-type :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’ UU (l1 âŠ” l2)
small-binomial-type A B =
  Î£ (A â†’ bool) (Î» f â†’ mere-equiv B (fib f true))

compute-small-binomial-type :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’
  binomial-type A B â‰ƒ small-binomial-type A B
compute-small-binomial-type A B =
  ( equiv-Î£
    ( Î» f â†’ mere-equiv B (fib f true))
    ( equiv-postcomp A equiv-bool-Decidable-Prop)
    ( Î» P â†’
      equiv-trunc-Prop
        ( equiv-postcomp-equiv
          ( equiv-tot (Î» a â†’ compute-equiv-bool-Decidable-Prop (P a)))
          ( B)))) âˆ˜e
  ( compute-binomial-type A B)
```

```agda
abstract
  binomial-type-over-empty :
    {l : Level} {X : UU l} â†’ is-contr (binomial-type X empty)
  binomial-type-over-empty {l} {X} =
    is-contr-equiv
      ( raise-empty l â†ªd X)
      ( left-unit-law-Î£-is-contr
        ( is-contr-component-UU-Level-empty l)
        ( Fin-UU-Fin l zero-â„•))
      ( is-contr-equiv
        ( empty â†ªd X)
        ( equiv-precomp-decidable-emb-equiv (compute-raise-empty l) X)
        ( is-contr-equiv
          ( is-decidable-emb ex-falso)
          ( left-unit-law-Î£-is-contr (universal-property-empty' X) ex-falso)
          ( is-proof-irrelevant-is-prop
            ( is-prop-is-decidable-emb ex-falso)
            ( is-decidable-emb-ex-falso))))

abstract
  binomial-type-empty-under :
    {l : Level} {X : UU l} â†’
    type-trunc-Prop X â†’ is-empty (binomial-type empty X)
  binomial-type-empty-under H Y =
    apply-universal-property-trunc-Prop H empty-Prop
      ( Î» x â†’
        apply-universal-property-trunc-Prop (pr2 (pr1 Y)) empty-Prop
          ( Î» e â†’ map-decidable-emb (pr2 Y) (map-equiv e x)))

abstract
  recursion-binomial-type' :
    {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’
    binomial-type' (Maybe A) (Maybe B) â‰ƒ
    (binomial-type' A B + binomial-type' A (Maybe B))
  recursion-binomial-type' A B =
    ( ( ( left-distributive-Î£-coprod
          ( A â†’ Decidable-Prop _)
          ( Î» P â†’ mere-equiv B (Î£ A _))
          ( Î» P â†’ mere-equiv (Maybe B) (Î£ A _))) âˆ˜e
        ( equiv-tot
          ( Î» P â†’
            ( ( equiv-coprod
                ( ( ( equiv-iff
                      ( mere-equiv-Prop (Maybe B) (Maybe (Î£ A _)))
                      ( mere-equiv-Prop B (Î£ A _))
                      ( map-trunc-Prop (equiv-equiv-Maybe))
                      ( map-trunc-Prop
                        ( Î» e â†’ equiv-coprod e id-equiv))) âˆ˜e
                    ( equiv-trunc-Prop
                      ( equiv-postcomp-equiv
                        ( equiv-coprod
                          ( id-equiv)
                          ( equiv-is-contr is-contr-raise-unit is-contr-unit))
                        ( Maybe B)))) âˆ˜e
                  ( left-unit-law-Î£-is-contr
                    ( is-contr-total-true-Prop)
                    ( pair (raise-unit-Prop _) raise-star)))
                ( ( equiv-trunc-Prop
                    ( equiv-postcomp-equiv
                      ( right-unit-law-coprod-is-empty
                        ( Î£ A _)
                        ( raise-empty _)
                        ( is-empty-raise-empty))
                      ( Maybe B))) âˆ˜e
                  ( left-unit-law-Î£-is-contr
                    ( is-contr-total-false-Prop)
                    ( pair (raise-empty-Prop _) map-inv-raise)))) âˆ˜e
              ( right-distributive-Î£-coprod
                ( Î£ (Prop _) type-Prop)
                ( Î£ (Prop _) (Â¬ âˆ˜ type-Prop))
                ( ind-coprod _
                  ( Î» Q â†’
                    mere-equiv (Maybe B) ((Î£ A _) + (type-Prop (pr1 Q))))
                  ( Î» Q â†’
                    mere-equiv
                      ( Maybe B)
                      ( (Î£ A _) + (type-Prop (pr1 Q))))))) âˆ˜e
            ( equiv-Î£
              ( ind-coprod _
                ( Î» Q â†’
                  mere-equiv
                    ( Maybe B)
                    ( ( Î£ A (Î» a â†’ type-Decidable-Prop (P a))) +
                      ( type-Prop (pr1 Q))))
                ( Î» Q â†’
                  mere-equiv
                    ( Maybe B)
                    ( ( Î£ A (Î» a â†’ type-Decidable-Prop (P a))) +
                      ( type-Prop (pr1 Q)))))
              ( split-Decidable-Prop)
              ( ind-Î£
                ( Î» Q â†’
                  ind-Î£
                    ( Î» H â†’
                      ind-coprod _ ( Î» q â†’ id-equiv) (Î» q â†’ id-equiv)))))))) âˆ˜e
      ( associative-Î£
        ( A â†’ Decidable-Prop _)
        ( Î» a â†’ Decidable-Prop _)
        ( Î» t â†’
          mere-equiv
            ( Maybe B)
            ( ( Î£ A (Î» a â†’ type-Decidable-Prop (pr1 t a))) +
              ( type-Decidable-Prop (pr2 t)))))) âˆ˜e
    ( equiv-Î£
      ( Î» p â†’
        mere-equiv
          ( Maybe B)
          ( ( Î£ A (Î» a â†’ type-Decidable-Prop (pr1 p a))) +
            ( type-Decidable-Prop (pr2 p))))
      ( equiv-universal-property-Maybe)
      ( Î» u â†’
        equiv-trunc-Prop
          ( equiv-postcomp-equiv
            ( ( equiv-coprod
                ( id-equiv)
                ( left-unit-law-Î£ (Î» y â†’ type-Decidable-Prop (u (inr y))))) âˆ˜e
              ( right-distributive-Î£-coprod A unit
                ( Î» x â†’ type-Decidable-Prop (u x))))
            ( Maybe B))))

abstract
  binomial-type-Maybe :
    {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’
    binomial-type (Maybe A) (Maybe B) â‰ƒ
    (binomial-type A B + binomial-type A (Maybe B))
  binomial-type-Maybe A B =
    ( inv-equiv
      ( equiv-coprod
        ( compute-binomial-type A B)
        ( compute-binomial-type A (Maybe B))) âˆ˜e
      ( recursion-binomial-type' A B)) âˆ˜e
    ( compute-binomial-type (Maybe A) (Maybe B))
```

### Theorem 17.5.9

```agda
equiv-small-binomial-type :
  {l1 l2 l3 l4 : Level} {A : UU l1} {A' : UU l2} {B : UU l3} {B' : UU l4} â†’
  (A â‰ƒ A') â†’ (B â‰ƒ B') â†’ small-binomial-type A' B' â‰ƒ small-binomial-type A B
equiv-small-binomial-type {l1} {l2} {l3} {l4} {A} {A'} {B} {B'} e f =
  equiv-Î£
    ( Î» P â†’ mere-equiv B (fib P true))
    ( equiv-precomp e bool)
    ( Î» P â†’
      equiv-trunc-Prop
        ( ( equiv-postcomp-equiv
            ( inv-equiv
              ( ( right-unit-law-Î£-is-contr
                  ( Î» u â†’
                    is-contr-map-is-equiv (is-equiv-map-equiv e) (pr1 u))) âˆ˜e
                ( equiv-compute-fib-comp P (map-equiv e) true))) B) âˆ˜e
          ( equiv-precomp-equiv f (fib P true))))

equiv-binomial-type :
  {l1 l2 l3 l4 : Level} {A : UU l1} {A' : UU l2} {B : UU l3} {B' : UU l4} â†’
  (A â‰ƒ A') â†’ (B â‰ƒ B') â†’ binomial-type A' B' â‰ƒ binomial-type A B
equiv-binomial-type e f =
  ( ( inv-equiv (compute-small-binomial-type _ _)) âˆ˜e
    ( equiv-small-binomial-type e f)) âˆ˜e
  ( compute-small-binomial-type _ _)

binomial-type-Fin :
  (n m : â„•) â†’ binomial-type (Fin n) (Fin m) â‰ƒ Fin (binomial-coefficient-â„• n m)
binomial-type-Fin zero-â„• zero-â„• =
  equiv-is-contr binomial-type-over-empty is-contr-Fin-one-â„•
binomial-type-Fin zero-â„• (succ-â„• m) =
  equiv-is-empty (binomial-type-empty-under (unit-trunc-Prop (inr star))) id
binomial-type-Fin (succ-â„• n) zero-â„• =
  equiv-is-contr binomial-type-over-empty is-contr-Fin-one-â„•
binomial-type-Fin (succ-â„• n) (succ-â„• m) =
  ( ( inv-equiv
      ( Fin-add-â„•
        ( binomial-coefficient-â„• n m)
        ( binomial-coefficient-â„• n (succ-â„• m)))) âˆ˜e
    ( equiv-coprod
      ( binomial-type-Fin n m)
      ( binomial-type-Fin n (succ-â„• m)))) âˆ˜e
  ( binomial-type-Maybe (Fin n) (Fin m))

has-cardinality-binomial-type :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (n m : â„•) â†’
  has-cardinality n A â†’ has-cardinality m B â†’
  has-cardinality (binomial-coefficient-â„• n m) (binomial-type A B)
has-cardinality-binomial-type {A = A} {B} n m H K =
  apply-universal-property-trunc-Prop H
    ( has-cardinality-Prop (binomial-coefficient-â„• n m) (binomial-type A B))
    ( Î» e â†’
      apply-universal-property-trunc-Prop K
        ( has-cardinality-Prop (binomial-coefficient-â„• n m) (binomial-type A B))
        ( Î» f â†’
          unit-trunc-Prop
            ( inv-equiv
              ( binomial-type-Fin n m âˆ˜e equiv-binomial-type e f))))

binomial-type-UU-Fin :
  {l1 l2 : Level} (n m : â„•) â†’ UU-Fin l1 n â†’ UU-Fin l2 m â†’
  UU-Fin (lsuc l1 âŠ” lsuc l2) (binomial-coefficient-â„• n m)
pr1 (binomial-type-UU-Fin n m A B) =
  binomial-type (type-UU-Fin n A) (type-UU-Fin m B)
pr2 (binomial-type-UU-Fin n m A B) =
  has-cardinality-binomial-type n m
    ( has-cardinality-type-UU-Fin n A)
    ( has-cardinality-type-UU-Fin m B)

has-finite-cardinality-binomial-type :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  has-finite-cardinality A â†’ has-finite-cardinality B â†’
  has-finite-cardinality (binomial-type A B)
pr1 (has-finite-cardinality-binomial-type (pair n H) (pair m K)) =
  binomial-coefficient-â„• n m
pr2 (has-finite-cardinality-binomial-type (pair n H) (pair m K)) =
  has-cardinality-binomial-type n m H K

abstract
  is-finite-binomial-type :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-finite A â†’ is-finite B â†’ is-finite (binomial-type A B)
  is-finite-binomial-type H K =
    is-finite-has-finite-cardinality
      ( has-finite-cardinality-binomial-type
        ( has-finite-cardinality-is-finite H)
        ( has-finite-cardinality-is-finite K))

binomial-type-ğ”½ : {l1 l2 : Level} â†’ ğ”½ l1 â†’ ğ”½ l2 â†’ ğ”½ (l1 âŠ” l2)
pr1 (binomial-type-ğ”½ A B) = small-binomial-type (type-ğ”½ A) (type-ğ”½ B)
pr2 (binomial-type-ğ”½ A B) =
  is-finite-equiv
    ( compute-small-binomial-type (type-ğ”½ A) (type-ğ”½ B))
    ( is-finite-binomial-type (is-finite-type-ğ”½ A) (is-finite-type-ğ”½ B))
```
