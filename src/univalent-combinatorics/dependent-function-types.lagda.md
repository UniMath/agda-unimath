---
title: Counting the elements of dependent function types
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module univalent-combinatorics.dependent-function-types where

open import elementary-number-theory.natural-numbers using (â„•; zero-â„•; succ-â„•)

open import foundation.coproduct-types using (inl; inr)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.functions using (_âˆ˜_)
open import foundation.functoriality-dependent-function-types using
  ( equiv-precomp-Î )
open import foundation.propositional-truncations using
  ( apply-universal-property-trunc-Prop; unit-trunc-Prop)
open import foundation.unit-type using (star)
open import foundation.universal-property-coproduct-types using
  ( equiv-dependent-universal-property-coprod)
open import foundation.universal-property-empty-type using
  ( dependent-universal-property-empty')
open import foundation.universal-property-unit-type using
  ( equiv-dependent-universal-property-unit)
open import foundation.universe-levels using (Level; UU; _âŠ”_)

open import univalent-combinatorics.cartesian-product-types using
  ( count-prod)
open import univalent-combinatorics.counting using
  ( count; count-is-contr; count-equiv'; equiv-count; map-equiv-count)
open import univalent-combinatorics.finite-choice using (finite-choice)
open import univalent-combinatorics.finite-types using
  ( is-finite; is-finite-Prop; ğ”½; type-ğ”½; is-finite-type-ğ”½)
open import univalent-combinatorics.standard-finite-types using (Fin)
```

## Idea

Dependent products of finite types indexed by a finite type are finite.

## Properties

### Counting dependent products indexed by standard finite types

If the elements of `A` can be counted and if for each `x : A` the elements of `B x` can be counted, then the elements of `Î  A B` can be counted.

```agda
count-Î -Fin :
  {l1 : Level} {k : â„•} {B : Fin k â†’ UU l1} â†’
  ((x : Fin k) â†’ count (B x)) â†’ count ((x : Fin k) â†’ B x)
count-Î -Fin {l1} {zero-â„•} {B} e =
  count-is-contr (dependent-universal-property-empty' B)
count-Î -Fin {l1} {succ-â„• k} {B} e =
  count-equiv'
    ( equiv-dependent-universal-property-coprod B)
    ( count-prod
      ( count-Î -Fin (Î» x â†’ e (inl x)))
      ( count-equiv'
        ( equiv-dependent-universal-property-unit (B âˆ˜ inr))
        ( e (inr star))))
```

### Counting on dependent function types

```agda
count-Î  :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  count A â†’ ((x : A) â†’ count (B x)) â†’ count ((x : A) â†’ B x)
count-Î  {l1} {l2} {A} {B} e f =
  count-equiv'
    ( equiv-precomp-Î  (equiv-count e) B)
    ( count-Î -Fin (Î» x â†’ f (map-equiv-count e x)))
```

### Finiteness of dependent function types

```agda
abstract
  is-finite-Î  :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
    is-finite A â†’ ((x : A) â†’ is-finite (B x)) â†’ is-finite ((x : A) â†’ B x)
  is-finite-Î  {l1} {l2} {A} {B} f g =
    apply-universal-property-trunc-Prop f
      ( is-finite-Prop ((x : A) â†’ B x))
      ( Î» e â†’
        apply-universal-property-trunc-Prop
          ( finite-choice f g)
          ( is-finite-Prop ((x : A) â†’ B x))
          ( Î» h â†’ unit-trunc-Prop (count-Î  e h)))

Î -ğ”½ : (A : ğ”½) (B : type-ğ”½ A â†’ ğ”½) â†’ ğ”½
pr1 (Î -ğ”½ A B) = (x : type-ğ”½ A) â†’ type-ğ”½ (B x)
pr2 (Î -ğ”½ A B) = is-finite-Î  (is-finite-type-ğ”½ A) (Î» x â†’ is-finite-type-ğ”½ (B x))
```
