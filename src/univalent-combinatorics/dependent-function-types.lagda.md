# Counting the elements of dependent function types

```agda
module univalent-combinatorics.dependent-function-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.multiplication-natural-numbers
open import elementary-number-theory.natural-numbers
open import elementary-number-theory.products-of-natural-numbers

open import foundation.action-on-identifications-functions
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.dependent-universal-property-equivalences
open import foundation.equivalences
open import foundation.function-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.propositional-truncations
open import foundation.unit-type
open import foundation.universal-property-coproduct-types
open import foundation.universal-property-empty-type
open import foundation.universal-property-unit-type
open import foundation.universe-levels

open import univalent-combinatorics.cartesian-product-types
open import univalent-combinatorics.counting
open import univalent-combinatorics.finite-choice
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

Dependent products of finite types indexed by a finite type are finite.

## Properties

### Counting dependent products indexed by standard finite types

If the elements of `A` can be counted and if for each `x : A` the elements of
`B x` can be counted, then the elements of `Î  A B` can be counted.

```agda
count-Î -Fin :
  {l1 : Level} (k : â„•) {B : Fin k â†’ UU l1} â†’
  ((x : Fin k) â†’ count (B x)) â†’ count ((x : Fin k) â†’ B x)
count-Î -Fin {l1} zero-â„• {B} c =
  count-is-contr (dependent-universal-property-empty' B)
count-Î -Fin {l1} (succ-â„• k) {B} c =
  count-equiv'
    ( equiv-dependent-universal-property-coproduct B)
    ( count-product
      ( count-Î -Fin k (Î» i â†’ c (inl-Fin k i)))
      ( count-equiv'
        ( equiv-dependent-universal-property-unit (B âˆ˜ inr))
        ( c (inr star))))

number-of-elements-count-Î -Fin :
  {l1 : Level} (k : â„•) {B : Fin k â†’ UU l1} â†’
  (c : (x : Fin k) â†’ count (B x)) â†’
  number-of-elements-count (count-Î -Fin k c) ï¼
  Î -â„• k (Î» i â†’ number-of-elements-count (c i))
number-of-elements-count-Î -Fin zero-â„• c = refl
number-of-elements-count-Î -Fin (succ-â„• k) c =
  ( number-of-elements-count-product
    ( count-Î -Fin k (Î» i â†’ c (inl-Fin k i)))
    ( c (inr star))) âˆ™
  ( ap
    ( _*â„• number-of-elements-count (c (inr star)))
    ( number-of-elements-count-Î -Fin k (Î» i â†’ c (inl-Fin k i))))
```

### Counting on dependent function types

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (c : count A) (d : (x : A) â†’ count (B x))
  where

  count-Î  : count ((x : A) â†’ B x)
  count-Î  =
    count-equiv'
      ( equiv-precomp-Î  (equiv-count c) B)
      ( count-Î -Fin
        ( number-of-elements-count c)
        ( Î» i â†’ d (map-equiv-count c i)))

  number-of-elements-count-Î  :
    number-of-elements-count count-Î  ï¼
    Î -â„•
      ( number-of-elements-count c)
      ( Î» i â†’
        number-of-elements-count (d (map-equiv-count c i)))
  number-of-elements-count-Î  =
    number-of-elements-count-Î -Fin
      ( number-of-elements-count c)
      ( Î» i â†’ d (map-equiv-count c i))
```

### Finiteness of dependent function types

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  where

  abstract
    is-finite-Î  :
      is-finite A â†’ ((x : A) â†’ is-finite (B x)) â†’ is-finite ((x : A) â†’ B x)
    is-finite-Î  f g =
      apply-universal-property-trunc-Prop f
        ( is-finite-Prop ((x : A) â†’ B x))
        ( Î» e â†’
          apply-universal-property-trunc-Prop
            ( finite-choice f g)
            ( is-finite-Prop ((x : A) â†’ B x))
            ( Î» h â†’ unit-trunc-Prop (count-Î  e h)))

    is-finite-Î ' :
      is-finite A â†’ ((x : A) â†’ is-finite (B x)) â†’ is-finite ({x : A} â†’ B x)
    is-finite-Î ' f g =
      is-finite-equiv
        (( pair
          ( Î» f {x} â†’ f x)
          ( is-equiv-is-invertible
            ( Î» g x â†’ g {x})
            ( refl-htpy)
            ( refl-htpy))))
        (is-finite-Î  f g)

Î -ğ”½ : {l1 l2 : Level} (A : ğ”½ l1) (B : type-ğ”½ A â†’ ğ”½ l2) â†’ ğ”½ (l1 âŠ” l2)
pr1 (Î -ğ”½ A B) = (x : type-ğ”½ A) â†’ type-ğ”½ (B x)
pr2 (Î -ğ”½ A B) = is-finite-Î  (is-finite-type-ğ”½ A) (Î» x â†’ is-finite-type-ğ”½ (B x))

Î -ğ”½' : {l1 l2 : Level} (A : ğ”½ l1) (B : type-ğ”½ A â†’ ğ”½ l2) â†’ ğ”½ (l1 âŠ” l2)
pr1 (Î -ğ”½' A B) = {x : type-ğ”½ A} â†’ type-ğ”½ (B x)
pr2 (Î -ğ”½' A B) =
  is-finite-Î ' (is-finite-type-ğ”½ A) (Î» x â†’ is-finite-type-ğ”½ (B x))
```
