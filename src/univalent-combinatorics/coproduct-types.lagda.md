# Coproducts of finite types

```agda
module univalent-combinatorics.coproduct-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.addition-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.functions
open import foundation.functoriality-coproduct-types
open import foundation.functoriality-propositional-truncation
open import foundation.homotopies
open import foundation.identity-types
open import foundation.mere-equivalences
open import foundation.propositional-truncations
open import foundation.type-arithmetic-coproduct-types
open import foundation.type-arithmetic-empty-type
open import foundation.universe-levels

open import univalent-combinatorics.counting
open import univalent-combinatorics.counting-decidable-subtypes
open import univalent-combinatorics.double-counting
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

Coproducts of finite types are finite, giving a coproduct operation on the type
ğ”½ of finite types.

## Properties

### The standard finite types are closed under coproducts

```agda
coprod-Fin :
  (k l : â„•) â†’ (Fin k + Fin l) â‰ƒ Fin (k +â„• l)
coprod-Fin k zero-â„• = right-unit-law-coprod (Fin k)
coprod-Fin k (succ-â„• l) =
  (equiv-coprod (coprod-Fin k l) id-equiv) âˆ˜e inv-associative-coprod

map-coprod-Fin :
  (k l : â„•) â†’ (Fin k + Fin l) â†’ Fin (k +â„• l)
map-coprod-Fin k l = map-equiv (coprod-Fin k l)

Fin-add-â„• :
  (k l : â„•) â†’ Fin (k +â„• l) â‰ƒ (Fin k + Fin l)
Fin-add-â„• k l = inv-equiv (coprod-Fin k l)

inl-coprod-Fin :
  (k l : â„•) â†’ Fin k â†’ Fin (k +â„• l)
inl-coprod-Fin k l = map-coprod-Fin k l âˆ˜ inl

inr-coprod-Fin :
  (k l : â„•) â†’ Fin l â†’ Fin (k +â„• l)
inr-coprod-Fin k l = map-coprod-Fin k l âˆ˜ inr

compute-inl-coprod-Fin :
  (k : â„•) â†’ inl-coprod-Fin k 0 ~ id
compute-inl-coprod-Fin k x = refl
```

### Inclusion of `coprod-Fin` into the natural numbers

```agda
nat-coprod-Fin :
  (n m : â„•) â†’ (x : Fin n + Fin m) â†’
  nat-Fin (n +â„• m) (map-coprod-Fin n m x) ï¼
  ind-coprod _ (nat-Fin n) (Î» i â†’ n +â„• (nat-Fin m i)) x
nat-coprod-Fin n zero-â„• (inl x) = refl
nat-coprod-Fin n (succ-â„• m) (inl x) = nat-coprod-Fin n m (inl x)
nat-coprod-Fin n (succ-â„• m) (inr (inl x)) = nat-coprod-Fin n m (inr x)
nat-coprod-Fin n (succ-â„• m) (inr (inr star)) = refl

nat-inl-coprod-Fin :
  (n m : â„•) (i : Fin n) â†’
  nat-Fin (n +â„• m) (inl-coprod-Fin n m i) ï¼ nat-Fin n i
nat-inl-coprod-Fin n m i = nat-coprod-Fin n m (inl i)

nat-inr-coprod-Fin :
  (n m : â„•) (i : Fin m) â†’
  nat-Fin (n +â„• m) (inr-coprod-Fin n m i) ï¼ n +â„• (nat-Fin m i)
nat-inr-coprod-Fin n m i = nat-coprod-Fin n m (inr i)
```

### Types equipped with a count are closed under coproducts

```agda
count-coprod :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’
  count X â†’ count Y â†’ count (X + Y)
pr1 (count-coprod (pair k e) (pair l f)) = k +â„• l
pr2 (count-coprod (pair k e) (pair l f)) =
  (equiv-coprod e f) âˆ˜e (inv-equiv (coprod-Fin k l))

abstract
  number-of-elements-count-coprod :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (e : count X) (f : count Y) â†’
    Id ( number-of-elements-count (count-coprod e f))
      ( (number-of-elements-count e) +â„• (number-of-elements-count f))
  number-of-elements-count-coprod (pair k e) (pair l f) = refl
```

### If both `Î£ A P` and `Î£ A Q` have a count, then `Î£ A P + Q` have a count

```agda
count-Î£-coprod :
  {l1 l2 l3 : Level} {A : UU l1} {P : A â†’ UU l2} {Q : A â†’ UU l3} â†’
  count (Î£ A P) â†’ count (Î£ A Q) â†’ count (Î£ A (Î» x â†’ (P x) + (Q x)))
pr1 (count-Î£-coprod count-P count-Q) = pr1 (count-coprod count-P count-Q)
pr2 (count-Î£-coprod count-P count-Q) =
  ( inv-equiv (left-distributive-Î£-coprod _ _ _)) âˆ˜e
  ( pr2 (count-coprod count-P count-Q))
```

### If `X + Y` has a count, then both `X` and `Y` have a count

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2}
  where

  count-left-summand : count (X + Y) â†’ count X
  count-left-summand e =
    count-equiv
      ( equiv-left-summand)
      ( count-decidable-subtype is-left-Decidable-Prop e)

  count-right-summand : count (X + Y) â†’ count Y
  count-right-summand e =
    count-equiv
      ( equiv-right-summand)
      ( count-decidable-subtype is-right-Decidable-Prop e)
```

### If each of `A`, `B`, and `A + B` come equipped with countings, then the number of elements of `A` and of `B` add up to the number of elements of `A + B`

```agda
abstract
  double-counting-coprod :
    { l1 l2 : Level} {A : UU l1} {B : UU l2}
    ( count-A : count A) (count-B : count B) (count-C : count (A + B)) â†’
    Id ( number-of-elements-count count-C)
       ( number-of-elements-count count-A +â„• number-of-elements-count count-B)
  double-counting-coprod count-A count-B count-C =
    ( double-counting count-C (count-coprod count-A count-B)) âˆ™
    ( number-of-elements-count-coprod count-A count-B)

abstract
  sum-number-of-elements-coprod :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : count (A + B)) â†’
    Id ( ( number-of-elements-count (count-left-summand e)) +â„•
               ( number-of-elements-count (count-right-summand e)))
       ( number-of-elements-count e)
  sum-number-of-elements-coprod e =
    ( inv
      ( number-of-elements-count-coprod
        ( count-left-summand e)
        ( count-right-summand e))) âˆ™
    ( inv
      ( double-counting-coprod
        ( count-left-summand e)
        ( count-right-summand e) e))
```

### Finite types are closed under coproducts

```agda
abstract
  is-finite-coprod :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’
    is-finite X â†’ is-finite Y â†’ is-finite (X + Y)
  is-finite-coprod {X = X} {Y} is-finite-X is-finite-Y =
    apply-universal-property-trunc-Prop is-finite-X
      ( is-finite-Prop (X + Y))
      ( Î» (e : count X) â†’
        apply-universal-property-trunc-Prop is-finite-Y
          ( is-finite-Prop (X + Y))
          ( is-finite-count âˆ˜ (count-coprod e)))

coprod-ğ”½ : {l1 l2 : Level} â†’ ğ”½ l1 â†’ ğ”½ l2 â†’ ğ”½ (l1 âŠ” l2)
pr1 (coprod-ğ”½ X Y) = (type-ğ”½ X) + (type-ğ”½ Y)
pr2 (coprod-ğ”½ X Y) = is-finite-coprod (is-finite-type-ğ”½ X) (is-finite-type-ğ”½ Y)

abstract
  is-finite-left-summand :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’ is-finite (X + Y) â†’
    is-finite X
  is-finite-left-summand =
    map-trunc-Prop count-left-summand

abstract
  is-finite-right-summand :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’ is-finite (X + Y) â†’
    is-finite Y
  is-finite-right-summand =
    map-trunc-Prop count-right-summand

coprod-UU-Fin :
  {l1 l2 : Level} (k l : â„•) â†’ UU-Fin l1 k â†’ UU-Fin l2 l â†’
  UU-Fin (l1 âŠ” l2) (k +â„• l)
pr1 (coprod-UU-Fin {l1} {l2} k l (pair X H) (pair Y K)) = X + Y
pr2 (coprod-UU-Fin {l1} {l2} k l (pair X H) (pair Y K)) =
  apply-universal-property-trunc-Prop H
    ( mere-equiv-Prop (Fin (k +â„• l)) (X + Y))
    ( Î» e1 â†’
      apply-universal-property-trunc-Prop K
        ( mere-equiv-Prop (Fin (k +â„• l)) (X + Y))
        ( Î» e2 â†’
          unit-trunc-Prop
            ( equiv-coprod e1 e2 âˆ˜e inv-equiv (coprod-Fin k l))))

coprod-eq-is-finite :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (P : is-finite X) (Q : is-finite Y) â†’
    Id
      ( (number-of-elements-is-finite P) +â„• (number-of-elements-is-finite Q))
      ( number-of-elements-is-finite (is-finite-coprod P Q))
coprod-eq-is-finite {X = X} {Y = Y} P Q =
  ap
    ( number-of-elements-has-finite-cardinality)
    ( all-elements-equal-has-finite-cardinality
      ( pair
        ( number-of-elements-is-finite P +â„• number-of-elements-is-finite Q)
        ( has-cardinality-type-UU-Fin
          ( number-of-elements-is-finite P +â„• number-of-elements-is-finite Q)
          ( coprod-UU-Fin
            ( number-of-elements-is-finite P)
            ( number-of-elements-is-finite Q)
            ( pair X
              ( mere-equiv-has-finite-cardinality
                ( has-finite-cardinality-is-finite P)))
            ( pair Y
              ( mere-equiv-has-finite-cardinality
                ( has-finite-cardinality-is-finite Q))))))
      ( has-finite-cardinality-is-finite (is-finite-coprod P Q)))
```
