# Coproducts of finite types

```agda
module univalent-combinatorics.coproduct-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.addition-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.action-on-identifications-functions
open import foundation.coproduct-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.equivalence-extensionality
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-coproduct-types
open import foundation.functoriality-propositional-truncation
open import foundation.homotopies
open import foundation.identity-types
open import foundation.injective-maps
open import foundation.mere-equivalences
open import foundation.propositional-truncations
open import foundation.type-arithmetic-coproduct-types
open import foundation.type-arithmetic-empty-type
open import foundation.universe-levels

open import univalent-combinatorics.counting
open import univalent-combinatorics.counting-decidable-subtypes
open import univalent-combinatorics.double-counting
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

Coproducts of finite types are finite, giving a coproduct operation on the type
ùîΩ of finite types.

## Properties

### The standard finite types are closed under coproducts

```agda
coproduct-Fin :
  (k l : ‚Ñï) ‚Üí (Fin k + Fin l) ‚âÉ Fin (k +‚Ñï l)
coproduct-Fin k zero-‚Ñï = right-unit-law-coproduct (Fin k)
coproduct-Fin k (succ-‚Ñï l) =
  (equiv-coproduct (coproduct-Fin k l) id-equiv) ‚àòe inv-associative-coproduct

map-coproduct-Fin :
  (k l : ‚Ñï) ‚Üí (Fin k + Fin l) ‚Üí Fin (k +‚Ñï l)
map-coproduct-Fin k l = map-equiv (coproduct-Fin k l)

Fin-add-‚Ñï :
  (k l : ‚Ñï) ‚Üí Fin (k +‚Ñï l) ‚âÉ (Fin k + Fin l)
Fin-add-‚Ñï k l = inv-equiv (coproduct-Fin k l)

inl-coproduct-Fin :
  (k l : ‚Ñï) ‚Üí Fin k ‚Üí Fin (k +‚Ñï l)
inl-coproduct-Fin k l = map-coproduct-Fin k l ‚àò inl

inr-coproduct-Fin :
  (k l : ‚Ñï) ‚Üí Fin l ‚Üí Fin (k +‚Ñï l)
inr-coproduct-Fin k l = map-coproduct-Fin k l ‚àò inr

compute-inl-coproduct-Fin :
  (k : ‚Ñï) ‚Üí inl-coproduct-Fin k 0 ~ id
compute-inl-coproduct-Fin k x = refl

map-Fin-add-‚Ñï :
  (k l : ‚Ñï) ‚Üí Fin (k +‚Ñï l) ‚Üí Fin k + Fin l
map-Fin-add-‚Ñï k zero-‚Ñï = inl
map-Fin-add-‚Ñï k (succ-‚Ñï l) =
  ( map-equiv (associative-coproduct {A = Fin k} {B = Fin l})) ‚àò
  ( map-coproduct (map-Fin-add-‚Ñï k l) id)

compute-map-Fin-add-‚Ñï :
  (k l : ‚Ñï) ‚Üí map-equiv (Fin-add-‚Ñï k l) ~ map-Fin-add-‚Ñï k l
compute-map-Fin-add-‚Ñï k zero-‚Ñï x = refl
compute-map-Fin-add-‚Ñï k (succ-‚Ñï l) x =
  ( htpy-eq
    ( distributive-map-inv-comp-equiv
      ( inv-associative-coproduct)
      ( equiv-coproduct (coproduct-Fin k l) id-equiv))
    ( x)) ‚àô
  ( htpy-eq-equiv
    ( inv-inv-equiv associative-coproduct)
    ( map-inv-equiv (equiv-coproduct (coproduct-Fin k l) id-equiv) x)) ‚àô
  ( ap
    ( map-associative-coproduct)
    ( ( compute-map-inv-equiv-coproduct (coproduct-Fin k l) (id-equiv) x) ‚àô
    ( htpy-map-coproduct (compute-map-Fin-add-‚Ñï k l) refl-htpy x)))
```

### Inclusion of `coproduct-Fin` into the natural numbers

```agda
nat-coproduct-Fin :
  (n m : ‚Ñï) ‚Üí (x : Fin n + Fin m) ‚Üí
  nat-Fin (n +‚Ñï m) (map-coproduct-Fin n m x) Ôºù
  ind-coproduct _ (nat-Fin n) (Œª i ‚Üí n +‚Ñï (nat-Fin m i)) x
nat-coproduct-Fin n zero-‚Ñï (inl x) = refl
nat-coproduct-Fin n (succ-‚Ñï m) (inl x) = nat-coproduct-Fin n m (inl x)
nat-coproduct-Fin n (succ-‚Ñï m) (inr (inl x)) = nat-coproduct-Fin n m (inr x)
nat-coproduct-Fin n (succ-‚Ñï m) (inr (inr _)) = refl

nat-inl-coproduct-Fin :
  (n m : ‚Ñï) (i : Fin n) ‚Üí
  nat-Fin (n +‚Ñï m) (inl-coproduct-Fin n m i) Ôºù nat-Fin n i
nat-inl-coproduct-Fin n m i = nat-coproduct-Fin n m (inl i)

nat-inr-coproduct-Fin :
  (n m : ‚Ñï) (i : Fin m) ‚Üí
  nat-Fin (n +‚Ñï m) (inr-coproduct-Fin n m i) Ôºù n +‚Ñï (nat-Fin m i)
nat-inr-coproduct-Fin n m i = nat-coproduct-Fin n m (inr i)
```

### Types equipped with a count are closed under coproducts

```agda
count-coproduct :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} ‚Üí
  count X ‚Üí count Y ‚Üí count (X + Y)
pr1 (count-coproduct (pair k e) (pair l f)) = k +‚Ñï l
pr2 (count-coproduct (pair k e) (pair l f)) =
  (equiv-coproduct e f) ‚àòe (inv-equiv (coproduct-Fin k l))

abstract
  number-of-elements-count-coproduct :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (e : count X) (f : count Y) ‚Üí
    Id ( number-of-elements-count (count-coproduct e f))
      ( (number-of-elements-count e) +‚Ñï (number-of-elements-count f))
  number-of-elements-count-coproduct (pair k e) (pair l f) = refl
```

### If both `Œ£ A P` and `Œ£ A Q` have a count, then `Œ£ A P + Q` have a count

```agda
count-Œ£-coproduct :
  {l1 l2 l3 : Level} {A : UU l1} {P : A ‚Üí UU l2} {Q : A ‚Üí UU l3} ‚Üí
  count (Œ£ A P) ‚Üí count (Œ£ A Q) ‚Üí count (Œ£ A (Œª x ‚Üí (P x) + (Q x)))
pr1 (count-Œ£-coproduct count-P count-Q) = pr1 (count-coproduct count-P count-Q)
pr2 (count-Œ£-coproduct count-P count-Q) =
  ( inv-equiv (left-distributive-Œ£-coproduct _ _ _)) ‚àòe
  ( pr2 (count-coproduct count-P count-Q))
```

### If `X + Y` has a count, then both `X` and `Y` have a count

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2}
  where

  count-left-summand : count (X + Y) ‚Üí count X
  count-left-summand e =
    count-equiv
      ( equiv-left-summand)
      ( count-decidable-subtype is-left-Decidable-Prop e)

  count-right-summand : count (X + Y) ‚Üí count Y
  count-right-summand e =
    count-equiv
      ( equiv-right-summand)
      ( count-decidable-subtype is-right-Decidable-Prop e)
```

### If each of `A`, `B`, and `A + B` come equipped with countings, then the number of elements of `A` and of `B` add up to the number of elements of `A + B`

```agda
abstract
  double-counting-coproduct :
    { l1 l2 : Level} {A : UU l1} {B : UU l2}
    ( count-A : count A) (count-B : count B) (count-C : count (A + B)) ‚Üí
    Id
      ( number-of-elements-count count-C)
      ( number-of-elements-count count-A +‚Ñï number-of-elements-count count-B)
  double-counting-coproduct count-A count-B count-C =
    ( double-counting count-C (count-coproduct count-A count-B)) ‚àô
    ( number-of-elements-count-coproduct count-A count-B)

abstract
  sum-number-of-elements-coproduct :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : count (A + B)) ‚Üí
    Id
      ( ( number-of-elements-count (count-left-summand e)) +‚Ñï
        ( number-of-elements-count (count-right-summand e)))
      ( number-of-elements-count e)
  sum-number-of-elements-coproduct e =
    ( inv
      ( number-of-elements-count-coproduct
        ( count-left-summand e)
        ( count-right-summand e))) ‚àô
    ( inv
      ( double-counting-coproduct
        ( count-left-summand e)
        ( count-right-summand e) e))
```

### Finite types are closed under coproducts

```agda
abstract
  is-finite-coproduct :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} ‚Üí
    is-finite X ‚Üí is-finite Y ‚Üí is-finite (X + Y)
  is-finite-coproduct {X = X} {Y} is-finite-X is-finite-Y =
    apply-universal-property-trunc-Prop is-finite-X
      ( is-finite-Prop (X + Y))
      ( Œª (e : count X) ‚Üí
        apply-universal-property-trunc-Prop is-finite-Y
          ( is-finite-Prop (X + Y))
          ( is-finite-count ‚àò (count-coproduct e)))

coproduct-ùîΩ : {l1 l2 : Level} ‚Üí ùîΩ l1 ‚Üí ùîΩ l2 ‚Üí ùîΩ (l1 ‚äî l2)
pr1 (coproduct-ùîΩ X Y) = (type-ùîΩ X) + (type-ùîΩ Y)
pr2 (coproduct-ùîΩ X Y) =
  is-finite-coproduct (is-finite-type-ùîΩ X) (is-finite-type-ùîΩ Y)

abstract
  is-finite-left-summand :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} ‚Üí is-finite (X + Y) ‚Üí
    is-finite X
  is-finite-left-summand =
    map-trunc-Prop count-left-summand

abstract
  is-finite-right-summand :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} ‚Üí is-finite (X + Y) ‚Üí
    is-finite Y
  is-finite-right-summand =
    map-trunc-Prop count-right-summand

coproduct-UU-Fin :
  {l1 l2 : Level} (k l : ‚Ñï) ‚Üí UU-Fin l1 k ‚Üí UU-Fin l2 l ‚Üí
  UU-Fin (l1 ‚äî l2) (k +‚Ñï l)
pr1 (coproduct-UU-Fin {l1} {l2} k l (pair X H) (pair Y K)) = X + Y
pr2 (coproduct-UU-Fin {l1} {l2} k l (pair X H) (pair Y K)) =
  apply-universal-property-trunc-Prop H
    ( mere-equiv-Prop (Fin (k +‚Ñï l)) (X + Y))
    ( Œª e1 ‚Üí
      apply-universal-property-trunc-Prop K
        ( mere-equiv-Prop (Fin (k +‚Ñï l)) (X + Y))
        ( Œª e2 ‚Üí
          unit-trunc-Prop
            ( equiv-coproduct e1 e2 ‚àòe inv-equiv (coproduct-Fin k l))))

coproduct-eq-is-finite :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (P : is-finite X) (Q : is-finite Y) ‚Üí
    Id
      ( (number-of-elements-is-finite P) +‚Ñï (number-of-elements-is-finite Q))
      ( number-of-elements-is-finite (is-finite-coproduct P Q))
coproduct-eq-is-finite {X = X} {Y = Y} P Q =
  ap
    ( number-of-elements-has-finite-cardinality)
    ( all-elements-equal-has-finite-cardinality
      ( pair
        ( number-of-elements-is-finite P +‚Ñï number-of-elements-is-finite Q)
        ( has-cardinality-type-UU-Fin
          ( number-of-elements-is-finite P +‚Ñï number-of-elements-is-finite Q)
          ( coproduct-UU-Fin
            ( number-of-elements-is-finite P)
            ( number-of-elements-is-finite Q)
            ( pair X
              ( mere-equiv-has-finite-cardinality
                ( has-finite-cardinality-is-finite P)))
            ( pair Y
              ( mere-equiv-has-finite-cardinality
                ( has-finite-cardinality-is-finite Q))))))
      ( has-finite-cardinality-is-finite (is-finite-coproduct P Q)))
```
