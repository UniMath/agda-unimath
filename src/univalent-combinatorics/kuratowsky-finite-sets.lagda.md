# Kuratowsky finite sets

```agda
module univalent-combinatorics.kuratowsky-finite-sets where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.decidable-equality
open import foundation.dependent-pair-types
open import foundation.existential-quantification
open import foundation.functoriality-propositional-truncation
open import foundation.law-of-excluded-middle
open import foundation.logical-equivalences
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.sets
open import foundation.subtypes
open import foundation.surjective-maps
open import foundation.universe-levels

open import foundation-core.equivalences
open import foundation-core.function-types
open import foundation-core.identity-types
open import foundation-core.transport-along-identifications

open import lists.functoriality-lists
open import lists.lists
open import lists.lists-subtypes

open import univalent-combinatorics.equality-finite-types
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.image-of-maps
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

A Kuratowsky finite type is a set `X` for which there exists a surjection into
`X` from a standard finite type. In other words, the Kuratowsky finite types are
the set-quotient of a standard finite type.

## Definition

```agda
is-kuratowsky-finite-set-Prop : {l : Level} â†’ Set l â†’ Prop l
is-kuratowsky-finite-set-Prop X =
  exists-structure-Prop â„• (Î» n â†’ Fin n â†  type-Set X)

is-kuratowsky-finite-set : {l : Level} â†’ Set l â†’ UU l
is-kuratowsky-finite-set X = type-Prop (is-kuratowsky-finite-set-Prop X)

ğ”½-Kuratowsky : (l : Level) â†’ UU (lsuc l)
ğ”½-Kuratowsky l = Î£ (Set l) is-kuratowsky-finite-set

module _
  {l : Level} (X : ğ”½-Kuratowsky l)
  where

  set-ğ”½-Kuratowsky : Set l
  set-ğ”½-Kuratowsky = pr1 X

  type-ğ”½-Kuratowsky : UU l
  type-ğ”½-Kuratowsky = type-Set set-ğ”½-Kuratowsky

  is-set-type-ğ”½-Kuratowsky : is-set type-ğ”½-Kuratowsky
  is-set-type-ğ”½-Kuratowsky = is-set-type-Set set-ğ”½-Kuratowsky

  is-kuratowsky-finite-set-ğ”½-Kuratowsky :
    is-kuratowsky-finite-set set-ğ”½-Kuratowsky
  is-kuratowsky-finite-set-ğ”½-Kuratowsky = pr2 X
```

## Second definition

```agda
is-kuratowsky-finite-set-list-Prop : {l : Level} â†’ Set l â†’ Prop l
is-kuratowsky-finite-set-list-Prop X =
  exists-structure-Prop (list (type-Set X))
    ( Î» l â†’ (x : type-Set X) â†’ is-in-subtype (list-subtype l) x)

is-kuratowsky-finite-set-list : {l : Level} â†’ Set l â†’ UU l
is-kuratowsky-finite-set-list X =
  type-Prop (is-kuratowsky-finite-set-list-Prop X)

is-kuratowsky-finite-set-is-kuratowsky-finite-set-list :
  {l : Level} (X : Set l) â†’
  is-kuratowsky-finite-set-list X â†’ is-kuratowsky-finite-set X
is-kuratowsky-finite-set-is-kuratowsky-finite-set-list X =
  elim-exists
    ( is-kuratowsky-finite-set-Prop X)
    ( Î» l all-list-subtype â†’
      intro-exists (length-list l)
        ( pair
          ( component-list l)
          ( Î» x â†’
            map-trunc-Prop
              ( Î» in-list â†’
                pair
                  ( index-in-list x l in-list)
                  ( inv (eq-component-list-index-in-list x l in-list)))
              ( all-list-subtype x))))

-- TODO: prove another implication
```

### Kuratowky finite subsets

```agda
is-kuratowsky-finite-subset :
  {l1 l2 : Level} (A : Set l1) (B : subtype l2 (type-Set A))
  (l : list (type-Set A)) â†’
  equiv-subtypes B (list-subtype l) â†’
  is-kuratowsky-finite-set (set-subset A B)
is-kuratowsky-finite-subset A B l e =
  is-kuratowsky-finite-set-is-kuratowsky-finite-set-list
    ( set-subset A B)
    ( intro-exists l'
      ( Î» (a , in-B) â†’
        map-trunc-Prop
          ( Î» a-in-l â†’
            tr
              ( Î» i â†’ (a , i) âˆˆ-list l')
              ( eq-is-prop (is-prop-type-Prop (B a)))
              ( in-dependent-map-list _ a-in-l))
          ( map-equiv (e a) in-B)))
  where
  l' : list (type-subtype B)
  l' =
    dependent-map-list l
      ( Î» a in-list â†’
        a , map-section-map-equiv (e a) (in-list-subtype-in-list in-list))
```

### List subtype is Kuratowky finite

```agda
is-kuratowski-finite-list-subtype :
  {l1 : Level} (A : Set l1) (l : list (type-Set A)) â†’
  is-kuratowsky-finite-set (set-subset A (list-subtype l))
is-kuratowski-finite-list-subtype A l =
  is-kuratowsky-finite-subset A (list-subtype l) l
    ( id-equiv-subtypes (list-subtype l))
```

## Properties

### A Kuratowsky finite set is finite if and only if it has decidable equality

```agda
is-finite-has-decidable-equality-type-ğ”½-Kuratowsky :
  {l : Level} (X : ğ”½-Kuratowsky l) â†’
  has-decidable-equality (type-ğ”½-Kuratowsky X) â†’
  is-finite (type-ğ”½-Kuratowsky X)
is-finite-has-decidable-equality-type-ğ”½-Kuratowsky X H =
  apply-universal-property-trunc-Prop
    ( is-kuratowsky-finite-set-ğ”½-Kuratowsky X)
    ( is-finite-Prop (type-ğ”½-Kuratowsky X))
    ( Î» (n , f , s) â†’ is-finite-codomain (is-finite-Fin n) s H)

has-decidable-equality-is-finite-type-ğ”½-Kuratowsky :
  {l : Level} (X : ğ”½-Kuratowsky l) â†’
  is-finite (type-ğ”½-Kuratowsky X) â†’
  has-decidable-equality (type-ğ”½-Kuratowsky X)
has-decidable-equality-is-finite-type-ğ”½-Kuratowsky X H =
  has-decidable-equality-is-finite H
```

### Kuratowsky finite sets are closed under surjections

```agda
is-kuratowsky-finite-set-surjection :
  {l1 l2 : Level} (X : Set l1) (Y : Set l2) â†’
  type-Set X â†  type-Set Y â†’
  is-kuratowsky-finite-set X â†’
  is-kuratowsky-finite-set Y
is-kuratowsky-finite-set-surjection X Y f =
  elim-exists
    ( is-kuratowsky-finite-set-Prop Y)
    ( Î» n g â†’ intro-exists n (surjection-comp f g))
```

### Any finite set is Kuratowsky finite

```agda
is-kuratowsky-finite-set-is-finite :
  {l : Level} (X : Set l) â†’
  is-finite (type-Set X) â†’
  is-kuratowsky-finite-set X
is-kuratowsky-finite-set-is-finite X =
  elim-exists
    ( is-kuratowsky-finite-set-Prop X)
    ( Î» n e â†’ intro-exists n (map-equiv e , is-surjective-map-equiv e))
```

### Classical facts

```agda
is-finite-is-kuratowsky-finite-set :
  {l : Level} (X : Set l) â†’
  LEM l â†’
  is-kuratowsky-finite-set X â†’ is-finite (type-Set X)
is-finite-is-kuratowsky-finite-set X lem is-fin =
  is-finite-has-decidable-equality-type-ğ”½-Kuratowsky
    ( X , is-fin)
    ( Î» x y â†’ lem (Id-Prop X x y))
```
