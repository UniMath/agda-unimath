# Untruncated π-finite types

```agda
module univalent-combinatorics.untruncated-pi-finite-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.0-connected-types
open import foundation.action-on-identifications-functions
open import foundation.contractible-types
open import foundation.coproduct-types
open import foundation.decidable-propositions
open import foundation.decidable-types
open import foundation.dependent-identifications
open import foundation.embeddings
open import foundation.empty-types
open import foundation.equality-coproduct-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.fiber-inclusions
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.functoriality-set-truncation
open import foundation.homotopies
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.maybe
open import foundation.mere-equality
open import foundation.propositional-extensionality
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.retracts-of-types
open import foundation.set-presented-types
open import foundation.set-truncations
open import foundation.sets
open import foundation.subtypes
open import foundation.surjective-maps
open import foundation.transport-along-identifications
open import foundation.type-arithmetic-coproduct-types
open import foundation.unit-type
open import foundation.univalence
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import univalent-combinatorics.coproduct-types
open import univalent-combinatorics.counting
open import univalent-combinatorics.dependent-pair-types
open import univalent-combinatorics.equality-finite-types
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.finitely-many-connected-components
open import univalent-combinatorics.finitely-presented-types
open import univalent-combinatorics.function-types
open import univalent-combinatorics.image-of-maps
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

A type is
{{#concept "untruncated πₙ-finite" Disambiguation="type" Agda=is-untruncated-π-finite Agda=Untruncated-π-Finite-Type}}
if it has
[finitely many connected components](univalent-combinatorics.finitely-many-connected-components.md)
and all of its [homotopy groups](synthetic-homotopy-theory.homotopy-groups.md)
up to level `n` at all base points are
[finite](univalent-combinatorics.finite-types.md). However, formally we define
the condition inductively:

- A type is untruncated π₀-finite if it has finitely many connected components.
- A type is untruncated πₙ₊₁-finite if it is untruncated π₀-finite and all its
  [identity types](foundation-core.identity-types.md) are untruncated πₙ-finite.

## Definitions

### The untruncated πₙ-finiteness predicate

```agda
is-untruncated-π-finite-Prop : {l : Level} (k : ℕ) → UU l → Prop l
is-untruncated-π-finite-Prop zero-ℕ X =
  has-finitely-many-connected-components-Prop X
is-untruncated-π-finite-Prop (succ-ℕ k) X =
  product-Prop
    ( is-untruncated-π-finite-Prop zero-ℕ X)
    ( Π-Prop X (λ x → Π-Prop X (λ y → is-untruncated-π-finite-Prop k (x ＝ y))))

is-untruncated-π-finite : {l : Level} (k : ℕ) → UU l → UU l
is-untruncated-π-finite k X = type-Prop (is-untruncated-π-finite-Prop k X)

is-prop-is-untruncated-π-finite :
  {l : Level} (k : ℕ) (X : UU l) → is-prop (is-untruncated-π-finite k X)
is-prop-is-untruncated-π-finite k X =
  is-prop-type-Prop (is-untruncated-π-finite-Prop k X)

has-finitely-many-connected-components-is-untruncated-π-finite :
  {l : Level} (k : ℕ) {A : UU l} →
  is-untruncated-π-finite k A → has-finitely-many-connected-components A
has-finitely-many-connected-components-is-untruncated-π-finite zero-ℕ H = H
has-finitely-many-connected-components-is-untruncated-π-finite (succ-ℕ k) H =
  pr1 H

is-untruncated-π-finite-Id :
  {l : Level} (k : ℕ) {A : UU l} →
  is-untruncated-π-finite (succ-ℕ k) A →
  (x y : A) → is-untruncated-π-finite k (x ＝ y)
is-untruncated-π-finite-Id k = pr2
```

### The subuniverse untruncated πₙ-finite types

```agda
Untruncated-π-Finite-Type : (l : Level) (k : ℕ) → UU (lsuc l)
Untruncated-π-Finite-Type l k = Σ (UU l) (is-untruncated-π-finite k)

type-Untruncated-π-Finite-Type :
  {l : Level} (k : ℕ) → Untruncated-π-Finite-Type l k → UU l
type-Untruncated-π-Finite-Type k = pr1

is-untruncated-π-finite-type-Untruncated-π-Finite-Type :
  {l : Level} (k : ℕ) (A : Untruncated-π-Finite-Type l k) →
  is-untruncated-π-finite k (type-Untruncated-π-Finite-Type {l} k A)
is-untruncated-π-finite-type-Untruncated-π-Finite-Type k = pr2
```

## Properties

### Untruncated πₙ-finite types are closed under retracts

```agda
is-untruncated-π-finite-retract :
  {l1 l2 : Level} (k : ℕ) {A : UU l1} {B : UU l2} →
  A retract-of B → is-untruncated-π-finite k B → is-untruncated-π-finite k A
is-untruncated-π-finite-retract zero-ℕ =
  has-finitely-many-connected-components-retract
pr1 (is-untruncated-π-finite-retract (succ-ℕ k) r H) =
  is-untruncated-π-finite-retract zero-ℕ r
    ( has-finitely-many-connected-components-is-untruncated-π-finite
      ( succ-ℕ k)
      ( H))
pr2 (is-untruncated-π-finite-retract (succ-ℕ k) r H) x y =
  is-untruncated-π-finite-retract k
    ( retract-eq r x y)
    ( is-untruncated-π-finite-Id k H
      ( inclusion-retract r x)
      ( inclusion-retract r y))
```

### Untruncated πₙ-finite types are closed under equivalences

```agda
is-untruncated-π-finite-equiv :
  {l1 l2 : Level} (k : ℕ) {A : UU l1} {B : UU l2} →
  A ≃ B → is-untruncated-π-finite k B → is-untruncated-π-finite k A
is-untruncated-π-finite-equiv k e =
  is-untruncated-π-finite-retract k (retract-equiv e)

is-untruncated-π-finite-equiv' :
  {l1 l2 : Level} (k : ℕ) {A : UU l1} {B : UU l2} →
  A ≃ B → is-untruncated-π-finite k A → is-untruncated-π-finite k B
is-untruncated-π-finite-equiv' k e =
  is-untruncated-π-finite-retract k (retract-inv-equiv e)
```

### Empty types are untruncated πₙ-finite

```agda
is-untruncated-π-finite-empty : (k : ℕ) → is-untruncated-π-finite k empty
is-untruncated-π-finite-empty zero-ℕ =
  has-finitely-many-connected-components-empty
is-untruncated-π-finite-empty (succ-ℕ k) =
  ( is-untruncated-π-finite-empty zero-ℕ , ind-empty)

empty-Untruncated-π-Finite-Type : (k : ℕ) → Untruncated-π-Finite-Type lzero k
empty-Untruncated-π-Finite-Type k = (empty , is-untruncated-π-finite-empty k)

is-untruncated-π-finite-is-empty :
  {l : Level} (k : ℕ) {A : UU l} → is-empty A → is-untruncated-π-finite k A
is-untruncated-π-finite-is-empty zero-ℕ =
  has-finitely-many-connected-components-is-empty
is-untruncated-π-finite-is-empty (succ-ℕ k) f =
  ( is-untruncated-π-finite-is-empty zero-ℕ f , ex-falso ∘ f)
```

### Contractible types are untruncated πₙ-finite

```agda
is-untruncated-π-finite-is-contr :
  {l : Level} (k : ℕ) {A : UU l} → is-contr A → is-untruncated-π-finite k A
is-untruncated-π-finite-is-contr zero-ℕ =
  has-finitely-many-connected-components-is-contr
pr1 (is-untruncated-π-finite-is-contr (succ-ℕ k) H) =
  is-untruncated-π-finite-is-contr zero-ℕ H
pr2 (is-untruncated-π-finite-is-contr (succ-ℕ k) H) x y =
  is-untruncated-π-finite-is-contr k (is-prop-is-contr H x y)

is-untruncated-π-finite-unit : (k : ℕ) → is-untruncated-π-finite k unit
is-untruncated-π-finite-unit k =
  is-untruncated-π-finite-is-contr k is-contr-unit

unit-Untruncated-π-Finite-Type : (k : ℕ) → Untruncated-π-Finite-Type lzero k
unit-Untruncated-π-Finite-Type k =
  ( unit , is-untruncated-π-finite-unit k)
```

### Coproducts of untruncated πₙ-finite types are untruncated πₙ-finite

```agda
is-untruncated-π-finite-coproduct :
  {l1 l2 : Level} (k : ℕ) {A : UU l1} {B : UU l2} →
  is-untruncated-π-finite k A → is-untruncated-π-finite k B →
  is-untruncated-π-finite k (A + B)
is-untruncated-π-finite-coproduct zero-ℕ =
  has-finitely-many-connected-components-coproduct
pr1 (is-untruncated-π-finite-coproduct (succ-ℕ k) H K) =
  is-untruncated-π-finite-coproduct zero-ℕ (pr1 H) (pr1 K)
pr2 (is-untruncated-π-finite-coproduct (succ-ℕ k) H K) (inl x) (inl y) =
  is-untruncated-π-finite-equiv k
    ( compute-eq-coproduct-inl-inl x y)
    ( is-untruncated-π-finite-Id k H x y)
pr2 (is-untruncated-π-finite-coproduct (succ-ℕ k) H K) (inl x) (inr y) =
  is-untruncated-π-finite-equiv k
    ( compute-eq-coproduct-inl-inr x y)
    ( is-untruncated-π-finite-empty k)
pr2 (is-untruncated-π-finite-coproduct (succ-ℕ k) H K) (inr x) (inl y) =
  is-untruncated-π-finite-equiv k
    ( compute-eq-coproduct-inr-inl x y)
    ( is-untruncated-π-finite-empty k)
pr2 (is-untruncated-π-finite-coproduct (succ-ℕ k) H K) (inr x) (inr y) =
  is-untruncated-π-finite-equiv k
    ( compute-eq-coproduct-inr-inr x y)
    ( is-untruncated-π-finite-Id k K x y)

coproduct-Untruncated-π-Finite-Type :
  {l1 l2 : Level} (k : ℕ) →
  Untruncated-π-Finite-Type l1 k →
  Untruncated-π-Finite-Type l2 k →
  Untruncated-π-Finite-Type (l1 ⊔ l2) k
pr1 (coproduct-Untruncated-π-Finite-Type k A B) =
  (type-Untruncated-π-Finite-Type k A + type-Untruncated-π-Finite-Type k B)
pr2 (coproduct-Untruncated-π-Finite-Type k A B) =
  is-untruncated-π-finite-coproduct k
    ( is-untruncated-π-finite-type-Untruncated-π-Finite-Type k A)
    ( is-untruncated-π-finite-type-Untruncated-π-Finite-Type k B)
```

### `Maybe A` of any untruncated πₙ-finite type `A` is untruncated πₙ-finite

```agda
is-untruncated-π-finite-Maybe :
  {l : Level} (k : ℕ) {A : UU l} →
  is-untruncated-π-finite k A → is-untruncated-π-finite k (Maybe A)
is-untruncated-π-finite-Maybe k H =
  is-untruncated-π-finite-coproduct k H (is-untruncated-π-finite-unit k)

Maybe-Untruncated-π-Finite-Type :
  {l : Level} (k : ℕ) →
  Untruncated-π-Finite-Type l k →
  Untruncated-π-Finite-Type l k
Maybe-Untruncated-π-Finite-Type k A =
  coproduct-Untruncated-π-Finite-Type k A (unit-Untruncated-π-Finite-Type k)
```

### Any standard finite type is untruncated πₙ-finite

```agda
is-untruncated-π-finite-Fin :
  (k n : ℕ) → is-untruncated-π-finite k (Fin n)
is-untruncated-π-finite-Fin k zero-ℕ =
  is-untruncated-π-finite-empty k
is-untruncated-π-finite-Fin k (succ-ℕ n) =
  is-untruncated-π-finite-Maybe k (is-untruncated-π-finite-Fin k n)

Fin-Untruncated-π-Finite-Type :
  (k : ℕ) (n : ℕ) → Untruncated-π-Finite-Type lzero k
Fin-Untruncated-π-Finite-Type k n = (Fin n , is-untruncated-π-finite-Fin k n)
```

### Types equipped with a counting are untruncated πₙ-finite

```agda
is-untruncated-π-finite-count :
  {l : Level} (k : ℕ) {A : UU l} → count A → is-untruncated-π-finite k A
is-untruncated-π-finite-count k (n , e) =
  is-untruncated-π-finite-equiv' k e (is-untruncated-π-finite-Fin k n)
```

### Finite types are untruncated πₙ-finite

```agda
is-untruncated-π-finite-is-finite :
  {l : Level} (k : ℕ) {A : UU l} → is-finite A → is-untruncated-π-finite k A
is-untruncated-π-finite-is-finite k {A} H =
  apply-universal-property-trunc-Prop H
    ( is-untruncated-π-finite-Prop k A)
    ( is-untruncated-π-finite-count k)

untruncated-π-finite-type-Finite-Type :
  {l : Level} (k : ℕ) → Finite-Type l → Untruncated-π-Finite-Type l k
pr1 (untruncated-π-finite-type-Finite-Type k A) = type-Finite-Type A
pr2 (untruncated-π-finite-type-Finite-Type k A) =
  is-untruncated-π-finite-is-finite k (is-finite-type-Finite-Type A)
```

### The type of all `n`-element types in `UU l` is untruncated πₙ-finite

```agda
is-untruncated-π-finite-Type-With-Cardinality-ℕ :
  {l : Level} (k n : ℕ) →
  is-untruncated-π-finite k (Type-With-Cardinality-ℕ l n)
is-untruncated-π-finite-Type-With-Cardinality-ℕ zero-ℕ n =
  has-finitely-many-connected-components-Type-With-Cardinality-ℕ n
pr1 (is-untruncated-π-finite-Type-With-Cardinality-ℕ (succ-ℕ k) n) =
  is-untruncated-π-finite-Type-With-Cardinality-ℕ zero-ℕ n
pr2 (is-untruncated-π-finite-Type-With-Cardinality-ℕ (succ-ℕ k) n) x y =
  is-untruncated-π-finite-equiv k
    ( equiv-equiv-eq-Type-With-Cardinality-ℕ n x y)
    ( is-untruncated-π-finite-is-finite k
      ( is-finite-type-equiv
        ( is-finite-has-finite-cardinality (n , pr2 x))
        ( is-finite-has-finite-cardinality (n , pr2 y))))

Type-With-Cardinality-ℕ-Untruncated-π-Finite-Type :
  (l : Level) (k n : ℕ) → Untruncated-π-Finite-Type (lsuc l) k
Type-With-Cardinality-ℕ-Untruncated-π-Finite-Type l k n =
  ( Type-With-Cardinality-ℕ l n ,
    is-untruncated-π-finite-Type-With-Cardinality-ℕ k n)
```

### Untruncated πₙ₊₁-finite types are untruncated πₙ-finite

```agda
is-untruncated-π-finite-is-untruncated-π-finite-succ-ℕ :
  {l : Level} (k : ℕ) {A : UU l} →
  is-untruncated-π-finite (succ-ℕ k) A → is-untruncated-π-finite k A
is-untruncated-π-finite-is-untruncated-π-finite-succ-ℕ zero-ℕ H =
  has-finitely-many-connected-components-is-untruncated-π-finite 1 H
pr1 (is-untruncated-π-finite-is-untruncated-π-finite-succ-ℕ (succ-ℕ k) H) =
  has-finitely-many-connected-components-is-untruncated-π-finite
    ( succ-ℕ (succ-ℕ k))
    ( H)
pr2 (is-untruncated-π-finite-is-untruncated-π-finite-succ-ℕ (succ-ℕ k) H) x y =
  is-untruncated-π-finite-is-untruncated-π-finite-succ-ℕ k
    ( is-untruncated-π-finite-Id (succ-ℕ k) H x y)

untruncated-π-finite-type-succ-Untruncated-π-Finite-Type :
  {l : Level} (k : ℕ) →
  Untruncated-π-Finite-Type l (succ-ℕ k) → Untruncated-π-Finite-Type l k
untruncated-π-finite-type-succ-Untruncated-π-Finite-Type k =
  tot (λ A → is-untruncated-π-finite-is-untruncated-π-finite-succ-ℕ k)
```

### Untruncated πₙ₊₁-finite types are untruncated π₁-finite

```agda
is-untruncated-π-finite-one-is-untruncated-π-finite-succ-ℕ :
  {l : Level} (k : ℕ) {A : UU l} →
  is-untruncated-π-finite (succ-ℕ k) A → is-untruncated-π-finite 1 A
is-untruncated-π-finite-one-is-untruncated-π-finite-succ-ℕ zero-ℕ H = H
is-untruncated-π-finite-one-is-untruncated-π-finite-succ-ℕ (succ-ℕ k) H =
  is-untruncated-π-finite-one-is-untruncated-π-finite-succ-ℕ k
    ( is-untruncated-π-finite-is-untruncated-π-finite-succ-ℕ (succ-ℕ k) H)
```

### Untruncated πₙ-finite sets are finite

```agda
is-finite-is-untruncated-π-finite :
  {l : Level} (k : ℕ) {A : UU l} → is-set A →
  is-untruncated-π-finite k A → is-finite A
is-finite-is-untruncated-π-finite k H K =
  is-finite-equiv'
    ( equiv-unit-trunc-Set (_ , H))
    ( has-finitely-many-connected-components-is-untruncated-π-finite k K)
```

### Finite products of untruncated πₙ-finite types are untruncated πₙ-finite

```agda
is-untruncated-π-finite-Π :
  {l1 l2 : Level} (k : ℕ) {A : UU l1} {B : A → UU l2} →
  is-finite A → ((a : A) → is-untruncated-π-finite k (B a)) →
  is-untruncated-π-finite k ((a : A) → B a)
is-untruncated-π-finite-Π zero-ℕ =
  has-finitely-many-connected-components-finite-Π
pr1 (is-untruncated-π-finite-Π (succ-ℕ k) H K) =
  is-untruncated-π-finite-Π zero-ℕ H (λ a → pr1 (K a))
pr2 (is-untruncated-π-finite-Π (succ-ℕ k) H K) f g =
  is-untruncated-π-finite-equiv k
    ( equiv-funext)
    ( is-untruncated-π-finite-Π k H
      ( λ a → is-untruncated-π-finite-Id k (K a) (f a) (g a)))

finite-Π-Untruncated-π-Finite-Type :
  {l1 l2 : Level} (k : ℕ) (A : Finite-Type l1)
  (B : type-Finite-Type A → Untruncated-π-Finite-Type l2 k) →
  Untruncated-π-Finite-Type (l1 ⊔ l2) k
pr1 (finite-Π-Untruncated-π-Finite-Type k A B) =
  (x : type-Finite-Type A) → type-Untruncated-π-Finite-Type k (B x)
pr2 (finite-Π-Untruncated-π-Finite-Type k A B) =
  is-untruncated-π-finite-Π k
    ( is-finite-type-Finite-Type A)
    ( λ x → is-untruncated-π-finite-type-Untruncated-π-Finite-Type k (B x))
```

### Dependent sums of types with finitely many connected components over a `0`-connected base

The total space of a family of types with finitely many connected components
over a `0`-connected base has finitely many connected components when the based
[loop spaces](synthetic-homotopy-theory.loop-spaces.md) of the base have
finitely many connected components.

```agda
has-finitely-many-connected-components-Σ-is-0-connected :
  {l1 l2 : Level} {A : UU l1} {B : A → UU l2} →
  is-0-connected A →
  ((a : A) → has-finitely-many-connected-components (a ＝ a)) →
  ((x : A) → has-finitely-many-connected-components (B x)) →
  has-finitely-many-connected-components (Σ A B)
has-finitely-many-connected-components-Σ-is-0-connected {A = A} {B} C H K =
  apply-universal-property-trunc-Prop
    ( is-inhabited-is-0-connected C)
    ( has-finitely-many-connected-components-Prop (Σ A B))
    ( α)
  where
  α : A → has-finitely-many-connected-components (Σ A B)
  α a =
    is-finite-codomain
      ( K a)
      ( is-surjective-map-trunc-Set
        ( fiber-inclusion B a)
        ( is-surjective-fiber-inclusion C a))
      ( apply-dependent-universal-property-trunc-Set'
        ( λ x →
          set-Prop
            ( Π-Prop
              ( type-trunc-Set (Σ A B))
              ( λ y → is-decidable-Prop (Id-Prop (trunc-Set (Σ A B)) x y))))
        ( β))
    where
    β :
      (x : Σ A B) (v : type-trunc-Set (Σ A B)) →
      is-decidable (unit-trunc-Set x ＝ v)
    β (x , y) =
      apply-dependent-universal-property-trunc-Set'
        ( λ u →
          set-Prop
            ( is-decidable-Prop
              ( Id-Prop (trunc-Set (Σ A B)) (unit-trunc-Set (x , y)) u)))
        ( γ)
      where
      γ :
        (v : Σ A B) →
        is-decidable ((unit-trunc-Set (x , y)) ＝ (unit-trunc-Set v))
      γ (x' , y') =
        is-decidable-equiv
          ( is-effective-unit-trunc-Set
            ( Σ A B)
            ( x , y)
            ( x' , y'))
          ( apply-universal-property-trunc-Prop
            ( mere-eq-is-0-connected C a x)
            ( is-decidable-Prop
              ( mere-eq-Prop (x , y) (x' , y')))
              ( δ))
        where
        δ : a ＝ x → is-decidable (mere-eq (x , y) (x' , y'))
        δ refl =
          apply-universal-property-trunc-Prop
            ( mere-eq-is-0-connected C a x')
            ( is-decidable-Prop
              ( mere-eq-Prop (a , y) (x' , y')))
            ( ε)
          where
          ε : a ＝ x' → is-decidable (mere-eq (x , y) (x' , y'))
          ε refl =
            is-decidable-equiv e
              ( is-decidable-type-trunc-Prop-is-finite
                ( is-finite-Σ
                  ( H a)
                  ( λ ω → is-finite-is-decidable-Prop (P ω) (d ω))))
            where
            ℙ :
              is-contr
                ( Σ ( hom-Set (trunc-Set (a ＝ a)) (Prop-Set _))
                    ( λ h →
                      ( λ a₁ → h (unit-trunc-Set a₁)) ~
                      ( λ ω₁ →
                        trunc-Prop (dependent-identification B ω₁ y y'))))
            ℙ =
              universal-property-trunc-Set
                ( a ＝ a)
                ( Prop-Set _)
                ( λ ω → trunc-Prop (dependent-identification B ω y y'))

            P : type-trunc-Set (a ＝ a) → Prop _
            P = pr1 (center ℙ)

            compute-P :
              (ω : a ＝ a) →
              type-Prop (P (unit-trunc-Set ω)) ≃
              type-trunc-Prop (dependent-identification B ω y y')
            compute-P ω = equiv-eq (ap pr1 (pr2 (center ℙ) ω))

            d : (t : type-trunc-Set (a ＝ a)) → is-decidable (type-Prop (P t))
            d =
              apply-dependent-universal-property-trunc-Set'
                ( λ t → set-Prop (is-decidable-Prop (P t)))
                ( λ ω →
                  is-decidable-equiv'
                    ( inv-equiv (compute-P ω))
                    ( is-decidable-equiv'
                      ( is-effective-unit-trunc-Set (B a) (tr B ω y) y')
                      ( has-decidable-equality-is-finite
                        ( K a)
                        ( unit-trunc-Set (tr B ω y))
                        ( unit-trunc-Set y'))))

            f : type-hom-Prop
                ( trunc-Prop (Σ (type-trunc-Set (a ＝ a)) (type-Prop ∘ P)))
                ( mere-eq-Prop {A = Σ A B} (a , y) (a , y'))
            f t =
              apply-universal-property-trunc-Prop t
                ( mere-eq-Prop (a , y) (a , y'))
                  λ (u , v) →
                  apply-dependent-universal-property-trunc-Set'
                    ( λ u' →
                      hom-set-Set
                        ( set-Prop (P u'))
                        ( set-Prop
                          ( mere-eq-Prop (a , y) (a , y'))))
                    ( λ ω v' →
                      apply-universal-property-trunc-Prop
                        ( map-equiv (compute-P ω) v')
                        ( mere-eq-Prop (a , y) (a , y'))
                        ( λ p → unit-trunc-Prop (eq-pair-Σ ω p)))
                    ( u)
                    ( v)
            e :
              mere-eq {A = Σ A B} (a , y) (a , y') ≃
              type-trunc-Prop (Σ (type-trunc-Set (a ＝ a)) (type-Prop ∘ P))
            e =
              equiv-iff
                ( mere-eq-Prop (a , y) (a , y'))
                ( trunc-Prop (Σ (type-trunc-Set (a ＝ a)) (type-Prop ∘ P)))
                ( λ t →
                  apply-universal-property-trunc-Prop t
                    ( trunc-Prop _)
                    ( ( λ (ω , r) →
                        unit-trunc-Prop
                          { A = Σ (type-trunc-Set (a ＝ a)) (type-Prop ∘ P)}
                          ( ( unit-trunc-Set ω) ,
                            ( map-inv-equiv
                              ( compute-P ω)
                              ( unit-trunc-Prop r)))) ∘
                      ( pair-eq-Σ)))
                ( f)
```

### Dependent sums of types with finitely many connected components

```agda
abstract
  has-finitely-many-connected-components-Σ' :
    {l1 l2 : Level} (k : ℕ) {A : UU l1} {B : A → UU l2} →
    (Fin k ≃ type-trunc-Set A) →
    ((x y : A) → has-finitely-many-connected-components (x ＝ y)) →
    ((x : A) → has-finitely-many-connected-components (B x)) →
    has-finitely-many-connected-components (Σ A B)
  has-finitely-many-connected-components-Σ' zero-ℕ e H K =
    has-finitely-many-connected-components-is-empty
      ( is-empty-is-empty-trunc-Set (map-inv-equiv e) ∘ pr1)
  has-finitely-many-connected-components-Σ' (succ-ℕ k) {A} {B} e H K =
    apply-universal-property-trunc-Prop
      ( has-presentation-of-cardinality-has-cardinality-connected-components
        ( succ-ℕ k)
        ( unit-trunc-Prop e))
      ( has-finitely-many-connected-components-Prop (Σ A B))
      ( α)
    where
    α : Σ (Fin (succ-ℕ k) → A) (λ f → is-equiv (unit-trunc-Set ∘ f)) →
        has-finitely-many-connected-components (Σ A B)
    α (f , Eηf) =
      is-finite-equiv
        ( equiv-trunc-Set g)
        ( is-finite-equiv'
          ( equiv-distributive-trunc-coproduct-Set
            ( Σ (im (f ∘ inl)) (B ∘ pr1))
            ( Σ (im (f ∘ inr)) (B ∘ pr1)))
          ( is-finite-coproduct
            ( has-finitely-many-connected-components-Σ' k
              ( h)
              ( λ x y →
                is-finite-equiv'
                  ( equiv-trunc-Set
                    ( equiv-ap-emb
                      ( pr1 , is-emb-inclusion-subtype ( λ u → trunc-Prop _))))
                  ( H (pr1 x) (pr1 y)))
              ( λ x → K (pr1 x)))
            ( has-finitely-many-connected-components-Σ-is-0-connected
              ( is-0-connected-im-is-0-connected-domain
                ( f ∘ inr)
                ( is-0-connected-unit))
              ( λ a →
                has-finitely-many-connected-components-equiv'
                  ( equiv-Eq-eq-im (f ∘ inr) a a)
                  ( H (pr1 a) (pr1 a)))
              ( λ x → K (pr1 x)))))
      where
      g : ((Σ (im (f ∘ inl)) (B ∘ pr1)) + (Σ (im (f ∘ inr)) (B ∘ pr1))) ≃
          Σ A B
      g =
        ( equiv-Σ B
          ( is-coproduct-codomain f
            ( unit-trunc-Set ∘ f , Eηf)
            ( refl-htpy))
          ( λ { (inl x) → id-equiv ; (inr x) → id-equiv})) ∘e
        ( inv-right-distributive-Σ-coproduct
          ( rec-coproduct (B ∘ pr1) (B ∘ pr1)))

      i : Fin k → type-trunc-Set (im (f ∘ inl))
      i = unit-trunc-Set ∘ map-unit-im (f ∘ inl)

      is-surjective-i : is-surjective i
      is-surjective-i =
        is-surjective-comp
          ( is-surjective-unit-trunc-Set (im (f ∘ inl)))
          ( is-surjective-map-unit-im (f ∘ inl))

      is-emb-i : is-emb i
      is-emb-i =
        is-emb-top-map-triangle
          ( (unit-trunc-Set ∘ f) ∘ inl)
          ( inclusion-trunc-im-Set (f ∘ inl))
          ( i)
          ( ( inv-htpy (naturality-unit-trunc-Set (inclusion-im (f ∘ inl)))) ·r
            ( map-unit-im (f ∘ inl)))
          ( is-emb-inclusion-trunc-im-Set (f ∘ inl))
          ( is-emb-comp
            ( unit-trunc-Set ∘ f)
            ( inl)
            ( is-emb-is-equiv Eηf)
            ( is-emb-inl))
      h : Fin k ≃ type-trunc-Set (im (f ∘ inl))
      h = i , (is-equiv-is-emb-is-surjective is-surjective-i is-emb-i)
```

### Dependent sums of untruncated πₙ-finite types

The dependent sum of a family of untruncated πₙ-finite types over a untruncated
πₙ₊₁-finite base is untruncated πₙ-finite.

```agda
has-finitely-many-connected-components-Σ :
  {l1 l2 : Level} {A : UU l1} {B : A → UU l2} →
  is-untruncated-π-finite 1 A →
  ((x : A) → has-finitely-many-connected-components (B x)) →
  has-finitely-many-connected-components (Σ A B)
has-finitely-many-connected-components-Σ {A = A} {B} H K =
  apply-universal-property-trunc-Prop
    ( pr1 H)
    ( has-finitely-many-connected-components-Prop (Σ A B))
    ( λ (k , e) →
      has-finitely-many-connected-components-Σ' k e
        ( λ x y → is-untruncated-π-finite-Id 0 H x y)
        ( K))

abstract
  is-untruncated-π-finite-Σ :
    {l1 l2 : Level} (k : ℕ) {A : UU l1} {B : A → UU l2} →
    is-untruncated-π-finite (succ-ℕ k) A →
    ((x : A) → is-untruncated-π-finite k (B x)) →
    is-untruncated-π-finite k (Σ A B)
  is-untruncated-π-finite-Σ zero-ℕ =
    has-finitely-many-connected-components-Σ
  pr1 (is-untruncated-π-finite-Σ (succ-ℕ k) H K) =
    has-finitely-many-connected-components-Σ
      ( is-untruncated-π-finite-one-is-untruncated-π-finite-succ-ℕ (succ-ℕ k) H)
      ( λ x →
        has-finitely-many-connected-components-is-untruncated-π-finite
          ( succ-ℕ k)
          ( K x))
  pr2 (is-untruncated-π-finite-Σ (succ-ℕ k) H K) (x , u) (y , v) =
    is-untruncated-π-finite-equiv k
      ( equiv-pair-eq-Σ (x , u) (y , v))
      ( is-untruncated-π-finite-Σ k
        ( is-untruncated-π-finite-Id (succ-ℕ k) H x y)
        ( λ where refl → is-untruncated-π-finite-Id k (K x) u v))

Σ-Untruncated-π-Finite-Type :
  {l1 l2 : Level} (k : ℕ) (A : Untruncated-π-Finite-Type l1 (succ-ℕ k))
  (B :
    (x : type-Untruncated-π-Finite-Type (succ-ℕ k) A) →
    Untruncated-π-Finite-Type l2 k) →
  Untruncated-π-Finite-Type (l1 ⊔ l2) k
pr1 (Σ-Untruncated-π-Finite-Type k A B) =
  Σ ( type-Untruncated-π-Finite-Type (succ-ℕ k) A)
    ( λ x → type-Untruncated-π-Finite-Type k (B x))
pr2 (Σ-Untruncated-π-Finite-Type k A B) =
  is-untruncated-π-finite-Σ k
    ( is-untruncated-π-finite-type-Untruncated-π-Finite-Type (succ-ℕ k) A)
    ( λ x → is-untruncated-π-finite-type-Untruncated-π-Finite-Type k (B x))
```

## See also

- [π-finite types](univalent-combinatorics.pi-finite-types.md)
- [Unbounded π-finite types](univalent-combinatorics.unbounded-pi-finite-types.md)
