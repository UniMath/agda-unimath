# Finite function types

```agda
module univalent-combinatorics.function-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.equality-natural-numbers
open import elementary-number-theory.exponentiation-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.action-on-identifications-binary-functions
open import foundation.equivalences
open import foundation.identity-types
open import foundation.propositional-truncations
open import foundation.sets
open import foundation.universe-levels

open import univalent-combinatorics.cartesian-product-types
open import univalent-combinatorics.counting
open import univalent-combinatorics.dependent-function-types
open import univalent-combinatorics.dependent-pair-types
open import univalent-combinatorics.equality-finite-types
open import univalent-combinatorics.finite-types
```

</details>

## Properties

### The type of functions between types equipped with a counting can be equipped with a counting

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  (c : count A) (d : count B)
  where

  count-function-type : count (A â†’ B)
  count-function-type = count-Î  c (Î» _ â†’ d)

  number-of-elements-count-function-type :
    number-of-elements-count count-function-type ï¼
    exp-â„• (number-of-elements-count d) (number-of-elements-count c)
  number-of-elements-count-function-type =
    number-of-elements-count-Î  c (Î» _ â†’ d) âˆ™
    compute-constant-product-â„•
      ( number-of-elements-count d)
      ( number-of-elements-count c)
```

### The type of functions between finite types is finite

```agda
abstract
  is-finite-function-type :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-finite A â†’ is-finite B â†’ is-finite (A â†’ B)
  is-finite-function-type f g = is-finite-Î  f (Î» x â†’ g)

  number-of-elements-function-type :
    {l1 l2 : Level} {A : UU l1} {B : UU l2}
    (H : is-finite A) (K : is-finite B) â†’
    number-of-elements-is-finite (is-finite-function-type H K) ï¼
    exp-â„• (number-of-elements-is-finite K) (number-of-elements-is-finite H)
  number-of-elements-function-type H K =
    apply-twice-universal-property-trunc-Prop H K
      ( Id-Prop â„•-Set _ _)
      ( Î» c d â†’
        inv
          ( compute-number-of-elements-is-finite
            ( count-function-type c d)
            ( is-finite-function-type H K)) âˆ™
        number-of-elements-count-function-type c d âˆ™
        ap-binary
          ( exp-â„•)
          ( compute-number-of-elements-is-finite d K)
          ( compute-number-of-elements-is-finite c H))

_â†’ğ”½_ : {l1 l2 : Level} â†’ Finite-Type l1 â†’ Finite-Type l2 â†’ Finite-Type (l1 âŠ” l2)
pr1 (A â†’ğ”½ B) = type-Finite-Type A â†’ type-Finite-Type B
pr2 (A â†’ğ”½ B) =
  is-finite-function-type
    ( is-finite-type-Finite-Type A)
    ( is-finite-type-Finite-Type B)
```

### The type of equivalences between finite types is finite

```agda
abstract
  is-finite-type-equiv :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
    is-finite A â†’ is-finite B â†’ is-finite (A â‰ƒ B)
  is-finite-type-equiv f g =
    is-finite-Î£
      ( is-finite-function-type f g)
      ( Î» h â†’
        is-finite-product
          ( is-finite-Î£
            ( is-finite-function-type g f)
            ( Î» k â†’
              is-finite-Î  g
                ( Î» y â†’ is-finite-eq (has-decidable-equality-is-finite g))))
          ( is-finite-Î£
            ( is-finite-function-type g f)
            ( Î» k â†’
              is-finite-Î  f
                ( Î» x â†’ is-finite-eq (has-decidable-equality-is-finite f)))))

infix 6 _â‰ƒğ”½_
_â‰ƒğ”½_ : {l1 l2 : Level} â†’ Finite-Type l1 â†’ Finite-Type l2 â†’ Finite-Type (l1 âŠ” l2)
pr1 (A â‰ƒğ”½ B) = type-Finite-Type A â‰ƒ type-Finite-Type B
pr2 (A â‰ƒğ”½ B) =
  is-finite-type-equiv
    ( is-finite-type-Finite-Type A)
    ( is-finite-type-Finite-Type B)
```

### The type of automorphisms on a finite type is finite

```agda
Aut-Finite-Type : {l : Level} â†’ Finite-Type l â†’ Finite-Type l
Aut-Finite-Type A = A â‰ƒğ”½ A
```
