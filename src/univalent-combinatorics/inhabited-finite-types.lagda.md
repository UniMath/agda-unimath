# Inhabited finite types

```agda
module univalent-combinatorics.inhabited-finite-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-dependent-function-types
open import foundation.identity-types
open import foundation.inhabited-types
open import foundation.propositions
open import foundation.subtypes
open import foundation.subuniverses
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.type-theoretic-principle-of-choice
open import foundation.universe-levels

open import univalent-combinatorics.dependent-pair-types
open import univalent-combinatorics.finite-types
```

</details>

## Idea

An **inhabited finite type** is a
[finite type](univalent-combinatorics.finite-types.md) that is
[inhabited](foundation.inhabited-types.md), meaning it is a type that is
[merely equivalent](foundation.mere-equivalences.md) to a
[standard finite type](univalent-combinatorics.standard-finite-types.md), and
that comes equipped with a term of its
[propositional truncation](foundation.propositional-truncations.md).

## Definitions

### Inhabited finite types

```agda
Inhabited-Finite-Type : (l : Level) â†’ UU (lsuc l)
Inhabited-Finite-Type l = Î£ (ğ”½ l) (Î» X â†’ is-inhabited (type-ğ”½ X))

module _
  {l : Level} (X : Inhabited-Finite-Type l)
  where

  finite-type-Inhabited-Finite-Type : ğ”½ l
  finite-type-Inhabited-Finite-Type = pr1 X

  type-Inhabited-Finite-Type : UU l
  type-Inhabited-Finite-Type = type-ğ”½ finite-type-Inhabited-Finite-Type

  is-finite-Inhabited-Finite-Type : is-finite type-Inhabited-Finite-Type
  is-finite-Inhabited-Finite-Type = is-finite-type-ğ”½ finite-type-Inhabited-Finite-Type

  is-inhabited-type-Inhabited-Finite-Type : is-inhabited type-Inhabited-Finite-Type
  is-inhabited-type-Inhabited-Finite-Type = pr2 X

  inhabited-type-Inhabited-Finite-Type : Inhabited-Type l
  pr1 inhabited-type-Inhabited-Finite-Type = type-Inhabited-Finite-Type
  pr2 inhabited-type-Inhabited-Finite-Type = is-inhabited-type-Inhabited-Finite-Type

compute-Inhabited-Finite-Type :
  {l : Level} â†’
  Inhabited-Finite-Type l â‰ƒ
  Î£ (Inhabited-Type l) (Î» X â†’ is-finite (type-Inhabited-Type X))
compute-Inhabited-Finite-Type = equiv-right-swap-Î£

is-finite-and-inhabited-Prop : {l : Level} â†’ UU l â†’ Prop l
is-finite-and-inhabited-Prop X =
  product-Prop (is-finite-Prop X) (is-inhabited-Prop X)

is-finite-and-inhabited : {l : Level} â†’ UU l â†’ UU l
is-finite-and-inhabited X =
  type-Prop (is-finite-and-inhabited-Prop X)

compute-Inhabited-Finite-Type' :
  {l : Level} â†’
  Inhabited-Finite-Type l â‰ƒ type-subuniverse is-finite-and-inhabited-Prop
compute-Inhabited-Finite-Type' = associative-Î£ _ _ _

map-compute-Inhabited-Finite-Type' :
  {l : Level} â†’
  Inhabited-Finite-Type l â†’ type-subuniverse is-finite-and-inhabited-Prop
map-compute-Inhabited-Finite-Type' = map-associative-Î£ _ _ _

map-inv-compute-Inhabited-Finite-Type' :
  {l : Level} â†’
  type-subuniverse is-finite-and-inhabited-Prop â†’ Inhabited-Finite-Type l
map-inv-compute-Inhabited-Finite-Type' = map-inv-associative-Î£ _ _ _
```

### Families of inhabited types

```agda
Fam-Inhabited-Types-ğ”½ :
  {l1 : Level} â†’ (l2 : Level) â†’ (X : ğ”½ l1) â†’ UU (l1 âŠ” lsuc l2)
Fam-Inhabited-Types-ğ”½ l2 X = type-ğ”½ X â†’ Inhabited-Finite-Type l2

module _
  {l1 l2 : Level} (X : ğ”½ l1) (Y : Fam-Inhabited-Types-ğ”½ l2 X)
  where

  type-Fam-Inhabited-Types-ğ”½ : type-ğ”½ X â†’ UU l2
  type-Fam-Inhabited-Types-ğ”½ x = type-Inhabited-Finite-Type (Y x)

  finite-type-Fam-Inhabited-Types-ğ”½ : type-ğ”½ X â†’ ğ”½ l2
  pr1 (finite-type-Fam-Inhabited-Types-ğ”½ x) = type-Fam-Inhabited-Types-ğ”½ x
  pr2 (finite-type-Fam-Inhabited-Types-ğ”½ x) = is-finite-Inhabited-Finite-Type (Y x)

  is-inhabited-type-Fam-Inhabited-Types-ğ”½ :
    (x : type-ğ”½ X) â†’ is-inhabited (type-Fam-Inhabited-Types-ğ”½ x)
  is-inhabited-type-Fam-Inhabited-Types-ğ”½ x =
    is-inhabited-type-Inhabited-Finite-Type (Y x)

  total-Fam-Inhabited-Types-ğ”½ : ğ”½ (l1 âŠ” l2)
  total-Fam-Inhabited-Types-ğ”½ = Î£-ğ”½ X finite-type-Fam-Inhabited-Types-ğ”½

compute-Fam-Inhabited-Finite-Type :
  {l1 l2 : Level} â†’ (X : ğ”½ l1) â†’
  Fam-Inhabited-Types-ğ”½ l2 X â‰ƒ
  Î£ ( Fam-Inhabited-Types l2 (type-ğ”½ X))
    ( Î» Y â†’ (x : type-ğ”½ X) â†’ is-finite (type-Inhabited-Type (Y x)))
compute-Fam-Inhabited-Finite-Type X =
  ( distributive-Î -Î£) âˆ˜e
  ( equiv-Î 
    ( Î» _ â†’ Î£ (Inhabited-Type _) (is-finite âˆ˜ type-Inhabited-Type))
    ( id-equiv)
    ( Î» _ â†’ compute-Inhabited-Finite-Type))
```

## Proposition

### Equality in inhabited finite types

```agda
eq-equiv-Inhabited-Finite-Type :
  {l : Level} â†’ (X Y : Inhabited-Finite-Type l) â†’
  type-Inhabited-Finite-Type X â‰ƒ type-Inhabited-Finite-Type Y â†’ X ï¼ Y
eq-equiv-Inhabited-Finite-Type X Y e =
  eq-type-subtype
    ( Î» X â†’ is-inhabited-Prop (type-ğ”½ X))
    ( eq-equiv-ğ”½
      ( finite-type-Inhabited-Finite-Type X)
      ( finite-type-Inhabited-Finite-Type Y)
      ( e))
```

### Every type in `UU-Fin (succ-â„• n)` is an inhabited finite type

```agda
is-finite-and-inhabited-type-UU-Fin-succ-â„• :
  {l : Level} â†’ (n : â„•) â†’ (F : UU-Fin l (succ-â„• n)) â†’
  is-finite-and-inhabited (type-UU-Fin (succ-â„• n) F)
pr1 (is-finite-and-inhabited-type-UU-Fin-succ-â„• n F) =
  is-finite-type-UU-Fin (succ-â„• n) F
pr2 (is-finite-and-inhabited-type-UU-Fin-succ-â„• n F) =
  is-inhabited-type-UU-Fin-succ-â„• n F
```
