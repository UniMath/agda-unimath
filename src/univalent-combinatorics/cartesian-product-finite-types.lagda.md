# Cartesian product of finite types

```agda
{-# OPTIONS --without-K --exact-split #-}

module univalent-combinatorics.cartesian-product-finite-types where

open import elementary-number-theory.multiplication-natural-numbers using
  ( mul-â„•)
open import elementary-number-theory.natural-numbers using (â„•)

open import foundation.cartesian-product-types using (_Ã—_)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.equivalences using (_âˆ˜e_; inv-equiv)
open import foundation.functions using (_âˆ˜_)
open import foundation.functoriality-cartesian-product-types using (equiv-prod)
open import foundation.functoriality-propositional-truncation using
  ( functor-trunc-Prop)
open import foundation.mere-equivalences using (mere-equiv-Prop)
open import foundation.propositional-truncations using
  ( apply-universal-property-trunc-Prop; unit-trunc-Prop)
open import foundation.universe-levels using (Level; UU; _âŠ”_)

open import univalent-combinatorics.counting using (count)
open import univalent-combinatorics.counting-cartesian-product-types using
  ( count-prod; count-left-factor; count-right-factor)
open import univalent-combinatorics.equivalences-standard-finite-types using
  ( prod-Fin)
open import univalent-combinatorics.finite-types using
  ( is-finite; is-finite-Prop; is-finite-count; ğ”½; type-ğ”½; is-finite-type-ğ”½;
    UU-Fin-Level; UU-Fin)
open import univalent-combinatorics.standard-finite-types using (Fin)
```

## Idea

The cartesian product of finite types is finite. We obtain a cartesian product operation on finite types.

```agda
abstract
  is-finite-prod :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’
    is-finite X â†’ is-finite Y â†’ is-finite (X Ã— Y)
  is-finite-prod {X = X} {Y} is-finite-X is-finite-Y =
    apply-universal-property-trunc-Prop is-finite-X
      ( is-finite-Prop (X Ã— Y))
      ( Î» (e : count X) â†’
        apply-universal-property-trunc-Prop is-finite-Y
          ( is-finite-Prop (X Ã— Y))
          ( is-finite-count âˆ˜ (count-prod e)))

prod-ğ”½ : ğ”½ â†’ ğ”½ â†’ ğ”½
pr1 (prod-ğ”½ X Y) = (type-ğ”½ X) Ã— (type-ğ”½ Y)
pr2 (prod-ğ”½ X Y) = is-finite-prod (is-finite-type-ğ”½ X) (is-finite-type-ğ”½ Y)

abstract
  is-finite-left-factor :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’
    is-finite (X Ã— Y) â†’ Y â†’ is-finite X
  is-finite-left-factor f y =
    functor-trunc-Prop (Î» e â†’ count-left-factor e y) f

abstract
  is-finite-right-factor :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} â†’
    is-finite (X Ã— Y) â†’ X â†’ is-finite Y
  is-finite-right-factor f x =
    functor-trunc-Prop (Î» e â†’ count-right-factor e x) f

prod-UU-Fin-Level :
  {l1 l2 : Level} {k l : â„•} â†’ UU-Fin-Level l1 k â†’ UU-Fin-Level l2 l â†’
  UU-Fin-Level (l1 âŠ” l2) (mul-â„• k l)
pr1 (prod-UU-Fin-Level {l1} {l2} {k} {l} (pair X H) (pair Y K)) = X Ã— Y
pr2 (prod-UU-Fin-Level {l1} {l2} {k} {l} (pair X H) (pair Y K)) =
  apply-universal-property-trunc-Prop H
    ( mere-equiv-Prop (Fin (mul-â„• k l)) (X Ã— Y))
    ( Î» e1 â†’
      apply-universal-property-trunc-Prop K
        ( mere-equiv-Prop (Fin (mul-â„• k l)) (X Ã— Y))
        ( Î» e2 â†’
          unit-trunc-Prop (equiv-prod e1 e2 âˆ˜e inv-equiv (prod-Fin k l))))

prod-UU-Fin :
  {k l : â„•} â†’ UU-Fin k â†’ UU-Fin l â†’ UU-Fin (mul-â„• k l)
prod-UU-Fin = prod-UU-Fin-Level
```
