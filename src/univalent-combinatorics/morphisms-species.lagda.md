---
title: Morphisms of species
---

```agda
{-# OPTIONS --allow-unsolved-metas --without-K --exact-split #-}

module univalent-combinatorics.morphisms-species where

open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.equality-dependent-function-types
open import foundation.equivalences
open import foundation.functions
open import foundation.fundamental-theorem-of-identity-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.univalence
open import foundation.universe-levels

open import univalent-combinatorics.finite-types
open import univalent-combinatorics.species
```

### Idea

A homomorphism between two species is a pointwise family of
maps between their values.

## Definitions

### Morphisms of species

```agda
hom-species :
  {l1 l2 : Level} â†’ species l1 â†’ species l2 â†’ UU (lsuc lzero âŠ” l1 âŠ” l2)
hom-species F G = (X : ğ”½) â†’ F X â†’ G X

id-hom-species : {l : Level} â†’ (F : species l) â†’ hom-species F F
id-hom-species F = Î» X x â†’ x 

comp-hom-species :
  {l1 l2 l3 : Level} {F : species l1} {G : species l2} {H : species l3} â†’
  hom-species G H â†’ hom-species F G â†’ hom-species F H
comp-hom-species f g X = (f X) âˆ˜ (g X)
```

### Homotopies between morphisms of species

```agda
htpy-hom-species :
  {l1 l2 : Level} {F : species l1} {G : species l2} â†’
  hom-species F G â†’ hom-species F G â†’ UU (lsuc lzero âŠ” l1 âŠ” l2)
htpy-hom-species f g = (X : ğ”½) â†’ (f X) ~ (g X)

refl-htpy-hom-species :
  {l1 l2 : Level} {F : species l1} {G : species l2} (f : hom-species F G) â†’
  htpy-hom-species f f
refl-htpy-hom-species f X = refl-htpy
```

## Properties

### Homotopies characterize the identity type of morphisms of species

```agda
htpy-eq-hom-species :
  {l1 l2 : Level} {F : species l1} {G : species l2} {f g : hom-species F G} â†’
  Id f g â†’ htpy-hom-species f g
htpy-eq-hom-species refl X y = refl

is-contr-htpy-hom-species :
  {l1 l2 : Level} {F : species l1} {G : species l2} (f : hom-species F G) â†’
  is-contr (Î£ (hom-species F G) (htpy-hom-species f))
is-contr-htpy-hom-species f =
  is-contr-total-Eq-Î  (Î» X h â†’ f X ~ h) (Î» X â†’ is-contr-total-htpy (f X))

is-equiv-htpy-eq-hom-species :
  {l1 l2 : Level} {F : species l1} {G : species l2} (f g : hom-species F G) â†’
  is-equiv (htpy-eq-hom-species {f = f} {g = g})
is-equiv-htpy-eq-hom-species f =
  fundamental-theorem-id f
    ( refl-htpy-hom-species f)
    ( is-contr-htpy-hom-species f)
    ( Î» g â†’ htpy-eq-hom-species {f = f} {g = g})

eq-htpy-hom-species :
  {l1 l2 : Level} {F : species l1} {G : species l2} {f g : hom-species F G} â†’
  htpy-hom-species f g â†’ Id f g 
eq-htpy-hom-species {f = f} {g = g} =
  map-inv-is-equiv (is-equiv-htpy-eq-hom-species f g)
```

### Associativity of composition

```agda
associative-comp-hom-species :
  {l1 l2 l3 l4 : Level}
  {F : species l1} {G : species l2} {H : species l3} {K : species l4}
  (h : hom-species H K) (g : hom-species G H) (f : hom-species F G) â†’
  Id ( comp-hom-species (comp-hom-species h g) f)
     ( comp-hom-species h (comp-hom-species g f))
associative-comp-hom-species h g f = refl
```

### Unit laws of composition

```agda
left-unit-law-comp-hom-species :
  {l1 l2 : Level} {F : species l1} {G : species l2} (f : hom-species F G) â†’
  Id (comp-hom-species (id-hom-species G) f) f
left-unit-law-comp-hom-species f = refl

right-unit-law-comp-hom-species :
  {l1 l2 : Level} {F : species l1} {G : species l2} (f : hom-species F G) â†’
  Id (comp-hom-species f (id-hom-species F)) f
right-unit-law-comp-hom-species f = refl
```
