# Finite types

```agda
module univalent-combinatorics.finite-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.equality-natural-numbers
open import elementary-number-theory.natural-numbers

open import foundation.0-connected-types
open import foundation.1-types
open import foundation.action-on-identifications-functions
open import foundation.connected-components-universes
open import foundation.contractible-types
open import foundation.decidable-types
open import foundation.dependent-pair-types
open import foundation.empty-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.functoriality-coproduct-types
open import foundation.functoriality-dependent-pair-types
open import foundation.functoriality-propositional-truncation
open import foundation.identity-types
open import foundation.inhabited-types
open import foundation.logical-equivalences
open import foundation.mere-equivalences
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.raising-universe-levels
open import foundation.sets
open import foundation.subtypes
open import foundation.subuniverses
open import foundation.transport-along-identifications
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.type-arithmetic-empty-type
open import foundation.unit-type
open import foundation.univalence
open import foundation.universe-levels

open import foundation-core.torsorial-type-families

open import univalent-combinatorics.counting
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

A type is
{{#concept "finite" Disambiguation="type" Agda=is-finite WD="finite set" WDID=Q272404}}
if it is [merely equivalent](foundation.mere-equivalences.md) to a
[standard finite type](univalent-combinatorics.standard-finite-types.md).

**Terminology.** This finiteness condition is also referred to as _Bishop
finiteness_. (Cf. the external links at the bottom of this page)

## Definition

### Finite types

```agda
is-finite-Prop :
  {l : Level} â†’ UU l â†’ Prop l
is-finite-Prop X = trunc-Prop (count X)

is-finite :
  {l : Level} â†’ UU l â†’ UU l
is-finite X = type-Prop (is-finite-Prop X)

abstract
  is-prop-is-finite :
    {l : Level} (X : UU l) â†’ is-prop (is-finite X)
  is-prop-is-finite X = is-prop-type-Prop (is-finite-Prop X)

abstract
  is-finite-count :
    {l : Level} {X : UU l} â†’ count X â†’ is-finite X
  is-finite-count = unit-trunc-Prop
```

### The type of all finite types of a universe level

```agda
ğ”½ : (l : Level) â†’ UU (lsuc l)
ğ”½ l = Î£ (UU l) is-finite

type-ğ”½ : {l : Level} â†’ ğ”½ l â†’ UU l
type-ğ”½ = pr1

is-finite-type-ğ”½ :
  {l : Level} (X : ğ”½ l) â†’ is-finite (type-ğ”½ X)
is-finite-type-ğ”½ = pr2
```

### Types with finite cardinality `k`

```agda
has-cardinality-Prop :
  {l : Level} â†’ â„• â†’ UU l â†’ Prop l
has-cardinality-Prop k = mere-equiv-Prop (Fin k)

has-cardinality :
  {l : Level} â†’ â„• â†’ UU l â†’ UU l
has-cardinality k = mere-equiv (Fin k)
```

### The type of all types of cardinality `k` of a given universe level

```agda
UU-Fin : (l : Level) â†’ â„• â†’ UU (lsuc l)
UU-Fin l k = Î£ (UU l) (mere-equiv (Fin k))

type-UU-Fin : {l : Level} (k : â„•) â†’ UU-Fin l k â†’ UU l
type-UU-Fin k = pr1

abstract
  has-cardinality-type-UU-Fin :
    {l : Level} (k : â„•) (X : UU-Fin l k) â†’
    mere-equiv (Fin k) (type-UU-Fin k X)
  has-cardinality-type-UU-Fin k = pr2
```

### Types of finite cardinality

```agda
has-finite-cardinality :
  {l : Level} â†’ UU l â†’ UU l
has-finite-cardinality X = Î£ â„• (Î» k â†’ has-cardinality k X)

number-of-elements-has-finite-cardinality :
  {l : Level} {X : UU l} â†’ has-finite-cardinality X â†’ â„•
number-of-elements-has-finite-cardinality = pr1

abstract
  mere-equiv-has-finite-cardinality :
    {l : Level} {X : UU l} (c : has-finite-cardinality X) â†’
    type-trunc-Prop (Fin (number-of-elements-has-finite-cardinality c) â‰ƒ X)
  mere-equiv-has-finite-cardinality = pr2
```

## Properties

### Finite types are closed under equivalences

```agda
abstract
  is-finite-equiv :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
    is-finite A â†’ is-finite B
  is-finite-equiv e =
    map-universal-property-trunc-Prop
      ( is-finite-Prop _)
      ( is-finite-count âˆ˜ (count-equiv e))

abstract
  is-finite-is-equiv :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-equiv f â†’ is-finite A â†’ is-finite B
  is-finite-is-equiv is-equiv-f =
    map-universal-property-trunc-Prop
      ( is-finite-Prop _)
      ( is-finite-count âˆ˜ (count-equiv (pair _ is-equiv-f)))

abstract
  is-finite-equiv' :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
    is-finite B â†’ is-finite A
  is-finite-equiv' e = is-finite-equiv (inv-equiv e)
```

### Finite types are closed under mere equivalences

```agda
abstract
  is-finite-mere-equiv :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ mere-equiv A B â†’
    is-finite A â†’ is-finite B
  is-finite-mere-equiv e H =
    apply-universal-property-trunc-Prop e
      ( is-finite-Prop _)
      ( Î» e' â†’ is-finite-equiv e' H)
```

### The empty type is finite

```agda
abstract
  is-finite-empty : is-finite empty
  is-finite-empty = is-finite-count count-empty

empty-ğ”½ : ğ”½ lzero
pr1 empty-ğ”½ = empty
pr2 empty-ğ”½ = is-finite-empty

empty-UU-Fin : UU-Fin lzero zero-â„•
pr1 empty-UU-Fin = empty
pr2 empty-UU-Fin = unit-trunc-Prop id-equiv
```

### The empty type has finite cardinality

```agda
has-finite-cardinality-empty : has-finite-cardinality empty
pr1 has-finite-cardinality-empty = zero-â„•
pr2 has-finite-cardinality-empty = unit-trunc-Prop id-equiv
```

### Empty types are finite

```agda
abstract
  is-finite-is-empty :
    {l1 : Level} {X : UU l1} â†’ is-empty X â†’ is-finite X
  is-finite-is-empty H = is-finite-count (count-is-empty H)
```

### Empty types have finite cardinality

```agda
has-finite-cardinality-is-empty :
  {l1 : Level} {X : UU l1} â†’ is-empty X â†’ has-finite-cardinality X
pr1 (has-finite-cardinality-is-empty f) = zero-â„•
pr2 (has-finite-cardinality-is-empty f) =
  unit-trunc-Prop (equiv-count (count-is-empty f))
```

### The unit type is finite

```agda
abstract
  is-finite-unit : is-finite unit
  is-finite-unit = is-finite-count count-unit

abstract
  is-finite-raise-unit :
    {l1 : Level} â†’ is-finite (raise-unit l1)
  is-finite-raise-unit {l1} =
    is-finite-equiv (compute-raise-unit l1) is-finite-unit

unit-ğ”½ : ğ”½ lzero
pr1 unit-ğ”½ = unit
pr2 unit-ğ”½ = is-finite-unit

unit-UU-Fin : UU-Fin lzero 1
pr1 unit-UU-Fin = unit
pr2 unit-UU-Fin = unit-trunc-Prop (left-unit-law-coproduct unit)
```

### Contractible types are finite

```agda
abstract
  is-finite-is-contr :
    {l1 : Level} {X : UU l1} â†’ is-contr X â†’ is-finite X
  is-finite-is-contr H = is-finite-count (count-is-contr H)

abstract
  has-cardinality-is-contr :
    {l1 : Level} {X : UU l1} â†’ is-contr X â†’ has-cardinality 1 X
  has-cardinality-is-contr H =
    unit-trunc-Prop (equiv-is-contr is-contr-Fin-one-â„• H)
```

### The standard finite types are finite

```agda
abstract
  is-finite-Fin : (k : â„•) â†’ is-finite (Fin k)
  is-finite-Fin k = is-finite-count (count-Fin k)

Fin-ğ”½ : â„• â†’ ğ”½ lzero
pr1 (Fin-ğ”½ k) = Fin k
pr2 (Fin-ğ”½ k) = is-finite-Fin k

has-cardinality-raise-Fin :
  {l : Level} (k : â„•) â†’ has-cardinality k (raise-Fin l k)
has-cardinality-raise-Fin {l} k = unit-trunc-Prop (compute-raise-Fin l k)

Fin-UU-Fin : (l : Level) (k : â„•) â†’ UU-Fin l k
pr1 (Fin-UU-Fin l k) = raise-Fin l k
pr2 (Fin-UU-Fin l k) = has-cardinality-raise-Fin k

Fin-UU-Fin' : (k : â„•) â†’ UU-Fin lzero k
pr1 (Fin-UU-Fin' k) = Fin k
pr2 (Fin-UU-Fin' k) = unit-trunc-Prop id-equiv
```

### Every type of cardinality `k` is finite

```agda
abstract
  is-finite-type-UU-Fin :
    {l : Level} (k : â„•) (X : UU-Fin l k) â†’
    is-finite (type-UU-Fin k X)
  is-finite-type-UU-Fin k X =
    is-finite-mere-equiv
      ( has-cardinality-type-UU-Fin k X)
      ( is-finite-Fin k)

finite-type-UU-Fin : {l : Level} (k : â„•) â†’ UU-Fin l k â†’ ğ”½ l
pr1 (finite-type-UU-Fin k X) = type-UU-Fin k X
pr2 (finite-type-UU-Fin k X) = is-finite-type-UU-Fin k X
```

### Having a finite cardinality is a proposition

```agda
abstract
  all-elements-equal-has-finite-cardinality :
    {l1 : Level} {X : UU l1} â†’ all-elements-equal (has-finite-cardinality X)
  all-elements-equal-has-finite-cardinality {l1} {X} (pair k K) (pair l L) =
    eq-type-subtype
      ( Î» k â†’ mere-equiv-Prop (Fin k) X)
      ( apply-universal-property-trunc-Prop K
        ( Id-Prop â„•-Set k l)
        ( Î» (e : Fin k â‰ƒ X) â†’
          apply-universal-property-trunc-Prop L
            ( Id-Prop â„•-Set k l)
            ( Î» (f : Fin l â‰ƒ X) â†’
              is-equivalence-injective-Fin (inv-equiv f âˆ˜e e))))

abstract
  is-prop-has-finite-cardinality :
    {l1 : Level} {X : UU l1} â†’ is-prop (has-finite-cardinality X)
  is-prop-has-finite-cardinality =
    is-prop-all-elements-equal all-elements-equal-has-finite-cardinality

has-finite-cardinality-Prop :
  {l1 : Level} (X : UU l1) â†’ Prop l1
pr1 (has-finite-cardinality-Prop X) = has-finite-cardinality X
pr2 (has-finite-cardinality-Prop X) = is-prop-has-finite-cardinality
```

### A type has a finite cardinality if and only if it is finite

```agda
module _
  {l : Level} {X : UU l}
  where

  abstract
    is-finite-has-finite-cardinality : has-finite-cardinality X â†’ is-finite X
    is-finite-has-finite-cardinality (pair k K) =
      apply-universal-property-trunc-Prop K
        ( is-finite-Prop X)
        ( is-finite-count âˆ˜ pair k)

  abstract
    is-finite-has-cardinality : (k : â„•) â†’ has-cardinality k X â†’ is-finite X
    is-finite-has-cardinality k H =
      is-finite-has-finite-cardinality (pair k H)

  has-finite-cardinality-count : count X â†’ has-finite-cardinality X
  pr1 (has-finite-cardinality-count e) = number-of-elements-count e
  pr2 (has-finite-cardinality-count e) = unit-trunc-Prop (equiv-count e)

  abstract
    has-finite-cardinality-is-finite : is-finite X â†’ has-finite-cardinality X
    has-finite-cardinality-is-finite =
      map-universal-property-trunc-Prop
        ( has-finite-cardinality-Prop X)
        ( has-finite-cardinality-count)

  number-of-elements-is-finite : is-finite X â†’ â„•
  number-of-elements-is-finite =
    number-of-elements-has-finite-cardinality âˆ˜ has-finite-cardinality-is-finite

  abstract
    mere-equiv-is-finite :
      (f : is-finite X) â†’ mere-equiv (Fin (number-of-elements-is-finite f)) X
    mere-equiv-is-finite f =
      mere-equiv-has-finite-cardinality (has-finite-cardinality-is-finite f)

  abstract
    compute-number-of-elements-is-finite :
      (e : count X) (f : is-finite X) â†’
      Id (number-of-elements-count e) (number-of-elements-is-finite f)
    compute-number-of-elements-is-finite e f =
      ind-trunc-Prop
        ( Î» g â†’
          Id-Prop â„•-Set
            ( number-of-elements-count e)
            ( number-of-elements-is-finite g))
        ( Î» g â†’
          ( is-equivalence-injective-Fin
            ( inv-equiv (equiv-count g) âˆ˜e equiv-count e)) âˆ™
          ( ap pr1
            ( eq-is-prop' is-prop-has-finite-cardinality
              ( has-finite-cardinality-count g)
              ( has-finite-cardinality-is-finite (unit-trunc-Prop g)))))
        ( f)

  has-cardinality-is-finite :
    (H : is-finite X) â†’ has-cardinality (number-of-elements-is-finite H) X
  has-cardinality-is-finite H =
    pr2 (has-finite-cardinality-is-finite H)

number-of-elements-ğ”½ : {l : Level} â†’ ğ”½ l â†’ â„•
number-of-elements-ğ”½ X = number-of-elements-is-finite (is-finite-type-ğ”½ X)
```

### If a type has cardinality `k` and cardinality `l`, then `k = l`

```agda
eq-cardinality :
  {l1 : Level} {k l : â„•} {A : UU l1} â†’
  has-cardinality k A â†’ has-cardinality l A â†’ Id k l
eq-cardinality H K =
  apply-universal-property-trunc-Prop H
    ( Id-Prop â„•-Set _ _)
    ( Î» e â†’
      apply-universal-property-trunc-Prop K
        ( Id-Prop â„•-Set _ _)
        ( Î» f â†’ is-equivalence-injective-Fin (inv-equiv f âˆ˜e e)))
```

### Any finite type is a set

```agda
abstract
  is-set-is-finite :
    {l : Level} {X : UU l} â†’ is-finite X â†’ is-set X
  is-set-is-finite {l} {X} H =
    apply-universal-property-trunc-Prop H
      ( is-set-Prop X)
      ( Î» e â†’ is-set-count e)

is-set-type-ğ”½ : {l : Level} (X : ğ”½ l) â†’ is-set (type-ğ”½ X)
is-set-type-ğ”½ X = is-set-is-finite (is-finite-type-ğ”½ X)

set-ğ”½ : {l : Level} â†’ ğ”½ l â†’ Set l
pr1 (set-ğ”½ X) = type-ğ”½ X
pr2 (set-ğ”½ X) = is-set-is-finite (is-finite-type-ğ”½ X)
```

### Any type of cardinality `k` is a set

```agda
is-set-has-cardinality :
  {l1 : Level} {X : UU l1} (k : â„•) â†’ has-cardinality k X â†’ is-set X
is-set-has-cardinality k H = is-set-mere-equiv' H (is-set-Fin k)

is-set-type-UU-Fin :
  {l : Level} (k : â„•) (X : UU-Fin l k) â†’ is-set (type-UU-Fin k X)
is-set-type-UU-Fin k X =
  is-set-has-cardinality k (has-cardinality-type-UU-Fin k X)

set-UU-Fin : {l1 : Level} (k : â„•) â†’ UU-Fin l1 k â†’ Set l1
pr1 (set-UU-Fin k X) = type-UU-Fin k X
pr2 (set-UU-Fin k X) = is-set-type-UU-Fin k X
```

### A finite type is empty if and only if it has 0 elements

```agda
abstract
  is-empty-is-zero-number-of-elements-is-finite :
    {l1 : Level} {X : UU l1} (f : is-finite X) â†’
    is-zero-â„• (number-of-elements-is-finite f) â†’ is-empty X
  is-empty-is-zero-number-of-elements-is-finite {l1} {X} f p =
    apply-universal-property-trunc-Prop f
      ( is-empty-Prop X)
      ( Î» e â†’
        is-empty-is-zero-number-of-elements-count e
          ( compute-number-of-elements-is-finite e f âˆ™ p))
```

### A finite type is contractible if and only if it has one element

```agda
is-one-number-of-elements-is-finite-is-contr :
  {l : Level} {X : UU l} (H : is-finite X) â†’
  is-contr X â†’ is-one-â„• (number-of-elements-is-finite H)
is-one-number-of-elements-is-finite-is-contr H K =
  eq-cardinality
    ( has-cardinality-is-finite H)
    ( has-cardinality-is-contr K)

is-contr-is-one-number-of-elements-is-finite :
  {l : Level} {X : UU l} (H : is-finite X) â†’
  is-one-â„• (number-of-elements-is-finite H) â†’ is-contr X
is-contr-is-one-number-of-elements-is-finite H p =
  apply-universal-property-trunc-Prop H
    ( is-contr-Prop _)
    ( Î» e â†’
      is-contr-equiv'
        ( Fin 1)
        ( ( equiv-count e) âˆ˜e
          ( equiv-tr Fin
            ( inv p âˆ™ inv (compute-number-of-elements-is-finite e H))))
        ( is-contr-Fin-one-â„•))

is-decidable-is-contr-is-finite :
  {l : Level} {X : UU l} (H : is-finite X) â†’ is-decidable (is-contr X)
is-decidable-is-contr-is-finite H =
  is-decidable-iff
    ( is-contr-is-one-number-of-elements-is-finite H)
    ( is-one-number-of-elements-is-finite-is-contr H)
    ( has-decidable-equality-â„• (number-of-elements-is-finite H) 1)
```

### The type of all pairs consisting of a natural number `k` and a type of cardinality `k` is equivalent to the type of all finite types

```agda
map-compute-total-UU-Fin : {l : Level} â†’ Î£ â„• (UU-Fin l) â†’ ğ”½ l
pr1 (map-compute-total-UU-Fin (pair k (pair X e))) = X
pr2 (map-compute-total-UU-Fin (pair k (pair X e))) =
  is-finite-has-finite-cardinality (pair k e)

compute-total-UU-Fin : {l : Level} â†’ Î£ â„• (UU-Fin l) â‰ƒ ğ”½ l
compute-total-UU-Fin =
  ( equiv-tot
    ( Î» X â†’
      equiv-iff-is-prop
        ( is-prop-has-finite-cardinality)
        ( is-prop-is-finite X)
        ( is-finite-has-finite-cardinality)
        ( has-finite-cardinality-is-finite))) âˆ˜e
  ( equiv-left-swap-Î£)
```

### Finite types are either inhabited or empty

```agda
is-inhabited-or-empty-is-finite :
  {l1 : Level} {A : UU l1} â†’ is-finite A â†’ is-inhabited-or-empty A
is-inhabited-or-empty-is-finite {l1} {A} f =
  apply-universal-property-trunc-Prop f
    ( is-inhabited-or-empty-Prop A)
    ( is-inhabited-or-empty-count)
```

### Finite types of cardinality greater than one are inhabited

```agda
is-inhabited-type-UU-Fin-succ-â„• :
  {l1 : Level} (n : â„•) (A : UU-Fin l1 (succ-â„• n)) â†’
  is-inhabited (type-UU-Fin (succ-â„• n) A)
is-inhabited-type-UU-Fin-succ-â„• n A =
  apply-universal-property-trunc-Prop
    ( pr2 A)
    ( is-inhabited-Prop (type-UU-Fin (succ-â„• n) A))
    ( Î» e â†’ unit-trunc-Prop (map-equiv e (zero-Fin n)))
```

### If `X` is finite, then its propositional truncation is decidable

```agda
is-decidable-type-trunc-Prop-is-finite :
  {l1 : Level} {A : UU l1} â†’ is-finite A â†’ is-decidable (type-trunc-Prop A)
is-decidable-type-trunc-Prop-is-finite H =
  map-coproduct
    ( id)
    ( map-universal-property-trunc-Prop empty-Prop)
      ( is-inhabited-or-empty-is-finite H)
```

### If a type is finite, then its propositional truncation is finite

```agda
abstract
  is-finite-type-trunc-Prop :
    {l1 : Level} {A : UU l1} â†’ is-finite A â†’ is-finite (type-trunc-Prop A)
  is-finite-type-trunc-Prop = map-trunc-Prop count-type-trunc-Prop

trunc-Prop-ğ”½ : {l : Level} â†’ ğ”½ l â†’ ğ”½ l
pr1 (trunc-Prop-ğ”½ A) = type-trunc-Prop (type-ğ”½ A)
pr2 (trunc-Prop-ğ”½ A) = is-finite-type-trunc-Prop (is-finite-type-ğ”½ A)
```

### We characterize the identity type of ğ”½

```agda
equiv-ğ”½ : {l1 l2 : Level} â†’ ğ”½ l1 â†’ ğ”½ l2 â†’ UU (l1 âŠ” l2)
equiv-ğ”½ X Y = type-ğ”½ X â‰ƒ type-ğ”½ Y

id-equiv-ğ”½ : {l : Level} â†’ (X : ğ”½ l) â†’ equiv-ğ”½ X X
id-equiv-ğ”½ X = id-equiv

extensionality-ğ”½ : {l : Level} â†’ (X Y : ğ”½ l) â†’ Id X Y â‰ƒ equiv-ğ”½ X Y
extensionality-ğ”½ = extensionality-subuniverse is-finite-Prop

is-torsorial-equiv-ğ”½ :
  {l : Level} â†’ (X : ğ”½ l) â†’ is-torsorial (Î» (Y : ğ”½ l) â†’ equiv-ğ”½ X Y)
is-torsorial-equiv-ğ”½ {l} X =
  is-contr-equiv'
    ( Î£ (ğ”½ l) (Id X))
    ( equiv-tot (extensionality-ğ”½ X))
    ( is-torsorial-Id X)

equiv-eq-ğ”½ : {l : Level} â†’ (X Y : ğ”½ l) â†’ Id X Y â†’ equiv-ğ”½ X Y
equiv-eq-ğ”½ X Y = map-equiv (extensionality-ğ”½ X Y)

eq-equiv-ğ”½ : {l : Level} â†’ (X Y : ğ”½ l) â†’ equiv-ğ”½ X Y â†’ Id X Y
eq-equiv-ğ”½ X Y = map-inv-equiv (extensionality-ğ”½ X Y)
```

### We characterize the identity type of families of finite types

```agda
equiv-fam-ğ”½ : {l1 l2 : Level} {X : UU l1} (Y Z : X â†’ ğ”½ l2) â†’ UU (l1 âŠ” l2)
equiv-fam-ğ”½ Y Z = equiv-fam (type-ğ”½ âˆ˜ Y) (type-ğ”½ âˆ˜ Z)

id-equiv-fam-ğ”½ : {l1 l2 : Level} {X : UU l1} â†’ (Y : X â†’ ğ”½ l2) â†’ equiv-fam-ğ”½ Y Y
id-equiv-fam-ğ”½ Y x = id-equiv

extensionality-fam-ğ”½ :
  {l1 l2 : Level} {X : UU l1} (Y Z : X â†’ ğ”½ l2) â†’ Id Y Z â‰ƒ equiv-fam-ğ”½ Y Z
extensionality-fam-ğ”½ = extensionality-fam-subuniverse is-finite-Prop
```

### We characterize the identity type of `UU-Fin`

```agda
equiv-UU-Fin :
  {l1 l2 : Level} (k : â„•) â†’ UU-Fin l1 k â†’ UU-Fin l2 k â†’ UU (l1 âŠ” l2)
equiv-UU-Fin k X Y = type-UU-Fin k X â‰ƒ type-UU-Fin k Y

id-equiv-UU-Fin :
  {l : Level} {k : â„•} (X : UU-Fin l k) â†’ equiv-UU-Fin k X X
id-equiv-UU-Fin X = id-equiv-component-UU-Level X

equiv-eq-UU-Fin :
  {l : Level} (k : â„•) {X Y : UU-Fin l k} â†’ Id X Y â†’ equiv-UU-Fin k X Y
equiv-eq-UU-Fin k p = equiv-eq-component-UU-Level p

abstract
  is-torsorial-equiv-UU-Fin :
    {l : Level} {k : â„•} (X : UU-Fin l k) â†’
    is-torsorial (Î» (Y : UU-Fin l k) â†’ equiv-UU-Fin k X Y)
  is-torsorial-equiv-UU-Fin {l} {k} X =
    is-torsorial-equiv-component-UU-Level X

abstract
  is-equiv-equiv-eq-UU-Fin :
    {l : Level} (k : â„•) (X Y : UU-Fin l k) â†’
    is-equiv (equiv-eq-UU-Fin k {X = X} {Y})
  is-equiv-equiv-eq-UU-Fin k X =
    is-equiv-equiv-eq-component-UU-Level X

eq-equiv-UU-Fin :
  {l : Level} (k : â„•) (X Y : UU-Fin l k) â†’
  equiv-UU-Fin k X Y â†’ Id X Y
eq-equiv-UU-Fin k X Y =
  eq-equiv-component-UU-Level X Y

equiv-equiv-eq-UU-Fin :
  {l : Level} (k : â„•) (X Y : UU-Fin l k) â†’
  Id X Y â‰ƒ equiv-UU-Fin k X Y
pr1 (equiv-equiv-eq-UU-Fin k X Y) = equiv-eq-UU-Fin k
pr2 (equiv-equiv-eq-UU-Fin k X Y) = is-equiv-equiv-eq-UU-Fin k X Y
```

### The type `UU-Fin l k` is a 1-type

```agda
is-1-type-UU-Fin : {l : Level} (k : â„•) â†’ is-1-type (UU-Fin l k)
is-1-type-UU-Fin k X Y =
  is-set-equiv
    ( equiv-UU-Fin k X Y)
    ( equiv-equiv-eq-UU-Fin k X Y)
    ( is-set-equiv-is-set
      ( is-set-type-UU-Fin k X)
      ( is-set-type-UU-Fin k Y))

UU-Fin-1-Type : (l : Level) (k : â„•) â†’ 1-Type (lsuc l)
pr1 (UU-Fin-1-Type l k) = UU-Fin l k
pr2 (UU-Fin-1-Type l k) = is-1-type-UU-Fin k
```

### The type `UU-Fin` is connected

```agda
abstract
  is-0-connected-UU-Fin :
    {l : Level} (n : â„•) â†’ is-0-connected (UU-Fin l n)
  is-0-connected-UU-Fin {l} n =
    is-0-connected-mere-eq
      ( Fin-UU-Fin l n)
      ( Î» A â†’
        map-trunc-Prop
          ( ( eq-equiv-UU-Fin n (Fin-UU-Fin l n) A) âˆ˜
            ( map-equiv
              ( equiv-precomp-equiv
                ( inv-equiv (compute-raise l (Fin n)))
                ( type-UU-Fin n A))))
          ( pr2 A))
```

```agda
  equiv-has-cardinality-id-number-of-elements-is-finite :
    {l : Level} (X : UU l) ( H : is-finite X) (n : â„•) â†’
    ( has-cardinality n X â‰ƒ Id (number-of-elements-is-finite H) n)
  pr1 (equiv-has-cardinality-id-number-of-elements-is-finite X H n) Q =
    ap
      ( number-of-elements-has-finite-cardinality)
      ( all-elements-equal-has-finite-cardinality
        ( has-finite-cardinality-is-finite H)
        ( pair n Q))
  pr2 (equiv-has-cardinality-id-number-of-elements-is-finite X H n) =
    is-equiv-has-converse-is-prop
      ( is-prop-type-trunc-Prop)
      ( is-set-â„• (number-of-elements-is-finite H) n)
      ( Î» p â†’
        tr
          ( Î» m â†’ has-cardinality m X)
          ( p)
          ( pr2 (has-finite-cardinality-is-finite H)))
```

## External links

- [Finiteness in Sheaf Topoi](https://grossack.site/2024/08/19/finiteness-in-sheaf-topoi),
  blog post by Chris Grossack
- [`Fin.Bishop`](https://www.cs.bham.ac.uk/~mhe/TypeTopology/Fin.Bishop.html) at
  TypeTopology
- [finite set](https://ncatlab.org/nlab/show/finite+set) at $n$Lab
- [finite object](https://ncatlab.org/nlab/show/finite+object) at $n$Lab
- [Finite set](https://en.wikipedia.org/wiki/Finite_set) at Wikipedia
- [Finite set](https://www.wikidata.org/wiki/Q272404) at Wikidata
