# Finite types

```agda
{-# OPTIONS --without-K --exact-split #-}

module univalent-combinatorics.finite-types where

open import elementary-number-theory.equality-natural-numbers using
  ( is-set-â„•; â„•-Set)
open import elementary-number-theory.modular-arithmetic using (â„¤-Mod)
open import elementary-number-theory.natural-numbers using
  ( â„•; zero-â„•; is-nonzero-â„•; succ-â„•; is-zero-â„•)

open import foundation.connected-components-universes using
  ( equiv-component-UU-Level; equiv-component-UU; id-equiv-component-UU-Level;
    id-equiv-component-UU; equiv-eq-component-UU-Level; equiv-eq-component-UU;
    is-contr-total-equiv-component-UU-Level; is-contr-total-equiv-component-UU;
    is-equiv-equiv-eq-component-UU-Level; is-equiv-equiv-eq-component-UU;
    eq-equiv-component-UU-Level; eq-equiv-component-UU)
open import foundation.connected-types using
  ( is-path-connected; is-path-connected-mere-eq)
open import foundation.contractible-types using (is-contr)
open import foundation.coproduct-types using (coprod; inl; inr)
open import foundation.decidable-equality using
  ( has-decidable-equality; has-decidable-equality-Prop;
    has-decidable-equality-equiv')
open import foundation.decidable-types using
  ( is-decidable; is-inhabited-or-empty; is-inhabited-or-empty-Prop)
open import foundation.dependent-pair-types using (Î£; pair; pr1; pr2)
open import foundation.empty-types using
  ( empty; is-empty; ex-falso; is-empty-Prop; empty-Prop)
open import foundation.equivalences using
  ( id-equiv; _â‰ƒ_; is-equiv; inv-equiv; _âˆ˜e_; map-equiv; equiv-precomp-equiv)
open import foundation.functions using (id; _âˆ˜_)
open import foundation.functoriality-coproduct-types using (map-coprod)
open import foundation.functoriality-dependent-pair-types using
  ( equiv-tot)
open import foundation.functoriality-propositional-truncation using
  ( functor-trunc-Prop)
open import foundation.identity-types using (refl; Id; _âˆ™_; ap)
open import foundation.mere-equivalences using
  ( mere-equiv-Prop; mere-equiv)
open import foundation.propositional-truncations using
  ( trunc-Prop; unit-trunc-Prop; map-universal-property-trunc-Prop;
    apply-universal-property-trunc-Prop; type-trunc-Prop; ind-trunc-Prop)
open import foundation.propositions using
  ( UU-Prop; type-Prop; is-prop; is-prop-type-Prop; is-proof-irrelevant-is-prop;
    all-elements-equal; is-prop-all-elements-equal; eq-is-prop; eq-is-prop';
    equiv-prop)
open import foundation.raising-universe-levels using (equiv-raise)
open import foundation.sets using (is-set; is-set-Prop; Id-Prop)
open import foundation.subtypes using (eq-subtype)
open import foundation.type-arithmetic-dependent-pair-types using
  ( equiv-left-swap-Î£)
open import foundation.type-arithmetic-empty-type using
  ( left-unit-law-coprod)
open import foundation.unit-type using (unit; star)
open import foundation.universe-levels using (Level; UU; _âŠ”_; lsuc; lzero)

open import univalent-combinatorics.counting using
  ( count; count-empty; count-is-empty; count-unit; count-is-contr; count-Fin;
    count-equiv; is-set-count; equiv-count; number-of-elements-count;
    is-empty-is-zero-number-of-elements-count; is-inhabited-or-empty-count;
    count-type-trunc-Prop)
open import univalent-combinatorics.equality-standard-finite-types using
  ( has-decidable-equality-Fin)
open import univalent-combinatorics.equivalences-standard-finite-types using
  ( is-injective-Fin)
open import univalent-combinatorics.standard-finite-types using
  ( Fin; raise-Fin; equiv-raise-Fin)
```

## Idea

A type is finite if it is merely equivalent to a standard finite type.

## Definition

```agda
-- Definition 16.3.1

is-finite-Prop :
  {l : Level} â†’ UU l â†’ UU-Prop l
is-finite-Prop X = trunc-Prop (count X)

is-finite :
  {l : Level} â†’ UU l â†’ UU l
is-finite X = type-Prop (is-finite-Prop X)

abstract
  is-prop-is-finite :
    {l : Level} (X : UU l) â†’ is-prop (is-finite X)
  is-prop-is-finite X = is-prop-type-Prop (is-finite-Prop X)

abstract
  is-finite-count :
    {l : Level} {X : UU l} â†’ count X â†’ is-finite X
  is-finite-count = unit-trunc-Prop

ğ”½ : UU (lsuc lzero)
ğ”½ = Î£ (UU lzero) is-finite

type-ğ”½ : ğ”½ â†’ UU lzero
type-ğ”½ X = pr1 X

abstract
  is-finite-type-ğ”½ : (X : ğ”½) â†’ is-finite (type-ğ”½ X)
  is-finite-type-ğ”½ X = pr2 X

has-cardinality-Prop :
  {l : Level} â†’ UU l â†’ â„• â†’ UU-Prop l
has-cardinality-Prop X k = mere-equiv-Prop (Fin k) X

has-cardinality :
  {l : Level} â†’ UU l â†’ â„• â†’ UU l
has-cardinality X k = mere-equiv (Fin k) X

UU-Fin-Level : (l : Level) â†’ â„• â†’ UU (lsuc l)
UU-Fin-Level l k = Î£ (UU l) (mere-equiv (Fin k))

type-UU-Fin-Level : {l : Level} {k : â„•} â†’ UU-Fin-Level l k â†’ UU l
type-UU-Fin-Level X = pr1 X

abstract
  mere-equiv-UU-Fin-Level :
    {l : Level} {k : â„•} (X : UU-Fin-Level l k) â†’
    mere-equiv (Fin k) (type-UU-Fin-Level X)
  mere-equiv-UU-Fin-Level X = pr2 X

UU-Fin : â„• â†’ UU (lsuc lzero)
UU-Fin k = UU-Fin-Level lzero k

type-UU-Fin : {k : â„•} â†’ UU-Fin k â†’ UU lzero
type-UU-Fin X = pr1 X

-- Remark 16.3.2

abstract
  is-finite-empty : is-finite empty
  is-finite-empty = is-finite-count count-empty

abstract
  is-finite-is-empty :
    {l1 : Level} {X : UU l1} â†’ is-empty X â†’ is-finite X
  is-finite-is-empty H = is-finite-count (count-is-empty H)

empty-ğ”½ : ğ”½
pr1 empty-ğ”½ = empty
pr2 empty-ğ”½ = is-finite-is-empty id

empty-UU-Fin : UU-Fin zero-â„•
pr1 empty-UU-Fin = empty
pr2 empty-UU-Fin = unit-trunc-Prop id-equiv

abstract
  is-finite-unit : is-finite unit
  is-finite-unit = is-finite-count count-unit

unit-ğ”½ : ğ”½
pr1 unit-ğ”½ = unit
pr2 unit-ğ”½ = is-finite-unit

unit-UU-Fin : UU-Fin 1
pr1 unit-UU-Fin = unit
pr2 unit-UU-Fin = unit-trunc-Prop (left-unit-law-coprod unit)

abstract
  is-finite-is-contr :
    {l1 : Level} {X : UU l1} â†’ is-contr X â†’ is-finite X
  is-finite-is-contr H = is-finite-count (count-is-contr H)

abstract
  is-finite-is-decidable-Prop :
    {l : Level} (P : UU-Prop l) â†’
    is-decidable (type-Prop P) â†’ is-finite (type-Prop P)
  is-finite-is-decidable-Prop P (inl x) =
    is-finite-is-contr (is-proof-irrelevant-is-prop (is-prop-type-Prop P) x)
  is-finite-is-decidable-Prop P (inr x) =
    is-finite-is-empty x

abstract
  is-finite-Fin : {k : â„•} â†’ is-finite (Fin k)
  is-finite-Fin {k} = is-finite-count (count-Fin k)

abstract
  is-finite-â„¤-Mod : {k : â„•} â†’ is-nonzero-â„• k â†’ is-finite (â„¤-Mod k)
  is-finite-â„¤-Mod {zero-â„•} H = ex-falso (H refl)
  is-finite-â„¤-Mod {succ-â„• k} H = is-finite-Fin

Fin-ğ”½ : â„• â†’ ğ”½
pr1 (Fin-ğ”½ k) = Fin k
pr2 (Fin-ğ”½ k) = is-finite-Fin

â„¤-Mod-ğ”½ : (k : â„•) â†’ is-nonzero-â„• k â†’ ğ”½
pr1 (â„¤-Mod-ğ”½ k H) = â„¤-Mod k
pr2 (â„¤-Mod-ğ”½ k H) = is-finite-â„¤-Mod H

Fin-UU-Fin : (k : â„•) â†’ UU-Fin k
pr1 (Fin-UU-Fin k) = Fin k
pr2 (Fin-UU-Fin k) = unit-trunc-Prop id-equiv
```

```agda
Fin-UU-Fin-Level : (l : Level) (k : â„•) â†’ UU-Fin-Level l k
pr1 (Fin-UU-Fin-Level l k) = raise-Fin l k
pr2 (Fin-UU-Fin-Level l k) = unit-trunc-Prop (equiv-raise-Fin l k)

abstract
  is-finite-equiv :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
    is-finite A â†’ is-finite B
  is-finite-equiv e =
    map-universal-property-trunc-Prop
      ( is-finite-Prop _)
      ( is-finite-count âˆ˜ (count-equiv e))

abstract
  is-finite-is-equiv :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
    is-equiv f â†’ is-finite A â†’ is-finite B
  is-finite-is-equiv is-equiv-f =
    map-universal-property-trunc-Prop
      ( is-finite-Prop _)
      ( is-finite-count âˆ˜ (count-equiv (pair _ is-equiv-f)))

abstract
  is-finite-equiv' :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
    is-finite B â†’ is-finite A
  is-finite-equiv' e = is-finite-equiv (inv-equiv e)

abstract
  is-finite-mere-equiv :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ mere-equiv A B â†’
    is-finite A â†’ is-finite B
  is-finite-mere-equiv e H =
    apply-universal-property-trunc-Prop e
      ( is-finite-Prop _)
      ( Î» e' â†’ is-finite-equiv e' H)

abstract
  is-finite-type-UU-Fin-Level :
    {l : Level} {k : â„•} (X : UU-Fin-Level l k) â†’ is-finite (type-UU-Fin-Level X)
  is-finite-type-UU-Fin-Level X =
    is-finite-mere-equiv
      ( mere-equiv-UU-Fin-Level X)
      ( is-finite-Fin)

abstract
  is-finite-type-UU-Fin :
    {k : â„•} (X : UU-Fin k) â†’ is-finite (type-UU-Fin X)
  is-finite-type-UU-Fin X = is-finite-type-UU-Fin-Level X

-- Theorem 16.3.3

abstract
  mere-equiv-UU-Fin :
    {k : â„•} (X : UU-Fin k) â†’ mere-equiv (Fin k) (type-UU-Fin X)
  mere-equiv-UU-Fin X = pr2 X

has-finite-cardinality :
  {l : Level} â†’ UU l â†’ UU l
has-finite-cardinality X = Î£ â„• (has-cardinality X)

number-of-elements-has-finite-cardinality :
  {l : Level} {X : UU l} â†’ has-finite-cardinality X â†’ â„•
number-of-elements-has-finite-cardinality = pr1

abstract
  mere-equiv-has-finite-cardinality :
    {l : Level} {X : UU l} (c : has-finite-cardinality X) â†’
    type-trunc-Prop (Fin (number-of-elements-has-finite-cardinality c) â‰ƒ X)
  mere-equiv-has-finite-cardinality = pr2

abstract
  all-elements-equal-has-finite-cardinality :
    {l1 : Level} {X : UU l1} â†’ all-elements-equal (has-finite-cardinality X)
  all-elements-equal-has-finite-cardinality {l1} {X} (pair k K) (pair l L) =
    eq-subtype
      ( Î» k â†’ mere-equiv-Prop (Fin k) X)
      ( apply-universal-property-trunc-Prop K
        ( pair (Id k l) (is-set-â„• k l))
        ( Î» (e : Fin k â‰ƒ X) â†’
          apply-universal-property-trunc-Prop L
            ( pair (Id k l) (is-set-â„• k l))
            ( Î» (f : Fin l â‰ƒ X) â†’ is-injective-Fin ((inv-equiv f) âˆ˜e e))))

abstract
  is-prop-has-finite-cardinality :
    {l1 : Level} {X : UU l1} â†’ is-prop (has-finite-cardinality X)
  is-prop-has-finite-cardinality =
    is-prop-all-elements-equal all-elements-equal-has-finite-cardinality

has-finite-cardinality-Prop :
  {l1 : Level} (X : UU l1) â†’ UU-Prop l1
pr1 (has-finite-cardinality-Prop X) = has-finite-cardinality X
pr2 (has-finite-cardinality-Prop X) = is-prop-has-finite-cardinality

module _
  {l : Level} {X : UU l}
  where

  abstract
    is-finite-has-finite-cardinality : has-finite-cardinality X â†’ is-finite X
    is-finite-has-finite-cardinality (pair k K) =
      apply-universal-property-trunc-Prop K
        ( is-finite-Prop X)
        ( is-finite-count âˆ˜ (pair k))

  abstract
    is-finite-has-cardinality : {k : â„•} â†’ has-cardinality X k â†’ is-finite X
    is-finite-has-cardinality {k} H =
      is-finite-has-finite-cardinality (pair k H)

  has-finite-cardinality-count : count X â†’ has-finite-cardinality X
  pr1 (has-finite-cardinality-count e) = number-of-elements-count e
  pr2 (has-finite-cardinality-count e) = unit-trunc-Prop (equiv-count e)

  abstract
    has-finite-cardinality-is-finite : is-finite X â†’ has-finite-cardinality X
    has-finite-cardinality-is-finite =
      map-universal-property-trunc-Prop
        ( has-finite-cardinality-Prop X)
        ( has-finite-cardinality-count)

  number-of-elements-is-finite : is-finite X â†’ â„•
  number-of-elements-is-finite =
    number-of-elements-has-finite-cardinality âˆ˜ has-finite-cardinality-is-finite

  abstract
    mere-equiv-is-finite :
      (f : is-finite X) â†’ mere-equiv (Fin (number-of-elements-is-finite f)) X
    mere-equiv-is-finite f =
      mere-equiv-has-finite-cardinality (has-finite-cardinality-is-finite f)

  abstract
    compute-number-of-elements-is-finite :
      (e : count X) (f : is-finite X) â†’
      Id (number-of-elements-count e) (number-of-elements-is-finite f)
    compute-number-of-elements-is-finite e f =
      ind-trunc-Prop
        ( Î» g â†’ Id-Prop â„•-Set ( number-of-elements-count e)
                              ( number-of-elements-is-finite g))
        ( Î» g â†’
          ( is-injective-Fin ((inv-equiv (equiv-count g)) âˆ˜e (equiv-count e))) âˆ™
          ( ap pr1
            ( eq-is-prop' is-prop-has-finite-cardinality
              ( has-finite-cardinality-count g)
              ( has-finite-cardinality-is-finite (unit-trunc-Prop g)))))
        ( f)

-- Some immediate conclusions of Theorem 16.3.3

has-finite-cardinality-empty : has-finite-cardinality empty
pr1 has-finite-cardinality-empty = zero-â„•
pr2 has-finite-cardinality-empty = unit-trunc-Prop id-equiv

has-finite-cardinality-is-empty :
  {l1 : Level} {X : UU l1} â†’ is-empty X â†’ has-finite-cardinality X
pr1 (has-finite-cardinality-is-empty f) = zero-â„•
pr2 (has-finite-cardinality-is-empty f) =
  unit-trunc-Prop (equiv-count (count-is-empty f))

abstract
  is-empty-is-zero-number-of-elements-is-finite :
    {l1 : Level} {X : UU l1} (f : is-finite X) â†’
    is-zero-â„• (number-of-elements-is-finite f) â†’ is-empty X
  is-empty-is-zero-number-of-elements-is-finite {l1} {X} f p =
    apply-universal-property-trunc-Prop f
      ( is-empty-Prop X)
      ( Î» e â†’
        is-empty-is-zero-number-of-elements-count e
          ( compute-number-of-elements-is-finite e f âˆ™ p))

-- Corollary 16.3.4

map-compute-total-UU-Fin : Î£ â„• UU-Fin â†’ ğ”½
pr1 (map-compute-total-UU-Fin (pair k (pair X e))) = X
pr2 (map-compute-total-UU-Fin (pair k (pair X e))) =
  is-finite-has-finite-cardinality (pair k e)

compute-total-UU-Fin : Î£ â„• UU-Fin â‰ƒ ğ”½
compute-total-UU-Fin =
  ( equiv-tot
    ( Î» X â†’
      equiv-prop
        ( is-prop-has-finite-cardinality)
        ( is-prop-is-finite X)
        ( is-finite-has-finite-cardinality)
        ( has-finite-cardinality-is-finite))) âˆ˜e
  ( equiv-left-swap-Î£)
```

### Finite types are either inhabited or empty

```agda
is-inhabited-or-empty-is-finite :
  {l1 : Level} {A : UU l1} â†’ is-finite A â†’ is-inhabited-or-empty A
is-inhabited-or-empty-is-finite {l1} {A} f =
  apply-universal-property-trunc-Prop f
    ( is-inhabited-or-empty-Prop A)
    ( is-inhabited-or-empty-count)
```

### If `X` is finite, then its propositional truncation is decidable

```agda
is-decidable-type-trunc-Prop-is-finite :
  {l1 : Level} {A : UU l1} â†’ is-finite A â†’ is-decidable (type-trunc-Prop A)
is-decidable-type-trunc-Prop-is-finite H =
  map-coprod
    ( id)
    ( map-universal-property-trunc-Prop empty-Prop)
      ( is-inhabited-or-empty-is-finite H)
```

```agda
abstract
  is-finite-type-trunc-Prop :
    {l1 : Level} {A : UU l1} â†’ is-finite A â†’ is-finite (type-trunc-Prop A)
  is-finite-type-trunc-Prop = functor-trunc-Prop count-type-trunc-Prop

trunc-Prop-ğ”½ : ğ”½ â†’ ğ”½
pr1 (trunc-Prop-ğ”½ A) = type-trunc-Prop (type-ğ”½ A)
pr2 (trunc-Prop-ğ”½ A) = is-finite-type-trunc-Prop (is-finite-type-ğ”½ A)
```

```agda
equiv-UU-Fin-Level : {l : Level} {k : â„•} â†’ (X Y : UU-Fin-Level l k) â†’ UU l
equiv-UU-Fin-Level X Y = equiv-component-UU-Level X Y

equiv-UU-Fin : {k : â„•} (X Y : UU-Fin k) â†’ UU lzero
equiv-UU-Fin X Y = equiv-component-UU X Y

id-equiv-UU-Fin-Level :
  {l : Level} {k : â„•} (X : UU-Fin-Level l k) â†’ equiv-UU-Fin-Level X X
id-equiv-UU-Fin-Level X = id-equiv-component-UU-Level X

id-equiv-UU-Fin :
  {k : â„•} (X : UU-Fin k) â†’ equiv-UU-Fin X X
id-equiv-UU-Fin X = id-equiv-component-UU X

equiv-eq-UU-Fin-Level :
  {l : Level} {k : â„•} {X Y : UU-Fin-Level l k} â†’ Id X Y â†’ equiv-UU-Fin-Level X Y
equiv-eq-UU-Fin-Level p = equiv-eq-component-UU-Level p

equiv-eq-UU-Fin :
  {k : â„•} {X Y : UU-Fin k} â†’ Id X Y â†’ equiv-UU-Fin X Y
equiv-eq-UU-Fin p = equiv-eq-component-UU p

abstract
  is-contr-total-equiv-UU-Fin-Level :
    {l : Level} {k : â„•} (X : UU-Fin-Level l k) â†’
    is-contr (Î£ (UU-Fin-Level l k) (equiv-UU-Fin-Level X))
  is-contr-total-equiv-UU-Fin-Level {l} {k} X =
    is-contr-total-equiv-component-UU-Level X

abstract
  is-contr-total-equiv-UU-Fin :
    {k : â„•} (X : UU-Fin k) â†’ is-contr (Î£ (UU-Fin k) (equiv-UU-Fin X))
  is-contr-total-equiv-UU-Fin X =
    is-contr-total-equiv-component-UU X

abstract
  is-equiv-equiv-eq-UU-Fin-Level :
    {l : Level} {k : â„•} (X Y : UU-Fin-Level l k) â†’
    is-equiv (equiv-eq-UU-Fin-Level {X = X} {Y})
  is-equiv-equiv-eq-UU-Fin-Level X =
    is-equiv-equiv-eq-component-UU-Level X

abstract
  is-equiv-equiv-eq-UU-Fin :
    {k : â„•} (X Y : UU-Fin k) â†’ is-equiv (equiv-eq-UU-Fin {X = X} {Y})
  is-equiv-equiv-eq-UU-Fin X =
    is-equiv-equiv-eq-component-UU X

eq-equiv-UU-Fin-Level :
  {l : Level} {k : â„•} (X Y : UU-Fin-Level l k) â†’
  equiv-UU-Fin-Level X Y â†’ Id X Y
eq-equiv-UU-Fin-Level X Y =
  eq-equiv-component-UU-Level X Y

eq-equiv-UU-Fin :
  {k : â„•} (X Y : UU-Fin k) â†’ equiv-UU-Fin X Y â†’ Id X Y
eq-equiv-UU-Fin X Y = eq-equiv-component-UU X Y

equiv-equiv-eq-UU-Fin-Level :
  {l : Level} {k : â„•} (X Y : UU-Fin-Level l k) â†’
  Id X Y â‰ƒ equiv-UU-Fin-Level X Y
pr1 (equiv-equiv-eq-UU-Fin-Level X Y) = equiv-eq-UU-Fin-Level
pr2 (equiv-equiv-eq-UU-Fin-Level X Y) = is-equiv-equiv-eq-UU-Fin-Level X Y

equiv-equiv-eq-UU-Fin :
  {k : â„•} (X Y : UU-Fin k) â†’ Id X Y â‰ƒ equiv-UU-Fin X Y
pr1 (equiv-equiv-eq-UU-Fin X Y) = equiv-eq-UU-Fin
pr2 (equiv-equiv-eq-UU-Fin X Y) = is-equiv-equiv-eq-UU-Fin X Y
```

### The types `UU-Fin-Level` and `UU-Fin` are connected

```agda
abstract
  is-path-connected-UU-Fin-Level :
    {l : Level} (n : â„•) â†’ is-path-connected (UU-Fin-Level l n)
  is-path-connected-UU-Fin-Level {l} n =
    is-path-connected-mere-eq
      ( Fin-UU-Fin-Level l n)
      ( Î» A â†’
        functor-trunc-Prop
          ( ( eq-equiv-UU-Fin-Level (Fin-UU-Fin-Level l n) A) âˆ˜
            ( map-equiv
              ( equiv-precomp-equiv
                ( inv-equiv (equiv-raise l (Fin n)))
                ( type-UU-Fin-Level A))))
          ( pr2 A))

abstract
  is-path-connected-UU-Fin :
    (n : â„•) â†’ is-path-connected (UU-Fin n)
  is-path-connected-UU-Fin n =
    is-path-connected-mere-eq
      ( Fin-UU-Fin n)
      ( Î» A â†’ functor-trunc-Prop (eq-equiv-UU-Fin (Fin-UU-Fin n) A) (pr2 A))
```
