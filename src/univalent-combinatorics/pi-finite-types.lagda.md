# Untruncated Ï€-finite types

```agda
module univalent-combinatorics.pi-finite-types where
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers

open import foundation.0-connected-types
open import foundation.action-on-identifications-functions
open import foundation.contractible-types
open import foundation.coproduct-types
open import foundation.decidable-propositions
open import foundation.decidable-types
open import foundation.dependent-identifications
open import foundation.embeddings
open import foundation.empty-types
open import foundation.equality-coproduct-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.fiber-inclusions
open import foundation.function-extensionality
open import foundation.function-types
open import foundation.functoriality-dependent-pair-types
open import foundation.functoriality-set-truncation
open import foundation.homotopies
open import foundation.identity-types
open import foundation.logical-equivalences
open import foundation.maybe
open import foundation.mere-equality
open import foundation.propositional-extensionality
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.retracts-of-types
open import foundation.set-presented-types
open import foundation.set-truncations
open import foundation.sets
open import foundation.subtypes
open import foundation.surjective-maps
open import foundation.transport-along-identifications
open import foundation.type-arithmetic-coproduct-types
open import foundation.unit-type
open import foundation.univalence
open import foundation.universe-levels
open import foundation.whiskering-homotopies-composition

open import univalent-combinatorics.coproduct-types
open import univalent-combinatorics.counting
open import univalent-combinatorics.dependent-pair-types
open import univalent-combinatorics.equality-finite-types
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.finitely-many-connected-components
open import univalent-combinatorics.finitely-presented-types
open import univalent-combinatorics.function-types
open import univalent-combinatorics.image-of-maps
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

A type is
{{#concept "untruncated Ï€â‚™-finite" Disambiguation="type" Agda=is-untruncated-Ï€-finite Agda=Untruncated-Ï€-Finite-Type}}
if it has [finitely](univalent-combinatorics.finite-types.md) many
[connected components](foundation.connected-components.md) and all of its
homotopy groups up to level `n` at all base points are finite.

## Definitions

### Untruncated Ï€-finite types

```agda
is-untruncated-Ï€-finite-Prop : {l : Level} (k : â„•) â†’ UU l â†’ Prop l
is-untruncated-Ï€-finite-Prop zero-â„• X =
  has-finitely-many-connected-components-Prop X
is-untruncated-Ï€-finite-Prop (succ-â„• k) X =
  product-Prop
    ( is-untruncated-Ï€-finite-Prop zero-â„• X)
    ( Î -Prop X (Î» x â†’ Î -Prop X (Î» y â†’ is-untruncated-Ï€-finite-Prop k (x ï¼ y))))

is-untruncated-Ï€-finite : {l : Level} (k : â„•) â†’ UU l â†’ UU l
is-untruncated-Ï€-finite k X = type-Prop (is-untruncated-Ï€-finite-Prop k X)

is-prop-is-untruncated-Ï€-finite :
  {l : Level} (k : â„•) (X : UU l) â†’ is-prop (is-untruncated-Ï€-finite k X)
is-prop-is-untruncated-Ï€-finite k X =
  is-prop-type-Prop (is-untruncated-Ï€-finite-Prop k X)

Untruncated-Ï€-Finite-Type : (l : Level) (k : â„•) â†’ UU (lsuc l)
Untruncated-Ï€-Finite-Type l k = Î£ (UU l) (is-untruncated-Ï€-finite k)

type-Untruncated-Ï€-Finite-Type :
  {l : Level} (k : â„•) â†’ Untruncated-Ï€-Finite-Type l k â†’ UU l
type-Untruncated-Ï€-Finite-Type k = pr1

is-untruncated-Ï€-finite-type-Untruncated-Ï€-Finite-Type :
  {l : Level} (k : â„•) (A : Untruncated-Ï€-Finite-Type l k) â†’
  is-untruncated-Ï€-finite k (type-Untruncated-Ï€-Finite-Type {l} k A)
is-untruncated-Ï€-finite-type-Untruncated-Ï€-Finite-Type k = pr2

has-finitely-many-connected-components-is-untruncated-Ï€-finite :
  {l : Level} (k : â„•) {A : UU l} â†’
  is-untruncated-Ï€-finite k A â†’ has-finitely-many-connected-components A
has-finitely-many-connected-components-is-untruncated-Ï€-finite zero-â„• H = H
has-finitely-many-connected-components-is-untruncated-Ï€-finite (succ-â„• k) H =
  pr1 H
```

## Properties

### Untruncated Ï€-finite types are closed under equivalences

```agda
is-untruncated-Ï€-finite-equiv :
  {l1 l2 : Level} (k : â„•) {A : UU l1} {B : UU l2} â†’
  A â‰ƒ B â†’ is-untruncated-Ï€-finite k B â†’ is-untruncated-Ï€-finite k A
is-untruncated-Ï€-finite-equiv zero-â„• =
  has-finitely-many-connected-components-equiv'
pr1 (is-untruncated-Ï€-finite-equiv (succ-â„• k) e H) =
  is-untruncated-Ï€-finite-equiv zero-â„• e (pr1 H)
pr2 (is-untruncated-Ï€-finite-equiv (succ-â„• k) e H) a b =
  is-untruncated-Ï€-finite-equiv k
    ( equiv-ap e a b)
    ( pr2 H (map-equiv e a) (map-equiv e b))

is-untruncated-Ï€-finite-equiv' :
  {l1 l2 : Level} (k : â„•) {A : UU l1} {B : UU l2} â†’
  A â‰ƒ B â†’ is-untruncated-Ï€-finite k A â†’ is-untruncated-Ï€-finite k B
is-untruncated-Ï€-finite-equiv' k e =
  is-untruncated-Ï€-finite-equiv k (inv-equiv e)
```

### Untruncated Ï€-finite types are closed under retracts

```agda
is-untruncated-Ï€-finite-retract :
  {l1 l2 : Level} (k : â„•) {A : UU l1} {B : UU l2} â†’
  A retract-of B â†’ is-untruncated-Ï€-finite k B â†’ is-untruncated-Ï€-finite k A
is-untruncated-Ï€-finite-retract zero-â„• =
  has-finitely-many-connected-components-retract
pr1 (is-untruncated-Ï€-finite-retract (succ-â„• k) r H) =
  is-untruncated-Ï€-finite-retract zero-â„• r
    ( has-finitely-many-connected-components-is-untruncated-Ï€-finite
      ( succ-â„• k)
      ( H))
pr2 (is-untruncated-Ï€-finite-retract (succ-â„• k) r H) x y =
  is-untruncated-Ï€-finite-retract k
    ( retract-eq r x y)
    ( pr2 H (inclusion-retract r x) (inclusion-retract r y))
```

### Empty types are untruncated Ï€-finite

```agda
is-untruncated-Ï€-finite-empty : (k : â„•) â†’ is-untruncated-Ï€-finite k empty
is-untruncated-Ï€-finite-empty zero-â„• =
  has-finitely-many-connected-components-empty
pr1 (is-untruncated-Ï€-finite-empty (succ-â„• k)) =
  is-untruncated-Ï€-finite-empty zero-â„•
pr2 (is-untruncated-Ï€-finite-empty (succ-â„• k)) = ind-empty

empty-Untruncated-Ï€-Finite-Type : (k : â„•) â†’ Untruncated-Ï€-Finite-Type lzero k
pr1 (empty-Untruncated-Ï€-Finite-Type k) = empty
pr2 (empty-Untruncated-Ï€-Finite-Type k) = is-untruncated-Ï€-finite-empty k

is-untruncated-Ï€-finite-is-empty :
  {l : Level} (k : â„•) {A : UU l} â†’ is-empty A â†’ is-untruncated-Ï€-finite k A
is-untruncated-Ï€-finite-is-empty zero-â„• =
  has-finitely-many-connected-components-is-empty
pr1 (is-untruncated-Ï€-finite-is-empty (succ-â„• k) f) =
  is-untruncated-Ï€-finite-is-empty zero-â„• f
pr2 (is-untruncated-Ï€-finite-is-empty (succ-â„• k) f) a = ex-falso (f a)
```

### Contractible types are untruncated Ï€-finite

```agda
is-untruncated-Ï€-finite-is-contr :
  {l : Level} (k : â„•) {A : UU l} â†’ is-contr A â†’ is-untruncated-Ï€-finite k A
is-untruncated-Ï€-finite-is-contr zero-â„• =
  has-finitely-many-connected-components-is-contr
pr1 (is-untruncated-Ï€-finite-is-contr (succ-â„• k) H) =
  is-untruncated-Ï€-finite-is-contr zero-â„• H
pr2 (is-untruncated-Ï€-finite-is-contr (succ-â„• k) H) x y =
  is-untruncated-Ï€-finite-is-contr k ( is-prop-is-contr H x y)

is-untruncated-Ï€-finite-unit : (k : â„•) â†’ is-untruncated-Ï€-finite k unit
is-untruncated-Ï€-finite-unit k =
  is-untruncated-Ï€-finite-is-contr k is-contr-unit

unit-Untruncated-Ï€-Finite-Type : (k : â„•) â†’ Untruncated-Ï€-Finite-Type lzero k
pr1 (unit-Untruncated-Ï€-Finite-Type k) = unit
pr2 (unit-Untruncated-Ï€-Finite-Type k) = is-untruncated-Ï€-finite-unit k
```

### Coproducts of untruncated Ï€-finite types are untruncated Ï€-finite

```agda
is-untruncated-Ï€-finite-coproduct :
  {l1 l2 : Level} (k : â„•) {A : UU l1} {B : UU l2} â†’
  is-untruncated-Ï€-finite k A â†’ is-untruncated-Ï€-finite k B â†’
  is-untruncated-Ï€-finite k (A + B)
is-untruncated-Ï€-finite-coproduct zero-â„• =
  has-finitely-many-connected-components-coproduct
pr1 (is-untruncated-Ï€-finite-coproduct (succ-â„• k) H K) =
  is-untruncated-Ï€-finite-coproduct zero-â„• (pr1 H) (pr1 K)
pr2 (is-untruncated-Ï€-finite-coproduct (succ-â„• k) H K) (inl x) (inl y) =
  is-untruncated-Ï€-finite-equiv k
    ( compute-eq-coproduct-inl-inl x y)
    ( pr2 H x y)
pr2 (is-untruncated-Ï€-finite-coproduct (succ-â„• k) H K) (inl x) (inr y) =
  is-untruncated-Ï€-finite-equiv k
    ( compute-eq-coproduct-inl-inr x y)
    ( is-untruncated-Ï€-finite-empty k)
pr2 (is-untruncated-Ï€-finite-coproduct (succ-â„• k) H K) (inr x) (inl y) =
  is-untruncated-Ï€-finite-equiv k
    ( compute-eq-coproduct-inr-inl x y)
    ( is-untruncated-Ï€-finite-empty k)
pr2 (is-untruncated-Ï€-finite-coproduct (succ-â„• k) H K) (inr x) (inr y) =
  is-untruncated-Ï€-finite-equiv k
    ( compute-eq-coproduct-inr-inr x y)
    ( pr2 K x y)

coproduct-Untruncated-Ï€-Finite-Type :
  {l1 l2 : Level} (k : â„•) â†’
  Untruncated-Ï€-Finite-Type l1 k â†’
  Untruncated-Ï€-Finite-Type l2 k â†’
  Untruncated-Ï€-Finite-Type (l1 âŠ” l2) k
pr1 (coproduct-Untruncated-Ï€-Finite-Type k A B) =
  (type-Untruncated-Ï€-Finite-Type k A + type-Untruncated-Ï€-Finite-Type k B)
pr2 (coproduct-Untruncated-Ï€-Finite-Type k A B) =
  is-untruncated-Ï€-finite-coproduct k
    ( is-untruncated-Ï€-finite-type-Untruncated-Ï€-Finite-Type k A)
    ( is-untruncated-Ï€-finite-type-Untruncated-Ï€-Finite-Type k B)
```

### `Maybe A` of any untruncated Ï€-finite type `A` is untruncated Ï€-finite

```agda
Maybe-Untruncated-Ï€-Finite-Type :
  {l : Level} (k : â„•) â†’
  Untruncated-Ï€-Finite-Type l k â†’
  Untruncated-Ï€-Finite-Type l k
Maybe-Untruncated-Ï€-Finite-Type k A =
  coproduct-Untruncated-Ï€-Finite-Type k A (unit-Untruncated-Ï€-Finite-Type k)

is-untruncated-Ï€-finite-Maybe :
  {l : Level} (k : â„•) {A : UU l} â†’
  is-untruncated-Ï€-finite k A â†’ is-untruncated-Ï€-finite k (Maybe A)
is-untruncated-Ï€-finite-Maybe k H =
  is-untruncated-Ï€-finite-coproduct k H (is-untruncated-Ï€-finite-unit k)
```

### Any stanadard finite type is untruncated Ï€-finite

```agda
is-untruncated-Ï€-finite-Fin :
  (k n : â„•) â†’ is-untruncated-Ï€-finite k (Fin n)
is-untruncated-Ï€-finite-Fin k zero-â„• =
  is-untruncated-Ï€-finite-empty k
is-untruncated-Ï€-finite-Fin k (succ-â„• n) =
  is-untruncated-Ï€-finite-Maybe k (is-untruncated-Ï€-finite-Fin k n)

Fin-Untruncated-Ï€-Finite-Type :
  (k : â„•) (n : â„•) â†’ Untruncated-Ï€-Finite-Type lzero k
pr1 (Fin-Untruncated-Ï€-Finite-Type k n) = Fin n
pr2 (Fin-Untruncated-Ï€-Finite-Type k n) = is-untruncated-Ï€-finite-Fin k n
```

### Any type equipped with a counting is untruncated Ï€-finite

```agda
is-untruncated-Ï€-finite-count :
  {l : Level} (k : â„•) {A : UU l} â†’ count A â†’ is-untruncated-Ï€-finite k A
is-untruncated-Ï€-finite-count k (n , e) =
  is-untruncated-Ï€-finite-equiv' k e (is-untruncated-Ï€-finite-Fin k n)
```

### Any finite type is untruncated Ï€-finite

```agda
is-untruncated-Ï€-finite-is-finite :
  {l : Level} (k : â„•) {A : UU l} â†’ is-finite A â†’ is-untruncated-Ï€-finite k A
is-untruncated-Ï€-finite-is-finite k {A} H =
  apply-universal-property-trunc-Prop H
    ( is-untruncated-Ï€-finite-Prop k A)
    ( is-untruncated-Ï€-finite-count k)

Ï€-finite-ğ”½ : {l : Level} (k : â„•) â†’ ğ”½ l â†’ Untruncated-Ï€-Finite-Type l k
pr1 (Ï€-finite-ğ”½ k A) = type-ğ”½ A
pr2 (Ï€-finite-ğ”½ k A) = is-untruncated-Ï€-finite-is-finite k (is-finite-type-ğ”½ A)
```

### The type of all `n`-element types in `UU l` is untruncated Ï€-finite

```agda
is-untruncated-Ï€-finite-UU-Fin :
  {l : Level} (k n : â„•) â†’ is-untruncated-Ï€-finite k (UU-Fin l n)
is-untruncated-Ï€-finite-UU-Fin zero-â„• n =
  has-finitely-many-connected-components-UU-Fin n
pr1 (is-untruncated-Ï€-finite-UU-Fin (succ-â„• k) n) =
  is-untruncated-Ï€-finite-UU-Fin zero-â„• n
pr2 (is-untruncated-Ï€-finite-UU-Fin (succ-â„• k) n) x y =
  is-untruncated-Ï€-finite-equiv k
    ( equiv-equiv-eq-UU-Fin n x y)
    ( is-untruncated-Ï€-finite-is-finite k
      ( is-finite-â‰ƒ
        ( is-finite-has-finite-cardinality (n , pr2 x))
        ( is-finite-has-finite-cardinality (n , pr2 y))))
```

### Untruncated Ï€â‚™â‚Šâ‚-finite types are untruncated Ï€â‚™-finite

```agda
is-untruncated-Ï€-finite-is-untruncated-Ï€-finite-succ-â„• :
  {l : Level} (k : â„•) {A : UU l} â†’
  is-untruncated-Ï€-finite (succ-â„• k) A â†’ is-untruncated-Ï€-finite k A
is-untruncated-Ï€-finite-is-untruncated-Ï€-finite-succ-â„• zero-â„• H =
  has-finitely-many-connected-components-is-untruncated-Ï€-finite 1 H
pr1 (is-untruncated-Ï€-finite-is-untruncated-Ï€-finite-succ-â„• (succ-â„• k) H) =
  has-finitely-many-connected-components-is-untruncated-Ï€-finite
    ( succ-â„• (succ-â„• k))
    ( H)
pr2 (is-untruncated-Ï€-finite-is-untruncated-Ï€-finite-succ-â„• (succ-â„• k) H) x y =
  is-untruncated-Ï€-finite-is-untruncated-Ï€-finite-succ-â„• k (pr2 H x y)
```

### Untruncated Ï€â‚™â‚Šâ‚-finite types are untruncated Ï€â‚-finite

```agda
is-untruncated-Ï€-finite-one-is-untruncated-Ï€-finite-succ-â„• :
  {l : Level} (k : â„•) {A : UU l} â†’
  is-untruncated-Ï€-finite (succ-â„• k) A â†’ is-untruncated-Ï€-finite 1 A
is-untruncated-Ï€-finite-one-is-untruncated-Ï€-finite-succ-â„• zero-â„• H = H
is-untruncated-Ï€-finite-one-is-untruncated-Ï€-finite-succ-â„• (succ-â„• k) H =
  is-untruncated-Ï€-finite-one-is-untruncated-Ï€-finite-succ-â„• k
    ( is-untruncated-Ï€-finite-is-untruncated-Ï€-finite-succ-â„• (succ-â„• k) H)
```

### Untruncated Ï€â‚™-finite sets are finite

```agda
is-finite-is-untruncated-Ï€-finite :
  {l : Level} (k : â„•) {A : UU l} â†’ is-set A â†’
  is-untruncated-Ï€-finite k A â†’ is-finite A
is-finite-is-untruncated-Ï€-finite k H K =
  is-finite-equiv'
    ( equiv-unit-trunc-Set (_ , H))
    ( has-finitely-many-connected-components-is-untruncated-Ï€-finite k K)
```

### Finite products of untruncated Ï€-finite types are untruncated Ï€-finite

```agda
is-untruncated-Ï€-finite-Î  :
  {l1 l2 : Level} (k : â„•) {A : UU l1} {B : A â†’ UU l2} â†’
  is-finite A â†’ ((a : A) â†’ is-untruncated-Ï€-finite k (B a)) â†’
  is-untruncated-Ï€-finite k ((a : A) â†’ B a)
is-untruncated-Ï€-finite-Î  zero-â„• =
  has-finitely-many-connected-components-finite-Î 
pr1 (is-untruncated-Ï€-finite-Î  (succ-â„• k) H K) =
  is-untruncated-Ï€-finite-Î  zero-â„• H (Î» a â†’ pr1 (K a))
pr2 (is-untruncated-Ï€-finite-Î  (succ-â„• k) H K) f g =
  is-untruncated-Ï€-finite-equiv k
    ( equiv-funext)
    ( is-untruncated-Ï€-finite-Î  k H (Î» a â†’ pr2 (K a) (f a) (g a)))

Untruncated-Ï€-Finite-Type-Î  :
  {l1 l2 : Level} (k : â„•) (A : ğ”½ l1)
  (B : type-ğ”½ A â†’ Untruncated-Ï€-Finite-Type l2 k) â†’
  Untruncated-Ï€-Finite-Type (l1 âŠ” l2) k
pr1 (Untruncated-Ï€-Finite-Type-Î  k A B) =
  (x : type-ğ”½ A) â†’ (type-Untruncated-Ï€-Finite-Type k (B x))
pr2 (Untruncated-Ï€-Finite-Type-Î  k A B) =
  is-untruncated-Ï€-finite-Î  k
    ( is-finite-type-ğ”½ A)
    ( Î» x â†’ is-untruncated-Ï€-finite-type-Untruncated-Ï€-Finite-Type k (B x))
```

### Dependent sums of types with finitely many connected components over a `0`-connected base

The total space of a family of types with finitely many connected components
over a `0`-connected base has finitely many connected components when the based
[loop spaces](synthetic-homotopy-theory.loop-spaces.md) of the base have
finitely many connected components.

```agda
has-finitely-many-connected-components-Î£-is-0-connected :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  is-0-connected A â†’
  ((a : A) â†’ has-finitely-many-connected-components (a ï¼ a)) â†’
  ((x : A) â†’ has-finitely-many-connected-components (B x)) â†’
  has-finitely-many-connected-components (Î£ A B)
has-finitely-many-connected-components-Î£-is-0-connected {A = A} {B} C H K =
  apply-universal-property-trunc-Prop
    ( is-inhabited-is-0-connected C)
    ( has-finitely-many-connected-components-Prop (Î£ A B))
    ( Î±)
  where
  Î± : A â†’ has-finitely-many-connected-components (Î£ A B)
  Î± a =
    is-finite-codomain
      ( K a)
      ( is-surjective-map-trunc-Set
        ( fiber-inclusion B a)
        ( is-surjective-fiber-inclusion C a))
      ( apply-dependent-universal-property-trunc-Set'
        ( Î» x â†’
          set-Prop
            ( Î -Prop
              ( type-trunc-Set (Î£ A B))
              ( Î» y â†’ is-decidable-Prop (Id-Prop (trunc-Set (Î£ A B)) x y))))
        ( Î²))
    where
    Î² :
      (x : Î£ A B) (v : type-trunc-Set (Î£ A B)) â†’
      is-decidable (unit-trunc-Set x ï¼ v)
    Î² (x , y) =
      apply-dependent-universal-property-trunc-Set'
        ( Î» u â†’
          set-Prop
            ( is-decidable-Prop
              ( Id-Prop (trunc-Set (Î£ A B)) (unit-trunc-Set (x , y)) u)))
        ( Î³)
      where
      Î³ :
        (v : Î£ A B) â†’
        is-decidable ((unit-trunc-Set (x , y)) ï¼ (unit-trunc-Set v))
      Î³ (x' , y') =
        is-decidable-equiv
          ( is-effective-unit-trunc-Set
            ( Î£ A B)
            ( x , y)
            ( x' , y'))
          ( apply-universal-property-trunc-Prop
            ( mere-eq-is-0-connected C a x)
            ( is-decidable-Prop
              ( mere-eq-Prop (x , y) (x' , y')))
              ( Î´))
        where
        Î´ : a ï¼ x â†’ is-decidable (mere-eq (x , y) (x' , y'))
        Î´ refl =
          apply-universal-property-trunc-Prop
            ( mere-eq-is-0-connected C a x')
            ( is-decidable-Prop
              ( mere-eq-Prop (a , y) (x' , y')))
            ( Îµ)
          where
          Îµ : a ï¼ x' â†’ is-decidable (mere-eq (x , y) (x' , y'))
          Îµ refl =
            is-decidable-equiv e
              ( is-decidable-type-trunc-Prop-is-finite
                ( is-finite-Î£
                  ( H a)
                  ( Î» Ï‰ â†’ is-finite-is-decidable-Prop (P Ï‰) (d Ï‰))))
            where
            â„™ :
              is-contr
                ( Î£ ( hom-Set (trunc-Set (a ï¼ a)) (Prop-Set _))
                    ( Î» h â†’
                      ( Î» aâ‚ â†’ h (unit-trunc-Set aâ‚)) ~
                      ( Î» Ï‰â‚ â†’
                        trunc-Prop (dependent-identification B Ï‰â‚ y y'))))
            â„™ =
              universal-property-trunc-Set
                ( a ï¼ a)
                ( Prop-Set _)
                ( Î» Ï‰ â†’ trunc-Prop (dependent-identification B Ï‰ y y'))

            P : type-trunc-Set (Id a a) â†’ Prop _
            P = pr1 (center â„™)

            compute-P :
              (Ï‰ : a ï¼ a) â†’
              type-Prop (P (unit-trunc-Set Ï‰)) â‰ƒ
              type-trunc-Prop (dependent-identification B Ï‰ y y')
            compute-P Ï‰ = equiv-eq (ap pr1 (pr2 (center â„™) Ï‰))

            d : (t : type-trunc-Set (a ï¼ a)) â†’ is-decidable (type-Prop (P t))
            d =
              apply-dependent-universal-property-trunc-Set'
                ( Î» t â†’ set-Prop (is-decidable-Prop (P t)))
                ( Î» Ï‰ â†’
                  is-decidable-equiv'
                    ( inv-equiv (compute-P Ï‰))
                    ( is-decidable-equiv'
                      ( is-effective-unit-trunc-Set (B a) (tr B Ï‰ y) y')
                      ( has-decidable-equality-is-finite
                        ( K a)
                        ( unit-trunc-Set (tr B Ï‰ y))
                        ( unit-trunc-Set y'))))

            f : type-hom-Prop
                ( trunc-Prop (Î£ (type-trunc-Set (Id a a)) (type-Prop âˆ˜ P)))
                ( mere-eq-Prop {A = Î£ A B} (a , y) (a , y'))
            f t =
              apply-universal-property-trunc-Prop t
                ( mere-eq-Prop (a , y) (a , y'))
                  Î» (u , v) â†’
                  apply-dependent-universal-property-trunc-Set'
                    ( Î» u' â†’
                      hom-set-Set
                        ( set-Prop (P u'))
                        ( set-Prop
                          ( mere-eq-Prop (a , y) (a , y'))))
                    ( Î» Ï‰ v' â†’
                      apply-universal-property-trunc-Prop
                        ( map-equiv (compute-P Ï‰) v')
                        ( mere-eq-Prop (a , y) (a , y'))
                        ( Î» p â†’ unit-trunc-Prop (eq-pair-Î£ Ï‰ p)))
                    ( u)
                    ( v)
            e :
              mere-eq {A = Î£ A B} (a , y) (a , y') â‰ƒ
              type-trunc-Prop (Î£ (type-trunc-Set (Id a a)) (type-Prop âˆ˜ P))
            e =
              equiv-iff
                ( mere-eq-Prop (a , y) (a , y'))
                ( trunc-Prop (Î£ (type-trunc-Set (a ï¼ a)) (type-Prop âˆ˜ P)))
                ( Î» t â†’
                  apply-universal-property-trunc-Prop t
                    ( trunc-Prop _)
                    ( ( Î» (Ï‰ , r) â†’
                        unit-trunc-Prop
                          { A = Î£ (type-trunc-Set (a ï¼ a)) (type-Prop âˆ˜ P)}
                          ( ( unit-trunc-Set Ï‰) ,
                            ( map-inv-equiv
                              ( compute-P Ï‰)
                              ( unit-trunc-Prop r)))) âˆ˜
                      ( pair-eq-Î£)))
                ( f)
```

### Dependent sums of types with finitely many connected components

```agda
abstract
  has-finitely-many-connected-components-Î£' :
    {l1 l2 : Level} (k : â„•) {A : UU l1} {B : A â†’ UU l2} â†’
    (Fin k â‰ƒ type-trunc-Set A) â†’
    ((x y : A) â†’ has-finitely-many-connected-components (x ï¼ y)) â†’
    ((x : A) â†’ has-finitely-many-connected-components (B x)) â†’
    has-finitely-many-connected-components (Î£ A B)
  has-finitely-many-connected-components-Î£' zero-â„• e H K =
    has-finitely-many-connected-components-is-empty
      ( is-empty-is-empty-trunc-Set (map-inv-equiv e) âˆ˜ pr1)
  has-finitely-many-connected-components-Î£' (succ-â„• k) {A} {B} e H K =
    apply-universal-property-trunc-Prop
      ( has-presentation-of-cardinality-has-cardinality-connected-components
        ( succ-â„• k)
        ( unit-trunc-Prop e))
      ( has-finitely-many-connected-components-Prop (Î£ A B))
      ( Î±)
    where
    Î± : Î£ (Fin (succ-â„• k) â†’ A) (Î» f â†’ is-equiv (unit-trunc-Set âˆ˜ f)) â†’
        has-finitely-many-connected-components (Î£ A B)
    Î± (f , EÎ·f) =
      is-finite-equiv
        ( equiv-trunc-Set g)
        ( is-finite-equiv'
          ( equiv-distributive-trunc-coproduct-Set
            ( Î£ (im (f âˆ˜ inl)) (B âˆ˜ pr1))
            ( Î£ (im (f âˆ˜ inr)) (B âˆ˜ pr1)))
          ( is-finite-coproduct
            ( has-finitely-many-connected-components-Î£' k
              ( h)
              ( Î» x y â†’
                is-finite-equiv'
                  ( equiv-trunc-Set
                    ( equiv-ap-emb
                      ( pr1 , is-emb-inclusion-subtype ( Î» u â†’ trunc-Prop _))))
                  ( H (pr1 x) (pr1 y)))
              ( Î» x â†’ K (pr1 x)))
            ( has-finitely-many-connected-components-Î£-is-0-connected
              ( is-0-connected-im-is-0-connected-domain
                ( f âˆ˜ inr)
                ( is-0-connected-unit))
              ( Î» a â†’
                has-finitely-many-connected-components-equiv'
                  ( equiv-Eq-eq-im (f âˆ˜ inr) a a)
                  ( H (pr1 a) (pr1 a)))
              ( Î» x â†’ K (pr1 x)))))
      where
      g : ((Î£ (im (f âˆ˜ inl)) (B âˆ˜ pr1)) + (Î£ (im (f âˆ˜ inr)) (B âˆ˜ pr1))) â‰ƒ
          Î£ A B
      g =
        ( equiv-Î£ B
          ( is-coproduct-codomain f
            ( unit-trunc-Set âˆ˜ f , EÎ·f)
            ( refl-htpy))
          ( Î» { (inl x) â†’ id-equiv ; (inr x) â†’ id-equiv})) âˆ˜e
        ( inv-equiv
          ( right-distributive-Î£-coproduct
            ( im (f âˆ˜ inl))
            ( im (f âˆ˜ inr))
            ( rec-coproduct (B âˆ˜ pr1) (B âˆ˜ pr1))))

      i : Fin k â†’ type-trunc-Set (im (f âˆ˜ inl))
      i = unit-trunc-Set âˆ˜ map-unit-im (f âˆ˜ inl)

      is-surjective-i : is-surjective i
      is-surjective-i =
        is-surjective-comp
          ( is-surjective-unit-trunc-Set (im (f âˆ˜ inl)))
          ( is-surjective-map-unit-im (f âˆ˜ inl))

      is-emb-i : is-emb i
      is-emb-i =
        is-emb-top-map-triangle
          ( (unit-trunc-Set âˆ˜ f) âˆ˜ inl)
          ( inclusion-trunc-im-Set (f âˆ˜ inl))
          ( i)
          ( ( inv-htpy (naturality-unit-trunc-Set (inclusion-im (f âˆ˜ inl)))) Â·r
            ( map-unit-im (f âˆ˜ inl)))
          ( is-emb-inclusion-trunc-im-Set (f âˆ˜ inl))
          ( is-emb-comp
            ( unit-trunc-Set âˆ˜ f)
            ( inl)
            ( is-emb-is-equiv EÎ·f)
            ( is-emb-inl))
      h : Fin k â‰ƒ type-trunc-Set (im (f âˆ˜ inl))
      h = i , (is-equiv-is-emb-is-surjective is-surjective-i is-emb-i)
```

### Dependent sums of untruncated Ï€-finite types

The dependent sum of a family of untruncated Ï€â‚™-finite types over a untruncated
Ï€â‚™â‚Šâ‚-finite base is untruncated Ï€â‚™-finite.

```agda
has-finitely-many-connected-components-Î£ :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  is-untruncated-Ï€-finite 1 A â†’
  ((x : A) â†’ has-finitely-many-connected-components (B x)) â†’
  has-finitely-many-connected-components (Î£ A B)
has-finitely-many-connected-components-Î£ {A = A} {B} H K =
  apply-universal-property-trunc-Prop
    ( pr1 H)
    ( has-finitely-many-connected-components-Prop (Î£ A B))
    ( Î» (k , e) â†’
      has-finitely-many-connected-components-Î£' k e (Î» x y â†’ pr2 H x y) K)

abstract
  is-untruncated-Ï€-finite-Î£ :
    {l1 l2 : Level} (k : â„•) {A : UU l1} {B : A â†’ UU l2} â†’
    is-untruncated-Ï€-finite (succ-â„• k) A â†’
    ((x : A) â†’ is-untruncated-Ï€-finite k (B x)) â†’
    is-untruncated-Ï€-finite k (Î£ A B)
  is-untruncated-Ï€-finite-Î£ zero-â„• =
    has-finitely-many-connected-components-Î£
  pr1 (is-untruncated-Ï€-finite-Î£ (succ-â„• k) H K) =
    has-finitely-many-connected-components-Î£
      ( is-untruncated-Ï€-finite-one-is-untruncated-Ï€-finite-succ-â„• (succ-â„• k) H)
      ( Î» x â†’
        has-finitely-many-connected-components-is-untruncated-Ï€-finite
          ( succ-â„• k)
          ( K x))
  pr2 (is-untruncated-Ï€-finite-Î£ (succ-â„• k) H K) (x , u) (y , v) =
    is-untruncated-Ï€-finite-equiv k
      ( equiv-pair-eq-Î£ (x , u) (y , v))
      ( is-untruncated-Ï€-finite-Î£ k
        ( pr2 H x y)
        ( Î» where refl â†’ pr2 (K x) u v))

Untruncated-Ï€-Finite-Type-Î£ :
  {l1 l2 : Level} (k : â„•) (A : Untruncated-Ï€-Finite-Type l1 (succ-â„• k))
  (B :
    (x : type-Untruncated-Ï€-Finite-Type (succ-â„• k) A) â†’
    Untruncated-Ï€-Finite-Type l2 k) â†’
  Untruncated-Ï€-Finite-Type (l1 âŠ” l2) k
pr1 (Untruncated-Ï€-Finite-Type-Î£ k A B) =
  Î£ ( type-Untruncated-Ï€-Finite-Type (succ-â„• k) A)
    ( Î» x â†’ type-Untruncated-Ï€-Finite-Type k (B x))
pr2 (Untruncated-Ï€-Finite-Type-Î£ k A B) =
  is-untruncated-Ï€-finite-Î£ k
    ( is-untruncated-Ï€-finite-type-Untruncated-Ï€-Finite-Type (succ-â„• k) A)
    ( Î» x â†’ is-untruncated-Ï€-finite-type-Untruncated-Ï€-Finite-Type k (B x))
```

## See also

- [Ï€-finite types](univalent-combinatorics.truncated-pi-finite-types.md)
- [Unbounded Ï€-finite types](univalent-combinatorics.unbounded-pi-finite-types.md)
