---
title: Equality in finite types
---

```agda
{-# OPTIONS --without-K --exact-split #-}

module univalent-combinatorics.equality-finite-types where

open import elementary-number-theory.natural-numbers using (â„•)

open import foundation.decidable-equality using
  ( has-decidable-equality; has-decidable-equality-Prop;
    has-decidable-equality-equiv')
open import foundation.dependent-pair-types using (pr1; pr2)
open import foundation.identity-types using (Id)
open import foundation.mere-equivalences using (is-set-mere-equiv')
open import foundation.propositional-truncations using
  ( apply-universal-property-trunc-Prop)
open import foundation.sets using (is-set; is-set-Prop; UU-Set)
open import foundation.universe-levels using (Level; UU; _âŠ”_; lzero)

open import univalent-combinatorics.counting using
  ( is-set-count; equiv-count)
open import univalent-combinatorics.counting-decidable-subtypes using
  ( count-eq)
open import univalent-combinatorics.equality-standard-finite-types using
  ( has-decidable-equality-Fin; is-set-Fin)
open import univalent-combinatorics.finite-types using
  ( is-finite; has-cardinality; is-finite-count; ğ”½; type-ğ”½; is-finite-type-ğ”½;
    UU-Fin-Level; UU-Fin; type-UU-Fin-Level; type-UU-Fin;
    mere-equiv-UU-Fin-Level; mere-equiv-UU-Fin)
```

## Idea

Any finite type is a set because it is merely equivalent to a standard finite type. Moreover, any finite type has decidable equality. In particular, this implies that the type of identifications between any two elements in a finite type is finite.

## Properties

### Any finite type is a set

```agda
abstract
  is-set-is-finite :
    {l : Level} {X : UU l} â†’ is-finite X â†’ is-set X
  is-set-is-finite {l} {X} H =
    apply-universal-property-trunc-Prop H
      ( is-set-Prop X)
      ( Î» e â†’ is-set-count e)

set-ğ”½ : ğ”½ â†’ UU-Set lzero
pr1 (set-ğ”½ X) = type-ğ”½ X
pr2 (set-ğ”½ X) = is-set-is-finite (is-finite-type-ğ”½ X)
```

### Any finite type has decidable equality

```agda
has-decidable-equality-is-finite :
  {l1 : Level} {X : UU l1} â†’ is-finite X â†’ has-decidable-equality X
has-decidable-equality-is-finite {l1} {X} is-finite-X =
  apply-universal-property-trunc-Prop is-finite-X
    ( has-decidable-equality-Prop X)
    ( Î» e â†’
      has-decidable-equality-equiv' (equiv-count e) has-decidable-equality-Fin)
```

### Any type of cardinality `k` is a set

```agda
is-set-has-cardinality :
  {l1 : Level} {X : UU l1} {k : â„•} â†’ has-cardinality k X â†’ is-set X
is-set-has-cardinality H = is-set-mere-equiv' H (is-set-Fin _)

set-UU-Fin-Level : {l1 : Level} {k : â„•} â†’ UU-Fin-Level l1 k â†’ UU-Set l1
pr1 (set-UU-Fin-Level X) = type-UU-Fin-Level X
pr2 (set-UU-Fin-Level X) = is-set-has-cardinality (mere-equiv-UU-Fin-Level X)

set-UU-Fin : {k : â„•} â†’ UU-Fin k â†’ UU-Set lzero
set-UU-Fin X = set-UU-Fin-Level X
```

### Any type of finite cardinality has decidable equality

```agda
has-decidable-equality-has-cardinality :
  {l1 : Level} {X : UU l1} {k : â„•} â†’
  has-cardinality k X â†’ has-decidable-equality X
has-decidable-equality-has-cardinality {l1} {X} {k} H =
  apply-universal-property-trunc-Prop H
    ( has-decidable-equality-Prop X)
    ( Î» e â†’ has-decidable-equality-equiv' e has-decidable-equality-Fin)
```

### The type of identifications between any two elements in a finite type is finite

```agda
abstract
  is-finite-eq :
    {l1 : Level} {X : UU l1} â†’
    has-decidable-equality X â†’ {x y : X} â†’ is-finite (Id x y)
  is-finite-eq d {x} {y} = is-finite-count (count-eq d x y)

Id-ğ”½ : (X : ğ”½) (x y : type-ğ”½ X) â†’ ğ”½
pr1 (Id-ğ”½ X x y) = Id x y
pr2 (Id-ğ”½ X x y) =
  is-finite-eq (has-decidable-equality-is-finite (is-finite-type-ğ”½ X))
```
