# Fibers of maps between finite types

```agda
module univalent-combinatorics.fibers-of-maps where

open import foundation.fibers-of-maps public
```

<details><summary>Imports</summary>

```agda
open import elementary-number-theory.natural-numbers
open import elementary-number-theory.sums-of-natural-numbers

open import foundation.contractible-types
open import foundation.decidable-types
open import foundation.equality-dependent-pair-types
open import foundation.equivalences
open import foundation.functoriality-dependent-pair-types
open import foundation.identity-types
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.sections
open import foundation.torsorial-type-families
open import foundation.transport-along-identifications
open import foundation.type-arithmetic-dependent-pair-types
open import foundation.universe-levels

open import univalent-combinatorics.counting
open import univalent-combinatorics.counting-dependent-pair-types
open import univalent-combinatorics.decidable-propositions
open import univalent-combinatorics.dependent-pair-types
open import univalent-combinatorics.double-counting
open import univalent-combinatorics.equality-finite-types
open import univalent-combinatorics.finite-types
open import univalent-combinatorics.standard-finite-types
```

</details>

## Idea

The fibers of maps between finite types are finite.

## Properties

### If `A` and `B` can be counted, then the fibers of a map `f : A â†’ B` can be counted

```agda
count-fiber :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  count A â†’ count B â†’ (y : B) â†’ count (fiber f y)
count-fiber f count-A count-B =
  count-fiber-count-Î£-count-base
    ( count-B)
    ( count-equiv' (equiv-total-fiber f) count-A)

abstract
  sum-number-of-elements-count-fiber :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
    (count-A : count A) (count-B : count B) â†’
    Id
      ( sum-count-â„• count-B
        ( Î» x â†’ number-of-elements-count (count-fiber f count-A count-B x)))
      ( number-of-elements-count count-A)
  sum-number-of-elements-count-fiber f count-A count-B =
    sum-number-of-elements-count-fiber-count-Î£ count-B
      ( count-equiv' (equiv-total-fiber f) count-A)

abstract
  double-counting-fiber :
    {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (count-A : count A) â†’
    (count-B : count B) (count-fiber-f : (y : B) â†’ count (fiber f y)) (y : B) â†’
    Id
      ( number-of-elements-count (count-fiber-f y))
      ( number-of-elements-count (count-fiber f count-A count-B y))
  double-counting-fiber f count-A count-B count-fiber-f y =
    double-counting (count-fiber-f y) (count-fiber f count-A count-B y)
```

### Fibers of maps between finite types are finite

```agda
abstract
  is-finite-fiber :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’
    is-finite X â†’ is-finite Y â†’ (y : Y) â†’ is-finite (fiber f y)
  is-finite-fiber f is-finite-X is-finite-Y y =
    apply-universal-property-trunc-Prop
      ( is-finite-X)
      ( is-finite-Prop (fiber f y))
      ( Î» H â†’
        apply-universal-property-trunc-Prop
          ( is-finite-Y)
          ( is-finite-Prop (fiber f y))
          ( Î» K â†’ unit-trunc-Prop (count-fiber f H K y)))

fiber-ğ”½ :
  {l1 l2 : Level} (X : ğ”½ l1) (Y : ğ”½ l2) (f : type-ğ”½ X â†’ type-ğ”½ Y) â†’
  type-ğ”½ Y â†’ ğ”½ (l1 âŠ” l2)
pr1 (fiber-ğ”½ X Y f y) = fiber f y
pr2 (fiber-ğ”½ X Y f y) =
  is-finite-fiber f (is-finite-type-ğ”½ X) (is-finite-type-ğ”½ Y) y
```

###

```agda
abstract
  is-finite-fiber-map-section-family :
    {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (b : (x : A) â†’ B x) â†’
    is-finite (Î£ A B) â†’ ((x : A) â†’ is-finite (B x)) â†’
    (t : Î£ A B) â†’ is-finite (fiber (map-section-family b) t)
  is-finite-fiber-map-section-family {l1} {l2} {A} {B} b f g (pair y z) =
    is-finite-equiv'
      ( ( ( left-unit-law-Î£-is-contr
            ( is-torsorial-Id' y)
            ( pair y refl)) âˆ˜e
          ( inv-associative-Î£ A
            ( Î» x â†’ Id x y)
            ( Î» t â†’ Id (tr B (pr2 t) (b (pr1 t))) z))) âˆ˜e
        ( equiv-tot (Î» x â†’ equiv-pair-eq-Î£ (pair x (b x)) (pair y z))))
      ( is-finite-eq (has-decidable-equality-is-finite (g y)))
```

### The fibers of maps between finite types are decidable

```agda
is-decidable-fiber-count :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  count A â†’ count B â†’ (y : B) â†’ is-decidable (fiber f y)
is-decidable-fiber-count f count-A count-B y =
  is-decidable-count (count-fiber f count-A count-B y)

is-decidable-fiber-Fin :
  {k l : â„•} (f : Fin k â†’ Fin l) â†’ (y : Fin l) â†’ is-decidable (fiber f y)
is-decidable-fiber-Fin {k} {l} f y =
  is-decidable-fiber-count f (count-Fin k) (count-Fin l) y
```

### If `f : A â†’ B` and `B` is finite, then `A` is finite if and only if the fibers of `f` are finite

```agda
equiv-is-finite-domain-is-finite-fiber :
  {l1 l2 : Level} {A : UU l1} â†’
  (B : ğ”½ l2) (f : A â†’ (type-ğ”½ B)) â†’
  ((b : type-ğ”½ B) â†’ is-finite (fiber f b)) â‰ƒ is-finite A
equiv-is-finite-domain-is-finite-fiber {A = A} B f =
  equiv-prop
    ( is-prop-Î  (Î» b â†’ is-prop-is-finite (fiber f b)))
    ( is-prop-is-finite A)
    ( Î» P â†’
      is-finite-equiv
        ( equiv-total-fiber f)
        ( is-finite-Î£ (is-finite-type-ğ”½ B) P))
    ( Î» P â†’ is-finite-fiber f P (is-finite-type-ğ”½ B))
```
