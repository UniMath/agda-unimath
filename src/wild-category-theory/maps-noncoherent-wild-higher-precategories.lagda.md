# Maps between noncoherent wild higher precategories

```agda
{-# OPTIONS --guardedness #-}

module wild-category-theory.maps-noncoherent-wild-higher-precategories where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.function-types
open import foundation.identity-types
open import foundation.universe-levels

open import structured-types.globular-maps
open import structured-types.globular-types

open import wild-category-theory.noncoherent-wild-higher-precategories
```

</details>

## Idea

A
{{#concept "map" Disambiguation="between noncoherent wild higher precategories" Agda=map-Noncoherent-Wild-Higher-Precategory}}
`f` between
[noncoherent wild higher precategories](wild-category-theory.noncoherent-wild-higher-precategories.md)
`ğ’œ` and `â„¬` is a [globular map](structured-types.globular-maps.md) between their
underlying [globular types](structured-types.globular-types.md). More
specifically, a map `F` between noncoherent wild higher precategories consists
of a map on objects `Fâ‚€ : obj ğ’œ â†’ obj â„¬`, and for every pair of $n$-morphisms
`f` and `g`, a map of $(n+1)$-morphisms

```text
  Fâ‚™â‚Šâ‚ : (ğ‘›+1)-hom ğ’ f g â†’ (ğ‘›+1)-hom ğ’Ÿ (Fâ‚™ f) (Fâ‚™ g).
```

A map between noncoherent wild higher precategories does not have to preserve
the identities or composition in any shape or form, and is the least structured
notion of a "morphism" between noncoherent wild higher precategories. For a
notion of "morphism" between noncoherent wild higher precategories that in one
sense preserves this additional structure, see
[colax functors between noncoherent wild higher precategories](wild-category-theory.colax-functors-noncoherent-wild-higher-precategories.md).

## Definitions

### Maps between noncoherent wild higher precategories

```agda
map-Noncoherent-Wild-Higher-Precategory :
  {l1 l2 l3 l4 : Level}
  (ğ’œ : Noncoherent-Wild-Higher-Precategory l1 l2)
  (â„¬ : Noncoherent-Wild-Higher-Precategory l3 l4) â†’ UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
map-Noncoherent-Wild-Higher-Precategory ğ’œ â„¬ =
  globular-map
    ( globular-type-Noncoherent-Wild-Higher-Precategory ğ’œ)
    ( globular-type-Noncoherent-Wild-Higher-Precategory â„¬)

module _
  {l1 l2 l3 l4 : Level}
  (ğ’œ : Noncoherent-Wild-Higher-Precategory l1 l2)
  (â„¬ : Noncoherent-Wild-Higher-Precategory l3 l4)
  (F : map-Noncoherent-Wild-Higher-Precategory ğ’œ â„¬)
  where

  obj-map-Noncoherent-Wild-Higher-Precategory :
    obj-Noncoherent-Wild-Higher-Precategory ğ’œ â†’
    obj-Noncoherent-Wild-Higher-Precategory â„¬
  obj-map-Noncoherent-Wild-Higher-Precategory =
    0-cell-globular-map F

  hom-globular-map-map-Noncoherent-Wild-Higher-Precategory :
    {x y : obj-Noncoherent-Wild-Higher-Precategory ğ’œ} â†’
    globular-map
      ( hom-globular-type-Noncoherent-Wild-Higher-Precategory ğ’œ x y)
      ( hom-globular-type-Noncoherent-Wild-Higher-Precategory â„¬
        ( obj-map-Noncoherent-Wild-Higher-Precategory x)
        ( obj-map-Noncoherent-Wild-Higher-Precategory y))
  hom-globular-map-map-Noncoherent-Wild-Higher-Precategory =
    1-cell-globular-map-globular-map F

  hom-map-Noncoherent-Wild-Higher-Precategory :
    {x y : obj-Noncoherent-Wild-Higher-Precategory ğ’œ} â†’
    hom-Noncoherent-Wild-Higher-Precategory ğ’œ x y â†’
    hom-Noncoherent-Wild-Higher-Precategory â„¬
      ( obj-map-Noncoherent-Wild-Higher-Precategory x)
      ( obj-map-Noncoherent-Wild-Higher-Precategory y)
  hom-map-Noncoherent-Wild-Higher-Precategory =
    0-cell-globular-map
      ( hom-globular-map-map-Noncoherent-Wild-Higher-Precategory)

  2-hom-map-Noncoherent-Wild-Higher-Precategory :
    {x y : obj-Noncoherent-Wild-Higher-Precategory ğ’œ}
    {f g : hom-Noncoherent-Wild-Higher-Precategory ğ’œ x y} â†’
    2-hom-Noncoherent-Wild-Higher-Precategory ğ’œ f g â†’
    2-hom-Noncoherent-Wild-Higher-Precategory â„¬
      ( hom-map-Noncoherent-Wild-Higher-Precategory f)
      ( hom-map-Noncoherent-Wild-Higher-Precategory g)
  2-hom-map-Noncoherent-Wild-Higher-Precategory =
    1-cell-globular-map
      ( hom-globular-map-map-Noncoherent-Wild-Higher-Precategory)

  hom-noncoherent-wild-higher-precategory-map-Noncoherent-Wild-Higher-Precategory :
    (x y : obj-Noncoherent-Wild-Higher-Precategory ğ’œ) â†’
    map-Noncoherent-Wild-Higher-Precategory
      ( hom-noncoherent-wild-higher-precategory-Noncoherent-Wild-Higher-Precategory
        ( ğ’œ)
        ( x)
        ( y))
      ( hom-noncoherent-wild-higher-precategory-Noncoherent-Wild-Higher-Precategory
        ( â„¬)
        ( obj-map-Noncoherent-Wild-Higher-Precategory x)
        ( obj-map-Noncoherent-Wild-Higher-Precategory y))
  hom-noncoherent-wild-higher-precategory-map-Noncoherent-Wild-Higher-Precategory
    x y =
    1-cell-globular-map-globular-map F
```

### The identity map on a noncoherent wild higher precategory

```agda
module _
  {l1 l2 : Level} (ğ’œ : Noncoherent-Wild-Higher-Precategory l1 l2)
  where

  id-map-Noncoherent-Wild-Higher-Precategory :
    map-Noncoherent-Wild-Higher-Precategory ğ’œ ğ’œ
  id-map-Noncoherent-Wild-Higher-Precategory =
    id-globular-map _
```

### Composition of maps between noncoherent wild higher precategories

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level}
  (ğ’œ : Noncoherent-Wild-Higher-Precategory l1 l2)
  (â„¬ : Noncoherent-Wild-Higher-Precategory l3 l4)
  (ğ’ : Noncoherent-Wild-Higher-Precategory l5 l6)
  (G : map-Noncoherent-Wild-Higher-Precategory â„¬ ğ’)
  (F : map-Noncoherent-Wild-Higher-Precategory ğ’œ â„¬)
  where

  comp-map-Noncoherent-Wild-Higher-Precategory :
    map-Noncoherent-Wild-Higher-Precategory ğ’œ ğ’
  comp-map-Noncoherent-Wild-Higher-Precategory =
    comp-globular-map G F
```
