# Maps between noncoherent wild higher precategories

```agda
{-# OPTIONS --guardedness #-}

module wild-category-theory.maps-noncoherent-wild-higher-precategories where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.function-types
open import foundation.identity-types
open import foundation.universe-levels

open import structured-types.globular-types
open import structured-types.maps-globular-types

open import wild-category-theory.noncoherent-wild-higher-precategories
```

</details>

## Idea

A
{{#concept "map" Disambiguation="between noncoherent wild higher precategories" Agda=map-Noncoherent-Wild-Higher-Precategory}}
`f` between
[noncoherent wild higher precategories](wild-category-theory.noncoherent-wild-higher-precategories.md)
`ğ’œ` and `â„¬` consists of a map on objects `Fâ‚€ : obj ğ’œ â†’ obj â„¬`, and for every
pair of $n$-morphisms `f` and `g`, a map of $(n+1)$-morphisms

```text
  Fâ‚™â‚Šâ‚ : (ğ‘›+1)-hom ğ’ f g â†’ (ğ‘›+1)-hom ğ’Ÿ (Fâ‚™ f) (Fâ‚™ g).
```

A map between noncoherent wild higher precategories does not have to preserve
the identities or composition in any shape or form, and is the least structured
notion of a "morphism" between noncoherent wild higher precategories. For a
notion of "morphism" between noncoherent wild higher precategories that in one
sense preserves this additional structure, see
[colax functors between noncoherent wild higher precategories](wild-category-theory.colax-functors-noncoherent-wild-higher-precategories.md).

## Definitions

### Maps between noncoherent wild higher precategories

```agda
record
  map-Noncoherent-Wild-Higher-Precategory
  {l1 l2 l3 l4 : Level}
  (ğ’œ : Noncoherent-Wild-Higher-Precategory l1 l2)
  (â„¬ : Noncoherent-Wild-Higher-Precategory l3 l4) : UU (l1 âŠ” l2 âŠ” l3 âŠ” l4)
  where
  field
    obj-map-Noncoherent-Wild-Higher-Precategory :
      obj-Noncoherent-Wild-Higher-Precategory ğ’œ â†’
      obj-Noncoherent-Wild-Higher-Precategory â„¬

    hom-globular-type-map-Noncoherent-Wild-Higher-Precategory :
      {x y : obj-Noncoherent-Wild-Higher-Precategory ğ’œ} â†’
      map-Globular-Type
        ( hom-globular-type-Noncoherent-Wild-Higher-Precategory ğ’œ x y)
        ( hom-globular-type-Noncoherent-Wild-Higher-Precategory â„¬
          ( obj-map-Noncoherent-Wild-Higher-Precategory x)
          ( obj-map-Noncoherent-Wild-Higher-Precategory y))

open map-Noncoherent-Wild-Higher-Precategory public

module _
  {l1 l2 l3 l4 : Level}
  {ğ’œ : Noncoherent-Wild-Higher-Precategory l1 l2}
  {â„¬ : Noncoherent-Wild-Higher-Precategory l3 l4}
  (F : map-Noncoherent-Wild-Higher-Precategory ğ’œ â„¬)
  where

  hom-map-Noncoherent-Wild-Higher-Precategory :
    {x y : obj-Noncoherent-Wild-Higher-Precategory ğ’œ} â†’
    hom-Noncoherent-Wild-Higher-Precategory ğ’œ x y â†’
    hom-Noncoherent-Wild-Higher-Precategory â„¬
      ( obj-map-Noncoherent-Wild-Higher-Precategory F x)
      ( obj-map-Noncoherent-Wild-Higher-Precategory F y)
  hom-map-Noncoherent-Wild-Higher-Precategory =
    0-cell-map-Globular-Type
      ( hom-globular-type-map-Noncoherent-Wild-Higher-Precategory F)

  2-hom-map-Noncoherent-Wild-Higher-Precategory :
    {x y : obj-Noncoherent-Wild-Higher-Precategory ğ’œ}
    {f g : hom-Noncoherent-Wild-Higher-Precategory ğ’œ x y} â†’
    2-hom-Noncoherent-Wild-Higher-Precategory ğ’œ f g â†’
    2-hom-Noncoherent-Wild-Higher-Precategory â„¬
      ( hom-map-Noncoherent-Wild-Higher-Precategory f)
      ( hom-map-Noncoherent-Wild-Higher-Precategory g)
  2-hom-map-Noncoherent-Wild-Higher-Precategory =
    1-cell-map-Globular-Type
      ( hom-globular-type-map-Noncoherent-Wild-Higher-Precategory F)

  hom-noncoherent-wild-higher-precategory-map-Noncoherent-Wild-Higher-Precategory :
    (x y : obj-Noncoherent-Wild-Higher-Precategory ğ’œ) â†’
    map-Noncoherent-Wild-Higher-Precategory
      ( hom-noncoherent-wild-higher-precategory-Noncoherent-Wild-Higher-Precategory
        ( ğ’œ)
        ( x)
        ( y))
      ( hom-noncoherent-wild-higher-precategory-Noncoherent-Wild-Higher-Precategory
        ( â„¬)
        ( obj-map-Noncoherent-Wild-Higher-Precategory F x)
        ( obj-map-Noncoherent-Wild-Higher-Precategory F y))
  hom-noncoherent-wild-higher-precategory-map-Noncoherent-Wild-Higher-Precategory
    x y =
    Î» where
    .obj-map-Noncoherent-Wild-Higher-Precategory â†’
      hom-map-Noncoherent-Wild-Higher-Precategory
    .hom-globular-type-map-Noncoherent-Wild-Higher-Precategory â†’
      globular-type-1-cell-map-Globular-Type
        ( hom-globular-type-map-Noncoherent-Wild-Higher-Precategory F)
```

### The identity map on a noncoherent wild higher precategory

```agda
module _
  {l1 l2 : Level} (ğ’œ : Noncoherent-Wild-Higher-Precategory l1 l2)
  where

  id-map-Noncoherent-Wild-Higher-Precategory :
    map-Noncoherent-Wild-Higher-Precategory ğ’œ ğ’œ
  id-map-Noncoherent-Wild-Higher-Precategory =
    Î» where
    .obj-map-Noncoherent-Wild-Higher-Precategory â†’
      id
    .hom-globular-type-map-Noncoherent-Wild-Higher-Precategory â†’
      id-map-Globular-Type _
```

### Composition of maps between noncoherent wild higher precategories

```agda
module _
  {l1 l2 l3 l4 l5 l6 : Level}
  {ğ’œ : Noncoherent-Wild-Higher-Precategory l1 l2}
  {â„¬ : Noncoherent-Wild-Higher-Precategory l3 l4}
  {ğ’ : Noncoherent-Wild-Higher-Precategory l5 l6}
  (G : map-Noncoherent-Wild-Higher-Precategory â„¬ ğ’)
  (F : map-Noncoherent-Wild-Higher-Precategory ğ’œ â„¬)
  where

  comp-map-Noncoherent-Wild-Higher-Precategory :
    map-Noncoherent-Wild-Higher-Precategory ğ’œ ğ’
  comp-map-Noncoherent-Wild-Higher-Precategory =
    Î» where
    .obj-map-Noncoherent-Wild-Higher-Precategory â†’
      obj-map-Noncoherent-Wild-Higher-Precategory G âˆ˜
      obj-map-Noncoherent-Wild-Higher-Precategory F
    .hom-globular-type-map-Noncoherent-Wild-Higher-Precategory â†’
      comp-map-Globular-Type
        ( hom-globular-type-map-Noncoherent-Wild-Higher-Precategory G)
        ( hom-globular-type-map-Noncoherent-Wild-Higher-Precategory F)
```
